rem --- rem bmr Indented BOM Listing II
rem --- Program bmr_indentedbill_o1 v8.0.0 09Jan2008 (bmr_zb)
rem --- Created by c:\codeport_bbj\adx_codeport.bbj v1.1.5 (01/09/2008 03:22:26)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyrights 1981-2011, BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

        seterr std_error
        setesc std_error

rem --- Retrieve the program path

        pgmdir$=stbl("+DIR_PGM",err=*next)
        precision 5

rem --- Document initializations

		use ::bbtranslator.bbj::BBTranslator
		
        OutVect!=bbjAPI().getSysGui().makeVector()
        HdrVect!=bbjAPI().getSysGui().makeVector()
        BrkVect!=bbjAPI().getSysGui().makeVector()
        TotVect!=bbjAPI().getSysGui().makeVector()
        rep_date$=date(0:"%Mz/%Dz/%Yd")
        rep_date_stamp$=date(0:"%Yd%Mz%Dz")
        rep_time$=date(0:"%hz:%mz %p")
        rep_time_stamp$=date(0:"%Hz%mz%sz")
        rep_prog$=pgm(-2)

rem --- Retrieve sysinfo data

        sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
        dim sysinfo$:sysinfo_template$
        sysinfo$=stbl("+SYSINFO",err=*next)
        milestone=num(stbl("+MILESTONE",err=*next),err=*next)
        firm_id$=sysinfo.firm_id$

rem --- Initializations

        call stbl("+DIR_SYP")+"bac_key_template.bbj","BMM_BILLMAT","PRIMARY",bmm02a_key_tpl$,rd_table_chans$[all],status$
        call stbl("+DIR_SYP")+"bac_key_template.bbj","BMM_BILLOPER","PRIMARY",bmm03a_key_tpl$,rd_table_chans$[all],status$
        dim bmm02a_key$:bmm02a_key_tpl$
        dim bmm03a_key$:bmm03a_key_tpl$
        dim junk[10],z[11],ivdesc$(desc[0])
        more=1

rem --- Headings

        h0=132
        h=0,headings=3,when$=sysinfo.system_date$,clock$=""
        dim headings$[headings],o[11],headline1$(h0),headline2$(h0)
        headings$[0]=sysinfo.firm_name$
        headings$[1]=sysinfo.task_desc$
        headings$[2]=Translate!.getTranslation("AON_LEVEL_SELECTION:_")
        if level$="" then headings$[2]=headings$[2]+Translate!.getTranslation("AON_ALL_LEVELS_SELECTED") else headings$[2]=headings$[2]+Translate!.getTranslation("AON_ALL_LEVELS_UP_TO_")+level$

        call pgmdir$+"adc_getmask.aon","","IV","A","",m3$,0,0
    
rem ---  column heading
    if showcost$="Y" then
        dim columns$[11,10]
        columns=11
            columns$[0,0]=Translate!.getTranslation("AON_SEQ"),               columns$[0,1]="C",columns$[0,2]="5"
            columns$[1,0]=Translate!.getTranslation("AON_LEVEL"),             columns$[1,1]="C",columns$[1,2]="5"
            columns$[2,0]=Translate!.getTranslation("AON_ITEM"),           columns$[2,1]="C",columns$[2,2]="8"
            columns$[3,0]=Translate!.getTranslation("AON_DESCRIPTION"),       columns$[3,1]="C",columns$[3,2]="20"
            columns$[4,0]=Translate!.getTranslation("AON_UM"),                columns$[4,1]="C",columns$[4,2]="3"
            columns$[5,0]=Translate!.getTranslation("AON_MATERIAL")+"^"+Translate!.getTranslation("AON_UNITS"),    columns$[5,1]="N",columns$[5,2]="10",columns$[5,3]=m2$,columns$[5,10]="T"
            columns$[6,0]=Translate!.getTranslation("AON_MATERIAL")+"^"+Translate!.getTranslation("AON_COST"),     columns$[6,1]="N",columns$[6,2]="12",columns$[6,3]=m3$,columns$[6,10]="T"
            columns$[7,0]=Translate!.getTranslation("AON_LABOR")+"^"+Translate!.getTranslation("AON_UNITS"),       columns$[7,1]="N",columns$[7,2]="10",columns$[7,3]=m2$,columns$[7,10]="T"
            columns$[8,0]=Translate!.getTranslation("AON_LABOR")+"^"+Translate!.getTranslation("AON_COST"),        columns$[8,1]="N",columns$[8,2]="12",columns$[8,3]=m3$,columns$[8,10]="T"
            columns$[9,0]=Translate!.getTranslation("AON_SUBCONTRACT")+"^"+Translate!.getTranslation("AON_UNITS"), columns$[9,1]="N",columns$[9,2]="10",columns$[9,3]=m2$,columns$[9,10]="T"
            columns$[10,0]=Translate!.getTranslation("AON_SUBCONTRACT")+"^"+Translate!.getTranslation("AON_COST"), columns$[10,1]="N",columns$[10,2]="12",columns$[10,3]=m3$,columns$[10,10]="T"
            columns$[11,0]=Translate!.getTranslation("AON_TOTAL"),            columns$[11,1]="N",columns$[11,2]="12",columns$[11,3]=m3$,columns$[11,10]="T"
    else
        dim columns$[7,10]
        columns=7
            columns$[0,0]=Translate!.getTranslation("AON_SEQ"),               columns$[0,1]="C",columns$[0,2]="5"
            columns$[1,0]=Translate!.getTranslation("AON_LEVEL"),             columns$[1,1]="C",columns$[1,2]="5"
            columns$[2,0]=Translate!.getTranslation("AON_ITEM"),           columns$[2,1]="C",columns$[2,2]="8"
            columns$[3,0]=Translate!.getTranslation("AON_DESCRIPTION"),       columns$[3,1]="C",columns$[3,2]="20"
            columns$[4,0]=Translate!.getTranslation("AON_UM"),                columns$[4,1]="C",columns$[4,2]="3"
            columns$[5,0]=Translate!.getTranslation("AON_MATERIAL")+"^"+Translate!.getTranslation("AON_UNITS"),    columns$[5,1]="N",columns$[5,2]="10",columns$[5,3]=m2$,columns$[5,10]="T"
            columns$[6,0]=Translate!.getTranslation("AON_LABOR")+"^"+Translate!.getTranslation("AON_UNITS"),       columns$[6,1]="N",columns$[6,2]="10",columns$[6,3]=m2$,columns$[6,10]="T"
            columns$[7,0]=Translate!.getTranslation("AON_SUBCONTRACT")+"^"+Translate!.getTranslation("AON_UNITS"), columns$[7,1]="N",columns$[7,2]="10",columns$[7,3]=m2$,columns$[7,10]="T"
    endif

   
rem --- Background

        call pgmdir$+"adc_progress.aon","NC","","","","",p[0],bmm01_dev,1,meter_num,status

rem --- Init Read

        extractrecord (bmm01_dev,key=firm_id$+billfrom$,dom=*next) bmm01a$

main_read: rem --- Main Read

    while more
        read record (bmm01_dev,end=done) bmm01a$
        if bmm01a.firm_id$<>firm_id$ then goto done
        x0$=bmm01a.firm_id$+bmm01a.bill_no$
        lot=bmm01a.std_lot_size,yield=bmm01a.est_yield
        if billthru$<>"" then if bmm01a.bill_no$>pad(billthru$,20) then goto done

rem --- Breaks

        gosub find_description
        if x0$="NONE" then continue
        call pgmdir$+"adc_progress.aon","S","","","",bmm01a.bill_no$,0,0,1,meter_num,status
        dim lab_cost[9],mat_cost[9],lab_qty[9],mat_qty[9],sub_qty[9],sub_cost[9],k9$(9*25)
        k$=bmm01a.firm_id$+bmm01a.bill_no$
        cur_level=1,mat_qty[cur_level]=1
        if level<>1 then xwk=fnblank(columns)
        first=1
        times=1
get_subcontractors: rem --- Get Subcontracts

        x$=k$
        if times=1 and level>1 then
            xwk=fnblank(1)
        endif
        levelprinted=0
        gosub add_labor
        times=times+1

        x$=k$
        gosub subcontractors
        cur_level=cur_level+1    


rem --- Find Parts
        first=0
        read (bmm02_dev,key=k$,dom=*next)
        while more
            bmm02a_key$=key(bmm02_dev,end=sub_totals)
            if pos(firm_id$=bmm02a_key$)=1 then 
              read record (bmm02_dev,key=bmm02a_key$) bmm02a$
              if bmm02a.firm_id$+bmm02a.bill_no$=k$ then 
                if bmm02a.line_type$<>"S" then continue
                if cvs(bmm02a.effect_date$,3)<>"" then if bmm02a.effect_date$>pdate$ then continue
                if cvs(bmm02a.obsolt_date$,3)<>"" then if bmm02a.obsolt_date$<=pdate$ then continue
                x0$=bmm02a.firm_id$+bmm02a.item_id$
                gosub find_description

rem --- Any Pieces??

                find (bmm01_dev,key=bmm02a.firm_id$+bmm02a.item_id$,dom=print_parts)
                if cur_level>9 then goto circular_reference
                k9$(((cur_level-1)*25)+1,25)=bmm02a.firm_id$+bmm02a.bill_no$+bmm02a.material_seq$
                k$=bmm02a.firm_id$+bmm02a.item_id$,mat_qty[cur_level]=fnqty(bmm02a.qty_required,bmm02a.alt_factor,bmm02a.divisor,yield,bmm02a.scrap_factor)

                if cur_level=2 and level>=2 then 
                    OutVect!.addItem(bmm02a.material_seq$) 
                else
                    if cur_level<=level then  OutVect!.addItem("") 
                endif
                goto get_subcontractors

print_parts: rem --- Print Parts

                if cur_level<=level then 
                    dim dots$(cur_level-1,".")
                    if cur_level=1 then 
                        um$=bmm01a.unit_measure$
                    else
                        um$=bmm02a.unit_measure$
                    endif

                    if cur_level=2 and level>=2 then
                        OutVect!.addItem(bmm02a.material_seq$)
                    else
                        OutVect!.addItem("")
                    endif
                    if bmm02a.divisor=0 then let bmm02a.divisor=1
                    OutVect!.addItem(dots$+str(cur_level:"0"))
					OutVect!.addItem(bmm02a.item_id$)
                    OutVect!.addItem(ivdesc$)
                    OutVect!.addItem(um$)
                    value=fnqty(bmm02a.qty_required,bmm02a.alt_factor,bmm02a.divisor,yield,bmm02a.scrap_factor)
                    OutVect!.addItem(str(value))
                    if showcost$="Y" then
                        value=ivm02a.unit_cost*fnqty(bmm02a.qty_required,bmm02a.alt_factor,bmm02a.divisor,yield,bmm02a.scrap_factor)
                        OutVect!.addItem(str(value))
                        xwk=fnblank(columns-6)
                    else
                        xwk=fnblank(columns-5)
                    endif  
                endif
                mat_cost[cur_level-1]=mat_cost[cur_level-1]+ivm02a.unit_cost*fnqty(bmm02a.qty_required,bmm02a.alt_factor,bmm02a.divisor,yield,bmm02a.scrap_factor)
                continue
              endif
            endif

sub_totals: rem --- Sub Totals

            cur_level=cur_level-1
            if cur_level<=level then 
                dim dots$(cur_level-1,".")
                if showcost$="Y"
                    if cur_level<level then
                        TotVect!.addItem(str(OutVect!.size()/(columns+1)))
                    endif
                    OutVect!.addItem("")
                    OutVect!.addItem(dots$+str(cur_level:"0"))
                    xwk=fnblank(1)
                    if cur_level<level then
                        OutVect!.addItem(Translate!.getTranslation("AON_TOTAL:"))
                    else
                        OutVect!.addItem("")  
                    endif
                    xwk=fnblank(1)
                    OutVect!.addItem(str(mat_qty[cur_level]))
                    OutVect!.addItem(str(mat_cost[cur_level]*mat_qty[cur_level]))
                    OutVect!.addItem(str(lab_qty[cur_level]*mat_qty[cur_level]))
                    OutVect!.addItem(str(lab_cost[cur_level]*mat_qty[cur_level]))
                    OutVect!.addItem(str(sub_qty[cur_level]))
                    OutVect!.addItem(str(sub_cost[cur_level]))
                    OutVect!.addItem(str(mat_cost[cur_level]*mat_qty[cur_level]+lab_cost[cur_level]*mat_qty[cur_level]+sub_cost[cur_level]))
                else
                    if cur_level<level then
                        TotVect!.addItem(str(OutVect!.size()/(columns+1)))
                    endif
                    OutVect!.addItem("")
                    OutVect!.addItem(dots$+str(cur_level:"0"))
                    xwk=fnblank(1)
                    if cur_level<level then
                        OutVect!.addItem(Translate!.getTranslation("AON_TOTAL:"))
                    else
                        OutVect!.addItem("")
                    endif
                    xwk=fnblank(1)
                    OutVect!.addItem(str(mat_qty[cur_level]))
                    OutVect!.addItem(str(lab_qty[cur_level]*mat_qty[cur_level]))
                    OutVect!.addItem(str(sub_qty[cur_level]))
                endif
            endif

rem --- Do the Next Level

            if cur_level=1 then goto main_read
            lab_cost[cur_level-1]=lab_cost[cur_level-1]+lab_cost[cur_level]*mat_qty[cur_level]
            mat_cost[cur_level-1]=mat_cost[cur_level-1]+mat_cost[cur_level]*mat_qty[cur_level]
            lab_qty[cur_level-1]=lab_qty[cur_level-1]+lab_qty[cur_level]*mat_qty[cur_level]
            sub_cost[cur_level-1]=sub_cost[cur_level-1]+sub_cost[cur_level]
            lab_cost[cur_level]=0
            mat_cost[cur_level]=0,lab_qty[cur_level]=0
            mat_qty[cur_level]=0
            sub_qty[cur_level]=0,sub_cost[cur_level]=0
            k$=k9$(((cur_level-1)*25)+1,25)
            k9$(((cur_level-1)*25)+1,25)=""
            x0$=k$(1,22)
            find record (bmm01_dev2,key=x0$,dom=unit_cost) bmm01a2$
            if bmm01a2.phantom_bill$<>"Y" then let lot=bmm01a2.std_lot_size,yield=bmm01a2.est_yield
unit_cost:
            gosub find_description
            read (bmm02_dev,key=k$)
            k$=k$(1,22)
        wend
    wend

done: rem --- End of File

    goto std_exit

add_labor: rem --- Add Labor

    find record (bmm01_dev2,key=x$,dom=add_labor_dom) bmm01a2$
    if bmm01a2.phantom_bill$<>"Y" then let lot=bmm01a2.std_lot_size,yield=bmm01a2.est_yield
    if bmm01a2.phantom_bill$="Y" then let yield=bmm01a2.est_yield

add_labor_dom:
    extractrecord (bmm03_dev,key=x$,dom=*next)
    while more
        bmm03a_key$=key(bmm03_dev,end=*break)
        if pos(firm_id$=bmm03a_key$)<>1 then break
        read record (bmm03_dev,key=bmm03a_key$) bmm03a$
        if bmm03a.firm_id$+bmm03a.bill_no$<>x$ then break
        if cvs(bmm03a.effect_date$,3)<>"" then if bmm03a.effect_date$>pdate$ then continue
        if cvs(bmm03a.obsolt_date$,3)<>"" then if bmm03a.obsolt_date$<=pdate$ then continue
        if bmm03a.pcs_per_hour=0 then continue
        dim w[13]
        read record (bmm08_dev,key=firm_id$+bmm03a.op_code$,dom=*next) bmm08a$
        if lot=0 then let lot=1
        directcost=fnop1(bmm03a.hrs_per_pce,bmm08a.direct_rate,bmm03a.pcs_per_hour,yield,bmm03a.setup_time,lot)
        ohcost=directcost*bmm08a.ovhd_factor
        if bmm03a.hrs_per_pce<>0 then let lab_cost[cur_level]=lab_cost[cur_level]+directcost+ohcost
        lab_qty[cur_level]=lab_qty[cur_level]+fnopqty(bmm03a.hrs_per_pce,bmm03a.pcs_per_hour,yield,bmm03a.setup_time,lot)
        
    wend

    if cur_level<=level then 
        dim dots$(cur_level-1,".")
        if cur_level=1 then let um$=bmm01a.unit_measure$ else let um$=bmm02a.unit_measure$
        if !levelprinted then 
            subbill$=" "
            find (bmm01_dev,key=firm_id$+k$(3),dom=printing)
            subbill$="*"
printing:

            if first=1 then OutVect!.addItem("")
            OutVect!.addItem(dots$+str(cur_level:"0"))
			if cur_level=1
				OutVect!.addItem(subbill$+k$(3)+$00$+"B")
			else
				OutVect!.addItem(subbill$+k$(3))
			endif
            levelprinted=1
            OutVect!.addItem(ivdesc$)
            OutVect!.addItem(um$)
            if showcost$="Y" then xwk=fnblank(2) else xwk=fnblank(1)
        endif      
        OutVect!.addItem(str(lab_qty[cur_level]))
        if showcost$="Y" then OutVect!.addItem(str(lab_cost[cur_level]))
    endif
    return

circular_reference: rem --- Circular Reference

    dim messages$[1]
    messages$[0]=Translate!.getTranslation("AON_MORE_THAN_9_LEVELS_DETECTED_IN_THIS_BILL,_CHECK_FOR_POSSIBLE")
    messages$[1]=Translate!.getTranslation("AON_CIRCULAR_REFERENCES,_<ENTER>_TO_RETURN_TO_MENU:_")
    call pgmdir$+"adc_stdmessage.aon",2,messages$[all],1,22,-1,v$,v3
    goto std_exit

subcontractors: rem --- Add Subcontracts

    read (bmm05_dev,key=x$,dom=*next)
    while more
        bmm05a_key$=key(bmm05_dev,end=*break)
        if pos(firm_id$=bmm05a_key$)<>1 then break
        read record (bmm05_dev,key=bmm05a_key$) bmm05a$
        if bmm05a.firm_id$+bmm05a.bill_no$<>x$ then break
        if cvs(bmm05a.effect_date$,3)<>"" then if bmm05a.effect_date$>pdate$ then continue
        if cvs(bmm05a.obsolt_date$,3)<>"" then if bmm05a.obsolt_date$<=pdate$ then continue
        sub_qty[cur_level]=sub_qty[cur_level]+bmm05a.qty_required
        if bmm05a.divisor<>0 then let sub_cost[cur_level]=sub_cost[cur_level]+bmm05a.qty_required*bmm05a.alt_factor*bmm05a.unit_cost/bmm05a.divisor else let sub_cost[cur_level]=0
    wend

    if cur_level<=level then 
        dim dots$(cur_level-1,".")
        if cur_level=1 then let um$=bmm01a.unit_measure$ else let um$=bmm02a.unit_measure$
        if !levelprinted then 
            subbill$=" "
            find (bmm01_dev,key=firm_id$+k$(3),dom=printing_dom)
            subbill$="*"
printing_dom:
            if first=1 then OutVect!.addItem("")
            OutVect!.addItem(dots$+str(cur_level:"0"))
            OutVect!.addItem(subbill$+k$(3))
            levelprinted=1
            OutVect!.addItem(ivdesc$)
            OutVect!.addItem(um$)
            if showcost$="Y" then xwk=fnblank(4) else xwk=fnblank(2)
        endif
        OutVect!.addItem(str(sub_qty[cur_level]))
        if showcost$="Y" then OutVect!.addItem(str(sub_cost[cur_level]));OutVect!.addItem("")
    endif
    return

find_description: rem --- Find Description & UnitCost
    dim ivdesc$(60)
    unitcost=0
    ivm01a.item_desc$=Translate!.getTranslation("AON_***NOT_ON_FILE***")
    find record (ivm01_dev,key=x0$,dom=*next) ivm01a$
    ivdesc$(1)=fnitem$(ivm01a.item_desc$,desc[1],desc[2],desc[3])
    find record (ivm02_dev,key=firm_id$+whse$+x0$(3,20),dom=*next) ivm02a$
    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)
    def fnqty(q1,q2,q3,q4,q5)=10000*q1*q2/(q3*q4*(100-q5))
    def fnop1(q1,q2,q3,q4,q5,q6)=100*(q1*q2/q3)/q4+q5*q2/q6
    def fnopqty(q1,q2,q3,q4,q5)=100*(q1/q2)/q3+q4/q5

rem --- Format inventory item description

    def fnitem$(q$,q1,q2,q3)=cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)

rem --- Date/time handling functions

    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem --- fnBlank : Create blank cells in OutVect!. No return value
   
    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend

rem #endinclude std_functions.src

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    call stbl("+DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src
rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)
    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
	out_action$="FORMAT"
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

    end
