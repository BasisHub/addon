rem ==========================================================================
rem --- bmo_AO_BmUtils.aon 
rem --- AO_BmUtils class (BmUtils superclass)
rem --- BM helper superclass for collection of utilities.
rem --- 
rem --- AddonSoftware Version 11.0
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved
rem ==========================================================================

class public AO_BmUtils

	rem /**
	rem  * Calculates Net Quantity Required.
	rem  *
	rem  * @param BBjNumber Quantity Required
	rem  * @param BBjNumber Alternate Factor
	rem  * @param BBjNumber Divisor
	rem  * @param BBjNumber Estimated Yield Percent
	rem  * @param BBjNumber Scrap Factor Percent
	rem  * @return BBjNumber
	rem  */
	method public static BBjNumber netQuantityRequired(BBjNumber qntyRequired, 
:													   BBjNumber altFactor, 
:													   BBjNumber divisor, 
:													   BBjNumber estYield, 
:													   BBjNumber scrapFactor)
        seterr method_error
		if divisor*estYield*(100-scrapFactor) = 0
			methodret 0
		else
			methodret 10000*qntyRequired*altFactor/(divisor*estYield*(100-scrapFactor))
		endif
       
method_error:rem --- Method error trap/handler
        rd_err_text$="", err_num=err
        if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
        call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
        if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
        if pos("RETRY"=rd_err_act$)<>0 retry
        x$=stbl("+THROWN_ERR","TRUE")   
        throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	methodend
	
	method public static BBjNumber directCost	(BBjNumber hrsPerPiece,
:												BBjNumber dirRate, 
:												BBjNumber pcsPerHour, 
:												BBjNumber yieldPct, 
:												BBjNumber setupTime,
:												BBjNumber lotSize)
        seterr method_error
		if pcsPerHour = 0
			pcsPerHour = 1
		endif
		if yieldPct = 0
			yieldPct = 100
		endif
		if lotSize = 0
			lotSize = 1
		endif
		methodret 100*(hrsPerPiece*dirRate/pcsPerHour)/yieldPct+setupTime*dirRate/lotSize

method_error:rem --- Method error trap/handler
        rd_err_text$="", err_num=err
        if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
        call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
        if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
        if pos("RETRY"=rd_err_act$)<>0 retry
        x$=stbl("+THROWN_ERR","TRUE")   
        throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	methodend
	
	method public static BBjNumber netSubQtyReq	(BBjNumber qtyReqd,
:												BBjNumber altFactor,
:												BBjNumber divisor)
        seterr method_error
		if divisor = 0
			methodret 0
		else
			methodret qtyReqd * altFactor / divisor
		endif
        
method_error:rem --- Method error trap/handler
        rd_err_text$="", err_num=err
        if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
        call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
        if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
        if pos("RETRY"=rd_err_act$)<>0 retry
        x$=stbl("+THROWN_ERR","TRUE")   
        throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	methodend
	
classend
