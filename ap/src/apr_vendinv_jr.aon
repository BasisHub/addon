rem Vendor Open Invoice Listing
rem Proprietary Software. BASIS International Ltd.
rem Program ID: apr_vendinv_irpt.aon  <Dec 10, 2008>

	use ::bbjasper.bbj::BBJasperReport
	use ::bbjasper.bbj::BBJasperViewerWindow

rem --- print 'show';rem troubleshooting

rem --- Get HashMap of Values in Options Entry Form

	params! = Option!.getOptionMap()

rem --- Retrieve sysinfo data

	sysinfo_tpl$=stbl("+SYSINFO_TPL",err=*next)
	dim sysinfo$:sysinfo_tpl$
	sysinfo$=stbl("+SYSINFO",err=*next)
	user_id$=sysinfo.user_id$
	proc_date$=date(jul(num(sysinfo.system_date$(1,4)),num(sysinfo.system_date$(5,2)),num(sysinfo.system_date$(7,2))):stbl("+DATE_MASK"))
    firm_id$=sysinfo.firm_id$

rem --- Make Document Archive Record and Get ID

	rep_date$=date(0:stbl("+DATE_MASK"))
	rep_date_stamp$=date(0:"%Yd%Mz%Dz")
	rep_time$=date(0:"%hz:%mz %p")
	rep_time_stamp$=date(0:"%Hz%mz%sz")

	rd_source_alias$=rd_alias_id$
	rd_source_type$="O"
	rd_doc_source$="E"
	rd_doc_ext$="JAS"
	rd_archive_action$=rd_out_status$

call rd_dir_syp$+"bac_documents.bbj",
:		rd_doc_id$,
:		rep_date_stamp$,
:		rep_time_stamp$,
:		rd_doc_source$,
:		rd_doc_ext$,
:		rd_doc_path$,
:		rd_source_type$,
:		rd_source_alias$,
:		rd_source_id$,
:		rd_source_ref$,
:		rd_table_chans$[all],
:		"DOC_ID-NOREPRINT"

rem --- Document Output Report Parameters

    params!.put("DOC_NO",rd_doc_id$)
    params!.put("DATE_REP",rep_date$+"  "+rep_time$)
    
rem --- Set Report Name & Subreport directory

	reportDir$ = stbl("+DIR_REPORTS",err=*next)
	temp = unt
	open (temp)reportDir$
	reportDir$ = fid(temp)(9)+"/"
	close (temp)
	reportBaseName$ = "vendor_open_invoices"
	filename$ = reportDir$ + reportBaseName$ + ".jasper"

rem --- Set Report Parameters/create report object

    params!.put("FIRM_ID",firm_id$)
    params!.put("BARISTA_WD",dir(""))
    params!.put("DIR_REPORT",reportDir$)

    declare BBJasperReport report!
        
    report! = new BBJasperReport(filename$,"jdbc:basis:localhost?database="+stbl("+DBNAME")+"&user=guest")
	report!.putParams(params!)

	locale$ = stbl("!LOCALE")
	locale$ = stbl("+USER_LOCALE",err=*next)
	report!.setLocale(locale$)

rem --- Fill Report and Show

    declare BBJasperViewerWindow viewerWindow!

	report!.fill()
    viewerWindow! = new BBJasperViewerWindow(report!,0,0,1150,500,"Vendor Open Invoices",$93$)
    viewerWindow!.setReleaseOnClose(1)
    viewerWindow!.show(0)
        
rem --- Event Control

	process_events,err=*same

	release

connect_err:rem --- Connection Error

	declare java.lang.Exception Exception!
	Exception! = bbjapi().getLastException()
	if Exception! = null() then 
	   x = msgbox(errmes(-1),16,"Connection Failed")
	else
	   x = msgbox(Exception!.getLocalizedMessage(),16,"Connection Failed")
	endif

	release
