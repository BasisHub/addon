rem AP Open Invoice Drilldown Link
rem Proprietary Software. BASIS International Ltd.
rem Program ID: apd_openinvoices.aon  <Aug 22, 2012>
rem --- Copyright BASIS International Ltd.  All Rights Reserved.

    enter rec_tpl$,table_chans$[all],status$

rem rec_tpl$       - Current inquiry record
rem table_chans$[] - Barista opened channels array
rem status$        - Returned status: "SKIP" = Skip current record

    apt_invdet=fnget_dev("APT_INVOICEDET")
    dim apt_invdet$:fnget_tpl$("APT_INVOICEDET"),err=*next
    
    if apt_invdet=0 or apt_invdet$="" then
        num_files=1
        dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
        open_tables$[1]="APT_INVOICEDET",open_opts$[1]="OTA"

        gosub open_tables

        apt_invdet=num(open_chans$[1])
        dim apt_invdet$:open_tpls$[1]
    endif

rem --- Main process

    status$=""

    hdr_key$=rec_tpl.firm_id$+rec_tpl.ap_type$+rec_tpl.vendor_id$+rec_tpl.ap_inv_no$
    read(apt_invdet,key=hdr_key$,dom=*next)
    while 1
        readrecord(apt_invdet,end=*break)apt_invdet$
        if apt_invdet.firm_id$+apt_invdet.ap_type$+apt_invdet.vendor_id$+apt_invdet.ap_inv_no$<>hdr_key$ break
        rec_tpl.invoice_amt=rec_tpl.invoice_amt+apt_invdet.trans_amt
    wend

    if rec_tpl.invoice_amt=0 then status$="SKIP"

    goto std_exit

rem --- Functions

    def fnget_dev(tmp0$)=num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3))
    def fnget_tpl$(tmp0$)=table_chans$[num(table_chans$[0,0](pos(pad(tmp0$,17)=table_chans$[0,0],20)+17,3)),0]

open_tables:rem --- Open Tables

	call stbl("+DIR_SYP")+"bac_open_tables.bbj",
:		open_beg,
:		open_end,
:		open_tables$[all],
:		open_opts$[all],
:		open_chans$[all],
:		open_tpls$[all],
:		rd_table_chans$[all],
:		open_batch,
:		open_status$

	if open_status$<>""
		msg_id$="ENTRY_OPEN_ERROR"
		dim msg_tokens$[1]
			msg_tokens$[1]=open_status$
		gosub disp_message
		goto std_exit
	endif

	return

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]

    return

std_exit: rem --- Exit Program

    exit
