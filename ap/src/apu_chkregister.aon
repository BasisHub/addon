rem --- Computer Check Register Update
rem --- Program apu_chkregister v8.0.0 27Oct2007 (apu_ba)
rem --- Created by adx_codeport.bbx v1.1.5 (10/27/2007 10:21:46)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    use java.io.File
    use ::ado_file.src::FileObject

    setesc std_error
    seterr std_error

	next_overlay$="";rem --- clear next_var and outvect! so adc_process_end doesn't loop...i.e., don't run anything after this
	if OutVect!<>null() OutVect!=null()

rem --- Close printer and destroy current progress meter (if one exists)

    close (printer_dev,err=*next)
    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status

rem --- Skip update if errors were encountered

    if errors goto std_exit_no_update

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)
    firm_id$=sysinfo.firm_id$

rem --- Open/Lock files

    files=17,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="aps_params",ids$[1]="APS_PARAMS"
    files$[3]="gls_calendar",ids$[3]="GLS_CALENDAR"
    files$[5]="apm-01",ids$[5]="APM_VENDMAST"
    files$[6]="apm-02",ids$[6]="APM_VENDHIST"
    files$[8]="apc_distribution",ids$[8]="APC_DISTRIBUTION"
    files$[9]="apt-01",ids$[9]="APT_INVOICEHDR"
    files$[10]="apt-02",ids$[10]="APT_INVOICEDIST"
    files$[11]="apt-05",ids$[11]="APT_CHECKHISTORY"
    files$[12]="apt-11",ids$[12]="APT_INVOICEDET"
    files$[13]="apw-01",ids$[13]="APW_CHECKINVOICE"
    files$[14]="apw_chkjasperprn",ids$[14]="APW_CHKJASPERPRN"
    files$[15]="glm-01",ids$[15]="GLM_ACCT"
    files$[16]="glt-04",ids$[16]="GLE_DAILYDETAIL"
    files$[17]="glt-05",ids$[17]="GLT_BANKCHECKS"
    call pgmdir$+"adc_fileopen.aon",action,begfile,14,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit_no_update
    ads01_dev=channels[1]
    gls_calendar_dev=channels[3]
    apm01_dev=channels[5]
    apm02_dev=channels[6]
    apm10b_dev=channels[8]
    apt01_dev=channels[9]
    apt02_dev=channels[10]
    apt05_dev=channels[11]
    apt11_dev=channels[12]
    apw01_dev=channels[13]
    apwChkJasperPrn_dev=channels[14]

rem --- Dimension string templates

    dim aps01a$:templates$[1],gls_calendar$:templates$[3]
    dim apm01a$:templates$[5],apm02a$:templates$[6]
    dim apm10b$:templates$[8],apt01a$:templates$[9],apt02a$:templates$[10]
    dim apt05a$:templates$[11],apt11a$:templates$[12],apw01a$:templates$[13]
    dim apwChkJasperPrn$:templates$[14]

rem --- Retrieve parameter records

    aps01a_key$=firm_id$+"AP00"
    find record (aps01_dev,key=aps01a_key$,err=std_missing_params) aps01a$
    find record (gls_calendar_dev,key=firm_id$+aps01a.current_year$,err=*next) gls_calendar$
    if cvs(gls_calendar.firm_id$,2)="" then
        msg_id$="AD_NO_FISCAL_CAL"
        dim msg_tokens$[1]
        msg_tokens$[1]=aps01a.current_year$
        gosub disp_message
        goto std_exit_no_update
    endif
    call stbl("+DIR_PGM")+"adc_getmask.aon","VENDOR_ID","","","",m0$,0,vendor_size

rem --- Parameters

    dim info$[20]
    br$=aps01a.br_interface$
    ap_year=num(aps01a.current_year$)
    call pgmdir$+"adc_application.aon","AP",info$[all]
    gl$=info$[9]
    call stbl("+DIR_PGM")+"adc_getmask.aon","GL_ACCOUNT","","","",g0$,0,account_length
    max_acct_len=max(10,account_length)

rem --- Initializations

    dim t[2]
    g9$=field(gls_calendar$,"per_ending_"+str(num(gls_calendar.total_pers$):"00"))
    w=ap_year
    if g9$<gls_calendar.per_ending_01$ w=w+1
    g9$=str(w)+g9$
    prior_g9$=str(w-1)+g9$(5,4)
    g8$=aps01a.cur_1099_yr$+"1231"
    prior_g8$=str(num(aps01a.cur_1099_yr$)-1:"0000")+"1231"
    more=1
    nacha_dev=-1

rem --- Additional File Opens

    if gl$<>"Y" goto confirm_update
    call pgmdir$+"adc_fileopen.aon",action,15,16,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit_no_update
    glm01_dev=channels[15];dim glm01a$:templates$[15]
    glt04_dev=channels[16];dim glt04a$:templates$[16]
    if br$<>"Y" goto confirm_update
    call pgmdir$+"adc_fileopen.aon",action,17,17,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit_no_update
    glt05_dev=channels[17];dim glt05a$:templates$[17]

confirm_update: rem --- Run Update?

    msg_id$="AON_UPDT_QUERY"
    dim x$:stbl("+SYSINFO_TPL")
    dim msg_tokens$[1]
    x$=stbl("+SYSINFO")                                                            
    msg_tokens$[1]=x.task_desc$
    gosub disp_message
    if msg_opt$<>"Y" then goto std_exit_no_update

rem --- File initializations

    call pgmdir$+"adc_progress.aon","NC","","","","",0,apw01_dev,1,meter_num,status

rem --- Init Read

    read (apw01_dev,key=firm_id$,dom=*next)

rem --- Main Read

    while more
        k0$=key(apw01_dev,end=done)
        read record (apw01_dev) apw01a$
        if pos(firm_id$=apw01a.firm_id$)<>1 goto done
        call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status
        if pos(apw01a.comp_or_void$="VX") then goto void_check

rem --- Check Detail

        k1$=k0$(1,4)+apw01a.vendor_id$+apw01a.ap_inv_no$
        read record (ape04_dev,key=k1$,dom=void_check) ape04a$
        if ape04a.check_no$<>apw01a.check_no$ goto void_check
        if apw01a.comp_or_void$="A" then gosub build_NACHA_file
        goto open_invoice

next_invoice: rem --- Next Invoice

        k1$=key(ape04_dev,end=remove_check)
        if k1$(1,10)<>k0$(1,4)+apw01a.vendor_id$ goto remove_check
        read record (ape04_dev) ape04a$

open_invoice: rem --- Open Invoice

        gosub update_open_invoice

rem --- Vendor

        while more
            extract record (apm02_dev,key=firm_id$+ape04a.vendor_id$+ape04a.ap_type$,dom=*break) apm02a$
            apm02a.lstpay_date$=apw01a.check_date$
            apm02a.open_invs=apm02a.open_invs-(ape04a.payment_amt+ape04a.discount_amt)
            if apw01a.check_date$<=prior_g9$
				apm02a.pri_yr_discs=apm02a.pro_yr_discs+ape04a.discount_amt
				apm02a.pyr_payments=apm02a.pyr_payments+ape04a.payment_amt
			endif
            if apw01a.check_date$>prior_g9$ and apw01a.check_date$<=g9$
				apm02a.ytd_discs=apm02a.ytd_discs+ape04a.discount_amt
				apm02a.ytd_payments=apm02a.ytd_payments+ape04a.payment_amt
			endif
            if apw01a.check_date$>g9$
				apm02a.nyr_disc=apm02a.nyr_disc+ape04a.discount_amt
				apm02a.nyr_payments=apm02a.nyr_payments+ape04a.payment_amt
			endif
            if apw01a.check_date$<=prior_g8$
				apm02a.pri_cal_pmt=apm02a.pri_cal_pmt+ape04a.payment_amt
			endif
            if apw01a.check_date$>prior_g8$ and apw01a.check_date$<=g8$
				apm02a.cur_cal_pmts=apm02a.cur_cal_pmts+ape04a.payment_amt
			endif
            if apw01a.check_date$>g8$
				apm02a.nxt_cyr_pmts=apm02a.nxt_cyr_pmts+ape04a.payment_amt
			endif
            apm02a$=field(apm02a$)
            write record (apm02_dev) apm02a$
            break
        wend

rem --- G/L Reference Fields

        if gl$="Y" 
            ref2$=ape04a.ap_inv_no$
            ref3$=apw01a.check_no$+" "+apw01a.ap_type$
            when$=apw01a.check_date$
            memo$=Translate!.getTranslation("AON_UNKNOWN_VENDOR")
            ref1$=fnmask$(ape04a.vendor_id$(1,vendor_size),m0$)
            find record (apm01_dev,key=firm_id$+ape04a.vendor_id$,dom=*next) apm01a$;memo$=apm01a.vendor_name$
        endif    

rem --- Dist Code

        if d9$<>k2$
            k2$=d9$
            dim apm10b$:fattr(apm10b$)
            find record (apm10b_dev,key=firm_id$+"B"+k2$,dom=*next) apm10b$
        endif    

rem --- Cash Basis

        if aps01a.cash_basis$<>"Y" goto open_checks
        read (apt02_dev,key=firm_id$+ape04a.ap_type$+ape04a.vendor_id$+"I"+ape04a.ap_inv_no$,dom=*next)
        while more
            k$=key(apt02_dev,end=*break)
            if len(k$)<21 break
            if k$(1,21)<>firm_id$+ape04a.ap_type$+ape04a.vendor_id$+"I"+ape04a.ap_inv_no$ break
            read record (apt02_dev) apt02a$
        wend    

open_checks: rem --- Open Checks

        dim apt05a$:fattr(apt05a$)
        apt05a.firm_id$=firm_id$
        apt05a.ap_type$=apw01a.ap_type$
        apt05a.check_no$=apw01a.check_no$
        apt05a.vendor_id$=ape04a.vendor_id$
        apt05a.ap_inv_no$=ape04a.ap_inv_no$
        apt05a.check_date$=apw01a.check_date$
        if apw01a.comp_or_void$="A" then
            apt05a.trans_type$="A"
        else
            apt05a.trans_type$="C"
        endif
        apt05a.ap_dist_code$=k2$ ;rem jpb k2$?
        apt05a.invoice_amt=ape04a.payment_amt+ape04a.discount_amt+ape04a.retention
        apt05a.discount_amt=ape04a.discount_amt
        apt05a.retention=ape04a.retention
        write record (apt05_dev,dom=remove_open_invoice) apt05a$
        if br$="Y" if gl$="Y" gosub update_bank_rec
        goto remove_open_invoice

remove_open_invoice: rem --- Delete Open Invoice

rem --- Remove Check Detail

        remove (ape04_dev,key=k1$)

        goto next_invoice

void_check: rem --- Void Check

        dim apt05a$:fattr(apt05a$)
        apt05a.firm_id$=firm_id$
        apt05a.ap_type$=apw01a.ap_type$
        apt05a.check_no$=apw01a.check_no$
        apt05a.check_date$=apw01a.check_date$
        apt05a.trans_type$="V"
        write record (apt05_dev,dom=remove_check) apt05a$
        if br$="Y" x9$=x9$+apt05a.check_no$

remove_check: rem --- Remove Check

        remove (apw01_dev,key=k0$,dom=*next)

    wend

done: rem --- Clear Remainder
    
    read (ape04_dev,key=firm_id$,dom=*next)
    while more
        k$=key(ape04_dev,end=*break)
        if pos(firm_id$=k$)<>1 break
        remove (ape04_dev,key=k$)
    wend

    rem --- Finish building complete NACHA text file for ACH payments
    if nacha_dev>-1 then gosub finish_NACHA_file

rem --- All done

    if gl$="Y"
       call pgmdir$+"glc_update.aon",glm01_dev,glt04_dev,glw11_dev,status
       call pgmdir$+"glc_ctlupdate.aon",status
    endif
    call pgmdir$+"adc_batchdelete.aon",err=*next,status
    goto std_exit

build_NACHA_file: rem --- Build NACHA text file for financial institution to process ACH payments for all ACH non-negotiable checks

    rem --- As necessary, create new NACHA text file
    if nacha_dev=-1 then
        rem --- Make sure ACH File Export Directory exists
        nachaDir$=cvs(FileObject.fixPath(apsACH.ach_export_dir$, "/"),2)
        FileObject.makeDirs(new File(nachaDir$))

        rem --- Create NACHA text file
        nacha$=nachaDir$+cvs(apsACH.bnk_acct_cd$,2)+"_NACHA_"+DATE(0:"%Yd%Mz%Dz")+"_"+DATE(0:"%Hz%mz")+".txt"
        erase nacha$,err=*next
        string nacha$
        nacha_dev=unt
        open(nacha_dev)nacha$
        
        rem --- Initialize NACHA data
        nacha_recs=0
        route_hash$=""
        batch_hash=0
        total_credits=0
        total_debits=0
        aba_no!=cvs(adcBankAcctCode.aba_no$,2)
        aba_no$=aba_no!.replace("-",""); rem --- remove hyphens
        bnk_acct_no!=cvs(adcBankAcctCode.bnk_acct_no$,2)
        bnk_acct_no$=bnk_acct_no!.replace("-",""); rem --- remove hyphens
        federal_id!=cvs(apsReport.federal_id$,2)
        federal_id$=federal_id!.replace("-",""); rem --- remove hyphens
        emailFax! = BBjAPI().makeVector()

        rem --- NACHA File Header Record (Record Type Code 1)
        nachaFileHdr$=""
        nachaFileHdr$="1"; rem --- Record Type Code
        nachaFileHdr$=nachaFileHdr$+"01";                                           rem --- Priority Code
        nachaFileHdr$=nachaFileHdr$+pad(aba_no$,10,"R"," ");                        rem --- Immediate Destination
        nachaFileHdr$=nachaFileHdr$+pad(federal_id$,10,"R"," ");                    rem --- Immediate Origin
        nachaFileHdr$=nachaFileHdr$+date(0:"%Yz%Mz%Dz%Hz%mz");                      rem --- File Creation Date and Time
        nachaFileHdr$=nachaFileHdr$+"A";                                            rem --- File ID Modifier
        nachaFileHdr$=nachaFileHdr$+"094";                                          rem --- Record Size
        nachaFileHdr$=nachaFileHdr$+"10";                                           rem --- Blocking Factor
        nachaFileHdr$=nachaFileHdr$+"1";                                            rem --- Format Code
        nachaFileHdr$=nachaFileHdr$+pad(adcBankAcctCode.bank_name$,23);             rem --- Immediate Destination Name
        nachaFileHdr$=nachaFileHdr$+pad(apsACH.firm_name$,23);                      rem --- Immediate Origin Name
        checkDate$=pad(apw01a.check_date$,8)
        nachaFileHdr$=nachaFileHdr$+checkDate$;                                     rem --- Reference Code
        write(nacha_dev)nachaFileHdr$+$0D$

        rem --- NACHA Batch Header Record (Record Type Code 5)
        nachaBatchHeader$=""
        nachaBatchHeader$=nachaBatchHeader$+"5";                                    rem --- Record Type Code
        nachaBatchHeader$=nachaBatchHeader$+"220";                                  rem --- Service Class Code
        nachaBatchHeader$=nachaBatchHeader$+pad(apsACH.firm_name$,16);              rem --- Company Name
        nachaBatchHeader$=nachaBatchHeader$+fill(20);                               rem --- Company Discretionary Data
        nachaBatchHeader$=nachaBatchHeader$+pad(federal_id$,10,"R"," ");            rem --- Company Identification
        nachaBatchHeader$=nachaBatchHeader$+"PPD";                                  rem --- Standard Entry Class Code
        nachaBatchHeader$=nachaBatchHeader$+pad("PAYABLE",10);                      rem --- Company Entry Description
        nachaBatchHeader$=nachaBatchHeader$+checkDate$(3,6);                        rem --- Company Descriptive Date
        nachaBatchHeader$=nachaBatchHeader$+checkDate$(3,6);                        rem --- Effective Entry Date
        nachaBatchHeader$=nachaBatchHeader$+fill(3);                                rem --- Settlement Date
        nachaBatchHeader$=nachaBatchHeader$+"1";                                    rem --- Originator Status Code
        nachaBatchHeader$=nachaBatchHeader$+pad(aba_no$,8);                         rem --- Originating Financial Institution
        nachaBatchHeader$=nachaBatchHeader$+str(1,"0000000");                       rem --- Batch Number
        write(nacha_dev)nachaBatchHeader$+$0D$
    endif
    
    rem --- Get info for this vendor
    redim apm01a$
    findrecord(apm01_dev,key=firm_id$+apw01a.vendor_id$,dom=*next)apm01a$

    rem --- Get info for this check
    read(apwChkJasperPrn_dev,key=firm_id$+apw01a.ap_type$+apw01a.check_no$,dom=*next)
    while 1
        redim apwChkJasperPrn$
        thisKey$=key(apwChkJasperPrn_dev,end=*break)
        if pos(firm_id$+apw01a.ap_type$+apw01a.check_no$=thisKey$)<>1 then break
        readrecord(apwChkJasperPrn_dev)apwChkJasperPrn$
        if apwChkJasperPrn.section_type$<>"C" then continue
        break
    wend

    rem --- NACHA Entry Detail Record (Record Type Code 6)
    nachaEntryDetail$=""
    nachaEntryDetail$=nachaEntryDetail$+"6";                                                rem --- Record Type Code
    nachaEntryDetail$=nachaEntryDetail$+"22";                                               rem --- Transaction Code
    vendor_aba_no!=cvs(apm01a.aba_no$,2)
    vendor_aba_no$=vendor_aba_no!.replace("-",""); rem --- remove hyphens
    nachaEntryDetail$=nachaEntryDetail$+pad(vendor_aba_no$,9);                              rem --- Receiving DFI Identification (includes Check Digit as 9th character)
    vendor_bnk_acct_no!=cvs(apm01a.bnk_acct_no$,2)
    vendor_bak_acct_no$=vendor_bnk_acct_no!.replace("-",""); rem --- remove hyphens
    nachaEntryDetail$=nachaEntryDetail$+pad(vendor_bak_acct_no$,17);                        rem --- DFI Account Number
    nachaEntryDetail$=nachaEntryDetail$+str(apwChkJasperPrn.chk_amt*100:"0000000000");      rem --- Amount
    nachaEntryDetail$=nachaEntryDetail$+pad("AP"+apm01a.vendor_id$+apw01a.check_no$,15);    rem --- Individual Identification Number
    nachaEntryDetail$=nachaEntryDetail$+pad(cvs(apm01a.vendor_name$,2),22);                 rem --- Individual Name
    nachaEntryDetail$=nachaEntryDetail$+fill(2);                                            rem --- Discretionary Data
    nachaEntryDetail$=nachaEntryDetail$+"0";                                                rem --- Addenda Record Indicator
    nachaEntryDetail$=nachaEntryDetail$+pad(aba_no$,8)+str(nacha_recs+1:"0000000");         rem --- Trace Number
    write(nacha_dev)nachaEntryDetail$+$0D$

    rem --- NACHA Entry Detail Addenda Record (Record Type Code 7)
    nachaEntryDetailAddenda$=""
    rem --- Record Type Code
    rem --- Addenda Type Code
    rem --- Payment Related Information
    rem --- Addenda Sequence Number
    rem --- Entry Detail Sequence Number

    rem --- Update NACHA data
    nacha_recs=nacha_recs+1
    route_hash$=vendor_bak_acct_no$
    batch_hash=batch_hash+num(route_hash$)
    if apwChkJasperPrn.chk_amt>0 then
        total_credits=total_credits+apwChkJasperPrn.chk_amt
    else
        total_debits=total_debits+apwChkJasperPrn.chk_amt
    endif
    if apsACH.total_required$="Y" then
        if apwChkJasperPrn.chk_amt<=0 then
            total_credits=total_credits+apwChkJasperPrn.chk_amt
        else
            total_debits=total_debits+apwChkJasperPrn.chk_amt
        endif
    endif

    rd_doc_id$=""
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rd_doc_source$="E"
    rd_doc_ext$="PDF"
    rd_doc_path$=cvs(apsACH.ach_check_dir$,2)
    rd_doc_path$=BBjAPI().getFileSystem().resolvePath(rd_doc_path$,err=*next)+"/"
    rd_source_type$="O"
    rem rd_source_alias$=rd_alias_id$
    rd_source_id$="C"
    rd_source_ref$=""
    rd_archive_action$="DOC_ID"
    rep_title$=apw01a.check_no$+"_"+apm01a.vendor_id$+"_ACH_Payment"
    rd_doc_name$=rep_title$+".pdf"
    rd_doc_keywords$=""
    gosub document_whse

    if rpt_ctl$="YES"
        found=reportControl!.getRecipientInfo(rpt_id$,"",apm01a.vendor_id$)
        if found         
            if reportControl!.getEmailYN()="Y" or reportControl!.getFaxYN()="Y"
                emailFax!.add(rpt_id$)
                emailFax!.add(apm01a.vendor_id$)
                emailFax!.add(rd_doc_id$)
            endif
        endif
    endif

    return
    
finish_NACHA_file: rem --- Finish building complete NACHA text file for ACH payments

    if apsACH.total_required$="Y" then
        rem --- Balance (Total) Record
        balanceRecord$=""
        balanceRecord$=balanceRecord$+"6";                                          rem --- Record Type Code
        balanceRecord$=balanceRecord$+"27";                                         rem --- Transaction Code
        balanceRecord$=balanceRecord$+pad(aba_no$,9);                               rem --- Destination ABA Number (includes Check Digit)
        balanceRecord$=balanceRecord$+pad(bnk_acct_no$,17);                         rem --- Bank Account Number
        balanceRecord$=balanceRecord$+str(total_debits*100:"0000000000");           rem --- Total Debit Entry Dollar Amount

        balanceRecord$=balanceRecord$+pad("AP"+federal_id$,15);                     rem --- Company Identification
        balanceRecord$=balanceRecord$+pad(cvs(apsACH.firm_name$,2),22);             rem --- Company Name
        balanceRecord$=balanceRecord$+fill(2);                                      rem --- Bank Discretionary Data
        balanceRecord$=balanceRecord$+"0";                                          rem --- Addenda Record Indicator
        balanceRecord$=balanceRecord$+pad(aba_no$,8);                               rem --- Originating Financial Institution
        balanceRecord$=balanceRecord$+str(nacha_recs+1:"0000000");                  rem --- Trace Number
        write(nacha_dev)balanceRecord$+$0D$

        nacha_recs=nacha_recs+1
    endif

    rem --- NACHA Batch Control Record (Record Type Code 8)
    nachaBatchControl$=""
    nachaBatchControl$=nachaBatchControl$+"8";                                      rem --- Record Type Code
    nachaBatchControl$=nachaBatchControl$+"220";                                    rem --- Service Class Code
    nachaBatchControl$=nachaBatchControl$+str(nacha_recs:"000000");                 rem --- Entry/Addenda Count
    nachaBatchControl$=nachaBatchControl$+pad(str(batch_hash),10,"R","0");          rem --- Entry Hash
    nachaBatchControl$=nachaBatchControl$+str(total_debits*100:"000000000000");     rem --- Total Debit Entry Dollar Amount
    nachaBatchControl$=nachaBatchControl$+str(total_credits*100:"000000000000");    rem --- Total Credit Entry Dollar Amount
    nachaBatchControl$=nachaBatchControl$+pad(federal_id$,10,"R"," ");              rem --- Company Identification
    nachaBatchControl$=nachaBatchControl$+fill(19);                                 rem --- Message Authentication Code
    nachaBatchControl$=nachaBatchControl$+fill(6);                                  rem --- Reserved
    nachaBatchControl$=nachaBatchControl$+pad(aba_no$,8);                           rem --- Originating DFI Identification
    nachaBatchControl$=nachaBatchControl$+str(1:"0000000");                         rem --- Batch Number
    write(nacha_dev)nachaBatchControl$+$0D$

    rem --- NACHA File Control Record (Record Type Code 9)
    entry_count=(nacha_recs+2+2)
    pad_count=10-mod(entry_count,10)
    block_count=(entry_count+pad_count)/10
    nachaFileControl$=""
    nachaFileControl$=nachaFileControl$+"9";                                    rem --- Record Type Code
    nachaFileControl$=nachaFileControl$+str(1:"000000");                        rem --- Batch Count
    nachaFileControl$=nachaFileControl$+str(block_count:"000000");              rem --- Block Count
    nachaFileControl$=nachaFileControl$+str(nacha_recs:"00000000");             rem --- Entry/Addenda Count
    nachaFileControl$=nachaFileControl$+pad(str(batch_hash),10,"R","0");        rem --- Entry Hash
    nachaFileControl$=nachaFileControl$+str(total_debits*100:"000000000000");   rem --- Total Debit Entry Dollar Amount in File
    nachaFileControl$=nachaFileControl$+str(total_credits*100:"000000000000");  rem --- Total Credit Entry Dollar Amount in File
    nachaFileControl$=nachaFileControl$+fill(39);                               rem --- Reserved
    write(nacha_dev)nachaFileControl$+$0D$
    if pad_count>0 then
        for x=1 to pad_count
            write(nacha_dev)fill(94,"9")+$0D$
        next x
    endif

    close(nacha_dev)

    if emailFax!.size()
        msg_id$ = "CONF_RPTCTL"
        gosub disp_message
        if msg_opt$="Y"
            for email_fax=0 to emailFax!.size()-1 step 3
                reportControl!.addToDocQueue(emailFax!.get(email_fax),"",emailFax!.get(email_fax+1),emailFax!.get(email_fax+2))    
            next email_fax
        endif
    endif    
    reportControl!.destroy(err=*next)

    return

 document_whse:rem --- Write record to Barista document warehouse (ads_documents)

    call stbl("+DIR_SYP")+"bac_documents.bbj",
:       rd_doc_id$,
:       rep_date_stamp$,
:       rep_time_stamp$,
:       rd_doc_source$,
:       rd_doc_ext$,
:       rd_doc_path$,
:       rd_source_type$,
:       rd_source_alias$,
:       rd_source_id$,
:       rd_source_ref$,
:       rd_table_chans$[all],
:       rd_archive_action$,
:       rd_doc_name$,
:       rep_title$,
:       rd_doc_keywords$

    return
    
update_open_invoice: rem --- update open invoice

    apt01_key$=firm_id$+ape04a.ap_type$+ape04a.vendor_id$+ape04a.ap_inv_no$
    extract record (apt01_dev,key=apt01_key$,dom=invoice_not_found) apt01a$;rem Advisory Locking
    apt01a.selected_for_pay$="N"
    t[0]=apt01a.invoice_amt
    t[1]=apt01a.discount_amt
    t[2]=apt01a.retention
    d9$=apt01a.ap_dist_code$
    read (apt11_dev,key=apt01_key$,dom=*next)

rem --- Accumulate any adjustments/payments

    while more
        k$=key(apt11_dev,end=*break)
        if pos(k$(1,20)=firm_id$+ape04a.ap_type$+ape04a.vendor_id$+ape04a.ap_inv_no$)<>1 break
        read record (apt11_dev) apt11a$
        lastseq=num(apt11a.sequence_no$)
        t[0]=t[0]+apt11a.trans_amt
        t[1]=t[1]+apt11a.trans_disc
        t[2]=t[2]+apt11a.trans_ret
    wend

rem --- Initialize new detail

    dim apt11a$:fattr(apt11a$)
    apt11a.firm_id$=firm_id$
    apt11a.ap_type$=ape04a.ap_type$
    apt11a.vendor_id$=ape04a.vendor_id$
    apt11a.ap_inv_no$=ape04a.ap_inv_no$
    apt11a.sequence_no$=str(lastseq+1:"000")
    
    extract record (apt11_dev,key=apt11a.firm_id$+apt11a.ap_type$+apt11a.vendor_id$+apt11a.ap_inv_no$+apt11a.sequence_no$,dom=*next)xwk$;rem Advisory Locking

rem --- Write detail

    apt11a.trans_type$="C"
    apt11a.trans_ref$=apw01a.check_no$
    apt11a.trans_date$=apw01a.check_date$
    apt11a.trans_amt=(-ape04a.payment_amt)-ape04a.discount_amt
    apt11a.trans_disc=-ape04a.discount_amt
    apt11a$=field(apt11a$)
    write record (apt11_dev) apt11a$
    
    t[0]=t[0]+apt11a.trans_amt
    t[1]=t[1]+apt11a.trans_disc
    t[2]=t[2]+apt11a.trans_ret
    apt01a.invoice_bal=t[0]
    apt01a$=field(apt01a$)
    write record (apt01_dev) apt01a$
    
invoice_not_found:
    return

update_bank_rec: rem --- Update glt-05

    while more
        dim glt05a$:fattr(glt05a$)
        if x9$="" break
        glt05a.firm_id$=firm_id$
        glt05a.gl_account$=apm10b.gl_cash_acct$(1,max_acct_len)
        glt05a.check_no$=x9$(1,7)
        glt05a.check_type$="A"
        glt05a.paid_code$="V"
        glt05a.bnk_chk_date$=apt05a.check_date$
        glt05a.description$="VOID"
        
        extract record (glt05_dev,key=firm_id$+glt05a.gl_account$+glt05a.check_no$,dom=*next)xwk$;rem Advisory Locking
        glt05a$=field(glt05a$)
        write record (glt05_dev) glt05a$
        if len(x9$)>7 x9$=x9$(8) else x9$=""
    wend

rem --- Detail

    glt05a.firm_id$=firm_id$
    glt05a.gl_account$=apm10b.gl_cash_acct$(1,max_acct_len)
    glt05a.check_no$=apt05a.check_no$
    glt05a.check_type$="A"
    glt05a.paid_code$="O"
    glt05a.bnk_chk_date$=apt05a.check_date$
    dim apm01a$:fattr(apm01a$)
    find record (apm01_dev,key=firm_id$+apt05a.vendor_id$,dom=*next) apm01a$
    let glt05a.description$=apm01a.vendor_name$
    let q$=glt05a.description$
    let glt05a.check_amount=ape04a.payment_amt
    extract record (glt05_dev,key=firm_id$+glt05a.gl_account$+glt05a.check_no$,dom=*next) glt05a$;
:      let glt05a.check_amount=glt05a.check_amount+ape04a.payment_amt,glt05a.description$=q$;rem Advisory Locking
    glt05a$=field(glt05a$)
    write record (glt05_dev) glt05a$

    return


rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Date/time handling functions

    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if cvs(q1$,2)="" return ""
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem #endinclude std_functions.src

rem #include disp_message.src

disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
return

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5)),
:                                   str(err),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    msg_id$="UPDATE_COMPLETE"
    dim msg_tokens$[1]
    msg_opt$=""
    gosub disp_message

std_exit_no_update:

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

    end
