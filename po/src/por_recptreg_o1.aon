rem --- Purchase Order Receipt Register (Stock Receipts Overlay)
rem --- Program por_recptreg_o1 v8.0.0 11Jun2009 (por_db)
rem --- Created by adx_codeport.bbj v1.1.5 (06/11/2009 12:21:34)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    seterr std_error
    setesc std_error

rem --- Document initializations

    OutVect!=bbjAPI().makeVector()
    HdrVect!=bbjAPI().makeVector()
    BrkVect!=bbjAPI().makeVector()
    TotVect!=bbjAPI().makeVector()

    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)
	
rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)
    firm_id$=sysinfo.firm_id$

rem --- set rd_alias_id$
    
    rd_alias_id$=pgm(-2)
    rd_alias_id$=rd_alias_id$(max(pos("/"="/"+rd_alias_id$,-1),pos("\"="\"+rd_alias_id$,-1)))
    rd_alias_id$=rd_alias_id$(1,pos("."=rd_alias_id$+".")-1)

rem --- Report Initializations

    call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","A","",m1$,0,0
    call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","U","",m2$,0,0
    call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","C","",m3$,0,0

    call stbl("+DIR_PGM")+"adc_getmask.aon","","AP","I","",m0$,0,vendlen
    call stbl("+DIR_PGM")+"adc_getmask.aon","CUSTOMER_ID","","","",customask$,0,custlen

    dim receipt[1],whse[1],report[1],totals[1]
    let m1=len(m1$),m2=len(m2$),m3=len(m3$),done=0
    let number=0,maxm1=15,maxm2=12,maxm3=12,l=l9+1,subhead$="",t0$="",t1$=""
 
    columns = 12
    if col_format$="Y" columns=16
    dim columns$[columns,10]
    columns$[0,0]=Translate!.getTranslation("AON_ITEM"), columns$[0,1]="C",columns$[0,2]="12"
    columns$[1,0]=Translate!.getTranslation("AON_DIST_CODE"), columns$[1,1]="C",columns$[1,2]="8"
    columns$[2,0]="UM", columns$[2,1]="C",columns$[2,2]="3"
    columns$[3,0]=Translate!.getTranslation("AON_QUANTITY^RECEIVED"), columns$[3,1]="C", columns$[3,2]=str(len(m2$)), columns$[3,5]="R"
    columns$[4,0]=Translate!.getTranslation("AON_AVG")+"^"+Translate!.getTranslation("AON_COST"), columns$[4,1]="C", columns$[4,2]=str(len(m3$)), columns$[4,5]="R"
    columns$[5,0]=Translate!.getTranslation("AON_AVG")+"^"+Translate!.getTranslation("AON_EXTENSION"), columns$[5,1]="C", columns$[5,2]=str(len(m1$)), columns$[5,5]="R", columns$[5,10]="T"
    columns$[6,0]=Translate!.getTranslation("AON_PO_COST"), columns$[6,1]="C", columns$[6,2]=str(len(m3$)), columns$[6,5]="R"
    columns$[7,0]=Translate!.getTranslation("AON_PO_COST^EXTENSION"), columns$[7,1]="C", columns$[7,2]=str(len(m1$)), columns$[7,5]="R", columns$[7,10]="T"
    columns$[8,0]=Translate!.getTranslation("AON_LOCATION"), columns$[8,1]="C", columns$[8,2]="10"
    columns$[9,0]=Translate!.getTranslation("AON_VENDOR"), columns$[9,1]="C", columns$[9,2]="8"
    columns$[10,0]=Translate!.getTranslation("AON_RCVR#_"), columns$[10,1]="C", columns$[10,2]="8"
    columns$[11,0]=Translate!.getTranslation("AON_PO_NUMBER"), columns$[11,1]="C",columns$[11,2]="8"
    columns$[12,0]=Translate!.getTranslation("AON_COMP?"), columns$[12,1]="C", columns$[12,2]="10"

rem --- Initializations

    precision i[1]
    dim x0$(32)
    HdrVect!.addItem(Translate!.getTranslation("AON_STOCK_INVENTORY_RECEIPTS"))
    BrkVect!.addItem(str(OutVect!.size()))

    avail=width-70
    x=2
    m=max(m0,6)
    nonstock=0

rem --- Background

    call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status

rem --- Position files

    read (pow04_dev,key=firm_id$,dom=*next)

rem --- Get next sort record
    while 1
        read record (pow04_dev,end=*break)pow04a$
        if pos(firm_id$=pow04a$)<>1 break

rem --- Level breaks?

        if t0$<>pow04a.warehouse_id$ then warehouse_id$=pow04a.warehouse_id$,recpt_date$=pow04a.recpt_date$; gosub l6000
        if t1$<>pow04a.recpt_date$ then recpt_date$=pow04a.recpt_date$; gosub l6200
        if subhead$<>"" gosub l5400

rem --- Read receipt header and detail

        dim poe04a$:fattr(poe04a$)
        dim poe14a$:fattr(poe14a$)

        poe04a.firm_id$=firm_id$
        poe04a.receiver_no$=pow04a.receiver_no$

        poe14a.firm_id$=firm_id$
        poe14a.receiver_no$=pow04a.receiver_no$
        poe14a.internal_seq_no$=pow04a.po_int_seq_ref$

        gosub l6400
        if status continue

rem --- Calculate extension

        precision 2
        po_extension=poe14a.unit_cost*poe14a.qty_received
        iv_extension=ivm02a.unit_cost*poe14a.qty_received*poe14a.conv_factor
        precision i[1]

rem --- Print detail line

        OutVect!.addItem(poe14a.item_id$)
        OutVect!.addItem(code$)
        OutVect!.addItem(poe14a.unit_measure$)
        OutVect!.addItem(str(poe14a.qty_received:m2$)+$00$+"R")
        OutVect!.addItem(str(ivm02a.unit_cost*poe14a.conv_factor:m3$)+$00$+"R")
        OutVect!.addItem(str(iv_extension:m1$)+$00$+"R")
        OutVect!.addItem(str(poe14a.unit_cost:m3$)+$00$+"R")
        OutVect!.addItem(str(po_extension:m1$)+$00$+"R")
        OutVect!.addItem(poe14a.location$)
        OutVect!.addItem(fnmask$(poe04a.vendor_id$(1,vendlen),m0$))
        OutVect!.addItem(poe04a.receiver_no$)
        OutVect!.addItem(poe04a.po_no$)
        OutVect!.addItem(complete$)

        OutVect!.addItem("   "+description$)
        xvf=fnblank(12)
        l=l+2

rem --- Accumulate totals

        receipt[0]=receipt[0]+iv_extension
        receipt[1]=receipt[1]+po_extension
        whse[0]=whse[0]+iv_extension
        whse[1]=whse[1]+po_extension
        report[0]=report[0]+iv_extension
        report[1]=report[1]+po_extension
        number=number+1

    wend

rem --- Done with stock receipts

    done=1
    if number=0 goto l2500
    gosub l6000
    totals$=Translate!.getTranslation("AON_TOTAL_FOR_REGISTER")
    totals[all]=report[all]
    gosub l6900

l2500: rem --- Non-stock receipt initializations

    avail=width-91
    x=1
    gosub l6700
    nonstock=1
    HdrVect!.addItem(Translate!.getTranslation("AON_NON-STOCK_INVENTORY_AND_OTHER_RECEIPTS"))
    BrkVect!.addItem(str(OutVect!.size()))

rem --- Position files

    read (pow05_dev,key=firm_id$,dom=*next)

rem --- Get next sort record
    while 1
        read record(pow05_dev,end=*break)pow05a$
        if pos(firm_id$=pow05a$)<>1 break

rem --- Level breaks?

        if t0$<>pow05a.warehouse_id$ then warehouse_id$=pow05a.warehouse_id$,recpt_date$=pow05a.recpt_date$; gosub l6000
        if t1$<>pow05a.recpt_date$ then recpt_date$=pow05a.recpt_date$; gosub l6200
        if subhead$<>"" gosub l5400

rem --- Read receipt header, detail and line code

        dim poe04a$:fattr(poe04a$)
        dim poe14a$:fattr(poe14a$)

        poe04a.firm_id$=firm_id$
        poe04a.receiver_no$=pow05a.receiver_no$

        poe14a.firm_id$=firm_id$
        poe14a.receiver_no$=pow05a.receiver_no$
        poe14a.internal_seq_no$=pow05a.po_int_seq_ref$

        gosub l6400
        if status continue

rem --- Set remaining fields

        wo$=""
        if cvs(poe14a.wo_no$,3)="" goto l3400
        wo$=poe14a.wo_no$
        if cvs(poe14a.wo_seq_ref$,3)<>"" wo$=wo$+"-"+poe14a.wo_seq_ref$

l3400: rem --- Calculate extensions

        qty=poe14a.qty_received
        if pom02a.line_type$="O" qty=1
        precision 2
        po_extension=poe14a.unit_cost*qty
        precision i[1]

rem --- Print non-stock detail line

        if pom02a.line_type$="N"
            OutVect!.addItem(poe14a.item_id$)
            OutVect!.addItem("")
            OutVect!.addItem(poe14a.unit_measure$)
            OutVect!.addItem(str(poe14a.qty_received:m2$)+$00$+"R")
            xvk=fnblank(2)
            OutVect!.addItem(str(poe14a.unit_cost:m3$)+$00$+"R")
            OutVect!.addItem(str(po_extension:m1$)+$00$+"R")
            OutVect!.addItem(poe14a.location$)
            OutVect!.addItem(fnmask$(poe04a.vendor_id$(1,vendlen),m0$))
            OutVect!.addItem(poe04a.receiver_no$)
            OutVect!.addItem(poe14a.po_no$)
            OutVect!.addItem(complete$+" "+wo$)
            goto l3700
        endif

rem --- Print other charge detail line

        xvk=fnblank(6)
        OutVect!.addItem(str(poe14a.unit_cost:m3$)+$00$+"R")
        OutVect!.addItem(str(po_extension:m1$)+$00$+"R")
        OutVect!.addItem(poe14a.location$)
        OutVect!.addItem(fnmask$(poe04a.vendor_id$(1,vendlen),m0$))
        OutVect!.addItem(poe04a.receiver_no$)
        OutVect!.addItem(poe14a.po_no$
        OutVect!.addItem(complete$)

l3700: rem --- Print non-stock/other charge description

        OutVect!.addItem(poe14a.order_memo$)
        xvk=fnblank(12)
        l=l+2

rem --- Accumulate totals

        receipt[1]=receipt[1]+po_extension
        number=number+1
        whse[1]=whse[1]+po_extension
        report[1]=report[1]+po_extension

    wend

rem --- All done

    done=1
    if number>0
        gosub l6000
        totals$=Translate!.getTranslation("AON_TOTAL_FOR_REGISTER")
        totals[all]=report[all]
        gosub l6900
    endif

rem --- Run next overlay

    next_overlay$="por_recptreg_o2.aon"
    goto std_exit

l5400: rem --- Subheading

    xvf=fnblank(13)

    OutVect!.addItem(subhead$)
    xvf=fnblank(12)
    
    xvf=fnblank(13)

    l=l+3
    subhead$=""
    return

l6000: rem --- Warehouse break

    if t0$<>""
        gosub l6200
        totals$=Translate!.getTranslation("AON_TOTAL_FOR_WAREHOUSE_")+cvs(whse$,2)
        totals[all]=whse[all]
        gosub l6900
    endif

    if done=0
        dim whse[1]
        t0$=warehouse_id$
        find record (ivm10_dev,key=firm_id$+"C"+t0$,dom=*next) ivm10c$
        whse$=ivm10c.warehouse_id$+" "+ivm10c.short_name$
        if t1$="" gosub l6200
        subhead$=Translate!.getTranslation("AON_WAREHOUSE_")+cvs(whse$,2)+Translate!.getTranslation("AON__-_RECEIPT_DATE_")+fndate$(t1$)
    endif

    return

l6200: rem --- Receipt date break

    if t1$="" goto l6300
    totals$=Translate!.getTranslation("AON_TOTAL_FOR_")+fndate$(t1$)
    totals[all]=receipt[all]
    gosub l6900
l6300:
    if done=0
        dim receipt[1]
        t1$=recpt_date$
        subhead$=Translate!.getTranslation("AON_WAREHOUSE_")+cvs(whse$,2)+Translate!.getTranslation("AON__-_RECEIPT_DATE_")+fndate$(t1$)
    endif

    return

l6400: rem --- Read receipt header and detail

    status=11
    find record (poe04_dev,key=poe04a.firm_id$+poe04a.receiver_no$,knum="PRIMARY",dom=l6690) poe04a$
    find record (poe14_dev,key=poe14a.firm_id$+poe14a.receiver_no$+poe14a.internal_seq_no$,knum="PRIMARY",dom=l6690) poe14a$
    if pom02a.firm_id$+pom02a.po_line_code$<>firm_id$+poe14a.po_line_code$
        dim pom02a$:fattr(pom02a$)
        pom02a.firm_id$=firm_id$
        pom02a.po_line_code$=poe14a.po_line_code$
        find record (pom02_dev,key=pom02a.firm_id$+pom02a.po_line_code$,dom=l6690) pom02a$
    endif

rem --- Retrieve inventory item and warehouse

    dim ivm01a$:fattr(ivm01a$)
    dim ivm02a$:fattr(ivm02a$)

    ivm01a.firm_id$=firm_id$
    ivm01a.item_id$=poe14a.item_id$
    description$(1)=poe14a.order_memo$
    ivm02a.firm_id$=firm_id$
    ivm02a.warehouse_id$=poe14a.warehouse_id$
    ivm02a.item_id$=poe14a.item_id$
    code$=""
    if pom02a.line_type$="S"
        find record (ivm01_dev,key=ivm01a.firm_id$+ivm01a.item_id$,dom=l6690) ivm01a$
        find record (ivm02_dev,key=ivm02a.firm_id$+ivm02a.warehouse_id$+ivm02a.item_id$,dom=l6690) ivm02a$
        description$(1)=fnitem$(ivm01a.item_desc$,i[3],i[4],i[5])
        code$=ivm02a.ar_dist_code$
    endif

rem --- Format detail line fields

    x=poe14a.qty_ordered-poe14a.qty_prev_rec-poe14a.qty_received
    complete$=Translate!.getTranslation("AON_NO")
    status=0
    if poe04a.rec_complete$="Y" complete$=Translate!.getTranslation("AON_YES")
    if x<=0 complete$=Translate!.getTranslation("AON_YES")
l6690:
    return

l6700: rem --- Report Initializations

    call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","A","",m1$,0,0
    call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","U","",m2$,0,0
    call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","C","",m3$,0,0

    call stbl("+DIR_PGM")+"adc_getmask.aon","","AP","I","",m0$,0,vendlen
    call stbl("+DIR_PGM")+"adc_getmask.aon","CUSTOMER_ID","","","",customask$,0,custlen

    dim receipt[1],whse[1],report[1],totals[1]
    let m1=len(m1$),m2=len(m2$),m3=len(m3$),done=0
    let number=0,maxm1=15,maxm2=12,maxm3=12,l=l9+1,subhead$="",t0$="",t1$=""
rem --- Print positions
l6800:
    if m+(m1*x)+m2+(m3*x)<avail goto l6890
    let maxm1=maxm1-1,maxm2=maxm2-1,maxm3=maxm3-1
    call pgmdir$+"adc_sizemask.aon",m1$,m1,11,maxm1
    call pgmdir$+"adc_sizemask.aon",m2$,m2,9,maxm2
    call pgmdir$+"adc_sizemask.aon",m3$,m3,6,maxm3
    goto l6800
l6890:
    return
rem --- Print totals
l6900:

    if nonstock goto l6950
    OutVect!.addItem(totals$)
    xvf=fnblank(4)
    OutVect!.addItem(str(totals[0]:m1$)+$00$+"R")
    xvf=fnblank(1)
    OutVect!.addItem(str(totals[0]:m1$)+$00$+"R")
    xvf=fnblank(5)
    goto l6980
l6950:
    OutVect!.addItem(totals$)
    xvf=fnblank(6)
    OutVect!.addItem(str(totals[0]:m1$)+$00$+"R")
    xvf=fnblank(5)

l6980:

    TotVect!.addItem(str(OutVect!.size()/(columns+1)))
    let l=l+1
    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fnisn$(q$)=q$(7)

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

    def fnitem$(q$,q1,q2,q3)=cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)

rem --- Format zip/postal code

    def fnzipcode$(q$)
        q$=cvs(q$,4)
        if len(cvs(q$,2))<>9 and len(cvs(q$,2))<>6 return q$
        if len(cvs(q$,2))=9
            q1$="[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]"
            q1=mask(cvs(q$,2),q1$)
            if q1=0 return q$
            if q1<>0
                if q$(6,4)="0000" return q$(1,5)
            return q$(1,5)+"-"+q$(6,4)
            endif
        endif
        if len(cvs(q$,2))=6
            q1$="[a-zA-Z][0-9][a-zA-Z][0-9][a-zA-Z][0-9]"
            q1=mask(cvs(q$,2),q1$)
            if q1=0 return q$ else return q$(1,3)+" "+q$(4,3)
        endif
    fnend

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend
    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem #endinclude std_functions.src

rem --- fnBlank : Create blank cells in OutVect!. No return value
   
    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    call stbl("+DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5)),
:                                str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5)),
:                                   str(err),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    out_action$="FORMAT"
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

    end
