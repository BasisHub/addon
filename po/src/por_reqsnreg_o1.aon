rem --- Purchase Requisition Register (Report Overlay)
rem --- Program por_reqsnreg_o1 v8.0.0 16Jul2007 (por_ab)
rem --- Created by adx_codeport.bbx v1.1.5 (07/16/2007 11:10:03)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2007 AddonSoftware
rem --- All Rights Reserved

    seterr std_error
    setesc std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$

rem --- Document initializations
   
    OutVect!=bbjAPI().getSysGui().makeVector()
    HdrVect!=bbjAPI().getSysGui().makeVector()
    BrkVect!=bbjAPI().getSysGui().makeVector()
    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)

rem --- Initializations

    dim headings$[5],o[14],vendor[1],req[1],report[1],totals[1]
    dim c0$(22),c1$(60),d0$(24),d1$(64),d[20],description$(40),r0$(8),r1$(30)
    dim w0$(18),w1$(48),w2$(32),w3$(22),w4$(40),w[12],y0$(4),y1$(32)
    dim h1$(132),h2$(132),h3$(132)
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$
    when$=sysinfo.system_date$
    clock$=""
    number=0
    headings=5
    width=132
    page=0
    l9=59
    l=l9+1
    done=0
    cost$="Avg"
    if sequence$="A" headings$[1]=sysinfo.task_desc$+" By Vendor Alternate Sequence"
    if sequence$="V" headings$[1]=sysinfo.task_desc$+" By Vendor"
    if sequence$="R" headings$[1]=sysinfo.task_desc$+" By Requisition Number"
    if ivs_params.cost_method$="S" cost$="Std"
    if ivs_params.cost_method$="R" cost$="Rep"
    desc_len_01=num(ivs_params.desc_len_01$)
    desc_len_02=num(ivs_params.desc_len_02$)
    desc_len_03=num(ivs_params.desc_len_03$)
    more=1
    recs=0

rem --- Document columns

    dim columns$[13,10]
    columns = 13
    rem --- per VARs, drop the sequence number; just rem'ing here (OutVect! code left alone) does the trick
    rem --- columns$[0,0]="Seq",                columns$[0,1]="C", columns$[0,2]="5"
    columns$[1,0]="Cd",                 columns$[1,1]="C", columns$[1,2]="3"
    columns$[2,0]="Wh",                 columns$[2,1]="C", columns$[2,2]="3"    
    columns$[3,0]="Item",               columns$[3,1]="C", columns$[3,2]="10"
    columns$[4,0]="Not^Before",         columns$[4,1]="C", columns$[4,2]="10"
    columns$[5,0]="Date^Required",      columns$[5,1]="C", columns$[5,2]="10"
    columns$[6,0]="Date^Promised",      columns$[6,1]="C", columns$[6,2]="10"
    columns$[7,0]="UM",                 columns$[7,1]="C", columns$[7,2]="5"
    columns$[8,0]="Quantity",           columns$[8,1]="N", columns$[8,2]="10"
    columns$[9,0]="Std Cost",           columns$[9,1]="N", columns$[9,2]="10"
    columns$[10,0]="Std Cost^Extension", columns$[10,1]="N", columns$[10,2]="10"
    columns$[11,0]="PO Cost",            columns$[11,1]="N", columns$[11,2]="10"
    columns$[12,0]="PO Cost^Extension", columns$[12,1]="N",columns$[12,2]="10"
    columns$[13,0]="Source",            columns$[13,1]="N",columns$[13,2]="10"

rem --- Print positions

    o[0]=0
    o[1]=4
    o[2]=7
    o[3]=10
    o[4]=o[3]+itemlen+1
    o[5]=o[4]+9
    o[6]=o[5]+9
    o[7]=o[6]+9
    o[13]=width-6
    o[12]=o[13]-m1
    o[14]=o[3]+41
    o[11]=o[12]-m3
    o[10]=o[11]-m1
    o[9]=o[10]-m3
    o[8]=o[9]-m2

rem --- Report headings

    x1$="First"
    x2$="Last"
    if begvend$="" goto no_beg_vendor
    v$=begvend$
    gosub ret_vendor
    x1$=vendor$

no_beg_vendor:

    if endvend$="" goto no_end_vendor
    v$=endvend$
    gosub ret_vendor
    x2$=vendor$

no_end_vendor:

    h1$(1)="From Vendor: "+x1$+ " to "+x2$
    x1$="First"
    x2$="Last"
    if begreq$<>"" x1$=begreq$
    if endreq$<>"" x2$=endreq$
    h2$(1)="From Requisition: "+x1$+" to "+x2$
    x1$="First"
    x2$="Last"
    if begdate$<>"" x1$=fndate$(begdate$)
    if enddate$<>"" x2$=fndate$(enddate$)
    h3$(1)="From "+datetitle$+": "+x1$+" to "+x2$
    headings$[2]=h1$
    headings$[3]=h2$
    headings$[4]=h3$
    if pagebreak$="Y" then headings$[headings]="[HEADER]"

rem --- Position files

    call pgmdir$ + "adc_progress.aon","N","","","","",0,pow_reqregalt_dev,1,meter_num,status

    if sequence$="A" then read (pow_reqregalt_dev,key=firm_id$,dom=*next)
    if sequence$="V" then read (pow_reqregven_dev,key=firm_id$,dom=*next)
    if sequence$="R" then read (pow_reqregreq_dev,key=firm_id$,dom=*next)

    while more; rem --- Begin loop for header file

            if sequence$="A"
                pow_reqregalt$=key(pow_reqregalt_dev,end=*break)
                if pos(firm_id$=pow_reqregalt$)<>1 break
                read (pow_reqregalt_dev)
                poe_reqhdr.firm_id$=pow_reqregalt.firm_id$
                poe_reqhdr.vendor_id$=pow_reqregalt.vendor_id$
                poe_reqhdr.req_no$=pow_reqregalt.req_no$
                if t0$<>pow_reqregalt.alt_sequence$+pow_reqregalt.vendor_id$ then gosub vend_break
                if t1$<>pow_reqregalt.req_no$ then gosub req_break
            endif
            if sequence$="V"
                pow_reqregven$=key(pow_reqregven_dev,end=*break)
                if pos(firm_id$=pow_reqregven$)<>1 break
                read (pow_reqregven_dev)
                poe_reqhdr.firm_id$=pow_reqregven.firm_id$
                poe_reqhdr.vendor_id$=pow_reqregven.vendor_id$
                poe_reqhdr.req_no$=pow_reqregven.req_no$
                if t0$<>pow_reqregven.vendor_id$ then gosub vend_break
                if t1$<>pow_reqregven.req_no$ then gosub req_break
            endif
            if sequence$="R"
                pow_reqregreq$=key(pow_reqregreq_dev,end=*break)
                if pos(firm_id$=pow_reqregreq$)<>1 break
                read (pow_reqregreq_dev)
                poe_reqhdr.firm_id$=pow_reqregreq.firm_id$
                poe_reqhdr.vendor_id$=pow_reqregreq.vendor_id$
                poe_reqhdr.req_no$=pow_reqregreq.req_no$
                if t1$<>pow_reqregreq.req_no$
                	gosub req_break
                	v$=pow_reqregreq.vendor_id$
                	gosub ret_vendor
                endif
            endif

        call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status
        
        if sequence$="A"
            apm_vendmast.firm_id$=pow_reqregalt.firm_id$
            apm_vendmast.vendor_id$=pow_reqregalt.vendor_id$
        endif
        if sequence$="V"
            apm_vendmast.firm_id$=pow_reqregven.firm_id$
            apm_vendmast.vendor_id$=pow_reqregven.vendor_id$
        endif
        if sequence$="R"
            apm_vendmast.firm_id$=pow_reqregreq.firm_id$
            apm_vendmast.vendor_id$=pow_reqregreq.vendor_id$
        endif
        
        find record (poe_reqhdr_dev,key=poe_reqhdr.firm_id$+poe_reqhdr.vendor_id$+poe_reqhdr.req_no$,knum=1,dom=*continue) poe_reqhdr$
        find record (apm_vendmast_dev,key=apm_vendmast.firm_id$+apm_vendmast.vendor_id$,dom=*next) apm_vendmast$
        gosub req_subheading

rem --- Position detail file

        read (poe_reqdet_dev,key=poe_reqhdr.firm_id$+poe_reqhdr.req_no$,knum="DISPLAY_SEQ",dom=*next); rem requisition detail

        while more; rem --- Begin loop for detail file

            poe_reqdet_key$=key(poe_reqdet_dev,end=*break)
            if pos(poe_reqhdr.firm_id$+poe_reqhdr.req_no$=poe_reqdet_key$)<>1 break

rem --- Read record from detail file

            read record (poe_reqdet_dev) poe_reqdet$
            if poc_linecode.firm_id$<>firm_id$ or poc_linecode.po_line_code$<>poe_reqdet.po_line_code$
                poc_linecode.firm_id$=firm_id$
                poc_linecode.po_line_code$=poe_reqdet.po_line_code$
                find record (poc_linecode_dev,key=poc_linecode.firm_id$+poc_linecode.po_line_code$,dom=other_line) poc_linecode$
            endif

rem --- Initialize detail line fields

            needed=2
            source$="Entry"
            wo$=""
            before$=cvs(poe_reqhdr.not_b4_date$,2)
            promised$=cvs(poe_reqhdr.promise_date$,2)
            if poc_linecode.dropship$="Y" then needed=3
            if poe_reqdet.source_code$="R" then source$="Replen"
            if poe_reqdet.source_code$="W" then source$="WO"
            if poe_reqdet.source_code$="M" then source$="MRP"
            if cvs(poe_reqdet.promise_date$,2)<>"" then promised$=poe_reqdet.promise_date$
            if cvs(poe_reqdet.not_b4_date$,2)<>"" then before$=poe_reqdet.not_b4_date$
            if cvs(before$,2)<>"" then before$=fndate$(before$)
            if cvs(promised$,2)<>"" then promised$=fndate$(promised$)

rem --- Work order?

            if sf$<>"N" 
                if cvs(poe_reqdet.wo_no$+poe_reqdet.sequence_no$,2)<>""
                    wo$=" WO: "+poe_reqdet.wo_no$
                    if cvs(poe_reqdet.sequence_no$,2)<>"" then wo$=wo$+"-"+poe_reqdet.sequence_no$
                endif
            endif

rem --- Retrieve inventory item for S lines
            ivm_itemmast.firm_id$=firm_id$
            ivm_itemmast.item_id$=poe_reqdet.item_id$
            ivm_itemwhse.firm_id$=firm_id$
            ivm_itemwhse.warehouse_id$=poe_reqdet.warehouse_id$
            ivm_itemwhse.item_id$=poe_reqdet.item_id$
            ivm_itemwhse.unit_cost=0
            if poc_linecode.line_type$="S"
                find record (ivm_itemmast_dev,key=ivm_itemmast.firm_id$+ivm_itemmast.item_id$,dom=*next) ivm_itemmast$
                find record (ivm_itemwhse_dev,key=ivm_itemwhse.firm_id$+ivm_itemwhse.warehouse_id$+ivm_itemwhse.item_id$,dom=*continue) ivm_itemwhse$
                description$=fnitem$(ivm_itemmast.item_desc$,desc_len_01,desc_len_02,desc_len_03)
            endif

rem --- Calculate extensions and branch based on line type
            qty = poe_reqdet.req_qty
            iv_extension=0
            if poc_linecode.line_type$="O" then qty=1
            po_extension=round(poe_reqdet.unit_cost*qty,2)
            if poc_linecode.line_type$="S" then iv_extension=round(ivm_itemwhse.unit_cost* qty*poe_reqdet.conv_factor,2)

            action=pos(poc_linecode.line_type$="SNVMO")
            std_line=1
            non_stock_line=2
            vend_line=3
            msg_line=4
            other_type_line=5
            flag=0

            switch action
                case std_line

                    OutVect!.addItem(poe_reqdet.po_line_no$)
                    OutVect!.addItem(poe_reqdet.po_line_code$)
                    OutVect!.addItem(poe_reqdet.warehouse_id$)
                    OutVect!.addItem(poe_reqdet.item_id$)
                    OutVect!.addItem(before$)
                    OutVect!.addItem(fndate$(poe_reqdet.reqd_date$))
                    OutVect!.addItem(promised$)
                    OutVect!.addItem(poe_reqdet.unit_measure$)
                    OutVect!.addItem(str(poe_reqdet.req_qty:m2$)+$00$+"R")
                    OutVect!.addItem(str(ivm_itemwhse.unit_cost:m3$)+$00$+"R")
                    OutVect!.addItem(str(iv_extension:m1$)+$00$+"R")
                    OutVect!.addItem(str(poe_reqdet.unit_cost:m3$)+$00$+"R")
                    OutVect!.addItem(str(po_extension:m1$)+$00$+"R")
                    OutVect!.addItem(source$)
                    xwk=fnblank(2)
                    OutVect!.addItem(description$)
                    xwk=fnblank(5)
                    OutVect!.addItem(wo$)
                    xwk=fnblank(5)
                    l=l+needed
                    recs=recs+1
                    break

               case non_stock_line
                                
                    OutVect!.addItem(poe_reqdet.po_line_no$)
                    OutVect!.addItem(poe_reqdet.po_line_code$)
                    OutVect!.addItem(poe_reqdet.warehouse_id$)
                    OutVect!.addItem(poe_reqdet.item_id$)
                    OutVect!.addItem(before$)
                    OutVect!.addItem(fndate$(poe_reqdet.reqd_date$))
                    OutVect!.addItem(promised$)
                    OutVect!.addItem(poe_reqdet.unit_measure$)
                    OutVect!.addItem(str(poe_reqdet.req_qty:m2$)+$00$+"R")
                    OutVect!.addItem(str(ivm_itemwhse.unit_cost:m3$)+$00$+"R")
                    OutVect!.addItem(str(iv_extension:m1$)+$00$+"R")
                    OutVect!.addItem(str(poe_reqdet.unit_cost:m3$)+$00$+"R")
                    OutVect!.addItem(str(po_extension:m1$)+$00$+"R")
                    OutVect!.addItem(source$)
                    xwk=fnblank(2)
                    OutVect!.addItem(poe_reqdet.order_memo$)
                    xwk=fnblank(5)
                    OutVect!.addItem(wo$)
                    xwk=fnblank(5)
                    l=l+needed
                    recs=recs+1
                    break

            case vend_line

                    OutVect!.addItem(poe_reqdet.po_line_no$)
                    OutVect!.addItem(poe_reqdet.po_line_code$)
                    OutVect!.addItem(poe_reqdet.warehouse_id$)
                    OutVect!.addItem(poe_reqdet.item_id$)
                    OutVect!.addItem(before$)
                    OutVect!.addItem(fndate$(poe_reqdet.reqd_date$))
                    OutVect!.addItem(promised$)
                    xwk=fnblank(6)
                    OutVect!.addItem(source$)
                    xwk=fnblank(8)
                    OutVect!.addItem(wo$)
                    xwk=fnblank(5)
                    l=l+needed
                    recs=recs+1
                    break

            case msg_line

                    OutVect!.addItem(poe_reqdet.po_line_no$)
                    OutVect!.addItem(poe_reqdet.po_line_code$)
                    OutVect!.addItem(poe_reqdet.warehouse_id$)
                    xwk=fnblank(1)
                    OutVect!.addItem(before$)
                    OutVect!.addItem(fndate$(poe_reqdet.reqd_date$))
                    OutVect!.addItem(promised$)
                    xwk=fnblank(6)
                    OutVect!.addItem(source$)
                    xwk=fnblank(2)
                    OutVect!.addItem(poe_reqdet.order_memo$)
                    xwk=fnblank(5)
                    OutVect!.addItem(wo$)
                    xwk=fnblank(5)
                    l=l+needed
                    recs=recs+1
                    break

            case other_type_line

                    gosub other_line
                    break

            case default
                    flag=1
                    break
            swend
            if flag then continue

rem --- Accumulate totals

            req[0]=req[0]+iv_extension
            req[1]=req[1]+po_extension
            number=number+1
            vendor[0]=vendor[0]+iv_extension
            vendor[1]=vendor[1]+po_extension
            report[0]=report[0]+iv_extension
            report[1]=report[1]+po_extension

        wend;   rem --- Loop back for next detail

    wend;       rem --- Loop back for next header

done: rem --- All done

    if recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    done=1
    action=pos(sequence$="AVR")
    alt_seq=1
    vend_seq=2
    req_seq=3

    switch action
        case alt_seq
        case vend_seq
            gosub vend_break
            break
        case req_seq
            gosub req_break
            break
    swend

    totals$="Total for Register"
    totals[0]=report[0]
    totals[1]=report[1]
    gosub print_totals
    if number=0 then goto std_exit
    
	next_overlay$="pou_reqsnreg.aon"
    out_action$="FORMAT"
	goto std_exit

req_subheading: rem --- Requisition subheading

     OutVect!.addItem(vendor$) 
     xwk = fnblank(4)
     OutVect!.addItem("Requisition #: "+poe_reqhdr.req_no$)
     xwk = fnblank(1)
     OutVect!.addItem("Date: "+fndate$(poe_reqhdr.ord_date$))
	 xwk=fnblank(6)
	 
rem --- Dropship?

	if poe_reqhdr.dropship$="Y"
		customer$=""
		arm_custmast.firm_id$=firm_id$
		arm_custmast.customer_id$=poe_reqhdr.customer_id$
		find record (arm_custmast_dev,key=arm_custmast.firm_id$+arm_custmast.customer_id$,dom=*next) arm_custmast$
		customer$="(Dropship to customer "+fnmask$(arm_custmast.customer_id$,customask$)+" "+cvs(arm_custmast.customer_name$,3)
		if cvs(poe_reqhdr.order_no$,2)<>"" then customer$=customer$+" , order " + poe_reqhdr.order_no$
		customer$=customer$+")"
		OutVect!.addItem(customer$)
		xwk=fnblank(13)
	endif
	  
	xwk = fnblank(14)
    l=l+3
    return

vend_break: rem --- Vendor break

    if t0$<>""
        gosub req_break
        totals$="Total for "+cvs(vendor$,2)
        totals[0]=vendor[0]
        totals[1]=vendor[1]
        gosub print_totals
    endif
    if done return
    dim vendor[1]
    t0$=poe_reqhdr.vendor_id$
    v$=t0$
    if sequence$="A" then t0$=pow_reqregalt.alt_sequence$+pow_reqregalt.vendor_id$
    gosub ret_vendor
    if pagebreak$="Y" 
    	BrkVect!.addItem(str(OutVect!.size()/(columns+1)))
    	HdrVect!.addItem("Vendor: "+vendor$)
    endif
    return

req_break: rem --- Requisition break

    if t1$<>""
        let totals$="Total for Requisition " + t1$
        let totals[0]=req[0],totals[1]=req[1]
        gosub print_totals
    endif

    if done then return
    dim req[1]
    let t1$=poe_reqhdr.req_no$
    if sequence$="R" and pagebreak$="Y" 
    	BrkVect!.addItem(str(OutVect!.size()/(columns+1)))
    	HdrVect!.addItem("Requisition: "+t1$)    
    endif
    return

ret_vendor: rem --- Retrieve vendor

    apm_vendmast.firm_id$=firm_id$
    apm_vendmast.vendor_id$=v$
    find record (apm_vendmast_dev,key=apm_vendmast.firm_id$+apm_vendmast.vendor_id$,dom=*next) apm_vendmast$
rem    vendor$=fnmask$(apm_vendmast.vendor_id$,m0$)+" "+apm_vendmast.vendor_name$
    vendor$=apm_vendmast.vendor_id$+" "+apm_vendmast.vendor_name$
    return

print_totals:   rem --- Print totals

    xwk=fnblank(5)
    OutVect!.addItem(totals$)
    xwk=fnblank(4)    
    OutVect!.addItem(str(totals[0]:m1$)+$00$+"R")
    xwk=fnblank(1)    
    OutVect!.addItem(str(totals[1]:m1$)+$00$+"R")
    xwk=fnblank(15)
    let l=l+1
    return

other_line:

    OutVect!.addItem(poe_reqdet.po_line_no$)
    OutVect!.addItem(poe_reqdet.po_line_code$)
    OutVect!.addItem(poe_reqdet.warehouse_id$)
    xwk=fnblank(1)
    OutVect!.addItem(before$)
    OutVect!.addItem(fndate$(poe_reqdet.reqd_date$))
    OutVect!.addItem(promised$)
    xwk=fnblank(5)
    OutVect!.addItem(str(po_extension:m1$))
    OutVect!.addItem(source$)
    xwk=fnblank(2)
    OutVect!.addItem(poe_reqdet.order_memo$)
    xwk=fnblank(5)
    OutVect!.addItem(wo$)
    xwk=fnblank(5)
    l=l+needed
    recs=recs+1
    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Format inventory item description

    def fnitem$(q$,q1,q2,q3)=cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem #endinclude std_functions.src

rem #include disp_message.src

disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call STBL("+DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:                                str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem --- fnBlank : Create blank cells in OutVect!. No return value
   
    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$ +"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

std_exit_no_report:

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status    
    OutVect!=null()
    goto std_exit

    end
