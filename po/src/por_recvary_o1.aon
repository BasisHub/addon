rem --- PO Receipt Variance Report (Print Overlay)
rem --- Program por_recvary_o1 v8.0.0 17Jul2007 (por_nb)
rem --- Created by adx_codeport.bbx v1.1.5 (07/17/2007 12:49:54)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    setesc std_error
    seterr std_error

rem --- Document initializations

    OutVect!=bbjAPI().makeVector()
    HdrVect!=bbjAPI().makeVector()
    BrkVect!=bbjAPI().makeVector()
    TotVect!=bbjAPI().makeVector()

    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Init Data

    precision ivprecision
    dim vendtot[3],prevk$(31),vendor$(6),prevvend$(6)
    printvtot$="N"
    printptot$="N"

    call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","I","",ivIMask$,0,0

    call stbl("+DIR_SYP")+"bac_key_template.bbj","POT_RECDET","PRIMARY",pot14a_key_tpl$,rd_table_chans$[all],status$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","POT_RECDET","VENDOR_RECDATE",pot14a_key_alttpl$,rd_table_chans$[all],status$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","APM_VENDMAST","PRIMARY",apm01a_key_tpl$,rd_table_chans$[all],status$

rem --- Headings and print positions

    headings=5
    dim headings$[headings]
    width=132
    page=0
    l9=59
    l=l9+1
    gosub assignheads
    more=1
    recs=0
    total_recs=0

    dim columns$[8,10]
    columns = 9
    columns$[0,0]="",                     columns$[0,1]="C",columns$[0,2]="7"
    columns$[1,0]=Translate!.getTranslation("AON_CD"),                   columns$[1,1]="C",columns$[1,2]="8"
    columns$[2,0]=Translate!.getTranslation("AON_WH"),                   columns$[2,1]="C",columns$[2,2]="6"
    columns$[3,0]=Translate!.getTranslation("AON_ITEM"),                 columns$[3,1]="C",columns$[3,2]="20"
    columns$[4,0]=Translate!.getTranslation("AON_DATE_RECEIVED"),        columns$[4,1]="C",columns$[4,2]="10"
    columns$[5,0]=Translate!.getTranslation("AON_DATE_REQUIRED"),        columns$[5,1]="C",columns$[5,2]="10"
    columns$[6,0]=Translate!.getTranslation("AON_DATE_PROMISED"),        columns$[6,1]="C",columns$[6,2]="10"
    columns$[7,0]=Translate!.getTranslation("AON_VARIANCE_REQ/REC"),     columns$[7,1]="N",columns$[7,2]="10",columns$[7,3]=mvar$,columns$[7,10]="T"
    columns$[8,0]=Translate!.getTranslation("AON_VARIANCE_PROM/REC"),    columns$[8,1]="N",columns$[8,2]="10",columns$[8,3]=mvar$,columns$[8,10]="T"

    call stbl("+DIR_PGM",err=*next)+"adc_docout_adj.aon",columns$[all]

rem --- Position files

    call pgmdir$+"adc_progress.aon","N","POT_RECHDR","","","",0,pot04a_dev,1,meter_num,status
    if status = 999 then goto std_exit

rem --- Build sql statement

    sql_prep$="select pot_rechdr.vendor_id, pot_rechdr.po_no, "
    sql_prep$=sql_prep$+"pot_rechdr.receiver_no, pot_recdet.po_int_seq_ref "
    sql_prep$=sql_prep$+"from pot_rechdr, pot_recdet "

    where_clause$="where pot_rechdr.firm_id = '"+firm_id$+"'"
    where_clause$=where_clause$+" AND pot_recdet.firm_id = '"+firm_id$+"' AND pot_rechdr.po_no = pot_recdet.po_no"
    where_clause$=where_clause$+" AND pot_rechdr.receiver_no = pot_recdet.receiver_no"
    if vendfrom$<>"" where_clause$=where_clause$+" AND pot_rechdr.vendor_id >= '" +vendfrom$+ "'"
    if vendthru$<>"" where_clause$=where_clause$+" AND pot_rechdr.vendor_id <= '" +vendthru$+ "'"
    if itemfrom$<>"" where_clause$=where_clause$+" AND pot_recdet.item_id >= '" +itemfrom$+ "'"
    if itemthru$<>"" where_clause$=where_clause$+" AND pot_recdet.item_id <= '" +itemthru$+ "'"
    if datefrom$<>"" where_clause$=where_clause$+" AND pot_rechdr.recpt_date >= '" + datefrom$+ "'"
    if datethru$<>"" where_clause$=where_clause$+" AND pot_rechdr.recpt_date <= '" + datethru$+ "'"
    if whsefor$<>"" where_clause$=where_clause$+" AND pot_recdet.warehouse_id = '"+whsefor$+ "'"
    order_by$=" order by pot_rechdr.vendor_id, pot_rechdr.recpt_date, pot_rechdr.po_no, pot_rechdr.receiver_no,pot_recdet.po_int_seq_ref"

    sql_prep$=sql_prep$ + where_clause$ + order_by$
    sql_chan=sqlunt
    sqlopen(sql_chan,err=*next)stbl("+DBNAME")
    sqlprep(sql_chan)sql_prep$
    dim read_tpl$:sqltmpl(sql_chan)
    sqlexec(sql_chan)

rem --- Read through Sort file

    while more

        read_tpl$=sqlfetch(sql_chan,err=*break)
        read record (pot04a_dev,key=firm_id$+read_tpl.po_no$+read_tpl.receiver_no$,dom=*break) pot04a$
        read record (pot14_dev2,key=firm_id$+read_tpl.po_no$+read_tpl.receiver_no$+read_tpl.po_int_seq_ref$) pot14a$

        total_recs=total_recs+1
        if mod(total_recs,milestone)=0 then call stbl("+DIR_PGM")+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status
        if status = 999 then goto std_exit

        vendor$=pot04a.vendor_id$
        ponum$=pot04a.po_no$
        receiver$=pot04a.receiver_no$
        linenum$=pot14a.po_int_seq_ref$
        dim rcptdate$(8),orddate$(8)
        if pot04a.recpt_date$<>"" rcptdate$=pot04a.recpt_date$
        if pot04a.ord_date$<>"" orddate$=pot04a.ord_date$

    rem --- Get detail

        whse$=pot14a.warehouse_id$
        item$=pot14a.item_id$

    rem --- Valid line type (ie S or N)?

        gosub getlinetype
        if pos(linetype$="SN")=0 break

    rem --- Valid, so assign vars

        linecode$=pot14a.po_line_code$
        dim dateprom$(8),reqdate$(8)
        if pot14a.promise_date$<>fill(8) dateprom$=pot14a.promise_date$
        if pot14a.reqd_date$<>fill(8) reqdate$=pot14a.reqd_date$

    rem --- Calculate variances

        reqvari=0
        promvari=0

        if rcptdate$<>fill(8)

            if reqdate$<>fill(8)
                if rcptdate$<reqdate$ multiplier=-1 else multiplier=1
                fromdate$=rcptdate$
                thrudate$=reqdate$
                call pgmdir$+"adc_daydates.aon",fromdate$,thrudate$,reqvari
                reqvari=reqvari*multiplier
            endif

            if datefrom$<>fill(8)

                if rcptdate$<dateprom$ multiplier=-1 else multiplier=1
                fromdate$=rcptdate$
                thrudate$=dateprom$
                call pgmdir$+"adc_daydates.aon",fromdate$,thrudate$,promvari
                promvari=promvari*multiplier

            endif

        endif

        if abs(reqvari)<minvari and abs(promvari)<minvari break

    rem --- Test breaks

        if vendor$<>prevvend$

            if printptot$="Y" gosub pototals
            if printvtot$="Y" gosub vendtotals
            gosub newvendor
            gosub newpo

        else

            if ponum$<>prevpo$
                if printptot$="Y" gosub pototals
                gosub newpo
            endif

        endif

    rem --- Get Item Desc

        dim itemdesc$(60),x1$(60)

        if linetype$<>"N"

            ivm01a.item_desc$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
            find record (ivm01a_dev,key=firm_id$+pot14a.item_id$,dom=brnch1) ivm01a$
            ivm01a.item_desc$=fnitem$(ivm01a.item_desc$,desclen[1],desclen[2],desclen[3])

        else

        rem --- Non-stock description

            itemdesc$(1)=pot14a.order_memo$

        endif

    brnch1: rem --- Go print the data

        gosub printline
        recs=recs+1

    rem --- Accumulate totals; then get next detail

        po_reqvar=po_reqvar+reqvari
        po_promvar=po_promvar+promvari
        vendreqvar=vendreqvar+reqvari
        vendpromvar=vendpromvar+promvari
        rptreqvar=rptreqvar+reqvari
        rptpromvar=rptpromvar+promvari

    rem --- Loop up for next header record via sort files

    wend

done: rem --- All Done

    if recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    gosub pototals
    gosub vendtotals
    TotVect!.addItem(str(OutVect!.size()/columns))
    qwk = fnblank(3)
    OutVect!.addItem(Translate!.getTranslation("AON_TOTAL_FOR_REPORT"))
    qwk = fnblank(3)
    OutVect!.addItem(str(rptreqvar:mvar$))
    OutVect!.addItem(str(rptpromvar:mvar$))
    call pgmdir$+"adc_progress.aon","D","","","","",0,poe04_dev,0,0,status
    
    goto std_exit

assignheads: rem --- Assign headings variables

    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$+Translate!.getTranslation("AON__FOR_VARIANCES_GREATER_THAN_OR_EQUAL_TO_")+str(minvari:mpcnt$)

    if vendfrom$="" vendf$=Translate!.getTranslation("AON_FIRST") else vendf$=fnmask$(vendfrom$(1,vendlen),vendomask$)
    if vendthru$="" vendt$=Translate!.getTranslation("AON_LAST") else vendt$=fnmask$(vendthru$(1,vendlen),vendomask$)
    if datefrom$="" datef$=Translate!.getTranslation("AON_FIRST") else datef$=fndate$(datefrom$)
    if datethru$="" datet$=Translate!.getTranslation("AON_LAST") else datet$=fndate$(datethru$)
    if itemfrom$="" itemf$=Translate!.getTranslation("AON_FIRST") else itemf$=itemfrom$(1,itemlen)
    if itemthru$="" itemt$=Translate!.getTranslation("AON_LAST") else itemt$=itemthru$(1,itemlen)

    if vendfrom$="" headings$[2]=Translate!.getTranslation("AON_FROM_VENDOR:_FIRST") else headings$[2]=Translate!.getTranslation("AON_FROM_VENDOR:_")+ vendf$
    if vendthru$="" headings$[2]=headings$[2]+Translate!.getTranslation("AON__--_TO_VENDOR:_LAST") else headings$[2]=headings$[2]+Translate!.getTranslation("AON__--_TO_VENDOR:_")+ vendt$
    if datefrom$="" headings$[3]=Translate!.getTranslation("AON_FROM_RECEIPT_DATE:_FIRST") else headings$[3]=Translate!.getTranslation("AON_FROM_RECEIPT_DATE:_")+ datef$
    if datethru$="" headings$[3]=headings$[3]+Translate!.getTranslation("AON__--_TO_RECEIPT_DATE:_LAST") else headings$[3]=headings$[3]+Translate!.getTranslation("AON__--_TO_RECEIPT_DATE:_")+ datet$
    if itemfrom$="" headings$[4]=Translate!.getTranslation("AON_FROM_ITEM:_FIRST") else headings$[4]=Translate!.getTranslation("AON_FROM_ITEM:_")+ cvs(fnmask$(itemf$,ivIMask$),2)
    if itemthru$="" headings$[4]=headings$[4]+Translate!.getTranslation("AON__--_TO_ITEM:_LAST") else headings$[4]=headings$[4]+Translate!.getTranslation("AON__--_TO_ITEM:_")+ cvs(fnmask$(itemt$,ivIMask$),2)
    if whsefor$=""  headings$[5]=Translate!.getTranslation("AON_ALL_WAREHOUSES")  else headings$[5]=Translate!.getTranslation("AON_FOR_WAREHOUSE_") + whsefor$+" "+cvs(whsename$,2)

    return

newvendor: rem --- New Vendor

    dim vendtot[3]
    vendname$=Translate!.getTranslation("AON_NOT_ON_FILE")
    prevvend$=vendor$
    printvtot$="Y"
    vendreqvar=0
    vendpromvar=0
    find record (apm01a_dev,key=firm_id$+vendor$,dom=brnch2) apm01a$
    vendname$=apm01a.vendor_name$

brnch2: 

    OutVect!.addItem(fnmask$(vendor$(1,vendlen),vendomask$))
    OutVect!.addItem(" ")
    OutVect!.addItem(" ")
    OutVect!.addItem(vendname$)
    qwk=fnblank(5)

    l=l+1

    return

newpo: rem --- New PO

    dim rdate$(8),odate$(8)
    po_reqvar=0
    po_promvar=0
    prevpo$=ponum$
    printptot$="Y"
    if rcptdate$<>fill(8) rdate$=fndate$(rcptdate$)
    if orddate$<>fill(8) odate$=fndate$(orddate$)

    OutVect!.addItem("  PO #: ")
    OutVect!.addItem(ponum$)
    OutVect!.addItem(Translate!.getTranslation("AON_ORDERED:_"))
    OutVect!.addItem("  " + odate$)
    qwk = fnblank(5)

    l=l+1

    return

vendtotals: rem --- Print Vendor total

    TotVect!.addItem(str(OutVect!.size()/columns))
    qwk = fnblank(3)
    OutVect!.addItem(Translate!.getTranslation("AON_TOTAL_FOR_VENDOR_")+fnmask$(prevvend$(1,vendlen),vendomask$))
    qwk = fnblank(3)
    OutVect!.addItem(str(vendreqvar:mvar$))
    OutVect!.addItem(str(vendpromvar:mvar$))
    qwk = fnblank(9)    
    l=l+1

    return

pototals: rem --- Print PO total

    TotVect!.addItem(str(OutVect!.size()/columns))
    qwk = fnblank(3)
    OutVect!.addItem(Translate!.getTranslation("AON_TOTAL_FOR_PO_#_")+prevpo$)
    qwk = fnblank(3)
    OutVect!.addItem(str(po_reqvar:mvar$))
    OutVect!.addItem(str(po_promvar:mvar$))
    qwk = fnblank(9)
    l=l+1

    return

getlinetype: rem --- Get Line Type

    linetype$="N"
    find record (pom02a_dev,key=pot14a.firm_id$+pot14a.po_line_code$,dom=brnch3) pom02a$
    linetype$=pom02a.line_type$

brnch3: 

    return

rem --- Print data line

printline: 

    dim dprom$(8),qdate$(8)
    if rcptdate$<>fill(8) rdate$=fndate$(rcptdate$)
    if dateprom$<>fill(8) dprom$=fndate$(dateprom$)
    if reqdate$<>fill(8) qdate$=fndate$(reqdate$)

    OutVect!.addItem("")
    OutVect!.addItem(linecode$)
    OutVect!.addItem(whse$)
    OutVect!.addItem(cvs(fnmask$(item$(1,itemlen),ivIMask$),2) + " " + itemdesc$(1,desclen))
    OutVect!.addItem(rdate$)
    OutVect!.addItem(qdate$)
    OutVect!.addItem(dprom$)
    OutVect!.addItem(str(reqvari:mvar$))
    OutVect!.addItem(str(promvari:mvar$))

    l=l+1

    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if cvs(q1$,2)="" return ""
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem #endinclude std_functions.src

rem #include disp_message.src

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

rem --- fnBlank : Create blank cells in OutVect!. No return value

    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1

    std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

        rd_err_text$=""
        if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
        pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
        call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:           str(err:"000"),rd_err_text$,rd_err_act$
    std_missing_params_exit: 
        master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
        sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
        dim sysinfo$:sysinfo_template$
        sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
        if cvs(sysinfo.user_id$,2)=master_user$ escape
    std_missing_params_release: 
        status=999
        if pgm(-1)<>pgm(-2) exit
        release

    rem #endinclude std_missing_params.src

    rem #include std_end.src

    std_exit: rem --- Standard program end (01Mar2006)

        call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,stat
        run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
        release
    rem #endinclude std_end.src

    std_exit_no_report: 

        call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status    
        OutVect!=null()
        goto std_exit

        end
