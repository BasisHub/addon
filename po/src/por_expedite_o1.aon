rem --- Purchase Order Expediting Report (Print Overlay)
rem --- Program por_expedite.o1 v8.0.0 17Jul2007 (por_kb)
rem --- Created by adx_codeport.bbx v1.1.5 (07/17/2007 12:48:50)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2007 AddonSoftware
rem --- All Rights Reserved
rem 23Oct96 Caj: Was printing date required on memos (4460)

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Document initializations

    OutVect!=bbjAPI().makeVector()
    HdrVect!=bbjAPI().makeVector()
    BrkVect!=bbjAPI().makeVector()
    TotVect!=bbjAPI().makeVector()

    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)

rem --- Init Data

    precision ivprecision
    grandtot=0
    more=1
    recs=0
    total_recs=0

rem --- Headings and print positions

    headings=5
    dim headings$[headings],o[11]
    width=132
    page=0
    l9=59
    l=l9+1
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$
    gosub assignheads
    headings$[1]=headings$[1]+" Through "+fndate$(datethru$)

rem --- Headings and print positions

    dim columns$[11,10]
    columns = 12
    columns$[0,0]=" ",                columns$[0,1]="C",columns$[0,2]="4"
    columns$[1,0]="Wh",                 columns$[1,1]="C",columns$[1,2]="10"
    columns$[2,0]="Item",               columns$[2,1]="C",columns$[2,2]="15"
    columns$[3,0]="Description",        columns$[3,1]="C",columns$[3,2]="20"
    columns$[4,0]="UM",                 columns$[4,1]="C",columns$[4,2]="4"
    columns$[5,0]="Date^Required",      columns$[5,1]="C",columns$[5,2]="12"
    columns$[6,0]="Qty^Ordered",        columns$[6,1]="N",columns$[6,2]=str(len(m2$)),    columns$[6,3]=m2$
    columns$[7,0]="Qty^In QA",          columns$[7,1]="N",columns$[7,2]=str(len(m2$)),    columns$[7,3]=m2$
    columns$[8,0]="Qty^Received",       columns$[8,1]="N",columns$[8,2]=str(len(m2$)),    columns$[8,3]=m2$
    columns$[9,0]="Balance^Due",        columns$[9,1]="N",columns$[9,2]=str(len(m2$)),    columns$[9,3]=m2$
    columns$[10,0]="Unit^Cost",         columns$[10,1]="N",columns$[10,2]=str(len(m3$)); rem ,  columns$[10,3]=m3$
    columns$[11,0]="Extension",         columns$[11,1]="N",columns$[11,2]=str(len(m1$)); rem ,  columns$[11,3]=m1$

positions: 

    o[1]=o[0]+4
    o[2]=o[1]+3
    o[3]=o[2]+itemlen+1
    o[4]=o[3]+28
    o[5]=o[4]+3
    o[6]=o[5]+9
    o[7]=o[6]+m2+1
    o[8]=o[7]+m2+1                       
    o[9]=o[8]+m2+1
    o[10]=o[9]+m2+1
    o[11]=o[10]+m3+1

    if o[11]+13>=width
        o[11]=width-m1
        o[10]=o[11]-m3
        o[9]=o[10]-m2
        o[8]=o[9]-m2
        o[7]=o[8]-m2
        o[6]=o[7]-m2
    endif

rem --- Get key templates

    call stbl("+DIR_SYP")+"bac_key_template.bbj","POE_POHDR","PRIMARY",poe02a_key_pritpl$,rd_table_chans$[all],status$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","POE_PODET","PRIMARY",poe12a_key_tpl$,rd_table_chans$[all],status$

rem --- Position files

    call pgmdir$+"adc_progress.aon","NC","POE_POHDR","","","",0,poe02a_dev,1,meter_num,status
    if status = 999 then goto std_exit

rem --- Sort by Purchase Order

    read (poe02a_dev,key=firm_id$+pofrom$,dom=*next)

rem --- Read through sort file

    while more

        k$=key(poe02a_dev,end=*break)
        if k$(1,2)<>firm_id$ then goto done
        read record (poe02a_dev, key=k$, dom=*continue) poe02a$

        total_recs=total_recs+1
        if mod(total_recs,milestone)=0 then call stbl("+DIR_PGM")+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status
        if status = 999 then goto std_exit

    rem --- Test Ranges

        printit$="N"
        if pofrom$<>"" and poe02a.po_no$<pofrom$ continue
        if pothru$<>"" and poe02a.po_no$>pothru$ continue
        if vendfrom$<>"" and poe02a.vendor_id$<vendfrom$ continue
        if vendthru$<>"" and poe02a.vendor_id$>vendthru$ continue
        if whse$<>"" and poe02a.warehouse_id$<>whse$ continue

        if poe02a.reqd_date$>datethru$ continue

    rem --- If req'd date of any detail is w/in date range, print whole PO

        dim poe12a_key$:poe12a_key_tpl$
        read (poe12_dev,key=k$,dom=*next)

    rem --- Test PO Detail date range

        while more

            poe12a_key$=key(poe12_dev,end=*break)
            if poe12a_key.firm_id$+poe12a_key.po_no$<>poe02a.firm_id$+poe02a.po_no$ break
            read record (poe12_dev,key=poe12a_key$) poe12a$
            if datethru$="" or poe12a.reqd_date$<=datethru$ printit$="Y"

        rem --- Detail passed, so go print header & whole PO

            if printit$="Y"
                gosub testbreaks
                gosub printdetail
                recs=recs+1
            endif

        rem --- Loop up to test next detail record

        wend

    wend

done: rem --- All Done

    if recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    gosub pototals

    xwk = fnblank(12)
    TotVect!.addItem(str(OutVect!.size()/columns))
    xwk = fnblank(9)
    OutVect!.addItem("Total For Report")
    xwk = fnblank(1)
    OutVect!.addItem(str(grandtot:m1$))
    
    out_action$="FORMAT"
    goto std_exit

rem --- Print Detail Subroutine

printdetail: rem --- Re-loop through detail & print whole PO

    call pgmdir$+"adc_progress.aon","S","","","","",0,poe02a_dev,1,meter_num,status

    read (poe12_dev,key=k$,dom=*next)

    while more

        poe12a_key$=key(poe12_dev,end=*break)
        if pos(k$=poe12a_key$)<>1 then break
        read record (poe12_dev,key=poe12a_key$) poe12a$

    rem --- Set up variables

        dim datereq$(8)
        whse_detail$=poe12a.warehouse_id$
        itemnum$=poe12a.item_id$
        unitmeas$=poe12a.unit_measure$
        if poe12a.reqd_date$<>fill(8) datereq$=fndate$(poe12a.reqd_date$)
        qtyord$=str(poe12a.qty_ordered)
        qtyinqa$=str(poe12a.qa_wip_qty)
        qtyrec$=str(poe12a.qty_received)
        baldue$=str(poe12a.qty_ordered-poe12a.qty_received:m2$)
        unitcost$=str(poe12a.unit_cost:m3$)

    rem --- Get Line Type

        linetype$="N"
        find record (pom02a_dev,key=poe12a.firm_id$+poe12a.po_line_code$,dom=brnch1) pom02a$
        linetype$=pom02a.line_type$

    brnch1: rem --- Get Item Desc for S and N type lines

        dim item_desc$(60)       
        if linetype$="N" item_desc$(1)=poe12a.order_memo$

        if linetype$="S" 
            item_desc$(1)="Not On File"
            i01_key$=firm_id$+poe12a.item_id$
            find record (ivm01a_dev,key=i01_key$,dom=brnch2) ivm01a$
            item_desc$(1)=fnitem$(ivm01a.item_desc$,desclen[1],desclen[2],desclen[3])
        endif

    brnch2: rem --- Get extension

        gosub calc_extension

    rem --- Format common part of print line

        dim pline$(132)
        pline$(1)=poe12a.po_line_no$+" "+whse_detail$

    rem --- Print Line Types 'M', 'O' & 'V'

        if pos(linetype$="MOV")<>0

            OutVect!.addItem("")
        rem --- dropping isn/line# per VARs; OutVect!.addItem(poe12a.po_line_no$)
            OutVect!.addItem(whse_detail$)

            if linetype$="V" then 
                OutVect!.addItem(poe12a.order_memo$(1,20))
            else 
                OutVect!.addItem(poe12a.order_memo$)
            endif

            if linetype$<>"M" then 
                xwk = fnblank(2)
                OutVect!.addItem(datereq$)
            else
                xwk = fnblank(3)
            endif

            if linetype$="O" then
                xwk = fnblank(4)
                OutVect!.addItem(unitcost$)
                OutVect!.addItem(extension$)
            else
                xwk = fnblank(6)
            endif

            l=l+1
        else

        rem --- Print Line Types 'S' & 'N'

            if pos(linetype$="SN")<>0

                OutVect!.addItem("")
            rem --- dropping isn/line# per VARs; OutVect!.addItem(poe12a.po_line_no$)
                OutVect!.addItem(whse_detail$)
                OutVect!.addItem(cvs(itemnum$,2))
                OutVect!.addItem(item_desc$(1,27))
                OutVect!.addItem(unitmeas$)
                OutVect!.addItem(datereq$)
                OutVect!.addItem(qtyord$)
                OutVect!.addItem(qtyinqa$)
                OutVect!.addItem(qtyrec$)
                OutVect!.addItem(baldue$)
                OutVect!.addItem(unitcost$)
                OutVect!.addItem(extension$)

                l=l+1
            endif
        endif

    rem --- Get next detail record to print

        potot=potot+extension
        grandtot=grandtot+extension
        extension=0
        extension$=""

    wend

rem --- Done printing detail

    return

assignheads: rem --- Assign headings variables

    if vendfrom$="" vendf$="First" else vendf$=fnmask$(vendfrom$(1,vendlen),vendomask$)
    if vendthru$="" vendt$="Last" else vendt$=fnmask$(vendthru$(1,vendlen),vendomask$)
    if whse$="" whsepagehead$="For All Warehouses" else whsepagehead$="For Warehouse "+whse$+" "+cvs(whsename$,2)
    if vendfrom$="" headings$[2]="From Vendor: First" else headings$[2]="From Vendor: "+ vendf$
    if vendthru$="" headings$[2]=headings$[2] + " To Last" else headings$[2]=headings$[2] + " -- To " + vendt$
    if pofrom$=""   headings$[3]="From Purchase Order: First" else headings$[3]="From Purchase Order: "+ pofrom$
    if pothru$=""   headings$[3]=headings$[3] + " Through Last" else headings$[3]=headings$[3] + " -- Through " + pothru$

    headings$[4] = whsepagehead$

    return

testbreaks: rem --- Test for Breaks

    if poe02a.po_no$<>ponum$
        if printptot$="Y" gosub pototals
    endif

    gosub newpo

    return

newpo: rem --- New PO

    potot=0
    ponum$=poe02a.po_no$
    printptot$="Y"
    vendor$=poe02a.vendor_id$
    termsdesc$="Not On File"
    vendname$="Not On File"
    find record (apm01a_dev,key=firm_id$+vendor$,dom=brnch5) apm01a$
    vendname$=apm01a.vendor_name$
    contact$=apm01a.contact_name$
    call stbl("+DIR_SYP")+"bac_getmask.bbj","T",cvs(apm01a.phone_no$,2),"",phone_mask$
    phone$=cvs(apm01a.phone_no$,2)
    phone$=str(phone$:phone_mask$,err=*next)

brnch5: 

    find record (apm10c_dev,key=firm_id$+"C"+poe02a.ap_terms_code$,dom=brnch6) apm10c$
    termsdesc$=apm10c.code_desc$

brnch6: 

    dim dateord$(8),dateprom$(8)
    if poe02a.ord_date$<>fill(8) dateord$=fndate$(poe02a.ord_date$)
    if poe02a.promise_date$<>fill(8) dateprom$=fndate$(poe02a.promise_date$)

    xwk = fnblank(12)
    OutVect!.addItem("PO#: "+$00$+"B")
    OutVect!.addItem(ponum$+$00$+"B")
    OutVect!.addItem("Ordered: " + dateord$)
    OutVect!.addItem("Vendor: " + fnmask$(vendor$(1,vendlen),vendomask$) + " " + vendname$)
    xwk = fnblank(1)
    OutVect!.addItem("Contact: " + contact$)
    xwk = fnblank(4)
    OutVect!.addItem("Ack By: ")
    OutVect!.addItem(poe02a.acknowledge$)
    xwk = fnblank(2)
    OutVect!.addItem("Promised " + dateprom$)
    xwk = fnblank(2)
    OutVect!.addItem("Phone " + phone$)
    xwk = fnblank(4)
    OutVect!.addItem("Terms " + poe02a.ap_terms_code$)
    OutVect!.addItem(termsdesc$)

    l=l+3

    if poe02a.dropship$="Y"
        dropname$=poe02a.ds_name$
        dropaddr$=poe02a.ds_addr_line_1$+poe02a.ds_addr_line_2$+poe02a.ds_addr_line_3$+
:           poe02a.ds_city$+poe02a.ds_state_cd$+poe02a.ds_zip_code$
        call pgmdir$+"adc_address.aon",dropaddr$,24,4,9,30

        xwk = fnblank(14)
        OutVect!.addItem("Dropship To Customer: "+fnmask$(poe02a.customer_id$,customask$)+", Order: " + poe02a.order_no$)
        xwk = fnblank(9)

        xwk = fnblank(2)
        OutVect!.addItem(dropname$)
        xwk=fnblank(9)

        xwk = fnblank(2)
        OutVect!.addItem(dropaddr$(1,30)) 
        xwk=fnblank(9)

        l=l+3

        if cvs(dropaddr$(31,30),3)<>""
            xwk = fnblank(2)
            OutVect!.addItem(dropaddr$(31,30))
            xwk=fnblank(9)
            let l=l+1
        endif

        if cvs(dropaddr$(61,30),3)<>"" 
            xwk = fnblank(2)
            OutVect!.addItem(dropaddr$(61,30))
            xwk=fnblank(9)
            let l=l+1
        endif

        if cvs(dropaddr$(91,30),3)<>""
            xwk = fnblank(2)
            OutVect!.addItem(custaddr$(91,30))
            xwk=fnblank(9)
            l=l+1
        endif

        xwk=fnblank(12)

    endif

    return

pototals: rem --- Print Purchase Order total

    TotVect!.addItem(str(OutVect!.size()/columns))
    xwk = fnblank(9)
    OutVect!.addItem("Total For PO"+$00$+"B")
    OutVect!.addItem(ponum$+$00$+"B")
    OutVect!.addItem(str(potot:m1$)+$00$+"B")

    l=l+2

    return

rem --- Calc extension for S, N and O types

calc_extension: 

    if pos(linetype$="SNO")<>0
        qty=poe12a.qty_ordered-poe12a.qty_received
        if linetype$="O" qty=1
        extension=round(poe12a.unit_cost*qty, 2)
        extension$=str(extension:m1$)
    endif

    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fnisn$(q$)=q$(7)

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

    def fnitem$(q$,q1,q2,q3)=cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend

    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
        return q$
    fnend

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem --- fnBlank : Create blank cells in OutVect!. No return value

    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend

rem #endinclude std_functions.src

rem #include disp_message.src

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)
    escape; rem *** Inside Standard Error
    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call stbl("+DIR_SYP"+"bac_error.bbj",err=std_error_exit),pgm(-2),str(tcb(5):"00000"),
:       str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit: 
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release: 
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:       str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit: 
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release: 
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

std_exit_no_report: 

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status    
    OutVect!=null()
    goto std_exit

    end
