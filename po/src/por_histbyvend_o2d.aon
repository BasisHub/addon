rem --- Purchase Order Receipt History Report (Detail - Print Overlay)
rem --- Program por_histbyvend_o2d v8.0.0 08Aug2007 (por_pc)
rem --- Created by adx_codeport.bbx v1.1.5 (08/08/2007 10:29:41)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Document initializations

    OutVect!=bbjAPI().makeVector()
    HdrVect!=bbjAPI().makeVector()
    BrkVect!=bbjAPI().makeVector()
    TotVect!=bbjAPI().makeVector()

    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)

rem --- Init Data

    precision ivprecision
    dim vendor$(6)
    grandtot=0
    printvtot$="N"
    printptot$="N"
    initializing$="Y"; rem "Init files 1st time through
    more=1
    recs=0
    total_recs=0

rem --- Headings and print positions

    headings=5
    dim headings$[headings],o[12]
    width=132
    page=0
    l9=59
    l=l9+1
    gosub assignheads

rem --- Columns

    dim columns$[8,3]
    columns = 9
    columns$[0,0]="PO Receipt^Date                       Seq     Cd     Wh",    columns$[0,1]="C",columns$[0,2]="25"
    columns$[1,0]="Item",                columns$[1,1]="C",columns$[1,2]="25"
    columns$[2,0]="Date Required",       columns$[2,1]="C",columns$[2,2]="12"
    columns$[3,0]="Location",            columns$[3,1]="C",columns$[3,2]="12"
    columns$[4,0]="Conv^UM     Factor",  columns$[4,1]="C",columns$[4,2]="12"
    columns$[5,0]="           Qty Ordered",    columns$[5,1]="C",columns$[5,2]="12"
    columns$[6,0]="                 Qty Received",   columns$[6,1]="C",columns$[6,2]="15"
    columns$[7,0]="         Unit Cost",      columns$[7,1]="C",columns$[7,2]="10"
    columns$[8,0]="         Extension",      columns$[8,1]="C",columns$[8,2]="10"

    call stbl("+DIR_PGM",err=*next)+"adc_docout_adj.aon",columns$[all]

rem --- Get key templates

    call stbl("+DIR_SYP")+"bac_key_template.bbj","POT_RECHDR","PRIMARY",pot04a_key_tpl$,rd_table_chans$[all],status$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","POT_RECDET","PRIMARY",pot14a_key_tpl$,rd_table_chans$[all],status$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","POT_RECHDR","VEND_RECDATE",pot04a_key_alttpl$,rd_table_chans$[all],status$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","APM_VENDMAST","AO_ALTSEQ_VEND",apm01a_key_alphtpl$,rd_table_chans$[all],status$

rem --- Position files

    call pgmdir$+"adc_progress.aon","N","POT_RECDET","","","",0,pot14_dev,1,meter_num,status
    if status = 999 then goto std_exit

rem --- Build sql statement

    sql_prep$="select pot_rechdr.firm_id, pot_rechdr.vendor_id, pot_rechdr.po_no, "
    sql_prep$=sql_prep$+"pot_rechdr.receiver_no, "
    sql_prep$=sql_prep$+"apm_vendmast.alt_sequence "
    sql_prep$=sql_prep$+"from pot_rechdr, apm_vendmast "

    where_clause$="where pot_rechdr.firm_id = '"+firm_id$+"' AND "
    where_clause$=where_clause$+"pot_rechdr.firm_id = apm_vendmast.firm_id AND "
    where_clause$=where_clause$+"pot_rechdr.vendor_id = apm_vendmast.vendor_id"
    if vendfrom$<>"" where_clause$=where_clause$+" AND pot_rechdr.vendor_id >= '" +vendfrom$+ "'"
    if vendthru$<>"" where_clause$=where_clause$+" AND pot_rechdr.vendor_id <= '" +vendthru$+ "'"
    if altfrom$<>"" where_clause$=where_clause$+" AND apm_vendmast.alt_sequence >= '" +altfrom$+ "'" 
    if altthru$<>"" where_clause$=where_clause$+" AND apm_vendmast.alt_sequence <= '" +altthru$+ "'"
    if datefrom$<>"" where_clause$=where_clause$+ " AND pot_rechdr.recpt_date >= '" + datefrom$+ "'"
    if datethru$<>"" where_clause$=where_clause$+ " AND pot_rechdr.recpt_date <= '" + datethru$+ "'"
    if inactive$="Y" then
       where_clause$=where_clause$+" AND apm_vendmast.vend_inactive<>'Y' "
    endif

    order_by$=" order by pot_rechdr.vendor_id,pot_rechdr.recpt_date,pot_rechdr.po_no,pot_rechdr.receiver_no"

    if printseq$="A"
        order_by$=" order by apm_vendmast.alt_sequence,pot_rechdr.vendor_id,pot_rechdr.recpt_date,pot_rechdr.po_no,pot_rechdr.receiver_no"
    endif

    sql_prep$=sql_prep$ + where_clause$ + order_by$
    sql_chan=sqlunt
    sqlopen(sql_chan,err=*next)stbl("+DBNAME")
    sqlprep(sql_chan)sql_prep$
    dim read_tpl$:sqltmpl(sql_chan)
    sqlexec(sql_chan)

rem --- Sorts

    while more
        read_tpl$=sqlfetch(sql_chan,err=*break)

        find record (pot04a_dev,key=firm_id$+read_tpl.po_no$+read_tpl.receiver_no$,dom=*break) pot04a$
        read (pot14_dev,key=firm_id$+read_tpl.po_no$+read_tpl.receiver_no$,dom=*next) ; rem trip read for later details

        total_recs=total_recs+1
        if mod(total_recs,milestone)=0 then call stbl("+DIR_PGM")+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status
        if status = 999 then goto std_exit

    rem --- Header passed tests

        gosub testbreaks

    rem --- Get detail

        dim pot14a_key$:pot14a_key_tpl$

        while more

            pot14a_key$=key(pot14_dev,end=*break)

            if pot14a_key.firm_id$+pot14a_key.receiver_no$<>pot04a.firm_id$+
:               pot04a.receiver_no$ break

            read record (pot14_dev,key=pot14a_key$) pot14a$

        rem --- Print detail

            gosub printdetail
            recs=recs+1

        rem --- Accumulate totals; then get next detail

            dim rcptdate$(8); rem "Only print Recpt date on 1st detail
            potot=potot+extension
            vendtot=vendtot+extension
            rcpttot=rcpttot+extension
            grandtot=grandtot+extension
            extension=0
            extension$=""
        wend

    rem --- Done w/detail for this PO

        gosub rcpttotals

    rem --- See if there's another po for vend before getting next alt seq

    wend

done: rem --- All Done

    if recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    if l+2>l9 gosub report_heading
    gosub pototals
    gosub vendtotals
    TotVect!.addItem(str(OutVect!.size()/columns))
    a=fnblank(6)
    OutVect!.addItem("Total For Report")
    a=fnblank(1)
    OutVect!.addItem(str(grandtot:m1$)+$00$+"R")
    
    out_action$="FORMAT"
    goto std_exit

rem --- Print Detail Subroutine

printdetail: rem --- Set up variables

    dim datereq$(8),rcptdate$(8)
    rcptdate$(1)=fndate$(pot04a.recpt_date$)
    whse$=pot14a.warehouse_id$
    itemnum$=pot14a.item_id$
    memo$=pot14a.order_memo$
    unitmeas$=pot14a.unit_measure$
    if pot14a.reqd_date$<>fill(8) datereq$=fndate$(pot14a.reqd_date$)
    convfact$=str(pot14a.conv_factor:m5$)
    qtyord$=str(pot14a.qty_ordered:m2$)
    qtyrec=pot14a.qty_received
    qtyrec$=str(pot14a.qty_received:m2$)
    location$=pot14a.location$
    unitcost$=str(pot14a.unit_cost:m3$)
    wonum$=pot14a.wo_no$
    woseq$=pot14a.wk_ord_seq_ref$
    custnum$=pot14a.customer_id$

rem --- Get Dropship flag & Line Type

    linetype$="N"
    dropship$=pot04a.dropship$
    find record (pom02a_dev,key=pot14a.firm_id$+pot14a.po_line_code$,dom=brnch1) pom02a$
    linetype$=pom02a.line_type$
    if pom02a.dropship$="Y" then dropship$=pom02a.dropship$

brnch1: rem --- Get Item Desc for S and N type lines

    dim itemdesc$(60),x1$(60)

    if linetype$="N"
        itemnum$=pot14a.ns_item_id$
        itemdesc$(1)=memo$
    else
        find record (ivm01a_dev,key=firm_id$+pot14a.item_id$,dom=brnch2) ivm01a$
        x1$(1)=ivm01a.item_desc$
        itemdesc$(1)=fnitem$(x1$,desclen[1],desclen[2],desclen[3])
    endif

brnch2: rem --- Get extension

    gosub calc_extension

rem --- Get Vendor's Part #

    dim vpartnum$(20)
    find record (ivm05a_dev,key=pot14a.firm_id$+pot04a.vendor_id$+itemnum$,dom=*next) ivm05a$

rem --- Format common part of print line

    dim pline$(132)
    pline$=rcptdate$+"                  "+pot14a.po_line_no$+"        "+pot14a.po_line_code$+"    "+whse$

rem --- Print Line Types 'M', 'O' & 'V'

    if pos(linetype$="MOV")<>0
        if linetype$="V" pline$(20)=memo$(1,20) else pline$(20)=memo$
        if linetype$="O" pline$(o[11]+1,m3)=unitcost$,pline$(o[12]+1)=extension$
        if l+1>l9 gosub report_heading
        OutVect!.addItem(pline$)
        a=fnblank(8)
        l=l+1
    else

    rem --- Print Line Types 'S' & 'N'

        if pos(linetype$="SN")<>0
            OutVect!.addItem(pline$)
            OutVect!.addItem(itemnum$)
            OutVect!.addItem(datereq$)
            OutVect!.addItem(location$)
            if linetype$="S" OutVect!.addItem(unitmeas$+" "+convfact$) 
:               else OutVect!.addItem(unitmeas$)
            OutVect!.addItem(qtyord$+$00$+"R")
            OutVect!.addItem(qtyrec$+$00$+"R")
            OutVect!.addItem(unitcost$+$00$+"R")
            OutVect!.addItem(extension$+$00$+"R")

            if wonum$<>fill(7)
                a=fnblank(1)
                OutVect!.addItem(itemdesc$(1,40))
                a=fnblank(2)
                OutVect!.addItem("WO: "+wonum$)
                a=fnblank(4)
            else
                a=fnblank(1)
                OutVect!.addItem(itemdesc$(1,40))
                a=fnblank(7)
            endif

            l=l+2

            if vpartnum$<>fill(20)
                if l+1>l9 gosub report_heading
                a=fnblank(1)
                OutVect!.addItem("Vendor's Part Number")
                OutVect!.addItem(vpartnum$)
                a=fnblank(6)
                l=l+1
            endif

        rem --- Print dropship info if applicable

            if dropship$="Y" 
                dim custinfo$(321),dropinfo$(173),custaddr$(120),dropaddr$(120)
                custname$="Not On File"
                dropname$="Not On File"
                find record (arm01a_dev,key=pot04a.firm_id$+pot04a.customer_id$,dom=brnch3) arm01a$
                custname$=arm01a.customer_name$
                custaddr$=arm01a.addr_line_1$+arm01a.addr_line_2$+arm01a.addr_line_3$+arm01a.city$+arm01a.state_code$+arm01a.zip_code$
                call pgmdir$+"adc_address.aon",custaddr$,24,4,9,30
            brnch3: 
                find record (arm03a_dev,key=pot04a.firm_id$+pot04a.customer_id$+pot14a.shipto_no$,dom=brnch4) arm03a$
                dropname$=arm03a.name$
                dropaddr$=arm03a.addr_line_1$+arm03a.addr_line_2$+arm03a.addr_line_3$+arm03a.city$+arm03a.state_code$+arm03a.zip_code$
                call pgmdir$+"adc_address.aon",dropaddr$,24,4,9,30
            brnch4: 
                if l+6>l9 gosub report_heading
                rem --- Use pot14a.so_int_seq_ref$ to get opt_invdet record and corresponding opt_invdet.line_no$
                opt11a_trip$=pot04a.firm_id$+"  "+pot04a.customer_id$+pot04a.order_no$
                read(opt11a_dev,key=opt11a_trip$,dom=*next)
                while 1
                    redim opt11a$
                    opt11a_key$=key(opt11a_dev,end=*break)
                    if pos(opt11a_trip$=opt11a_key$)<>1 then break
                    readrecord(opt11a_dev)opt11a$
                    if opt11a.internal_seq_no$=pot14a.so_int_seq_ref$ then break
                wend
                a=fnblank(1)
                OutVect!.addItem("Dropship Above Line To Customer")
                OutVect!.addItem(fnmask$(arm01a.customer_id$(1,custlen),customask$))
                OutVect!.addItem(" Order")
                OutVect!.addItem(pot04a.order_no$+" "+opt11a.line_no$)
                OutVect!.addItem("Ship To:")
                OutVect!.addItem(pot14a.shipto_no$)
                a=fnblank(2)

                a=fnblank(2)
                OutVect!.addItem("Name"+custname$)
                OutVect!.addItem(dropname$)
                a=fnblank(7)
                OutVect!.addItem("Address"+custaddr$(1,30))
                OutVect!.addItem(dropaddr$(1,30))
                a=fnblank(5)
                l=l+3

                if custaddr$(31,30)<>fill(30) or dropaddr$(31,30)<>fill(30) 
                    a=fnblank(2)
                    OutVect!.addItem("       "+custaddr$(31,30))
                    OutVect!.addItem(dropaddr$(31,30))
                    a=fnblank(5)
                    l=l+1
                endif

                if custaddr$(61,30)<>fill(30) or dropaddr$(61,30)<>fill(30) 
                    a=fnblank(2)
                    OutVect!.addItem("       "+custaddr$(61,30))
                    OutVect!.addItem(dropaddr$(61,30))
                    a=fnblank(5)
                    l=l+1
                endif

                if custaddr$(91,30)<>fill(30) or dropaddr$(91,30)<>fill(30) 
                    a=fnblank(2)
                    OutVect!.addItem("       "+custaddr$(91,30))
                    OutVect!.addItem(dropaddr$(91,30))
                    a=fnblank(5)
                    l=l+1
                endif

            endif

        endif

    endif

    return

report_heading: rem --- Report Heading

    l=headings+1
    l=l+3

    return

assignheads: rem --- Assign headings variables

    dim tmph3$(width),tmph4$(width)
    if printseq$="A" vendf$=vendfrom$ else if vendfrom$<>"" vendf$=fnmask$(vendfrom$(1,vendlen),vendomask$)
    if vendfrom$="" vendf$="First"
    if printseq$="A" vendt$=vendthru$ else if vendthru$<>"" vendt$=fnmask$(vendthru$(1,vendlen),vendomask$)
    if vendthru$="" vendt$="Last"
    if datefrom$="" datef$="First" else datef$=fndate$(datefrom$)
    if datethru$="" datet$="Last" else datet$=fndate$(datethru$)
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$
    headings$[2]=leveldesc$+" Sorted By "+seqdesc$
    headings$[3]="Vendor: From "+vendf$+" To "+vendt$
    headings$[4]="From Receipt Date: "+datef$+" To "+datet$
    headings$[5]=Translate!.getTranslation("AON_EXCLUDE_INACTIVE")+Translate!.getTranslation("AON_VENDORS")+" = "+inactive$

    return

testbreaks: rem --- Test for Breaks

    if pot04a.vendor_id$<>vendor$
        if printptot$="Y" gosub pototals
        if printvtot$="Y" gosub vendtotals
        gosub newvendor
        gosub newpo
    else
        if pot04a.po_no$<>ponum$ then 
            if printptot$="Y" then 
                gosub pototals
                gosub newpo
            else 
                if pot04a.receiver_no$<>receivernum$ then 
                    gosub newrcpt
                endif
            endif
        endif
    endif

    return

newvendor: rem --- New Vendor

    vendtot=0
    vendname$="Not On File"
    vendor$=pot04a.vendor_id$
    printvtot$="Y"
    find record (apm01a_dev,key=firm_id$+vendor$,dom=brnch5) apm01a$
    vendname$=apm01a.vendor_name$
brnch5: 
    if l+2>l9 gosub report_heading
    OutVect!.addItem("Vendor "+fnmask$(vendor$(1,vendlen),vendomask$)+" "+vendname$)
    a=fnblank(8)
    l=l+1

    return

newpo: rem --- New PO

    rcpttot=0
    printptot$="Y"
    potot=0
    ponum$=pot04a.po_no$
    receivernum$=pot04a.receiver_no$
    termsdesc$="Not On File"
    paddrdesc$=""
    shpwhsdesc$="Not On File"
    find record (apm10c_dev,key=firm_id$+"C"+pot04a.ap_terms_code$,dom=brnch6) apm10c$
    termsdesc$=apm10c.code_desc$

brnch6: 

    find record (apm05a_dev,key=firm_id$+vendor$+pot04a.purch_addr$,dom=brnch7) apm05a$
    paddrdesc$=apm05a.name$(1,24)

brnch7: 

    find record (ivm10c_dev,key=firm_id$+"C"+pot04a.warehouse_id$,dom=brnch8) ivm10c$
    shpwhsdesc$=ivm10c.short_name$

brnch8: 

    if l+5>l9 gosub report_heading
    dim dateord$(8)
    if pot04a.ord_date$<>fill(8) dateord$=fndate$(pot04a.ord_date$)
    a=fnblank(9)
    OutVect!.addItem("PO #: "+ponum$+" Ordered "+dateord$)
    OutVect!.addItem("Terms"+pot04a.ap_terms_code$+" "+termsdesc$)
    OutVect!.addItem("Ship Via")
    OutVect!.addItem(pot04a.ap_ship_via$)

    if paddrdesc$<>"" 
        OutVect!.addItem("Purchase From")
        OutVect!.addItem(pot04a.purch_addr$)
        OutVect!.addItem(paddrdesc$)
    else
        a=fnblank(3)
    endif

    a=fnblank(2)

    OutVect!.addItem("Rec#: "+receivernum$)
    OutVect!.addItem("Ack By"+pot04a.acknowledge$)
    OutVect!.addItem("F.O.B.")
    OutVect!.addItem(pot04a.fob$)
    OutVect!.addItem("Ship To ")
    OutVect!.addItem(pot04a.warehouse_id$+" "+shpwhsdesc$)
    a=fnblank(12)
    l=l+4

    return

vendtotals: rem --- Print Vendor total

    if l+1>l9 gosub report_heading
    TotVect!.addItem(str(OutVect!.size()/columns))
    a=fnblank(6)
    OutVect!.addItem("Total For Vendor")
    OutVect!.addItem(fnmask$(vendor$(1,vendlen),vendomask$))
    OutVect!.addItem(str(vendtot:m1$)+$00$+"R")
    l=l+1

    return

pototals: rem --- Print PO total

    if l+1>l9 gosub report_heading
    TotVect!.addItem(str(OutVect!.size()/columns))
    a=fnblank(6)
    OutVect!.addItem("Total For PO #")
    OutVect!.addItem(ponum$)
    OutVect!.addItem(str(potot:m1$)+$00$+"R")
    l=l+1

    return

rcpttotals: rem --- Print Receiver Total

    if l+1>l9 gosub report_heading
    TotVect!.addItem(str(OutVect!.size()/columns))
    a=fnblank(6)
    OutVect!.addItem("Total For Receiver # ")
    OutVect!.addItem(receivernum$)
    OutVect!.addItem(str(rcpttot:m1$)+$00$+"R")
    l=l+1

    return

newrcpt: rem --- New Receiver

    rcpttot=0,receivernum$=pot04a.receiver_no$
    OutVect!.addItem("Rec#: "+receivernum$)
    a=fnblank(8)
    l=l+1

    return

rem --- Calc extension for S, N and O types; init it for others

calc_extension: 

    if pos(linetype$="SNO")<>0
        let qty=qtyrec
        if linetype$="O" qty=1
        precision 2
        let extension=pot14a.unit_cost*qty,extension$=str(extension:m1$)
        precision ivprecision
    endif

    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnBlank : Create blank cells in OutVect!. No return value

    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend


rem #endinclude std_functions.src

rem #include disp_message.src

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$

std_missing_params_exit: 

    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape

std_missing_params_release: 

    status=999
    if pgm(-1)<>pgm(-2) exit 
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

std_exit_no_report: 

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status    
    OutVect!=null()
    goto std_exit

    end
