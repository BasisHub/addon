rem --- Inventory Transfer Register
rem --- Program ivr_transferreg.aon v8.0.0 09Jul2007 (ivr_ga)
rem --- Created by adx_codeport.bbx v1.1.5 (07/09/2007 11:40:02)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM")

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL")
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO")
    firm_id$=sysinfo.firm_id$
    
rem --- set rd_alias_id$, if needed, for DocOut Settings
    
    if rd_alias_id$=""
        rd_alias_id$=pgm(-2)
        rd_alias_id$=rd_alias_id$(max(pos("/"="/"+rd_alias_id$,-1),pos("\"="\"+rd_alias_id$,-1)))
        rd_alias_id$=rd_alias_id$(1,pos("."=rd_alias_id$+".")-1)
    endif    
    
rem --- see if batching (batching.CAH)

    call stbl("+DIR_PGM")+"adc_getbatch.aon","",pgm(-2),rd_table_chans$[all]
    batch_no$=stbl("+BATCH_NO")
    process_id$=stbl("+PROCESS_ID")    

rem --- Run Report?

    msg_id$="AON_RUN_QUERY"
    dim msg_tokens$[1]
    msg_tokens$[1]=sysinfo.task_desc$
    gosub disp_message
    if msg_opt$<>"Y" then goto std_exit_no_report

rem --- Open/Lock files

    files=30,begfile=6,endfile=14
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[6]="ive-02",        ids$[6]="IVE_TRANSFER"
    files$[7]="ivm-01",        ids$[7]="IVM_ITEMMAST"
    files$[8]="ivm-02",        ids$[8]="IVM_ITEMWHSE"
    files$[10]="ivc_whsecode", ids$[10]="IVC_WHSECODE"
    files$[12]="ars_params",   ids$[12]="ARS_PARAMS"
    files$[13]="gls_params",   ids$[13]="GLS_PARAMS"
    files$[14]="ivs_params",   ids$[14]="IVS_PARAMS"
    
    if num(batch_no$)=0 then options$[6]="L";rem lock if not batching.CAH    

    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status then goto std_exit_no_report

    ive02_dev  = channels[6]
    ivm01_dev  = channels[7]
    ivm02_dev  = channels[8]
    ivm10c_dev = channels[10]
    ars01a_dev = channels[12]
    gls01a_dev = channels[13]
    ivs01a_dev = channels[14]

rem --- Dimension string templates

    dim ive02a$:templates$[6]
    dim ivm01a$:templates$[7]
    dim ivm02a$:templates$[8]
    dim ivm10c$:templates$[10]
    dim ars01a$:templates$[12]
    dim gls01a$:templates$[13]
    dim ivs01a$:templates$[14]

rem --- Parameters

    dim p[5],info$[20]
    n2$="IV"
    source$=pgm(-2)
    when$=sysinfo.system_date$

    find record (ivs01a_dev,key=firm_id$+"IV00",err=std_missing_params) ivs01a$

    p[0]=ivs01a.item_id_len
    p[1]=ivs01a.ls_no_len
    p[2]=num(ivs01a.precision$)
    p[3]=num(ivs01a.desc_len_01$)
    p[4]=num(ivs01a.desc_len_02$)
    p[5]=num(ivs01a.desc_len_03$)

    lf$="N"
    ls$="N"
    if pos(ivs01a.lifofifo$="LF")    then lf$="Y"
    if pos(ivs01a.lotser_flag$="LS") then ls$="Y"

    ar$="N"
    dist_gl_by_item$="N"
    call pgmdir$+"adc_application.aon","AR",info$[all]
    ar$=info$[20]
    if ar$="Y" then
        find record (ars01a_dev,key=firm_id$+"AR00",dom=std_missing_params) ars01a$
        dist_gl_by_item$=ars01a.dist_by_item$
        places=num(ivs01a.precision$)
    endif

    gl$="N"
    status=0
    if dist_gl_by_item$="Y" then call pgmdir$+"glc_ctlcreate.aon",err=*next,source$,n2$,glw11$,gl$,status
    if status then goto std_exit_no_report

    if gl$="Y" then 
        find record (gls01a_dev,key=firm_id$+"GL00",err=std_missing_params) gls01a$
    endif

rem --- Document initializations

    declare BBjVector OutVect!
    declare BBjVector HdrVect!
    declare BBjVector BrkVect!
    declare BBjVector TotVect!

    OutVect!=bbjAPI().getSysGui().makeVector()
    HdrVect!=bbjAPI().getSysGui().makeVector()
    BrkVect!=bbjAPI().getSysGui().makeVector()
    TotVect!=bbjAPI().getSysGui().makeVector()

    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)

rem --- Other initializations

    precision num(ivs01a.precision$)

    dim o[4]

	call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","A","",m1$,0,m1
	call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","U","",m2$,0,m2
	call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","C","",m3$,0,m3

    if ls$ = "Y" then p8$=Translate!.getTranslation("AON__AND_LOT/SERIAL") else p8$ = ""

    width=132
    units=0

    o[4]=width-9
    o[3]=o[4]-m1-1
    o[2]=o[3]-m2
    o[1]=o[2]-m3
    o[0]=o[1]-3

    errors=0
    glstatus=0
    more=1
    recs=0

rem --- Column headings

    columns=7
    dim columns$[columns,10]
    col_2$ = Translate!.getTranslation("AON_ITEM_") + p8$ + Translate!.getTranslation("AON_^DESCRIPTION")

    columns$[0,0]=Translate!.getTranslation("AON_TO"),         columns$[0,1]="C", columns$[0,2]="5"
    columns$[1,0]=Translate!.getTranslation("AON_WAREHOUSE"),  columns$[1,1]="C", columns$[1,2]="24"
    columns$[2,0]=col_2$,       columns$[2,1]="C", columns$[2,2]=str( p[0]+p[1]+1 )
    columns$[3,0]="UM",         columns$[3,1]="C", columns$[3,2]="5"
    columns$[4,0]=Translate!.getTranslation("AON_COST"),       columns$[4,1]="N", columns$[4,2]=str(m3), columns$[4,3]=m3$
    columns$[5,0]=Translate!.getTranslation("AON_QUANTITY"),   columns$[5,1]="N", columns$[5,2]=str(m2), columns$[4,3]=m2$
    columns$[6,0]=Translate!.getTranslation("AON_TOTAL_COST"), columns$[6,1]="N", columns$[6,2]=str(m1), columns$[6,3]=m1$, columns$[5,10]="T" 
    columns$[7,0]=Translate!.getTranslation("AON_REFERENCE"),  columns$[7,1]="C", columns$[7,2]="9"

rem --- Report headings

    headings=3
    dim headings$[headings]
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$ 
    if num(batch_no$)<>0 then headings$[1]=headings$[1]+ Translate!.getTranslation("AON__(BATCH:_")+batch_no$+")"    
    headings$[headings]="[HEADER]"

rem --- Additional File Opens

    if lf$="Y" then 
        files$[20]="ivm-04", ids$[20]="IVM_ITEMTIER"
        files$[21]="ivw-04", ids$[21]="IVW_LFDET", options$[21]="CL"
    endif

    if gl$="Y" then
        files$[22]="glm-01", ids$[22]="GLM_ACCT"
        files$[23]="glw-11",   ids$[23]="GLW_DAILYDETAIL"
        rem --- options$[23]="CL";do clear partial instead of full clear since everyone sharing glw-11. (batching.CAH)
    endif

    if gl$="Y" and dist_gl_by_item$="Y" then 
        files$[24]="arc_distcode", ids$[24]="ARC_DISTCODE"
    endif

    begfile=20, endfile=24
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status    
    if status then goto std_exit_no_report

    ivm04_dev  = channels[20]
    ivw04_dev  = channels[21]
    glm01_dev  = channels[22]
    glw11_dev  = channels[23]
    arm10d_dev = channels[24]

    if gl$="Y" and dist_gl_by_item$="Y" then 
        dim arm10d$:templates$[24]
    endif

rem --- Clear trans for this firm/process/batch from glw-11 work file (if not batching, batch_no$ is zero, and will clear all recs this firm/process)

    if gl$="Y"
        rec_pfx$=firm_id$+process_id$+batch_no$
        call stbl("+DIR_PGM")+"adc_clearpartial.aon","N",glw11_dev,rec_pfx$,status
    endif

rem --- Lifo/fifo setup

    dim lf_chans[10]
    dim lf_tmpls$[10]
    dim lf_item$[10]

    lf_chans[1]=ivm02_dev
    lf_chans[2]=ivm04_dev
    lf_chans[3]=ivw04_dev

    lf_tmpls$[1]=templates$[8];  rem ivm-02
    lf_tmpls$[2]=templates$[20]; rem ivm-04
    lf_tmpls$[3]=templates$[21]; rem ivw-04

rem --- Progress meter

    call pgmdir$+"adc_progress.aon","N",sysinfo.task_desc$,"",Translate!.getTranslation("AON_PRINTING"),"",p[0]+3,0,1,meter_num,ignore_status

rem --- Initial Read

    prev_whse$=""
    prev_trans_date$=""
    last_time$="N"

    read (ive02_dev,key=firm_id$+batch_no$,knum="BATCH_KEY",dom=*next)

rem --- Heading Read

    while more
        read record (ive02_dev,end=*break) ive02a$
        if firm_id$ <> ive02a.firm_id$ or batch_no$ <> ive02a.batch_no$ then break
        cost=ive02a.unit_cost

rem --- Breaks

        if ive02a.warehouse_id$ <> prev_whse$ then 
            gosub trans_date_totals
            gosub whse_totals
            gosub whse_new
            gosub trans_date_new
        else
            if ive02a.trans_date$<>prev_trans_date$ then 
                gosub trans_date_totals
                gosub trans_date_new
            endif
        endif

        call pgmdir$+"adc_progress.aon","S","","","",ive02a.warehouse_id$+" "+ive02a.item_id$,0,0,1,meter_num,ignore_status

        gosub get_item_data
        
        prev_whse$=ive02a.warehouse_id$
        prev_trans_date$=ive02a.trans_date$

rem --- LIFO/FIFO?

        if lf$="Y" then

            lf_item$[1] = ive02a.warehouse_id$
            lf_item$[2] = ive02a.item_id$
            lf_date$    = ive02a.trans_date$
            action$     = ivs01a.lifofifo$ + "I"
            quantity    = ive02a.trans_qty
            cost        = ive02a.unit_cost
            ret_cost    = 0
            type$       = "R"

            call pgmdir$+"ivc_lifofifo.aon",action$,type$,lf_date$,lf_chans[all],lf_tmpls$[all],lf_item$[all],places,quantity,cost,ret_cost,ignore_status

rem --- Cost change?

            if ret_cost and ive02a.ext_cost <> ive02a.trans_qty * ret_cost then
                ive02a.ext_cost = ive02a.trans_qty * ret_cost
                ive02a.unit_cost = ret_cost
                cost = ret_cost

                ive02a$ = field(ive02a$)
                write record (ive02_dev) ive02a$
            endif

        endif

rem --- Detail line

        tot_cost=round(ive02a.ext_cost, 2)
        OutVect!.addItem(ive02a.warehouse_id_to$)
        OutVect!.addItem(ivm10c.short_name$)
        OutVect!.addItem(ive02a.item_id$+" "+ive02a.lotser_no$)
        OutVect!.addItem(um$)
        OutVect!.addItem(str(cost))
        OutVect!.addItem(str(ive02a.trans_qty))
        OutVect!.addItem(str(tot_cost))
        OutVect!.addItem(ive02a.inv_xfer_no$)

        ignore = fnblank(2)
        OutVect!.addItem(item_desc$)
        ignore = fnblank(5)

        whse_total=whse_total+tot_cost
        date_total=date_total+tot_cost
        recs=recs+1
        
rem --- Warn if date is outside the GL period

        if gl$="Y" then
            call pgmdir$+"glc_datecheck.aon",ive02a.trans_date$,"N",period$,year$,glstatus

            if glstatus then
                call pgmdir$+"glc_datewarn.aon",0,ive02a.trans_date$,glstatus,lines,errors,ignore_status

                if stbl("+DTWARN_MSG",err=*endif)<>""
                    ignore = fnblank(2)
                    OutVect!.addItem(stbl("+DTWARN_MSG"))
                    ignore = fnblank(5)
                endif
            endif
        endif

rem --- Generate G/L Recap

        if gl$<>"Y" or dist_gl_by_item$<>"Y" then continue

rem --- From Warehouse

        gldate$=ive02a.trans_date$
        ref1$=Translate!.getTranslation("AON_XFR_")+ive02a.inv_xfer_no$
        memo$=ive02a.item_id$+" "+item_desc$
        ref2$=Translate!.getTranslation("AON_FR_WHSE_")+ive02a.warehouse_id$
        ref3$=Translate!.getTranslation("AON_TO_WHSE_")+ive02a.warehouse_id_to$

        find record (ivm02_dev,key=firm_id$+ive02a.warehouse_id$+ive02a.item_id$,dom=to_whse) ivm02a$
        find record (arm10d_dev,key=firm_id$+"D"+ivm02a.ar_dist_code$,dom=to_whse) arm10d$

        account$=arm10d.gl_inv_acct$
        amount=-(ive02a.trans_qty)*cost
        units=0

        gosub glrecap

to_whse: rem --- To Warehouse

        x$=ref2$
        ref2$=ref3$
        ref3$=x$

        find record (ivm02_dev,key=firm_id$+ive02a.warehouse_id_to$+ive02a.item_id$,dom=gl_recap_done) ivm02a$
        find record (arm10d_dev,key=firm_id$+"D"+ivm02a.ar_dist_code$,dom=gl_recap_done) arm10d$

        account$=arm10d.gl_inv_acct$
        amount=ive02a.trans_qty*cost
        units=0

        gosub glrecap

gl_recap_done:

    wend

rem --- End of register

    if recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

rem --- Last break
    last_time$="Y"
    gosub trans_date_totals
    gosub whse_totals

rem --- Destroy progress meter
    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,ignore_status

rem --- Don't update if there are errors in the register
    update$="ivu_transferreg.aon"

    if errors
        gosub errors_in_register
        if override$<>"Y" then update$=""
    endif

rem --- Determine overlay 
    if gl$="Y" and dist_gl_by_item$="Y" then 
        overlay$=pgmdir$+"glr_recap.aon"
    else
        overlay$=update$
    endif

rem --- Print register via DocOut
    out_action$="PRT-PDF"
    next_overlay$=overlay$
    if update$=""
        goto std_exit_no_update
    else
        goto std_exit
    endif

whse_totals: rem --- Previous Warehouse Totals

    if prev_whse$<>"" then

        ignore = fnblank_line(void)

        TotVect!.addItem(str(OutVect!.size()/(columns+1)))
        ignore = fnblank(2)
        OutVect!.addItem(Translate!.getTranslation("AON_**TOTAL_FOR_WAREHOUSE_") + prev_whse$ + " " + whse_desc$)
        ignore = fnblank(3)
        OutVect!.addItem(str(whse_total))
        ignore = fnblank(1)

        whse_total=0

    endif
    
    return

whse_new: rem --- Start a new warehouse 

    if last_time$="N" then 

        ivm10c.short_name$=Translate!.getTranslation("AON_UNKNOWN_WAREHOUSE")
        date_total=0
        find record (ivm10c_dev,key=firm_id$+"C"+ive02a.warehouse_id$,dom=*next) ivm10c$

rem --- Break goes on the header
        new_whse$ = Translate!.getTranslation("AON_FROM_WAREHOUSE_") + cvs(ive02a.warehouse_id$ + " " + ivm10c.short_name$, 2)
        BrkVect!.addItem(str(OutVect!.size()/(columns+1)))
        HdrVect!.addItem(new_whse$)
        whse_desc$=cvs(ivm10c.short_name$,2)

rem --- Print new whse on the report
        ignore = fnblank_line(void)

        ignore = fnblank(2)
        OutVect!.addItem(new_whse$)
        ignore = fnblank(5)

    endif

    return

trans_date_totals: rem --- Trans Date, previous totals

    if prev_trans_date$<>"" then

        ignore = fnblank_line(void)

        TotVect!.addItem(str(OutVect!.size()/(columns+1)))
        ignore = fnblank(2)
        OutVect!.addItem(Translate!.getTranslation("AON_*TOTAL_FOR_")+fndate$(prev_trans_date$))
        ignore = fnblank(3)
        OutVect!.addItem(str(date_total))
        ignore = fnblank(1)

    endif

    return

trans_date_new: rem --- Trans Date: start new date  

    if last_time$="N" then

        date_total=0

rem --- Print break on header
        new_date$ = Translate!.getTranslation("AON_TRANSFER_DATE_") + fndate$(ive02a.trans_date$)
        rem debug --- I don't think you can have two page-level breaks in the same report
        rem BrkVect!.addItem(str(OutVect!.size()/(columns+1)))
        rem HdrVect!.addItem(new_date$)

rem --- Print break on report
        ignore = fnblank_line(void)

        ignore = fnblank(2)
        OutVect!.addItem(new_date$)
        ignore = fnblank(5)

    endif

    return

get_item_data: rem --- Get item data

    item_desc$=Translate!.getTranslation("AON_UNKNOWN_ITEM")
    um$=""
    ivm01a$=""
    ivm10c.short_name$=Translate!.getTranslation("AON_UNKNOWN_WAREHOUSE")
    find record (ivm01_dev,key=firm_id$+ive02a.item_id$,dom=*next) ivm01a$
    find record (ivm10c_dev,key=firm_id$+"C"+ive02a.warehouse_id_to$,dom=*next) ivm10c$

    if ivm01a$<>"" then 
        item_desc$ = fnitem$(ivm01a.item_desc$, p[3], p[4], p[5])
        um$ = ivm01a.purchase_um$
    endif

    return

glrecap: rem --- Standard G/L Recap Routine

    if gl$="Y" then
        call pgmdir$+"glc_glpost.aon",glm01_dev,glw11_dev,unused,account$,gldate$,ref1$,ref2$,ref3$,memo$,amount,units,ignore_status
    endif

    return

errors_in_register: rem --- Display errors in register message

    msg_id$="AON_REG_ERRS"
    override$="N"
    gosub disp_message
    if pos("PASSVALID"=msg_opt$) then override$="Y"
    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Date/time handling functions

    def fndate$(q$)
    q1$=""
    q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
    if q1$="" q1$=q$
    return q1$
    fnend

    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
    if q2$="" q2$=fill(len(q1$),"0")
    return str(-num(q1$,err=*next):q2$,err=*next)
    q=1
    q0=0
    while len(q2$(q))
        if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
        q=q+1
    wend
    if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
    return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
    q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
    if q then q$=q$(q+1)
    if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
    q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
    return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
    q1$=stbl(q$,err=*next)
    return q1$
    fnend

rem #endinclude std_functions.src

rem --- fnBlank : Create blank cells in OutVect!. No return value

    def fnblank(q0)
    for q1=1 to q0
        OutVect!.addItem("")
    next q1
    return void
    fnend

rem --- Create a blank line.  'void' is simply a variable; it's there to remind you that this function takes no input

    def fnblank_line(void)=fnblank(columns+1)

rem #include disp_message.src

disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,rd_table_chans$[all]

    return

rem #endinclude disp_message.src

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_SYP",err=std_error_exit)
    call pgmdir$+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:   str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit_no_report:

    OutVect!=null();rem nothing to print, or some other err such that no report
    
std_exit_no_update:

    if num(batch_no$)<>0
        unlock_key$=firm_id$+stbl("+PROCESS_ID")+stbl("+BATCH_NO")
        lock_status$=""
        call stbl("+DIR_SYP")+"bac_lock_record.bbj","ADM_PROCBATCHES",unlock_key$,"X","",rd_table_chan,rd_table_chans$[all],lock_status$
    endif

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status
    run stbl("+DIR_SYP",err=*next)+"bas_process_end.bbj",err=*next
    release

rem #endinclude std_end.src

    end
