rem --- Rebuild Inventory On-Order Quantities
rem --- Program ivx_ea v8.0.0 09Jul2007 (ivx_ea)
rem --- Created by adx_codeport.bbx v1.1.5 (07/09/2007 11:55:30)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright (c) 1981-2007 AddonSoftware
rem --- All Rights Reserved

    seterr std_error
    setesc std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)
    firm_id$=sysinfo.firm_id$
    
rem --- set rd_alias_id$, if needed, for DocOut Settings
    
    if rd_alias_id$=""
        rd_alias_id$=pgm(-2)
        rd_alias_id$=rd_alias_id$(max(pos("/"="/"+rd_alias_id$,-1),pos("\"="\"+rd_alias_id$,-1)))
        rd_alias_id$=rd_alias_id$(1,pos("."=rd_alias_id$+".")-1)
    endif

rem --- Get user approval to proceed

    msg_id$="AON_RUN_QUERY"
    dim x$:stbl("+SYSINFO_TPL")
    dim msg_tokens$[1]
    x$=stbl("+SYSINFO")                                                            
    msg_tokens$[1]=x.task_desc$
    gosub disp_message
    if msg_opt$<>"Y" then goto std_exit_no_update
    

rem --- Open/Lock files

    files=6,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[2]="ivm-02",ids$[2]="IVM_ITEMWHSE"
    files$[3]="poe-12",ids$[3]="POE_PODET"
    files$[5]="sfe-01",ids$[5]="SFE_WOMASTR"
    files$[6]="ivs_params",ids$[6]="IVS_PARAMS"
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit_no_update
    
    ivm02_dev=channels[2]
    poe12_dev=channels[3]
    sfe01_dev=channels[5]
    ivs01a_dev=channels[6]
    
rem --- Dimension string templates

    dim ivm02a$:templates$[2],poe12a$:templates$[3],
:       sfe01a$:templates$[5],ivs01a$:templates$[6]

rem --- Retrieve parameter records

    ivs01a_key$=firm_id$+"IV00"
    find record (ivs01a_dev,key=ivs01a_key$,err=std_missing_params) ivs01a$

rem --- Parameters

    dim p[1],info$[20]
    p[0]=num(ivs01a.item_id_len$)
    p[1]=num(ivs01a.precision$)
    call pgmdir$+"adc_application.aon","PO",info$[all]
    po$=info$[20]
    call pgmdir$+"adc_application.aon","SF",info$[all]
    sf$=info$[20]
    
    if po$<>"Y" and sf$<>"Y"
        msg_id$="IV_REBUILD_OO"
        gosub disp_message
        goto std_exit_no_update
    endif
    
 
rem --- Initializations

        precision num(ivs01a.precision$)
        more=1

rem --- Position warehouse file

        x$=Translate!.getTranslation("AON_CLEARING_CURRENT_ON-ORDER_QUANTITIES")
        x=3+p[0]
        gosub Update_Screen
        read (ivm02_dev,key=firm_id$,dom=*next)

rem --- Clear ivm-02 On Order
        while more
            ivm02a_key$=key(ivm02_dev,end=*break)
            if pos(firm_id$=ivm02a_key$)<>1 break
            call pgmdir$+"adc_progress.aon","S","","","",ivm02a.warehouse_id$+" "+ivm02a.item_id$,0,0,1,meter_num,status
            extract record (ivm02_dev,key=ivm02a_key$) ivm02a$
            ivm02a.qty_on_order=0
            write record (ivm02_dev,key=ivm02a_key$) ivm02a$
        wend

rem --- Purchase Orders

        if po$="Y" then
            x$=Translate!.getTranslation("AON_PURCHASE_ORDER_ON-ORDER_QUANTITIES")
            x=3+p[0]
            gosub Update_Screen
            read (poe12_dev,key=firm_id$,knum="ITEM_PO",dom=*next)
        
rem --- Read next PO record; use knum=ITEM_PO, po's by item
            while more
                read record(poe12_dev,end=*break)poe12a$
                if pos(firm_id$=poe12a$)<>1 break
                if cvs(poe12a.item_id$,3)="" then continue
                call pgmdir$+"adc_progress.aon","S","","","",poe12a.item_id$,0,poe12_dev,1,meter_num,status
                ivm02a_key$=ivm02a.firm_id$+poe12a.warehouse_id$+poe12a.item_id$
                extract record (ivm02_dev,key=ivm02a_key$,dom=*continue) ivm02a$
                ivm02a.qty_on_order=ivm02a.qty_on_order+((poe12a.qty_ordered-poe12a.qty_received)*poe12a.conv_factor)
                ivm02a$=field(ivm02a$)
                write record (ivm02_dev,key=ivm02a.firm_id$+ivm02a.warehouse_id$+ivm02a.item_id$) ivm02a$
            wend
        endif
        
rem --- Work Orders

        if sf$="Y" then
            x$=Translate!.getTranslation("AON_WORK_ORDER_ON-ORDER_QUANTITIES")
            x=7
            gosub Update_Screen
            read (sfe01_dev,key=firm_id$,dom=*next)

rem --- Read next work order
            while more
                sfe01a_key$=key(sfe01_dev,end=*break)
                if pos(firm_id$=sfe01a_key$)<>1 break
                call pgmdir$+"adc_progress.aon","S","","","",sfe01a.wo_no$,0,sfe01_dev,1,meter_num,status
                read record (sfe01_dev) sfe01a$

rem --- Is it open?

                if sfe01a.wo_category$<>"I" continue
                if pos(sfe01a.wo_status$="CPQ")>0 continue
                if sfe01a.sch_prd_qty<=sfe01a.qty_cls_todt continue

rem --- Update warehouse

                ivm02a_key$=sfe01a.firm_id$+sfe01a.warehouse_id$+sfe01a.item_id$
                extract record (ivm02_dev,key=ivm02a_key$,dom=*continue) ivm02a$
                ivm02a.qty_on_order=ivm02a.qty_on_order+(sfe01a.sch_prd_qty-sfe01a.qty_cls_todt)
                ivm02a$=field(ivm02a$)                
                write record (ivm02_dev,key=ivm02a_key$) ivm02a$
            wend
        endif
        
done: rem --- All done

        goto std_exit
    

Update_Screen: rem --- Update screen

    call pgmdir$+"adc_progress.aon","N","","","","",0,0,1,meter_num,status
    return
    
disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return        

rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_SYP",err=std_error_exit)
    call pgmdir$+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:   str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    msg_id$="UPDATE_COMPLETE"
    dim msg_tokens$[1]
    msg_opt$=""
    gosub disp_message

std_exit_no_update:

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    rem run pgmdir$+"ads_process_end.aon",err=*next
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release

rem #endinclude std_end.src

    end
