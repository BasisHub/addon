rem --- Cost Change Register
rem --- Program ivr_costchgreg.aon v8.0.0 09Jul2007 (ivr_ja)
rem --- Created by adx_codeport.bbx v1.1.5 (07/09/2007 11:42:29)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    seterr std_error
    setesc std_error
    
rem --- Use for type checking

    use ::sys/prog/bao_option.bbj::Option
    
rem --- Retrieve the program path
    
    pgmdir$=""
    pgmdir$=stbl("+DIR_PGM",err=*next)
    sypdir$=""
    sypdir$=stbl("+DIR_SYP",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL")
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO")
    firm_id$=sysinfo.firm_id$

rem --- Assign form input values to local variables
    
    declare Option option!
    effect_date$ = option!.getOptionData("EFFECT_DATE")

rem --- Test Effective Date against System Date

	if effect_date$ > sysinfo.system_date$
		msg_id$="IV_COST_CHG_BAD_DATE"
		gosub disp_message
		if pos("PASSVALID"=msg_opt$)=0
			goto std_exit
		endif
	endif

rem --- get batch# (zero if not batching.CAH)

    batch_no$=stbl("+BATCH_NO",err=*next)

rem --- Open all files

    num_files = 7
    open_beg  = 1
    open_end  = 6
    
    dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
    
    open_tables$[1]="IVS_PARAMS",   open_opts$[1]="OTA"
    open_tables$[2]="IVE_COSTCHG",  open_opts$[2]="OTA"
    open_tables$[3]="IVM_ITEMMAST", open_opts$[3]="OTA"
    open_tables$[4]="IVM_ITEMWHSE", open_opts$[4]="OTA"
    open_tables$[5]="IVC_PRODCODE", open_opts$[5]="OTA"
    open_tables$[6]="IVC_WHSECODE", open_opts$[6]="OTA"
    
    if num(batch_no$)=0 then let open_opts$[2]=open_opts$[2]+"L"; rem lock only if not batching

    gosub open_tables

    ivs_params_dev = num( open_chans$[1] )
    costchg_dev    = num( open_chans$[2] )
    itemmast_dev   = num( open_chans$[3] )
    itemwhse_dev   = num( open_chans$[4] )
    prodcode_dev   = num( open_chans$[5] )
    whsecode_dev   = num( open_chans$[6] )
    
    dim ivs_params_rec$:open_tpls$[1]
    dim costchg_rec$:open_tpls$[2]
    dim itemmast_rec$:open_tpls$[3]
    dim itemwhse_rec$:open_tpls$[4]
    dim prodcode_rec$:open_tpls$[5]
    dim whsecode_rec$:open_tpls$[6] 
    
rem --- Parameters

    dim p[5]
    n2$="IV"
    find record (ivs_params_dev, key=firm_id$+n2$+"00", dom=std_missing_params) ivs_params_rec$
    
    if ivs_params_rec.cost_method$<>"S" then goto std_cost
    
    p[0]=ivs_params_rec.item_id_len
    p[1]=num(ivs_params_rec.precision$)
    p[3]=num(ivs_params_rec.desc_len_01$)
    p[4]=num(ivs_params_rec.desc_len_02$)
    p[5]=num(ivs_params_rec.desc_len_03$)
    
    if pos(ivs_params_rec.lotser_flag$="LS") then ls$="Y" else ls$="N"
    
rem --- Additional opens
rem --- This is used in the overlay, not here

    if ls$="Y" then 
        open_beg = 7, open_end = 7
        open_tables$[7]="IVM_LSMASTER", open_opts$[7]="OTA"
        gosub open_tables
        lsmaster_dev = num( open_chans$[7] )
        dim lsmaster_rec$:open_tpls$[7] 
    endif

rem --- Initializations

    precision p[1]
    
    width=132
    m9$="-###0.00%"
    m9=len(m9$)
	call stbl("+DIR_PGM")+"adc_getmask.aon","","IV","C","",m3$,0,m3

rem --- Print positions
   
    dim o[8]
    o[7]=width-m3
    o[6]=o[7]-m9
    o[5]=o[6]-m3
    o[4]=o[5]-m3
    o[3]=o[4]-m3
    o[2]=o[3]-3
    o[1]=p[0]+1
    v=o[2]-o[1]
    dw=p[3]+p[4]+p[5]
    if dw>v then split=1,dw=o[2]-3 else split=0
    dim description$(dw)

rem --- Document initializations

    declare BBjVector OutVect!
    declare BBjVector HdrVect!
    declare BBjVector BrkVect!
    declare BBjVector TotVect!

    OutVect!=bbjAPI().getSysGui().makeVector()
    HdrVect!=bbjAPI().getSysGui().makeVector()
    BrkVect!=bbjAPI().getSysGui().makeVector()
    TotVect!=bbjAPI().getSysGui().makeVector()
    
    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)   
    
rem --- Column headings

    columns=7
    dim columns$[columns,10]
    
    columns$[0,0]=Translate!.getTranslation("AON_ITEM"),           columns$[0,1]="C", columns$[0,2]=str(p[0])
    columns$[1,0]=Translate!.getTranslation("AON_DESCRIPTION"),    columns$[1,1]="C", columns$[1,2]=str(dw)
    columns$[2,0]="UM",             columns$[2,1]="C", columns$[2,2]="2"
    columns$[3,0]=Translate!.getTranslation("AON_CURRENT^COST"),   columns$[3,1]="N", columns$[3,2]=str(m3), columns$[3,3]=m3$
    columns$[4,0]=Translate!.getTranslation("AON_NEW^COST"),       columns$[4,1]="N", columns$[4,2]=str(m3), columns$[4,3]=m3$
    columns$[5,0]=Translate!.getTranslation("AON_COST^CHANGE"),    columns$[5,1]="N", columns$[5,2]=str(m3), columns$[5,3]=m3$
    columns$[6,0]=Translate!.getTranslation("AON_PERCENT^CHANGE"), columns$[6,1]="N", columns$[6,2]=str(m9), columns$[6,3]=m9$
    columns$[7,0]=Translate!.getTranslation("AON_GROSS^MARGIN"),   columns$[7,1]="N", columns$[7,2]=str(m3), columns$[7,3]=m3$
    
rem --- Report headings

    headings=3
    dim headings$[headings]
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$ 
    if num(batch_no$)<>0 then headings$[1]=headings$[1]+ Translate!.getTranslation("AON__(BATCH:_")+batch_no$+")"    
    headings$[headings]="[HEADER]"

rem --- Sort by product type using SQL  

    call pgmdir$+"adc_progress.aon","N","","","","",0,costchg_dev,1,meter_num,ignore_status
    
    unlock(costchg_dev,err=*next); rem SQL won't execute with file locked

    sql_prep$ = ""
    sql_prep$ = sql_prep$ + "SELECT ive_costchg.effect_date, ive_costchg.warehouse_id, ivm_itemmast.product_type, ive_costchg.item_id "
    sql_prep$ = sql_prep$ + "  FROM ive_costchg INNER JOIN ivm_itemmast ON ive_costchg.firm_id = ivm_itemmast.firm_id AND "
    sql_prep$ = sql_prep$ + "       ive_costchg.item_id = ivm_itemmast.item_id "
    sql_prep$ = sql_prep$ + " WHERE ive_costchg.firm_id = '" + firm_id$ + "' AND ive_costchg.batch_no = '" + batch_no$ + "' AND "
    sql_prep$ = sql_prep$ + "       ive_costchg.effect_date <= '" + effect_date$ +"' "
    sql_prep$ = sql_prep$ + "ORDER BY ive_costchg.effect_date, ive_costchg.warehouse_id, ivm_itemmast.product_type$, ive_costchg.item_id "

    sql_chan=sqlunt
    sqlopen(sql_chan)stbl("+DBNAME")
    sqlprep(sql_chan)sql_prep$
    dim sql_rec$:sqltmpl(sql_chan)
    sqlexec(sql_chan)

rem --- Inits for record loop

    call pgmdir$+"adc_progress.aon","S","",Translate!.getTranslation("AON_PRINTING"),"","",0,costchg_dev,1,0,ignore_status

    prev_date$ = ""
    prev_whse$ = ""
    prev_type$ = ""
    errors = 0
    number = 0
    more = 1

rem --- Next SQL record

    while more

        sql_rec$=sqlfetch(sql_chan,err=*break)
        
        call pgmdir$+"adc_progress.aon","S","","","","",0,costchg_dev,1,meter_num,ignore_status
        
        costchg_key$ = firm_id$ + batch_no$ + sql_rec.effect_date$ + sql_rec.warehouse_id$ + sql_rec.item_id$
        read record (costchg_dev, key=costchg_key$, knum="BATCH_KEY", dom=*continue) costchg_rec$
        
rem --- Additional reads

        find record (itemmast_dev, key=firm_id$+costchg_rec.item_id$, dom=*continue) itemmast_rec$
        find record (itemwhse_dev, key=firm_id$+costchg_rec.warehouse_id$+costchg_rec.item_id$, dom=*continue) itemwhse_rec$    
        
rem --- Level breaks?

        if costchg_rec.effect_date$<>prev_date$ then 
            gosub date_break
            gosub whse_break
            gosub product_break
        else
            if costchg_rec.warehouse_id$<>prev_whse$ then 
                gosub whse_break
                gosub product_break
            else
                if sql_rec.product_type$<>prev_type$ then
                    gosub product_break
                endif
            endif
        endif
        
rem --- Calculations
        
        dim description$(dw)
        description$(1) = fnitem$( itemmast_rec.item_desc$, p[3], p[4], p[5] )

        change  = costchg_rec.new_cost-itemwhse_rec.unit_cost
        margin  = itemwhse_rec.cur_price-costchg_rec.new_cost
        percent = 100
        
        if itemwhse_rec.unit_cost<>0 then
            percent = max( min( ((costchg_rec.new_cost - itemwhse_rec.unit_cost) * 100) / itemwhse_rec.unit_cost, 9999.99), -9999.99 )
        endif
        
rem --- Print detail line

        if split then 
            descr_hold$ = ""
        else
            descr_hold$ = description$
        endif
        
        OutVect!.addItem(costchg_rec.item_id$)
        OutVect!.addItem(descr_hold$)
        OutVect!.addItem(itemmast_rec.unit_of_sale$)
        OutVect!.addItem( str(itemwhse_rec.unit_cost) )
        OutVect!.addItem( str(costchg_rec.new_cost) )
        OutVect!.addItem( str(change) )
        OutVect!.addItem( str(percent) )
        OutVect!.addItem( str(margin) )
        
        if split then
            OutVect!.addItem("")
            OutVect!.addItem(description$)
            ignore = fnblank(6)
        endif
        
        number=number+1
        prev_date$=costchg_rec.effect_date$
        prev_whse$=costchg_rec.warehouse_id$
        prev_type$=sql_rec.product_type$
        
        if costchg_rec.new_cost<0 then errors=errors+1

rem --- Loop back for next record

    wend

rem --- All done

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,ignore_status
    
    if !number then
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif
    
rem --- Any negative costs?

    if errors then
        msg_id$ = "IV_REG_NEG_COSTS"
        call sypdir$+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,rd_table_chans$[all]
		if pos("PASSVALID"=msg_opt$)=0
			next_overlay$=""
			goto std_exit_no_update
		endif
    endif

rem --- Run update overlay

    rem --- Values for Price/Cost Update program
    channel = costchg_dev
    o0 = 1; rem signals a cost update
    first$ = firm_id$ + batch_no$ + effect_date$
    overlay$ = pgmdir$+"ivu_pricecostchgupdt.aon"
    
    rem --- Print register via DocOut
    out_action$="PRT-PDF"
    next_overlay$=overlay$
    goto std_exit

std_cost: rem --- Must be Standard Costing 

    msg_id$="IV_NO_STD_COST"
    call sypdir$+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,rd_table_chans$[all]
    status=999
    goto std_exit_no_report

date_break: rem --- Date Break

    BrkVect!.addItem( str(OutVect!.size()/(columns+1)) )
    HdrVect!.addItem( Translate!.getTranslation("AON_FOR_CHANGES_EFFECTIVE_")+fndate$(costchg_rec.effect_date$) )
    
    return

whse_break: rem --- Warehouse Break

    whsecode_rec.short_name$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
    find record (whsecode_dev, key=firm_id$+"C"+costchg_rec.warehouse_id$, dom=*next) whsecode_rec$
    
    ignore = fnblank_line(void)
    OutVect!.addItem( Translate!.getTranslation("AON_WAREHOUSE_")+costchg_rec.warehouse_id$+" "+cvs(whsecode_rec.short_name$,2) )
    ignore = fnblank(7)
    
    return

product_break: rem --- Product Break

    prodcode_rec.code_desc$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
    find record (prodcode_dev,key=firm_id$+"A"+sql_rec.product_type$,dom=*next) prodcode_rec$
    
    ignore = fnblank_line(void)
    OutVect!.addItem( Translate!.getTranslation("AON_PRODUCT_")+sql_rec.product_type$+" "+prodcode_rec.code_desc$ )
    ignore = fnblank(7)
    
    return
    
open_tables: rem --- Call the open tables routine

    call sypdir$+"bac_open_tables.bbj",
:       open_beg,
:       open_end,
:       open_tables$[all],
:       open_opts$[all],
:       open_chans$[all],
:       open_tpls$[all],
:       rd_table_chans$[all],
:       open_batch,
:       open_status$

    if open_status$<>"" then
        msg_id$="ENTRY_OPEN_ERROR"
        dim msg_tokens$[1]
        msg_tokens$[1]=open_status$
        call sypdir$+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,rd_table_chans$[all]
        status=912
        exitto std_exit_no_report
    endif

    return  
    
disp_message: rem --- Display a message

	call sypdir$+"bac_message.bbj",
:		msg_id$,
:		msg_tokens$[all],
:		msg_opt$,
:		rd_table_chans$[all]

	return
	
rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend
    
    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem #endinclude std_functions.src

rem --- fnBlank : Create blank cells in OutVect!. No return value
   
    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return void
    fnend

rem --- Create a blank line.  'void' is simply a variable; it's there to remind you that this function takes no input

    def fnblank_line(void)=fnblank(columns+1)
    
rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_SYP",err=std_error_exit)
    call pgmdir$+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5):"00000"),
:   str(err:"000"),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit_no_report:

    OutVect!=null();rem nothing to print, or some other err such that no report
    
std_exit_no_update:

    if num(batch_no$)<>0
        unlock_key$=firm_id$+stbl("+PROCESS_ID")+stbl("+BATCH_NO")
        lock_status$=""
        call stbl("+DIR_SYP")+"bac_lock_record.bbj","ADM_PROCBATCHES",unlock_key$,"X","",rd_table_chan,rd_table_chans$[all],lock_status$
    endif

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status
    run stbl("+DIR_SYP",err=*next)+"bas_process_end.bbj",err=*next
    release
    
rem #endinclude std_end.src

    end
