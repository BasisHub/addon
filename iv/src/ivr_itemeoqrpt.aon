rem --- EOQ, Order Point & Safety Stock Report
rem --- Program ivr_itemeoqrpt.aon v8.0.0 09Jul2007 (ivr_oa)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    seterr std_error
    setesc std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$

    milestone=50
    milestone=num(stbl("+MILESTONE",err=*next))

rem --- Open/Lock files

    files=11,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="ivc_whsecode",ids$[1]="IVC_WHSECODE"
    files$[2]="ivm-01",ids$[2]="IVM_ITEMMAST"
    files$[3]="ivm-02",ids$[3]="IVM_ITEMWHSE"
    files$[4]="ivc_prodcode",ids$[4]="IVC_PRODCODE"
    files$[5]="ivm-12",ids$[5]="IVM_ITEMACT"
    files$[6]="ivw-11",ids$[6]="IVW_EOQPROD"
    files$[7]="ivw-21",ids$[7]="IVW_EOQWHSE"
    files$[8]="gls_calendar",ids$[8]="GLS_CALENDAR"
    files$[9]="ivs_params",ids$[9]="IVS_PARAMS"
    files$[10]="ivs_setcarry",ids$[10]="IVS_SETCARRY"
    files$[11]="ivs_abcparam",ids$[11]="IVS_ABCPARAM"    
    options$[6]="CL", options$[7]="CL"
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    ivm10c_dev=channels[1]
    ivm01_dev=channels[2]
    ivm02_dev=channels[3]
    ivm10a_dev=channels[4]
    ivm12_dev=channels[5]
    ivw11_dev=channels[6]
    ivw21_dev=channels[7]
    gls_calendar_dev=channels[8]
    ivs01a_dev=channels[9]
    ivs01b_dev=channels[10]
    ivs01c_dev=channels[11] 

rem --- Dimension string templates

    dim ivm10c$:templates$[1],ivm01a$:templates$[2],ivm02a$:templates$[3],
:       ivm10a$:templates$[4],ivm12a$:templates$[5],ivw11a$:templates$[6],
:       ivw21a$:templates$[7],gls_calendar$:templates$[8],ivs01a$:templates$[9],
:       ivs01b$:templates$[10],ivs01c$:templates$[11]


rem --- Assign form input values to local variables

    level$="W"; rem --- only doing warehouse level stocking 
    begwhse$=option!.getOptionData("warehouse_id_1")
    endwhse$=option!.getOptionData("warehouse_id_2")
    begprod$=option!.getOptionData("product_type_1")
    endprod$=option!.getOptionData("product_type_2")
    begitem$=option!.getOptionData("item_id_1")
    enditem$=option!.getOptionData("item_id_2")
    type$=option!.getOptionData("item_type")
    rem "ITEM INACTIVE FEATURE"
    inactive$=Option!.getOptionData("ITEM_INACTIVE")

rem --- Retrieve parameter records

    ivs01a_key$=firm_id$+"IV00"
    find record (ivs01a_dev,key=ivs01a_key$,err=std_missing_params) ivs01a$
    ivs01b_key$=firm_id$+"IV01"
    find record (ivs01b_dev,key=ivs01b_key$,err=std_missing_params) ivs01b$
    ivs01c_key$=firm_id$+"IV02"
    find record (ivs01c_dev,key=ivs01c_key$,err=std_missing_params) ivs01c$
    find record (gls_calendar_dev,key=firm_id$+ivs01a.current_year$,err=*next) gls_calendar$
    if cvs(gls_calendar.firm_id$,2)="" then
        msg_id$="AD_NO_FISCAL_CAL"
        dim msg_tokens$[1]
        msg_tokens$[1]=ivs01a.current_year$
        gosub disp_message
        goto std_exit
    endif

    rem --- Parameters

    dim p[5],g[1],i[1],percents[25],factors[25],ivchannels[20]
    p[0]=ivs01a.item_id_len
    p[1]=num(ivs01a.precision$)
    g[0]=num(gls_calendar.total_pers$)
    p[3]=num(ivs01a.desc_len_01$)
    p[4]=num(ivs01a.desc_len_02$)
    p[5]=num(ivs01a.desc_len_03$)
    carry=ivs01b.carry_cost/100
    setup=ivs01b.setup_cost
    more=1

rem --- Initializations

    precision p[1]
    baseperiod$=ivs01a.current_per$+ivs01a.current_year$
    curperiod$=baseperiod$
    when$=sysinfo.system_date$
    x$=fndate$(when$)
    day_of_month=num(x$(4,2))
    ivchannels[1]=ivm01_dev
    ivchannels[2]=ivm02_dev
    ivchannels[3]=ivm02_dev
    ivchannels[12]=ivm12_dev

	seterr initializations

	for x=0 to 25
        factors[x]=nfield(ivs01c$,"abc_factors_"+str(x+1:"00"))
    next x
    
initializations:

    seterr std_error

rem --- Resize masks

    call pgmdir$+"adc_getmask.aon","","IV","A",imask$,m1$,0,0
    call pgmdir$+"adc_getmask.aon","","IV","U",imask$,m2$,0,0
    call pgmdir$+"adc_getmask.aon","","IV","C",imask$,m3$,0,0
    call pgmdir$+"adc_sizemask.aon",m1$,m1,12,15
    call pgmdir$+"adc_sizemask.aon",m2$,m2,6,12
    call pgmdir$+"adc_sizemask.aon",m3$,m3,10,15
   
rem --- Retrieve key template

    call stbl("+DIR_SYP")+"bac_key_template.bbj","IVM_ITEMMAST","PRIMARY",ivm01_key_tpl$,rd_table_chans$[all],status$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","IVW_EOQWHSE","PRIMARY",ivw21_key_tpl$,rd_table_chans$[all],status$

    dim ivm01a_key$:ivm01_key_tpl$
	dim ivw21a_key$:ivw21_key_tpl$

rem --- Run overlay

    run pgmdir$+"ivr_itemeoqrpt_o1.aon"

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend

    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
    return q$

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
return

rem #endinclude std_functions.src

rem #include std_error.src

std_error: rem --- Standard error handler (18Nov2014)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:                                   str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

    end
