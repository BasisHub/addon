rem --- Inventory Shortage & Reorder Report
rem --- Program ivr_stkreordr_o1.aon v8.0.0 16Jul2007 (ivr_db)
rem --- Created by adx_codeport.bbx v1.1.5 (07/16/2007 12:55:05)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    seterr std_error
    setesc std_error

rem --- Document initializations

    OutVect!=bbjAPI().makeVector()
    HdrVect!=bbjAPI().makeVector()
    BrkVect!=bbjAPI().makeVector()
    
    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)

rem --- Initializations

    more=1
    recs=0
    total_recs=0

    if report$="R" headings$[1]=Translate!.getTranslation("AON_INVENTORY_REORDER_REPORT") else headings$[1]=Translate!.getTranslation("AON_INVENTORY_SHORTAGE_REPORT")
    headings$[1]=headings$[1]+Translate!.getTranslation("AON__BY_WAREHOUSE")
    headings$[3]="[HEADER]" 
    if begitem$="" headings$[4]=Translate!.getTranslation("AON_FROM_ITEM:_FIRST") else headings$[4]=Translate!.getTranslation("AON_FROM_ITEM:_")+begitem$
    if enditem$="" headings$[4]=headings$[4]+Translate!.getTranslation("AON__--_TO_ITEM:_LAST") else headings$[4]=headings$[4]+Translate!.getTranslation("AON__--_TO_ITEM:_")+enditem$
    if begprod$="" headings$[5]=Translate!.getTranslation("AON_FROM_PRODUCT:_FIRST") else headings$[5]=Translate!.getTranslation("AON_FROM_PRODUCT:_")+begprod$                
    if endprod$="" headings$[5]=headings$[5]+Translate!.getTranslation("AON__--_TO_PRODUCT:_LAST") else headings$[5]=headings$[5]+Translate!.getTranslation("AON__--_TO_PRODUCT:_")+endprod$
    if type$="" headings$[6]=Translate!.getTranslation("AON_ALL_ITEM_TYPES") else headings$[6]=Translate!.getTranslation("AON_ITEM_TYPE:_")+type$
    headings$[7]=Translate!.getTranslation("AON_EXCLUDE_INACTIVE")+Translate!.getTranslation("AON_ITEMS")+" = "+inactive$

rem --- column heading

    dim columns$[13,10]
    columns=14
    columns$[0,0]=Translate!.getTranslation("AON_ITEM/DESCRIPTION"),       columns$[0,1]="C",columns$[0,2]="25"
    columns$[1,0]=Translate!.getTranslation("AON_ABC^CODE"),               columns$[1,1]="C",columns$[1,2]="5"
    columns$[2,0]=Translate!.getTranslation("AON_QUANTITY^ON_HAND"),       columns$[2,1]="N",columns$[2,2]="10",columns$[2,3]=m2$
    columns$[3,0]=Translate!.getTranslation("AON_COMMITTED^QUANTITY"),     columns$[3,1]="N",columns$[3,2]="10",columns$[3,3]=m2$
    columns$[4,0]=Translate!.getTranslation("AON_QUANTITY^AVAILABLE"),     columns$[4,1]="N",columns$[4,2]="10",columns$[4,3]=m2$
    columns$[5,0]=Translate!.getTranslation("AON_QUANTITY^ON_ORDER"),      columns$[5,1]="N",columns$[5,2]="10",columns$[5,3]=m2$
    columns$[6,0]=Translate!.getTranslation("AON_ORDER^POINT"),            columns$[6,1]="N",columns$[6,2]="10",columns$[6,3]=m2$
    columns$[7,0]="",                       columns$[7,1]="C",columns$[7,2]="3"
    columns$[8,0]="EOQ",                    columns$[8,1]="N",columns$[8,2]="10",columns$[8,3]=m2$
    columns$[9,0]="",                       columns$[9,1]="C",columns$[9,2]="3"
    columns$[10,0]=Translate!.getTranslation("AON_SAFETY^STOCK"),          columns$[10,1]="N",columns$[10,2]="10",columns$[10,3]=m2$
    columns$[11,0]="",                      columns$[11,1]="C",columns$[11,2]="3"
    columns$[12,0]=Translate!.getTranslation("AON_AVG_USAGE^PER_PERIOD"),  columns$[12,1]="N",columns$[12,2]="10",columns$[12,3]=m2$
    columns$[13,0]="",                      columns$[13,1]="C",columns$[13,2]="3"

    call stbl("+DIR_PGM",err=*next)+"adc_docout_adj.aon",columns$[all]

rem --- Position files

    call pgmdir$+"adc_progress.aon","N","IVM_ITEMWHSE","","","",0,ivm02_dev,1,meter_num,status

rem --- Loop Starting

    sql_prep$=          "SELECT ivm_itemwhse.firm_id, ivm_itemwhse.item_id,"
    sql_prep$=sql_prep$+"       ivm_itemwhse.warehouse_id, ivm_itemmast.product_type,"
    sql_prep$=sql_prep$+"       ivm_itemmast.item_type "
    sql_prep$=sql_prep$+"  FROM ivm_itemwhse INNER JOIN ivm_itemmast "
    sql_prep$=sql_prep$+"    ON ivm_itemwhse.item_id=ivm_itemmast.item_id AND "
    sql_prep$=sql_prep$+"       ivm_itemwhse.firm_id=ivm_itemmast.firm_id "
    where_clause$="       WHERE ivm_itemwhse.firm_id = '"+firm_id$+"' AND "
    if begwhse$<>"" where_clause$=where_clause$+"ivm_itemwhse.warehouse_id >= '"+begwhse$+"' AND "
    if endwhse$<>"" where_clause$=where_clause$+"ivm_itemwhse.warehouse_id <= '"+endwhse$+"' AND "
    if begprod$<>"" where_clause$=where_clause$+"ivm_itemmast.product_type >= '"+begprod$+"' AND "
    if endprod$<>"" where_clause$=where_clause$+"ivm_itemmast.product_type <= '"+endprod$+"' AND "
    if begitem$<>"" where_clause$=where_clause$+"ivm_itemwhse.item_id >= '"+begitem$+"' AND "
    if enditem$<>"" where_clause$=where_clause$+"ivm_itemwhse.item_id <= '"+enditem$+"' AND"
    if type$<>"" where_clause$=where_clause$+"ivm_itemmast.item_type = '"+type$+"'"

    where_clause$=cvs(where_clause$,2)
    if where_clause$(len(where_clause$)-2,3)="AND" where_clause$=where_clause$(1,len(where_clause$)-3)
    where_clause$=where_clause$+" ORDER BY ivm_itemwhse.warehouse_id, ivm_itemmast.product_type, ivm_itemwhse.item_id"
    if where_clause$<>"" sql_prep$=sql_prep$+where_clause$

    sql_chan=sqlunt
    sqlopen(sql_chan,err=*next)stbl("+DBNAME")
    sqlprep(sql_chan)sql_prep$
    dim read_tpl$:sqltmpl(sql_chan)
    sqlexec(sql_chan)

rem --- Main Read

    while more

        read_tpl$=sqlfetch(sql_chan,err=*break)
        product$=read_tpl.product_type$
        item$=read_tpl.item_id$
        whse$=read_tpl.warehouse_id$

rem --- Display Progress

        total_recs=total_recs+1
        if mod(total_recs,milestone)=0 then call stbl("+DIR_PGM")+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status

rem --- Retrieve item

        dim item$[2],whse$[2],w[20]
        ivm01a_key$=firm_id$+item$
        ivm02a_key$=firm_id$+whse$+item$
        find record (ivm01_dev,key=ivm01a_key$,dom=*continue) ivm01a$
        if type$<>"" and  ivm01a.item_type$<>type$ then continue
        rem -- Check for inactive item
        if inactive$="Y" and ivm01a.item_inactive$="Y" then continue

        item$[0]=firm_id$
        item$[2]=item$
        item$[1]=whse$,action$=""
        find record (ivm02_dev,key=ivm02a_key$,knum="PRIMARY",dom=*continue) ivm02a$
        call pgmdir$+"ivc_whseinfo.aon",action$,ivchannels[all],item$[all],whse$[all],w[all],value,rd_table_chans$[all],whseinfo_tpls$[all],status
        if report$="R" and w[0]+w[1]-w[2]>w[4] then continue
        if report$="S" and w[0]+w[1]-w[2]>=w[5] then continue
		
rem --- Level breaks?

        if whse$<>t0$ gosub whse_break
        if product$<>t1$ gosub product_break

        call pgmdir$+"ivc_usage.aon","W",ivchannels[all],item$[all],factors[all],day_of_mth,period$+year$,ivs01a.current_per$+ivs01a.current_year$,g[0],p[1],usage,estimated,flag$,usage_tpls$[all],status
        if flag$<>"" usage=estimated
        avg_usage=usage/g[0]
        op_code$=ivm02a.ord_pnt_code$,eoq_code$=ivm02a.eoq_code$,safe_code$=ivm02a.saf_stk_code$

rem --- Print detail line

        last_issue$=fnl$(ivm02a.lstsal_date$,ivm02a.lstiss_date$) 
        item_desc$=fnitem$(ivm01a.item_desc$,p[3],p[4],p[5])
        if h9$<>"" gosub prod_heading
        column_value$=ivm01a.item_id$+ivm02a.location$

        OutVect!.addItem(column_value$)
        OutVect!.addItem(ivm02a.abc_code$)
        OutVect!.addItem(str(w[0]))
        OutVect!.addItem(str(w[2]))
        OutVect!.addItem(str(w[0]-w[2]))
        OutVect!.addItem(str(w[1]))
        OutVect!.addItem(str(w[4]))
        OutVect!.addItem(op_code$)
        OutVect!.addItem(str(w[6]))
        OutVect!.addItem(eoq_code$)
        OutVect!.addItem(str(w[5]))
        OutVect!.addItem(safe_code$)
        OutVect!.addItem(str(avg_usage))
        OutVect!.addItem(flag$)
        OutVect!.addItem("   "+item_desc$)
        xwk=fnblank(columns-1)
        number=number+1
        recs=recs+1

    wend; rem --- Loop Ending

done: rem --- All done

    if recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    if number>0 goto std_exit
    xwk=fnblank(columns)
    OutVect!.addItem(Translate!.getTranslation("AON_NO_ITEMS/WAREHOUSES_FOUND"))
    xwk=fnblank(columns-1)
    goto std_exit

prod_heading: rem --- Product subheading

    xwk=fnblank(columns)
    OutVect!.addItem(h9$)
    xwk=fnblank(columns-1)
    xwk=fnblank(columns)
    h9$=""
    return

whse_break: rem --- Warehouse Break

    if t0$<>"" then gosub product_break
    t0$=whse$,ivm10c.short_name$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
    find record (ivm10c_dev,key=firm_id$+"C"+t0$,dom=*next) ivm10c$
    x$=Translate!.getTranslation("AON_WAREHOUSE_")+t0$+" "+cvs(ivm10c.short_name$,2)
    BrkVect!.addItem(str(OutVect!.size()/(columns)))
    HdrVect!.addItem(x$)
    return

product_break: rem --- Product break

    t1$=product$,ivm10a.code_desc$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
    find record (ivm10a_dev,key=firm_id$+"A"+t1$,dom=*next) ivm10a$
    h9$=Translate!.getTranslation("AON_PRODUCT_TYPE_")+t1$+" "+ivm10a.code_desc$
    return

rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Date/time handling functions

    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
        return q$
    fnend

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend

rem --- Compare two dates
    def fnl$(q1$,q2$)
        if CVS(q1$,2)="" THEN LET q1$="zzzzzzzz"
        if CVS(q2$,2)="" THEN LET q2$="zzzzzzzz"
        q$=q1$
        if q2$>q1$ then q$=q2$
        if q$="zzzzzzzz" then q$="        "
        return q$
    fnend
rem --- fnBlank : Create blank cells in OutVect!. No return value

    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend

rem #endinclude std_functions.src

rem #include disp_message.src

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:       str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit: 
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release: 
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release
rem #endinclude std_end.src

std_exit_no_report: 

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status    
    OutVect!=null()
    goto std_exit

    end
