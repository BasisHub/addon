rem --- Inventory Stock Status Report (Report Overlay)
rem --- Program ivr_stkstatus_o1.aon v8.0.0 16Jul2007 (ivr_bb)
rem --- Created by adx_codeport.bbx v1.1.5 (07/16/2007 12:54:33)

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    seterr std_error
    setesc std_error

rem --- Document initializations

    OutVect!=bbjAPI().makeVector()
    HdrVect!=bbjAPI().makeVector()
    BrkVect!=bbjAPI().makeVector()
    TotVect!=bbjAPI().makeVector()
    
    rep_date$=date(0:"%Mz/%Dz/%Yd")
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")
    rep_prog$=pgm(-2)

rem --- Initialize HEADINGS$

    headings$[1]=headings$[1]+Translate!.getTranslation("AON__BY_WAREHOUSE")
    headings$[7]="[HEADER]"
    if begitem$="" headings$[3]=Translate!.getTranslation("AON_FROM_ITEM:_FIRST") else headings$[3]=Translate!.getTranslation("AON_FROM_ITEM:_")+begitem$
    if enditem$="" headings$[3]=headings$[3]+Translate!.getTranslation("AON__--_TO_ITEM:_LAST") else headings$[3]=headings$[3]+Translate!.getTranslation("AON__--_TO_ITEM:_")+enditem$
    if begprod$="" headings$[4]=Translate!.getTranslation("AON_FROM_PRODUCT:_FIRST") else headings$[4]=Translate!.getTranslation("AON_FROM_PRODUCT:_")+begprod$                
    if endprod$="" headings$[4]=headings$[4]+Translate!.getTranslation("AON__--_TO_PRODUCT:_LAST") else headings$[4]=headings$[4]+Translate!.getTranslation("AON__--_TO_PRODUCT:_")+endprod$
    if type$="" headings$[5]=Translate!.getTranslation("AON_ALL_ITEM_TYPES") else headings$[5]=Translate!.getTranslation("AON_ITEM_TYPE:_")+type$
    headings$[6]=Translate!.getTranslation("AON_EXCLUDE_INACTIVE")+Translate!.getTranslation("AON_ITEMS")+" = "+inactive$

    dim columns$[9,10]
    columns=10
    columns$[0,0]=Translate!.getTranslation("AON_ITEM"),                  columns$[0,1]="C",columns$[0,2]="15"
    columns$[1,0]=Translate!.getTranslation("AON_DESCRIPTION"),           columns$[1,1]="C",columns$[1,2]="25"
    columns$[2,0]=Translate!.getTranslation("AON_LOCATION"),              columns$[2,1]="C",columns$[2,2]="10"
    columns$[3,0]="UM",                                                   columns$[3,1]="C",columns$[3,2]="5"
    columns$[4,0]=Translate!.getTranslation("AON_ON_HAND"),               columns$[4,1]="N",columns$[4,2]="10",columns$[4,3]=m2$
    columns$[5,0]=Translate!.getTranslation("AON_COMMITTED"),             columns$[5,1]="N",columns$[5,2]="10",columns$[5,3]=m2$
    columns$[6,0]=Translate!.getTranslation("AON_AVAILABLE"),             columns$[6,1]="N",columns$[6,2]="10",columns$[6,3]=m2$
    columns$[7,0]=Translate!.getTranslation("AON_ON_ORDER"),              columns$[7,1]="N",columns$[7,2]="10",columns$[7,3]=m2$
    columns$[8,0]=Translate!.getTranslation("AON_UNIT_COST"),             columns$[8,1]="N",columns$[8,2]="10",columns$[8,3]=m3$
    columns$[9,0]=Translate!.getTranslation("AON_TOTAL_VALUE"),           columns$[9,1]="N",columns$[9,2]="10",columns$[9,3]=m1$,columns$[9,10]="T"

    call stbl("+DIR_PGM",err=*next)+"adc_docout_adj.aon",columns$[all]

rem --- Query

    sql_prep$=""
    sql_prep$=sql_prep$+"SELECT ivm_itemwhse.firm_id, ivm_itemwhse.warehouse_id, ivm_itemwhse.item_id "
    sql_prep$=sql_prep$+"  FROM ivm_itemwhse "
    
    where_clause$="WHERE ivm_itemwhse.firm_id = '"+firm_id$+"' AND "
    if begwhse$<>"" where_clause$=where_clause$+"ivm_itemwhse.warehouse_id >= '"+begwhse$+"' AND "
    if endwhse$<>"" where_clause$=where_clause$+"ivm_itemwhse.warehouse_id <= '"+endwhse$+"' AND "
    if begprod$<>"" where_clause$=where_clause$+"ivm_itemwhse.product_type >= '"+begprod$+"' AND "
    if endprod$<>"" where_clause$=where_clause$+"ivm_itemwhse.product_type <= '"+endprod$+"' AND "
    if begitem$<>"" where_clause$=where_clause$+"ivm_itemwhse.item_id >= '"+begitem$+"' AND "
    if enditem$<>"" where_clause$=where_clause$+"ivm_itemwhse.item_id <= '"+enditem$+"' AND "
    
    where_clause$=cvs(where_clause$,2)
    if where_clause$(len(where_clause$)-2,3)="AND" where_clause$=where_clause$(1,len(where_clause$)-3)
    sql_prep$=sql_prep$+where_clause$

    call stbl("+DIR_PGM")+"adc_progress.aon","N","IVM_ITEMWHSE","","","",0,ivm01_dev,1,meter_num,status
    total_recs=0
    
    sql_chan=sqlunt
    sqlopen(sql_chan,err=*next)stbl("+DBNAME")
    sqlprep(sql_chan)sql_prep$
    dim read_tpl$:sqltmpl(sql_chan)
    sqlexec(sql_chan)

rem --- Main Read

    resultsMap!=new java.util.TreeMap()
    while more
        read_tpl$=sqlfetch(sql_chan,err=*break) 
        whse$=read_tpl.warehouse_id$
        item$=read_tpl.item_id$

        if mod(total_recs,milestone)=0 then call stbl("+DIR_PGM")+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status
        readrecord(ivm01_dev,key=firm_id$+item$,dom=*continue)ivm01a$
        if type$<>"" and ivm01a.item_type$ <> type$ then continue
        rem -- Check for inactive item
        if inactive$="Y" and ivm01a.item_inactive$="Y" then continue
        
        dataHash!=new java.util.HashMap()
        dataHash!.put("warehouse_id",whse$)
        dataHash!.put("item_id",item$)
        dataHash!.put("product_type",ivm01a.product_type$)
        dataHash!.put("item_desc",ivm01a.item_desc$)
        dataHash!.put("unit_of_sale",ivm01a.unit_of_sale$)
        resultsMap!.put(whse$+ivm01a.product_type$+item$,dataHash!)
    wend

rem --- Print report results

    total_recs=0
    if resultsMap!.size()>0 then
        resultsIter!=resultsMap!.keySet().iterator()
        while resultsIter!.hasNext()
            resultsKey$=resultsIter!.next()
            dataHash!=resultsMap!.get(resultsKey$)
            whse$=dataHash!.get("warehouse_id")
            item$=dataHash!.get("item_id")
            product$=dataHash!.get("product_type")
            item_desc$=dataHash!.get("item_desc")
            unit_of_sale$=dataHash!.get("unit_of_sale")
        
            rem --- Level breaks?
    
            total_recs=total_recs+1
            if whse$<>t0$ then gosub whse_break
            if product$<>t1$ then gosub product_break
            dim item$[2],whse$[2],w[20]
            action$="A"
            item$[0]=firm_id$
            item$[2]=item$
            item$[1]=whse$,action$=""
            call pgmdir$+"ivc_whseinfo.aon",action$,files[all],item$[all],whse$[all],w[all],value,rd_table_chans$[all],whseinfo_tpls$[all],status
    
            rem --- Print detail line
    
            x$=""
            location$=""
            cost$=""
            available=w[0]-w[2]
            dim ivm02a$:fattr(ivm02a$)
            ivm02a$=whse$[1]
            location$=ivm02a.location$
            x$=fnitem$(item_desc$,p[3],p[4],p[5])
            if h9$<>"" gosub prodheading
    
            OutVect!.addItem(item$)
            OutVect!.addItem(x$)
            OutVect!.addItem(location$)
            OutVect!.addItem(unit_of_sale$)
            OutVect!.addItem(str(w[0]))
            OutVect!.addItem(str(w[2]))
            OutVect!.addItem(str(available))
            OutVect!.addItem(str(w[1]))
            OutVect!.addItem(str(w[11]))
            OutVect!.addItem(str(value))
    
            rem --- Accumulate totals
    
            product=product+value
            warehouse=warehouse+value
            report=report+value
        wend
    endif

done: rem --- All done

    if total_recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    done=1
    gosub whse_break
    total$=Translate!.getTranslation("AON_REPORT")
    total=report
    gosub print_total
    goto std_exit


prodheading: rem --- Product subheading

    xwk=fnblank(columns)
    OutVect!.addItem(h9$)
    OutVect!.addItem(prod_desc$)
    xwk=fnblank(columns-2)
    xwk=fnblank(columns)
    h9$=""
    return

whse_break: rem --- Warehouse Break

    if t0$<>"" then
        gosub product_break
        let total$=Translate!.getTranslation("AON_WAREHOUSE_")+t0$,total=warehouse
        gosub print_total
    endif

    if done return
    t0$=whse$
    ivm10c.short_name$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
    warehouse=0
    find record (ivm10c_dev,key=firm_id$+"C"+t0$,dom=*next) ivm10c$
    x$=Translate!.getTranslation("AON_WAREHOUSE_")+t0$+" "+cvs(ivm10c.short_name$,2)
    BrkVect!.addItem(str(OutVect!.size()/(columns)))
    HdrVect!.addItem(x$)
    return 

product_break: rem --- Product break

    if t1$<>"" then
        let total$=Translate!.getTranslation("AON_PRODUCT_")+t1$,total=product
        gosub print_total
    endif

    if done return
    t1$=product$
    prod_desc$=Translate!.getTranslation("AON_(NOT_ON_FILE)")
    product=0
    find record (ivm10a_dev,key=firm_id$+"A"+t1$,dom=*next) ivm10a$
    let h9$=Translate!.getTranslation("AON_PRODUCT_TYPE_")+t1$
    prod_desc$=ivm10a.code_desc$
    return

print_total: rem --- Print total line

    total$=Translate!.getTranslation("AON_TOTAL_FOR_")+cvs(total$,2)
    xwk=fnblank(columns)
    TotVect!.addItem(str(OutVect!.size()/(columns)))
    OutVect!.addItem("")
    OutVect!.addItem(total$)
    xwk=fnblank(columns-3)
    OutVect!.addItem(str(total))
    xwk=fnblank(columns)                 
    return


rem #include std_functions.src
rem --- Standard AddonSoftware functions (01Mar2006)
rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:       cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)
    def fnget_table$(q0$)=rd_alias_id$

rem --- Miscellaneous functions

    def fncenter(q$,q)=int((q-len(q$))/2)

rem --- Format inventory item description

	def fnitem$(q$,q1,q2,q3)
		q$=pad(q$,q1+q2+q3)
		return cvs(q$(1,q1)+" "+q$(q1+1,q2)+" "+q$(q1+q2+1,q3),32)
	fnend

rem --- Date/time handling functions

    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem --- fnbasename$: Strip path and optionally the suffix from a file name

    def fnbasename$(q$,q0$)
        q=max(pos("/"=q$,-1),pos(":"=q$,-1),pos(">"=q$,-1),pos("\"=q$,-1))
        if q then q$=q$(q+1)
        if q0$<>"" then q=mask(q$,q0$); if q q$=q$(1,q-1)
        return q$
    fnend

rem --- fnglobal: Return numeric value of passed stbl variable

    def fnglobal(q$,q1)
        q1$=stbl(q$,err=*next),q1=num(q1$,err=*next)
        return q1
    fnend

rem --- fnglobal$: Return string value of passed STBL variable

    def fnglobal$(q$,q1$)
        q1$=stbl(q$,err=*next)
        return q1$
    fnend
rem --- fnBlank : Create blank cells in OutVect!. No return value

    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend

rem #endinclude std_functions.src

rem #include disp_message.src

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (18Nov2014)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5):"00000"),
:       str(err:"000"),rd_err_text$,rd_err_act$
std_missing_params_exit: 
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release: 
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)
    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release

std_exit_no_report: 

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status    
    OutVect!=null()
    goto std_exit

    end
