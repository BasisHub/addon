rem DataPort Data File Conversion Utility (Template creation program)
rem Program create_template.bbj v8.0.0 28Oct07
rem
rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Feb2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+
rem
rem --- This overlay creates templates for version 6/7 data files using
rem --- the Addon dictionary files ddm-01, ddm-03 and ddm-04 and then
rem --- copies to Addon 8 data files after performing date format changes.
rem --- Normalization of data files will also be done in this program.
rem

    setesc std_error
    seterr std_error

    use java.io.File
    use java.io.FileReader
    use java.io.FileWriter
    use java.util.HashMap
    use java.util.Properties
    use java.util.Vector
    use ::ado_file.src::FileObject

rem --- Dictionary file templates
dim ddm01$:"data_name:c(12*),description:c(30),lstrev:c(6),data_type:c(1),reserved_str:c(68*),fld_length:n(3*),display_len:n(3*),reserved_num:n(1*)"
dim ddm03$:"file_name:c(6),record_id:c(1*),description:c(30),reserved_str:c(33*),reserved_num:n(3*)"
dim ddm04$:"file_name:c(6),record_id:c(1),layout_seq:c(3*),data_name:c(12),reserved_str1:c(41),fld_sep:c(1),reserved_str2:c(68*),reserved_num:n(1*),fld_repeats:n(3*),fld_occurs:n(3)"

rem --- Open files and/or retrieve templates that may be needed for PO file conversion

    num_files=10
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="ADS_SEQUENCES",rd_open_opts$[1]="OTA"
    rd_open_tables$[2]="ARM_CUSTMAST",rd_open_opts$[2]="T"
    rd_open_tables$[3]="ARM_CUSTSHIP",rd_open_opts$[3]="T"
    rd_open_tables$[4]="OPE_ORDSHIP",rd_open_opts$[4]="T"
    rd_open_tables$[5]="OPT_INVSHIP",rd_open_opts$[5]="T"
    rd_open_tables$[6]="OPT_INVHDR",rd_open_opts$[6]="T"
    rd_open_tables$[7]="DDM_SYSTEMS",rd_open_opts$[7]="OTA"
    rd_open_tables$[8]="DDM_ELEMENTS",rd_open_opts$[8]="OTA"
    rd_open_tables$[9]="ADM_MODULES",rd_open_opts$[9]="OTA"
    rd_open_tables$[10]="DDM_TABLE_COLS",rd_open_opts$[10]="OTA"
    
    gosub open_tables
    
    ads_sequences_dev=num(rd_open_chans$[1]);dim ads_sequences$:rd_open_tpls$[1]
    arm_custmast_tpl$=rd_open_tpls$[2]
    arm_custship_tpl$=rd_open_tpls$[3]
    ope_ordship_tpl$=rd_open_tpls$[4]
    opt_invship_tpl$=rd_open_tpls$[5]
    opt_invhdr_tpl$=rd_open_tpls$[6]
    ddm_systems=num(rd_open_chans$[7]);dim ddm_systems$:rd_open_tpls$[7]
    ddm_elements=num(rd_open_chans$[8]);dim ddm_elements$:rd_open_tpls$[8]
    adm_modules_dev=num(rd_open_chans$[9]);dim adm_modules$:rd_open_tpls$[9]
    ddm_table_cols=num(rd_open_chans$[10]);dim ddm_table_cols$:rd_open_tpls$[10]
    
rem --- initialization

    upgradedModules$=""
    source_version = num(callpoint!.getDevObject("source_version"))
    version_cvs = iff(source_version=6,4,8)
    source_folder$ = callpoint!.getDevObject("source_folder")
    destin_folder$ = callpoint!.getDevObject("destin_folder")
    vectPort! = callpoint!.getDevObject("vectPort")
	
    dictfile$=stbl("+DIR_BRD")
    dictpgm$=stbl("+DIR_SYP")
    filedir$=stbl("+DATAPORT_FILES")
    logdir$=stbl("+DATAPORT_LOGS")

    mkdir logdir$,err=*next

    logfile$=logdir$+"DataPort_"+DATE(0:"%Mz%Dz%Yz")+"_"+DATE(0:"%Hz%mz")+".txt"
    erase logfile$,err=create_logfile
    create_logfile:
    string logfile$
    log_dev=unt
    open (log_dev)logfile$
    print (log_dev)"DataPort Start Time: "+DATE(0:"%Mz/%Dz/%Y %hz:%mz:%sz")+" "+$0a$

    print (log_dev)" Porting Addon "+str(source_version)+" data files from folder "+source_folder$+" to "+destin_folder$+$0a$

    errfile$=logdir$+"DataPortError_"+DATE(0:"%Mz%Dz%Yz")+"_"+DATE(0:"%Hz%mz")+".txt"
    erase errfile$,err=create_errfile
    create_errfile:
    string errfile$
    err_dev=unt
    open (err_dev)errfile$
    print (err_dev)"DataPort Start Time: "+DATE(0:"%Mz/%Dz/%Y %hz:%mz:%sz")+" "+$0a$

    print (err_dev)" Porting Addon "+str(source_version)+" data files from folder "+source_folder$+" to "+destin_folder$+$0a$

rem --- Disable triggers found in data directories
    rd_dbserver$="localhost"
    rd_dbserver$=stbl("+DBSERVER",err=*next)
    rd_dbport=2002
    rd_dbport=num(stbl("+DBPORT",err=*next),err=*next)
    rd_dbssl=0
    rd_dbssl=num(stbl("+DBSSL",err=*next),err=*next)
    rd_user$="admin"
    rd_user$=stbl("+USER_ID",err=*next)
    rd_password$="admin123"
    rdNSGroup!=BBjAPI().getGroupNamespace()
    rdAdmin!=rdNSGroup!.getValue("+bar_admin_"+cvs(rd_user$,11),err=*next)
    if rdAdmin!<>null() then
        rd_user$=rdAdmin!.getUser()
        rd_password$=rdAdmin!.getPassword()
    endif
    aAdminBase!=com.basis.api.admin.BBjAdminFactory.getBBjAdmin(java.net.InetAddress.getByName(rd_dbserver$),rd_dbport,rd_dbssl,rd_user$,rd_password$)
    disabledTriggers!=BBjAPI().makeVector()
    dataDirs!=BBjAPI().makeVector()
    dataDirs!.addItem(source_folder$); rem --- DataPort source directory
    dataDirs!.addItem(destin_folder$); rem --- DataPort destination directory
    dataDirs!.addItem(stbl("+DIR_DAT")); rem --- Data directory for current Addon instance
    dirsIter!=dataDirs!.iterator()
    while dirsIter!.hasNext()
        triggerDir!=new File(dirsIter!.next())
        triggers!=new Vector(java.util.Arrays.asList(triggerDir!.listFiles(new TriggerFilter())))
        if triggers!.size() then
            iter!=triggers!.iterator()
            while iter!.hasNext()
                trigger$=iter!.next().getAbsolutePath()
                trigger$=trigger$(1,pos(".trigger"=trigger$,-1)-1)
                aAdminTriggers!=aAdminBase!.getTriggers(trigger$)
                enable!=aAdminTriggers!.getBoolean(BBjAdminTriggers.ENABLED)
                if enable! then
                    aAdminTriggers!.setBoolean(BBjAdminTriggers.ENABLED, 0)
                    aAdminTriggers!.commit()
                    disabledTriggers!.addItem(trigger$)
                    print "Disabled triggers for: ",trigger$
                    print (log_dev)"Disabled triggers for: ",trigger$
                endif
            wend
        endif
    wend

rem --- Set the hook for dealer custom call

    dropship$="N"
    invalid_date$="N"
    dupe_rec$="N"
    blank_fields$=""
    custom_prog$=""
    while 1
        custom_prog$=cvs(stbl("+CUSTOM_DP_PROG",err=*break),3)
        if custom_prog$<>""
            addr custom_prog$,err=*next;break
            custom_prog$=""
        endif
        break
    wend

rem --- Get current Customer and Vendor number sizes

	custSizeMap! = new java.util.HashMap()
	vendSizeMap! = new java.util.HashMap()
    prYearMap! = new java.util.HashMap()
	dim key$(6)
    sys01_dev=unt
    open (sys01_dev)source_folder$+"/"+cvs("sys-01",version_cvs)
	while 1
		key$(1)=key(sys01_dev,end=*break)
		firm$=key$(1,2)
		rec$=key$(3)
		switch (BBjAPI().TRUE)
			case rec$ = "AR00"
				rem --- Customer number size
				dim p2$(6)
				read(sys01_dev)*,*,p2$(1)
				custSizeMap!.put(firm$,num(p2$(1,2)))
				break
			case rec$ = "AP00"
				rem --- Vendor number size
				dim p2$(4)
				read(sys01_dev)*,*,p2$(1)
				vendSizeMap!.put(firm$,num(p2$(1,2)))
				break
			case rec$ = "PR00"
			    rem --- Payroll current year
			    dim p4$(4)
                read(sys01_dev)*,*,*,*,p4$(1)
                value$=p4$(3,2)
                gosub fix_yymmdd_date
                prYearMap!.put(firm$,value$)
			    break
    		case default
				read(sys01_dev)
				break
		swend

	wend
	close(sys01_dev)
	
rem --- Set the Temporary Vendor Number here

    temp_vend$=""
    temp_vend_ok$=""
    temp_vend$=stbl("+DP_TEMP_VEND",err=*next)
    if len(temp_vend$)>6 temp_vend$=""
    if len(temp_vend$)>1
        temp_vend$=pad(temp_vend$,6,"L","0")
    endif

rem --- Set the number of years to add here

    add_year=0
    add_year=num(stbl("+DEMO_DATE_INCREMENT",err=open_files))

open_files:
rem --- Open files

ddm01=unt
open (ddm01)source_folder$+"/"+cvs("ddm-01",version_cvs)
lock(ddm01)

ddm03=unt
open (ddm03)source_folder$+"/"+cvs("ddm-03",version_cvs)

ddm04=unt
open (ddm04)source_folder$+"/"+cvs("ddm-04",version_cvs)

ddm_table_tpls = unt
open (ddm_table_tpls)dictfile$+"ddm_table_tpls.dat"
dim ddm_table_tpls$:"table_name:c(16*),file_name:c(30*),template:c(10230*)"

ddm_tables = unt
open (ddm_tables)dictfile$+"ddm_tables.dat"

rem --- Load xref files into memory

rd_meter_data$="Loading aliases..."
rd_meter_action$="WIN-LST-OK"
rd_meter_title$="DataPort"
gosub disp_meter

rem --- read file_alias into hash map

file_alias! = new java.util.HashMap()
dim file_alias$:"file_id:c(16*=124),alias:c(16*=124),comments:c(1*)"
file_alias = unt
open (file_alias)filedir$+"file_alias"
while 1
   read (file_alias,end=*break)file_alias$
   if len(file_alias$) then
      keyval$ = cvs(file_alias.file_id$,7)
      dataval$ = cvs(file_alias.alias$,7)
      if file_alias!.get(keyval$)=null() then
         file_alias!.put(keyval$,dataval$)
      else
         print "*** skip duplicate file_alias: ",file_alias$
      endif
   endif
wend
close (file_alias)

rem --- read file_xref into hash map

file_xref! = new java.util.HashMap()
dim file_xref$:"old_filename:c(16*=124),new_filename:c(16*=124),comments:c(1*)"
file_xref = unt
open (file_xref)filedir$+"file_xref"
while 1
	read (file_xref,end=*break)file_xref$
	if len(file_xref$) then
		if len(cvs(file_xref.old_filename$,2))=7
			file_xref.old_filename$=cvs(file_xref.old_filename$(1,6),7)+file_xref.old_filename$(7,1)
		else
			file_xref.old_filename$=cvs(file_xref.old_filename$,7)
		endif
		keyval$ = cvs(file_xref.old_filename$,3)
		dataval$ = cvs(file_xref.new_filename$,3)
		file_xref!.put(keyval$,dataval$)
	endif
wend
close (file_xref)

rem --- read field_file_xref into hash map

field_file_xref! = new java.util.HashMap()
dim field_file_xref$:"alias:c(16*=124),old_fieldname:c(16*=124),new_fieldname:c(16*=)"
field_file_xref = unt
open (field_file_xref)filedir$+"field_file_xref"
while 1
   read (field_file_xref,end=*break)field_file_xref$
   if len(field_file_xref$) then
      keyval$ = cvs(field_file_xref.alias$,7)+"."+cvs(field_file_xref.old_fieldname$,7)
      dataval$ = cvs(field_file_xref.new_fieldname$,7)
      field_file_xref!.put(keyval$,dataval$)
   endif
wend
close (field_file_xref)

rem --- read field_name_xref into hash map

field_name_xref! = new java.util.HashMap()
dim field_name_xref$:"old_fieldname:c(16*=124),new_fieldname:c(16*=)"
field_name_xref = unt
open (field_name_xref)filedir$+"field_name_xref"
while 1
   read (field_name_xref,end=*break)field_name_xref$
   if len(field_name_xref$) then
      keyval$ = cvs(field_name_xref.old_fieldname$,7)
      dataval$ = cvs(field_name_xref.new_fieldname$,7)
      field_name_xref!.put(keyval$,dataval$)
   endif
wend
close (field_name_xref)

rem --- read internal_seq into hash map

internal_seq! = new java.util.HashMap()
dim int_seq$:"alias:c(16*=124),disp_seq:c(16*=124),int_seq:c(16*=)"
int_seq_xref = unt
open (int_seq_xref)filedir$+"internal_seq"
while 1
   read (int_seq_xref,end=*break)int_seq$
   if len(int_seq$) then
      keyval$ = cvs(int_seq.alias$,7)+"."+cvs(int_seq.disp_seq$,7)
      dataval$ = cvs(int_seq.int_seq$,7)
      internal_seq!.put(keyval$,dataval$)
   endif
wend
close (int_seq_xref)

rem --- read field_translations into hash map

field_translations! = new java.util.HashMap()
code_masterfile! = new java.util.HashMap()
dim field_translations$:"fieldname:c(16*=124),old_value:c(16*=124),new_value:c(16*=124),masterfile:c(20*=)"
field_translations = unt
open (field_translations)filedir$+"field_translations"
while 1
   read (field_translations,end=*break)field_translations$
   if len(field_translations$) then
      keyval$ = cvs(field_translations.fieldname$,7)+"."+cvs(field_translations.old_value$,3)
      dataval$ = cvs(field_translations.new_value$,7)
      field_translations!.put(keyval$,dataval$)
      fieldname$ = cvs(field_translations.fieldname$,7)
      masterfile$ = cvs(field_translations.masterfile$,3)
      code_masterfile!.put(fieldname$,masterfile$)
   endif
wend
close (field_translations)
used_translations! = new java.util.HashMap()

rem --- read new_field_defaults into hash map

new_field_defaults! = new java.util.HashMap()
dim new_field_defaults$:"filename:c(16*=124),fieldname:c(16*=124),default_value:c(16*=)"
new_field_defaults = unt
open (new_field_defaults)filedir$+"new_field_defaults"
while 1
   read (new_field_defaults,end=*break)new_field_defaults$
   if len(new_field_defaults$) then
      keyval$ = cvs(new_field_defaults.filename$,7)+"."+cvs(new_field_defaults.fieldname$,3)
      dataval$ = cvs(new_field_defaults.default_value$,7)
      new_field_defaults!.put(keyval$,dataval$)
   endif
wend
close (new_field_defaults)


rem --- read po_files into hash map

po_files! = new java.util.HashMap()
dim po_files$:"det_filename:c(16*=124),hdr_filename:c(16*=)"
po_files = unt
open (po_files)filedir$+"po_files"
while 1
   read (po_files,end=*break)po_files$
   if len(po_files$) then
      keyval$ = cvs(po_files.det_filename$,3)
      dataval$ = cvs(po_files.hdr_filename$,3)
      po_files!.put(keyval$,dataval$)
   endif
wend
close (po_files)

rem --- read sequence_no into hash map

seq_no_files! = new java.util.HashMap()
sequence_no! = new java.util.HashMap()
sequence_no_tpl$="seq_no_id:c(16*=124),v6_file:c(16*=124),v6_field:c(16*=124),v6_element_no:c(16*=124),v7_file:c(16*=124),v7_field:c(16*=124),v7_element_no:c(16*=)"
dim sequence_no$:sequence_no_tpl$
sequence_no = unt
open (sequence_no)filedir$+"sequence_no"
while 1
   read (sequence_no,end=*break)sequence_no$
   if len(sequence_no$) then
      keyval$ = cvs(sequence_no.seq_no_id$,3)
      dataval$ = cvs(sequence_no$,3)
      sequence_no!.put(keyval$,dataval$)
      if source_version=6
          old_file$=cvs(sequence_no.v6_file$,11)
      else
          old_file$=cvs(sequence_no.v7_file$,11)
      endif
      seq_no_files!.put(old_file$,"n")
   endif
wend
close (sequence_no)

rem --- Read sym-06 to get list of valid firms to be ported
sym06_dev=unt
open (sym06_dev)source_folder$+"/"+cvs("sym-06",version_cvs)
while 1
	read(sym06_dev,end=*break) sym06_0$
	firms$=firms$+sym06_0$
wend
close (sym06_dev)

rem --- misc init; set up meter
rd_meter_data$="DataPort Start Time: "+DATE(0:"%Mz/%Dz/%Y %hz:%mz:%sz")
rd_meter_proc_recs=rd_meter_total_recs
rd_meter_action$="LST"
gosub disp_meter

rem --- Locate aon/config/adx_conversionCtrl.ini
aonDir$=""
readrecord(ddm_systems,key=pad("ADDON",16," "),knum="SYSTEM_ID",err=*next)ddm_systems$
if cvs(ddm_systems.mount_dir$,2)<>"" then
    aonDir$=ddm_systems.mount_dir$
endif
convCtrlIniFileName$=aonDir$+"config/adx_conversionCtrl.ini"
convCtrlIniFile!=new File(convCtrlIniFileName$,err=*next)
if !convCtrlIniFile!.exists() then
    rem --- Report can't find the required config/adx_conversionCtrl.ini initialization file
    err_msg$="***** ERROR: Required file not found, "+convCtrlIniFileName$+" *****"
    print(log_dev)err_msg$
    print(err_dev)err_msg$
    rd_meter_data$=err_msg$
    rd_meter_action$="LST"
    gosub disp_meter
    goto done
endif

rem --- Read the v6/v7 data directory
old_dir=unt
open (old_dir)source_folder$
if vectPort! = null() then
    vectPort!=BBjAPI().makeVector()
    while 1
        readrecord (old_dir,end=*break)datafile$
        vectPort!.addItem(datafile$)
    wend
endif

rem --- The order files are ported in can be important (see ivm-01 and ivm-09 for v18). Sort vectPort! to get consistent ordering.
java.util.Collections.sort(vectPort!)

rd_meter_data$="Processing files . . ."
rd_meter_total_recs=vectPort!.size()
rd_meter_action$="LST"
gosub disp_meter

next_file:
   datafile$ = str(vectPort!.removeItem(0),err=end_read_loop)

    datafile$=cvs(datafile$,version_cvs)
    rd_meter_data$="   "+datafile$
    rd_meter_action$="LST"
    gosub disp_meter

    if vectPort!.size(err=*endif) then
       rd_meter_proc_recs=rd_meter_proc_recs+1
       rd_meter_action$="MTR-"
       gosub disp_meter
    endif

    rem --- skip these files
    if pos("."=datafile$)=1 or
:   pos("_"=datafile$)=1 or
:   pos("z"=cvs(datafile$,8))=1 or
:   pos("dd"=cvs(datafile$,8))=1 or
:   pos("w-"=cvs(datafile$,8))=3 or
:   pos("sh"=cvs(datafile$,8))=1 or
:   pos(cvs(datafile$,8)="apm-04;apt-03;",7) or
:   pos(cvs(datafile$,8)="are-07;are-43;arm-04;art-43;art-53;",7) or
:   pos(cvs(datafile$,8)="bmm-04;bmm-06;",7)or
:   pos(cvs(datafile$,8)="glt-05;",7) or
:   pos(cvs(datafile$,8)="ivm-03;ivm-08;",7) or
:   pos(cvs(datafile$,8)="poe-31;poe-32;poe-33;poe-34;poe-41;poe-42;po3-43;poe-44;poe-52;poe-53;poe-54;pot-24;pot-34;pot-35;pot-44;",7) or
:   pos(cvs(datafile$,8)="sym-04;sym-49;sym-59;sym-69;sys-60;",7) or
:   pos(cvs(datafile$,8)="woe-03;wom-07;",7) then
        print "Skipped: ",datafile$
        print (log_dev)"Skipped: ",datafile$
        goto next_file
    endif

    rem --- If source POM-02 isn't available, then skip poe-11, poe-12, poe-13, poe-14 and pot-14
    if pos(cvs(datafile$,8)="poe-11;poe-12;poe-13;poe-14;pot-14",7) and !pom02_dev then
        found_pom02=0
        pom02_dev=unt
        open (pom02_dev,err=*next)source_folder$+"/"+cvs("pom-02",version_cvs);found_pom02=1
        if found_pom02 then
            readrecord(ddm_table_tpls,key=pad("POC_LINECODE",16))ddm_table_tpls$
            pom02_tpl$=ddm_table_tpls.template$
        else
            print "*** ERROR ***: ",datafile$," requires ",cvs("pom-02",version_cvs)
            print (log_dev)"*** ERROR ***: ",datafile$," requires ",cvs("pom-02",version_cvs)," for non-stock line items"
            print (err_dev)"*** ERROR ***: ",datafile$," requires ",cvs("pom-02",version_cvs)," for non-stock line items"
            goto next_file
        endif
    endif

    print "Data file: ",datafile$
    print (log_dev)"Data file: ",datafile$
    
    theModule$=cvs(datafile$(1,2),4)
    if theModule$="SY" then theModule$="AD"
    if theModule$="WO" then theModule$="SF"
    if pos(theModule$=upgradedModules$,2)=0 then
        upgradedModules$=upgradedModules$+theModule$
    endif

    if cvs(datafile$,8)="sys-01" then
       normalize_sys01=1
       do_make_masks=1
       
       use java.util.GregorianCalendar
       Calendar! = new GregorianCalendar()
    endif; rem ' sys-01

    rem --- Does this file contain sequence numbers that need to be processed separately later?
    if seq_no_files!.containsKey(cvs(datafile$,8)) then
        seq_no_files!.put(cvs(datafile$,8),"y")
        do_sequence_numbers=1
    endif

    rem --- Build APM_VENDMAST.MEMO_1024 (apm-01) from apm-09 (APM_VENDCMTS) Vendor Comments
    if cvs(datafile$,8)="apm-09" then
        gosub init_apm01_memo1024
        goto next_file
    endif

    rem --- Build ARM_CUSTMAST.MEMO_1024 (arm-01) from arm-05 (ARM_CUSTCMTS) Customer Comments
    if cvs(datafile$,8)="arm-05" then
        gosub init_arm01_memo1024
        goto next_file
    endif

    rem --- Build BMM_BILLMAST.MEMO_1024 (bmm-01) from bmm-09 (BMM_BILLCMTS) BOM Comments
    if cvs(datafile$,8)="bmm-09" then
        gosub init_bmm01_memo1024
        goto next_file
    endif
    
    rem --- Initialize and update glm-06 and adm-09/19/39
    if cvs(datafile$,8)="glm-06" then
        gosub init_update_glm06
        goto next_file
    endif

    rem --- Build GLM_ACCT.MEMO_1024 (glm-01) from glm-09 (GLM_ACCTCOMMENTS) Account Comments
    if cvs(datafile$,8)="glm-09" then
        gosub init_glm01_memo1024
        goto next_file
    endif

    rem --- Build IVM_ITEMMAST.MEMO_1024 (ivm-01) from ivm-09 (IVM_ITEMCMTS) Item Comments
    if cvs(datafile$,8)="ivm-09" then
        gosub init_ivm01_memo1024
        goto next_file
    endif

    rem --- Build SFE_WOMASTR.MEMO_1024 (sfe-01) from woe-07 (SFE_WOCOMNT (sfe-07)) Work Order Comments
    if cvs(datafile$,8)="woe-07" then
        gosub init_sfe01_memo1024
        goto next_file
    endif

    rem --- Split old header/detail files into separate files
    switch pos(cvs(datafile$,8)="woe-11;woe-21;woe-31;",7)
        case 1; rem --- woe-11
            split_record_id$="A"
            header_keys_end_with$="00"
            headerfile$="wox-11"
            detailfile$="wox-12"

            rem --- Copy header records to temporary header file.
            header_file=1
            split_temp_file$=headerfile$
            split_temp_file_desc$="DataPort temp header re "+datafile$
            gosub split_header_detail_recs

            rem --- Copy detail records to temporary detail file.
            header_file=0
            split_temp_file$=detailfile$
            split_temp_file_desc$="DataPort temp detail re "+datafile$
            gosub split_header_detail_recs

            break
        case 8; rem --- woe-21
            split_record_id$="A"
            header_keys_end_with$="00"
            headerfile$="wox-21"
            detailfile$="wox-22"

            rem --- Copy header records to temporary header file.
            header_file=1
            split_temp_file$=headerfile$
            split_temp_file_desc$="DataPort temp header re "+datafile$
            gosub split_header_detail_recs

            rem --- Copy detail records to temporary detail file.
            header_file=0
            split_temp_file$=detailfile$
            split_temp_file_desc$="DataPort temp detail re "+datafile$
            gosub split_header_detail_recs

            break
        case 15; rem --- woe-31
            split_record_id$="A"
            header_keys_end_with$="00"
            headerfile$="wox-31"
            detailfile$="wox-32"

            rem --- Copy header records to temporary header file.
            header_file=1
            split_temp_file$=headerfile$
            split_temp_file_desc$="DataPort temp header re "+datafile$
            gosub split_header_detail_recs

            rem --- Copy detail records to temporary detail file.
            header_file=0
            split_temp_file$=detailfile$
            split_temp_file_desc$="DataPort temp detail re "+datafile$
            gosub split_header_detail_recs

            break
        case default
            rem --- unknown file, don't split
            break
    swend
    if pos(cvs(datafile$,8)="woe-11;woe-21;woe-31;",7) then
        rem --- Don't port files that were split. Their temporary header and detail files will be ported.
        print "   "+datafile$+" was split into separate header and detail files."
        print "   "+cvs(headerfile$,version_cvs)+" and "+cvs(detailfile$,version_cvs)+" will be ported separately."
        print (log_dev)"   "+datafile$+" was split into separate header and detail files."
        print (log_dev)"   "+cvs(headerfile$,version_cvs)+" and "+cvs(detailfile$,version_cvs)+" will be ported separately."
        goto next_file
    endif

rem --- Read the dictionary file ddm-03 to get data file info

    read(ddm03,key=pad(datafile$,6),dom=*next)

next_ddm03:
    if copy_sys01 then
       readrecord(ddm03,key=ddm03_key$,dom=read_normalize_xref)ddm03$
    else
       readrecord(ddm03,end=next_file)ddm03$
       if cvs(ddm03.file_name$,3)<>cvs(datafile$,3) then
          goto next_file
       endif
    endif

    rem --- Open/create ARS_CUSTDFLT and ARS_PARAMS for ARS-01A record
    if ddm03.file_name$="ARS-01" and ddm03.record_id$="A" then
        rem --- Add ARS_CUSTDFLT to file_xref! HashMap for ARS-01A key
        file_xref!.put("ARS-01A","ARS_CUSTDFLT")
        gosub get_new_template
        ars_custdflt_dev=new_file
        dim ars_custdflt_rec$:fattr(new_rec$)
        rem --- Reset ARS_PARAMS to file_xref! HashMap for ARS-01A key
        file_xref!.put("ARS-01A","ARS_PARAMS")
    endif

    rem --- Open/create GLS_CALENDAR and GLS_PARAMS for GLS-01A record
    if ddm03.file_name$="GLS-01" and ddm03.record_id$="A" then
        rem --- Add GLS_CALENDAR to file_xref! HashMap for GLS-01A key
        file_xref!.put("GLS-01A","GLS_CALENDAR")
        gosub get_new_template
        gls_calendar_dev=new_file
        dim gls_calendar_rec$:fattr(new_rec$)
        rem --- Reset GLS_PARAMS to file_xref! HashMap for GLS-01A key
        file_xref!.put("GLS-01A","GLS_PARAMS")
    endif

    rem --- Initialize opc_message file introduced in verision 17.10 to replace opc_msg_hdr (opm-04) and opc_msg_det (opm-14).
    rem --- Build opc_message from version 7 opm-04 and opm-14 records. Both opc_msg_hdr and opc_msg_det will NOT exist in v19 Addon.
    if source_version=7 and pos(cvs(datafile$,8)="opm-04;opm-14;",7) then
        print "   Copying data from  "+ddm03.file_name$+ddm03.record_id$
        print(log_dev)"   Copying data from    "+ddm03.file_name$+ddm03.record_id$
        if !opcMessage_done then
            rem --- Open opc_message file in Barista-Addon's data directory
            num_files=1
            dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
            rd_open_tables$[1]="OPC_MESSAGE",rd_open_opts$[1]="OTA"
            
            gosub open_tables
            
            opcMessage_dev=num(rd_open_chans$[1]);dim opcMessage$:rd_open_tpls$[1]

            rem --- Create and open opc_message in target directory
            table_alias$="OPC_MESSAGE",new_datafile$="opc_message",file_id$=new_datafile$
            erase destin_folder$+"/"+cvs(new_datafile$,version_cvs),err=*next
            gosub check_for_definition
            opcMessage_trgt=new_file

            rem --- Open v7 opm-04 and opm-14 source files
            opm04_dev=unt
            open (opm04_dev)source_folder$+"/"+cvs("opm-04",version_cvs)
            lock (opm04_dev)
            opm14_dev=unt
            open (opm14_dev)source_folder$+"/"+cvs("opm-14",version_cvs)
            lock (opm14_dev)

            rem --- Build v17.10 opc_message file from v7 opm-04 and opm-14 files
            read(opm04_dev,key="",dom=*next)
            while 1
                opm04_key$=key(opm04_dev,end=*break)
                readrecord(opm04_dev)opm04_rec$
                memo$=""
                read(opm14_dev,key=opm04_key$,dom=*next)
                while 1
                    readrecord(opm14_dev,end=*break)opm14_rec$(1)
                    if pos(opm14_rec$(1,4)=opm04_key$)<>1 then break
                    memo$=memo$+cvs(opm14_rec$(8,40),2)+$0A$; rem --- must trim all trailing $00$ from record
                wend
                redim opcMessage$
                opcMessage.firm_id$=opm04_rec$(1,2)
                opcMessage.message_code$=opm04_rec$(3,2)
                opcMessage.code_desc$=opm04_rec$(5,20)
                opcMessage.code_inactive$=""
                opcMessage.memo_1024$=memo$
                opcMessage$=field(opcMessage$)
                writerecord(opcMessage_trgt)opcMessage$
            wend

            unlock (opm04_dev,err=*next)
            close (opm04_dev,err=*next)
            unlock (opm14_dev,err=*next)
            close (opm14_dev,err=*next)
            opcMessage_done=1
        endif
        goto next_file
    endif

    rem --- Initialize poc_message file introduced in verision 17.10 to replace poc_msgcode (pom-04) and poc_msgline (pom-14).
    rem --- Both poc_msgcode and poc_msgline will NOT exist in v19 Addon.
    if pos(cvs(datafile$,8)="pom-04;pom-14;",7) then
        print "   Copying data from  "+ddm03.file_name$+ddm03.record_id$
        print(log_dev)"   Copying data from    "+ddm03.file_name$+ddm03.record_id$
        if !pocMessage_done then
            rem --- Open poc_message file in Barista-Addon's data directory
            num_files=1
            dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
            rd_open_tables$[1]="POC_MESSAGE",rd_open_opts$[1]="OTA"
            
            gosub open_tables
            
            pocMessage_dev=num(rd_open_chans$[1]);dim pocMessage$:rd_open_tpls$[1]

            rem --- Create and open poc_message in target directory
            table_alias$="POC_MESSAGE",new_datafile$="poc_message",file_id$=new_datafile$
            erase destin_folder$+"/"+cvs(new_datafile$,version_cvs),err=*next
            gosub check_for_definition
            pocMessage_trgt=new_file

            rem --- Open pom-04 and pom-14 source files
            pom04_dev=unt
            open (pom04_dev)source_folder$+"/"+cvs("pom-04",version_cvs)
            lock (pom04_dev)
            pom14_dev=unt
            open (pom14_dev)source_folder$+"/"+cvs("pom-14",version_cvs)
            lock (pom14_dev)

            rem --- Build v17.10 poc_message file pom-04 and pom-14 files
            read(pom04_dev,key="",dom=*next)
            while 1
                pom04_key$=key(pom04_dev,end=*break)
                readrecord(pom04_dev)pom04_rec$
                memo$=""
                read(pom14_dev,key=pom04_key$,dom=*next)
                while 1
                    readrecord(pom14_dev,end=*break)pom14_rec$
                    if pos(pom14_rec$(1,5)=pom04_key$)<>1 then break
                    memo$=memo$+cvs(pom14_rec$(9,40),2)+$0A$; rem --- must trim all trailing $00$ from record
                wend
                redim pocMessage$
                pocMessage.firm_id$=pom04_rec$(1,2)
                pocMessage.po_msg_code$=pom04_rec$(3,3)
                pocMessage.code_desc$=pom04_rec$(6,20)
                pocMessage.message_type$=pom04_rec$(26,1)
                pocMessage.code_inactive$=pom04_rec$(29,1)
                pocMessage.memo_1024$=memo$
                pocMessage$=field(pocMessage$)
                writerecord(pocMessage_trgt)pocMessage$
            wend

            unlock (pom04_dev,err=*next)
            close (pom04_dev,err=*next)
            unlock (pom14_dev,err=*next)
            close (pom14_dev,err=*next)
            pocMessage_done=1
        endif
        goto next_file
    endif

    gosub get_new_template
    rd_gen_table_tpl$=""
    data_seq_str$=""
    fields=0
    date_format$ = $$
    print "   Creating template for "+ddm03.file_name$+ddm03.record_id$+"  ("+table_alias$+")"
    print (log_dev)"   Creating template for "+ddm03.file_name$+ddm03.record_id$

    read(ddm04,key=ddm03.file_name$+ddm03.record_id$,dom=*next)
next_ddm04:
    readrecord(ddm04,end=end_ddm04)ddm04$
    if ddm04.file_name$<>ddm03.file_name$ or ddm04.record_id$<>ddm03.record_id$ then
       goto end_ddm04
    endif

    findrecord(ddm01,key=ddm04.data_name$,dom=next_ddm04)ddm01$
    rd_col_length=ddm01.fld_length
    if ddm04.fld_repeats>1 then
       rd_col_length=ddm01.fld_length*ddm04.fld_repeats
    endif
    rd_col_occurs_sfx$=""
    rd_col_occurs$=""
    rd_col_occurs=1
    if ddm04.fld_occurs>1 then
       rd_col_occurs$="YES"
       rd_col_occurs=ddm04.fld_occurs
    endif

    data_seq_pos=pos(pad(ddm04.data_name$,16)=data_seq_str$,20)
    if data_seq_pos<>0 then
       rd_occur_adj=num(data_seq_str$(data_seq_pos+17,2))
    else
       rd_occur_adj=0
       data_seq_str$=data_seq_str$+pad(ddm04.data_name$,16)+"-00;"
    endif

    for rd_curr_occur=1 to rd_col_occurs
       data_seq_pos=pos(pad(ddm04.data_name$,16)=data_seq_str$,20)
       data_seq_str$(data_seq_pos+17,2)=str(num(data_seq_str$(data_seq_pos+17,2))+1:"00")
       if rd_col_occurs$="YES" or rd_occur_adj>0 then
          rd_col_occurs_sfx$="_"+str(rd_curr_occur+rd_occur_adj:"00")
       endif

           rem --- Remove slashes from data name
       data_name$ = ddm04.data_name$
       s_pos = pos("/" = data_name$)

       while s_pos
          data_name$ = data_name$(1, s_pos-1) + data_name$(s_pos+1)
          s_pos = pos("/" = data_name$)
       wend

       rem ' field names can only contain [A-Z0-9_]
       data_name$=tbl(cvs(data_name$,7),tbl=az_09)
       rd_temp_seg$=cvs(data_name$,3)+rd_col_occurs_sfx$+":"
       datatype$=iff(ddm01.data_type$="N","N","C")
       rd_temp_seg$=rd_temp_seg$+datatype$+"("+str(rd_col_length)
       if rd_col_occurs$<>"YES"
         if ddm04.fld_sep$="X" or ddm04.fld_sep$="Y" or ddm04.fld_sep$="E" then
           rd_temp_seg$=rd_temp_seg$+"*"
         endif
       endif    
       if rd_col_occurs$="YES" or rd_occur_adj>0
         if rd_curr_occur+rd_occur_adj=rd_col_occurs and datatype$="C" or           datatype$<>"C"
           if ddm04.fld_sep$="X" or ddm04.fld_sep$="Y" or ddm04.fld_sep$="E"
             if rd_temp_seg$(len(rd_temp_seg$),1)<>"*"
               rd_temp_seg$=rd_temp_seg$+"*"
             endif
           endif
         endif
       endif  
       rd_temp_seg$=rd_temp_seg$+")"
       fields = fields + 1
       if len(rd_gen_table_tpl$) then
          rd_gen_table_tpl$=rd_gen_table_tpl$+","
       endif
       rd_gen_table_tpl$=rd_gen_table_tpl$+rd_temp_seg$
       rem ' Date formats
       if ddm01.data_type$="D" then
          date_format$ = date_format$ + chr(rd_col_length)
       else
          if ddm01.data_type$="A" and rd_col_length=2 then
             date_format$ = date_format$ + chr(255)
          else
             date_format$ = date_format$ + chr(0)
          endif
       endif
   next rd_curr_occur

   goto next_ddm04

az_09: table 7f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 30 31 32 33 34 35 36 37 38 39 5f 5f 5f 5f 5f 5f 5f 41 42 43 44 45 46 47 48 49 4a 4b 4c 4d 4e 4f 50 51 52 53 54 55 56 57 58 59 5a 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f 5f

end_ddm04:
    if rd_gen_table_tpl$="" then
       escape; goto eof
    endif

    dim old_rec$:rd_gen_table_tpl$
    fields$="name["+str(fields)+"]:c(20*)"
    dim old_fieldlist$:fields$
    old_fieldlist$=fattr(old_rec$,"")
    record_id_field$=""
    for j=1 to fields
        if pos("RECORD_ID_"=old_fieldlist.name$[j]) then
           record_id_field$=old_fieldlist.name$[j]
           break
        endif
    next

    rem ' Data record ID does not match dictionary record ID.
    if pos(cvs(datafile$,8)="pre-21;pre-31;pre-41;pre-51;prt-11;prt-21;prt-31;",7)
:   or pos(cvs(datafile$,8)="woe-12;woe-22;woe-32;woe-42;wom-03;wot-01;wot-03;wot-11;wot-12;wot-21;wot-23;wot-31;wot-33;",7) then 
        record_id_field$=""
    endif
    
    rem ' Special address processing?
    addr_lines=0
    if source_version=6 and pos($0a$+"CITY"+$0a$=new_fields$) and pos($0a$+"STATE_CODE"+$0a$=new_fields$)
       if pos($0a$+"ADDR_LINE_3"+$0a$=old_fieldlist$) then
          addr_lines=3
       endif
       if pos($0a$+"ADDR_LINE_4"+$0a$=old_fieldlist$) then
          addr_lines=4
       endif
       if pos($0a$+"ADDR_LINE_5"+$0a$=old_fieldlist$) then
          addr_lines=5
       endif
    endif

    rem ' Do we need to set the shipto type?
    ar_shipto = table_alias$="OPE_INVHDR" or table_alias$="OPT_INVHDR"

    rem ' Correlate old fieldnames to new fieldnames
    dim new_fieldlist$:fields$
    new_fieldlist$=$$
    for field=1 to fields
        old_fieldname$=old_fieldlist.name$[field]
		
		rem --- Change old "WO" to new "SF" for sfs_params
		if copy_sys01 and old_fieldname$="WO" then 
			old_fieldname$="SF"
		endif
		
		gosub set_new_fieldname
        new_fieldlist$ = new_fieldlist$ + new_fieldname$ + $0a$
    next field
    if copy_sys01 then
       datafile$=cvs("sys-01",version_cvs)
    endif
    if pom02_dev and pos(cvs(datafile$,8)="pom-02") then
        close (pom02_dev,err=*next)
        pom02_dev=0
    endif
    old_file=unt
    open (old_file)source_folder$+"/"+datafile$
    lock (old_file)
    print "   Copying data from  "+ddm03.file_name$+ddm03.record_id$
    print(log_dev)"   Copying data from    "+ddm03.file_name$+ddm03.record_id$

    rem --- Source PRM-10 W records require special handling to build PRC_W2BOX
    if file_id$="PRC_W2BOX" then
          gosub split_prm10w_recs
          goto next_file
    endif

next_rec:
    readrecord(old_file,end=eof)old_rec$
    if pos("00"=old_rec$)=1 goto next_rec
	if pos("FIRM_ID"=fattr(old_rec$))=1 then
	    if pos(old_rec.firm_id$=firms$,2)=0 then
	        goto next_rec
        else
            thisFirm$=old_rec.firm_id$
        endif
    else
        thisFirm$=""
    endif

    rem --- Don't port blank customer numbers in ARE-05 Simple Invoice Entry Header
    if cvs(ddm03.file_name$,8)="are-05"
        if cvs(old_rec.customer_nbr$,3)=""
            goto next_rec
        endif
    endif

    rem --- Don't port blank GL account numbers in GLM-10 Chart Of Accounts Breaks
    if cvs(ddm03.file_name$,8)="glm-10"
        if cvs(old_rec.acct_nbr_brk$,3)=""
            goto next_rec
        endif
    endif

	rem --- fix data based on an old bug discovered in v6 (never fixed in v6) while writing data
	rem --- IV Period End swapped the Operator ID and System Date fields in the BB records
	if cvs(ddm03.file_name$,8)="ivt-04" or cvs(ddm03.file_name$,8)="ivt-01"
		if old_rec.trans_source$="BB"
			if old_rec.operator_id$(2,1)>=$21$ and old_rec.operator_id$(2,1)<=$2c$
				tmp_fld$=old_rec.operator_id$
				old_rec.operator_id$=old_rec.system_date$
				old_rec.system_date$=tmp_fld$
			endif
		endif
	endif

    if record_id_field$<>"" and field(old_rec$,record_id_field$,err=*continue)<>ddm03.record_id$ then
       goto next_rec
    endif
    if copy_sys01 and field(old_rec$,old_fieldlist.name$[2],err=*continue) + field(old_rec$,old_fieldlist.name$[3],err=*continue) <> file_normalization$(1,4) then
       goto next_rec
    endif
    if copy_sys01 and file_normalization$(1,4)="AR00" then
        rem --- Write ARS_CUSTDFLT for ARS-01A records
        gosub write_ars_custdflt_recs
    endif
    if copy_sys01 and file_normalization$(1,4)="GL00" then
        rem --- Write GLS_CALENDAR for GLS-01A records
        gosub write_gls_calendar_recs
    endif

    rem --- Initialize opc_message file introduced in verision 17.10 to replace opc_msg_hdr (opm-04) and opc_msg_det (opm-14).
    rem --- Build opc_message from version 6 ARM-10G records.
    if source_version=6 and datafile$="ARM-10" and record_id_field$="RECORD_ID_G" then
        opcMessage_trgt=new_file
        dim opcMessage$:fattr(new_rec$)
        gosub write_opcMessage_recs
        goto next_rec
    endif

rem --- start field loop

    for field=1 to fields

rem --- Retrieve value$ if applicable

        if new_fieldlist.name$[field]=$$ then continue
        value$=field(old_rec$,old_fieldlist.name$[field],err=*continue)

rem --- Populate new internal sequence from old display sequence

        keyval$ = table_alias$+"."+new_fieldlist.name$[field]
        int_seq! = internal_seq!.get(keyval$)
        if int_seq! <> null() then
            int_seq$ = int_seq!
            if pos(cvs(int_seq$,3)+$0a$=new_fieldlist$)<>0
                if cvs(value$,3)<>"" then value$=str(num(value$):"000000000000")
            else
                field new_rec$,int_seq$=str(num(value$):"000000000000")
            endif
        endif

rem --- check for specific values that can't be found any other way

        if cvs(new_fieldlist.name$[field],3)="REPORT_ALIAS"
            if cvs(value$,3)="AR_STATEMENT" value$="ARR_STATEMENTS"
            if cvs(value$,3)="OP_INVOICE" value$="OPR_INVOICES"
            if cvs(value$,3)="OP_PICKINGLIST" value$="OPR_ODERPICKLST"
            if cvs(value$,3)="OP_QUOTE" value$="OPR_QUOTE"
            if cvs(value$,3)="PO_PURCHASE_ORDER" value$="POR_POPRINT"
            if cvs(value$,3)="PO_REQS"value$="POR_REQS"
        endif
        if source_version=6 and (datafile$="ARM-10" OR datafile$="APM-10") and old_fieldlist.name$[field]="PROX_OR_DAYS" and cvs(value$,2)="A"
            value$="P"
        endif
		
		rem --- Change old "WO" to new "SF" for sfs_params
		if copy_sys01 and old_fieldlist.name$[field]="WO"
			value$="SF"
		endif
        
		if source_version=6 and datafile$="GLM-22"
            while pos($22$=value$)
                value$(pos($22$=value$,1))=" "
            wend
            if pos("OUTPUT_OPER"=old_fieldlist.name$[field])=1
                x$=old_fieldlist.name$[field]
                if pos(value$="+-")=0
                    value$=" "
                endif
                if value$=" " and num(field(new_rec$,"output_total_"+x$(len(x$)-1,2)))<>0
                    value$="+"
                endif
            endif
            value$=cvs(value$,2)
        endif
        if old_fieldlist.name$[field]="ZIP_CODE" and len(cvs(value$,2))=9
            if value$(6,4)="0000"
                value$=value$(1,5)
            endif
        endif    
		if new_fieldlist.name$[field]="OP_INT_SEQ_REF"
			if cvs(value$,3)<>"" then value$=str(num(value$):"000000000000")
		endif

rem --- GLM-32 sequence numbers need to start at '001', not '000' in Barista/Addon
        if cvs(datafile$,8)="glm-32" then
            rem --- if sequence numbers start with '000' for a firm+report,
            rem --- then increment all sequence numbers (except '999') for that firm+report
             if old_rec$(1,4)<>glm32_rpt$
                glm32_rpt$=old_rec$(1,4)
                if old_rec$(5,3)="000"
                    glm32_inc=1
                else
                    glm32_inc=0
                endif
            endif
            if new_fieldlist.name$[field]="SEQUENCE_NO"
                if cvs(value$,3)<>"999" then value$=str(num(value$)+glm32_inc:"000")
            endif
        endif
		
rem --- Check for Temp Vendor

        if cvs(old_fieldlist.name$[field],2)="VENDOR_NBR"
            if pos("TEMP  "=value$)=1
                if temp_vend$=""
                    temp_vend_ok$="N"
                    goto next_rec
                else
                    value$=temp_vend$
                endif
            endif
        endif

rem --- Set all Retain Invoice History Detail flags to Y

        if old_fieldlist.name$[field]="INV_HIST_FLG"
            value$="Y"
        endif

rem --- Set all Retain Customer flags to Y

        if new_fieldlist.name$[field]="RETAIN_CUST"
            value$="Y"
        endif
		
rem --- set Inventory auto number flag

        if cvs(datafile$,8)="sys-01"
            if old_rec$(3,4)="IV00"
                new_rec.auto_no_iv$=old_rec.auto_number$
            endif
        endif

rem --- Set blank Credit Hold (Y;N;E) field values to N
        if cvs(new_fieldlist.name$[field],3)="CRED_HOLD"
            if cvs(value$,2)="" value$="N"
        endif

        rem --- set Stocking Level to "W" for all items
        if cvs(new_fieldlist.name$[field],3)="STOCK_LEVEL"
            value$="W"
        endif

rem --- CUSTOMER_ID and VENDOR_ID data needs adjusting when masks are less than full size

		if cvs(new_fieldlist.name$[field],3) = "CUSTOMER_ID"
			if custSizeMap!.containsKey(thisFirm$) then
				cust_size = cast(BBjNumber, custSizeMap!.get(thisFirm$))
				if cust_size > 0 and cust_size < 6 then
					value$=pad(value$(1,cust_size),6,"R","0")
				endif
			endif
		endif
		
		if cvs(new_fieldlist.name$[field],3) = "VENDOR_ID"
			if vendSizeMap!.containsKey(thisFirm$) then
				vend_size = cast(BBjNumber, vendSizeMap!.get(thisFirm$))
				if vend_size > 0 and vend_size < 6 then
					value$=pad(value$(1,vend_size),6,"R","0")
				endif
			endif
		endif
		
rem --- Call custom program - Note - there is no CALL/ENTER list, so all variables will be 
rem     available. Common usages for this would be things like data conversion, since value$
rem     holds the data in the field coming in, and old_fieldlist.name$[field] is the variable
rem     name from the dictionary of the source data. datafile$ is the source datafile name.

        if custom_prog$<>""
            call custom_prog$
        endif

rem --- Check value of new field for blanks if part of primary key
        if pos($0a$+new_fieldlist.name$[field]+$0a$=$0a$+key_temp$+$0a$)>0
            if cvs(value$,2)=""
                if pos(new_fieldlist.name$[field]+$0a$=blank_fields$)=0
                    if field_translations!.get(new_fieldlist.name$[field]+"."+cvs(value$,3))=null()
                            rem --- Do NOT report these missing key fields, they are optional
                            if cvs(new_datafile$,2)="apt-05" and cvs(new_fieldlist.name$[field],2)="AP_INV_NO" goto no_msg
                            if cvs(new_datafile$,2)="art-06" and cvs(new_fieldlist.name$[field],2)="AR_CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="art-16" and cvs(new_fieldlist.name$[field],2)="AR_CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="art-26" and cvs(new_fieldlist.name$[field],2)="AR_CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="bme-01" and cvs(new_fieldlist.name$[field],2)="BM_REFERENCE" goto no_msg
                            if cvs(new_datafile$,2)="glt-06" and cvs(new_fieldlist.name$[field],2)="GL_ADT_NO" goto no_msg
                            if cvs(new_datafile$,2)="ope-03" and cvs(new_fieldlist.name$[field],2)="ORDER_NO" goto no_msg
                            if cvs(new_datafile$,2)="pre-01" and cvs(new_fieldlist.name$[field],2)="CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="pre-11" and cvs(new_fieldlist.name$[field],2)="CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="pre-21" and cvs(new_fieldlist.name$[field],2)="CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="pre-31" and cvs(new_fieldlist.name$[field],2)="CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="pre-41" and cvs(new_fieldlist.name$[field],2)="CHECK_NO" goto no_msg
                            if cvs(new_datafile$,2)="pre-51" and cvs(new_fieldlist.name$[field],2)="CHECK_NO" goto no_msg
                            if pos("sam-"=cvs(new_datafile$,2))=1 and cvs(new_fieldlist.name$[field],2)="ITEM_ID" goto no_msg
                        blank_fields$=blank_fields$+new_fieldlist.name$[field]+$0a$
                        print (err_dev) "field "+new_fieldlist.name$[field]+" in file "+new_datafile$+" has a blank value and no translation specified." 
no_msg:
                    endif
                endif    
            endif
        endif

rem --- if this field.value is listed in flat file "field_translations" then translate to new value

        translate_value!=field_translations!.get(new_fieldlist.name$[field]+"."+cvs(value$,3))
        if translate_value!<>null()
            translate_value$=str(translate_value!)
            value$=translate_value$
            rem --- track translations used so can later verify records exist for them in master files
            if used_translations!.containsKey(new_fieldlist.name$[field]) then
                rem --- add current firm to firmSet! for this translations
                firmSet!=cast(java.util.TreeSet,used_translations!.get(new_fieldlist.name$[field]))
            else
                rem --- init new firmSet! for this translation and add current firm to it
                firmSet! = new java.util.TreeSet()
            endif
            firmSet!.add(thisFirm$)
            used_translations!.put(new_fieldlist.name$[field],firmSet!)
        endif

        rem --- Check value of new field for quotes if part of primary key or file verified field, and NOT a packed date.
        if asc(date_format$(field))<>3 and asc(date_format$(field))<>255 then
            redim ddm_elements$
            readrecord(ddm_elements,key=pad(new_fieldlist.name$[field],16),dom=*next)ddm_elements$
            if cvs(ddm_elements.dd_data_name$,2)="" then
                readrecord(ddm_table_cols,key=pad(new_fieldlist.name$[field],16)+pad(cvs(new_datafile$,4),16),knum="DBVAR_NAME",dom=*endif)ddm_table_cols$
                readrecord(ddm_elements,key=pad(ddm_table_cols.dd_data_name$,16),dom=*next)ddm_elements$
            endif
            if pos($0a$+new_fieldlist.name$[field]+$0a$=$0a$+key_temp$+$0a$)>0 or cvs(ddm_elements.dd_attr_dtab$,2)<>"" then
                if pos($22$=value$) then
                    print (err_dev) "field "+new_fieldlist.name$[field]+" in file "+new_datafile$+" contains a quote that can cause problems." 
                endif
            endif
        endif

rem --- date formatting

        attr$=fattr(old_rec$,old_fieldlist.name$[field])
        fld_sep$=attr$(3,1)
        fld_typ$=attr$(1,1)
        fld_len=dec(attr$(10,2))
        if fld_sep$=$0a$ and len(value$)>fld_len and dec(fld_typ$)=1 
            if pos($20$<>value$(fld_len+1))=0
                value$=value$(1,fld_len)
            endif
        endif
        switch asc(date_format$(field))
           case 0
              break
           case 3
              if len(cvs(value$,3)) then
                 value$=str(asc(value$)-32+1900+add_year)+str(asc(value$(2))-32:"00")+str(mod(asc(value$(3))-32,100):"00")
              else
                 value$=pad("",8)
              endif
              if date(jul(num(value$(1,4)),num(value$(5,2)),num(value$(7,2)),err=*next),err=*next)>"" break
              print (err_dev)"There is an invalid date in table "+new_datafile$+". Value was "+value$+" in field "+new_fieldlist.name$[field]+" and has been changed to blanks."
              value$=""
              invalid_date$="Y"
              break
           case 2
           case 6
              gosub fix_yymmdd_date
              break
           case 8
              value$=str(num(value$(1,4))+add_year)+value$(5)
              if date(jul(num(value$(1,4)),num(value$(5,2)),num(value$(7,2)),err=*next),err=*next)>"" break
              print (err_dev)"There is an invalid date in table "+new_datafile$+". Value was "+value$+" in field "+new_fieldlist.name$[field]+" and has been changed to blanks."
              value$=""
              invalid_date$="Y"
              break
           case 255
              if len(cvs(value$,3)) then
                 value$=str(asc(value$)-32:"00")+str(asc(value$(2))-32:"00")
              else
                 value$=pad("",8)
              endif
              break
           case default; escape; rem ' should never get here
        swend

rem --- replace non-print characters with spaces

        value$=cvs(value$,16)

rem --- Field terminators can result in short character fields that need to be padded.
        rem --- Get length of character fields from string template.
        if hta(fattr(new_rec$,new_fieldlist.name$[field]))(1,2)="01" then
            fieldlen$=hta(fattr(new_rec$,new_fieldlist.name$[field]))(19,4)
            fieldlen=dec(ath(fieldlen$))
            rem --- Pad field as necessary
            if len(value$)<fieldlen then
                value$=pad(value$, fieldlen)
            endif
        endif

        field new_rec$,new_fieldlist.name$[field]=value$

    next field

rem --- final misc processing prior to writing new record    

    if addr_lines then gosub reformat_address

    if ar_shipto then
       shipto_no = -1
       shipto_no = num(field(new_rec$,"shipto_no"),err=*next)
       switch shipto_no
           case 0; shipto_type$="B"; break
           case 99; shipto_type$="M"; break
           case default; shipto_type$="S"; break
       swend
       field new_rec$,"shipto_type"=shipto_type$
    endif

rem --- As needed, fix ivs_default.inventoried

    if table_alias$="IVS_DEFAULTS" then
        if new_rec.lotser_item$<>"Y" then new_rec.inventoried$="N"
    endif
    
rem --- Initialize NS_ITEM_ID in POE_REQDET, POE_PODET, POE_QADET, POE_RECDET, and POT_RECDET

    if pos(cvs(datafile$,8)="poe-11;poe-12;poe-13;poe-14;pot-14",7)
        dim pom02$:pom02_tpl$
        readrecord(pom02_dev,key=new_rec.firm_id$+new_rec.po_line_code$,dom=*endif)pom02$
        if pom02.line_type$="N" then
            new_rec.ns_item_id$=new_rec.item_id$
            new_rec.item_id$(1)=""
        else
            new_rec.ns_item_id$(1)=""
        endif
    endif

rem --- In ARE_INVOICEDET, ARE_RECURRINGDET and ART_INVOICEDET, convert 2-char SEQUENCE_NUM to 3-char zero filled SEQUENCE_NO.
    
    if pos(cvs(datafile$,8)="ape-11;ape-13;apt-11",7) then
        wk$=fattr(new_rec$,"sequence_no")
        new_rec.sequence_no$=pad(cvs(new_rec.sequence_no$,3), dec(wk$(10,2)), "R", "0")
    endif

rem --- Initialize BMM_BILLOPER.WO_OP_REF and SFE_WOOPRTN.WO_OP_REF

    if pos(cvs(datafile$,8)="bmm-03;woe-02",7)
        wk$=fattr(new_rec$,"wo_op_ref")
        new_rec.wo_op_ref$=pad(new_rec.op_seq$, dec(wk$(10,2)), "R", "0")
    endif

rem --- Initialize BMM_BILLMAT.WO_REF_NUM and SFE_WOMATL.WO_REF_NUM

    if pos(cvs(datafile$,8)="bmm-02;woe-22",7)
        wk$=fattr(new_rec$,"wo_ref_num")
        new_rec.wo_ref_num$=pad(new_rec.material_seq$, dec(wk$(10,2)), "R")
    endif

rem --- Initialize BMM_BILLMSUB.WO_REF_NUM and  SFE_WOSUBCNT.WO_REF_NUM

    if pos(cvs(datafile$,8)="bmm-05;woe-32",7)
        wk$=fattr(new_rec$,"wo_ref_num")
        new_rec.wo_ref_num$=pad(new_rec.subcont_seq$, dec(wk$(10,2)), "R")
    endif

rem ---  "PRM-01 - Employee Inactive - License Count"
    if cvs(new_datafile$,2)="prm-01" and cvs(new_rec.term_date$,3)>"" then
         new_rec.empl_inactive$="Y"
    endif

rem --- "prt-02 and prt-03 have new sequence number in new file only"
    if pos(";"+cvs(new_datafile$,11)+";"=";prt-02;prt-03;pre-02;pre-11;pre-21;pre-31;pre-41;pre-51;")>0 then
        dflt_seq_key$="INTERNAL_SEQ_NO"
        call stbl("+DIR_SYP")+"bas_sequences.bbj",dflt_seq_key$,new_seq$,table_chans$[all]
        new_rec.internal_seq_no$=new_seq$
    endif

rem --- If new_field_defaults not set for Payroll year, use Current Year from source Payroll parameters.
        if pos(";"+cvs(new_datafile$,11)+";"=";prc_taxcode;prc_contcode;prc_taxtable;prc_w2box;prt-01;prt-05;prt-11;prt-21;prt-31;")>0 and cvs(default_pr_year$,2)="" then
            if prYearMap!.containsKey(thisFirm$) then
                new_rec.year$=prYearMap!.get(thisFirm$)
            endif
        endif

    new_rec$=field(new_rec$)

write_record:

    if keysize then
       k$=xkgen(new_rec$,xfin$,0)
       if len(k$)>keysize then
          writerecord(new_file,key=k$(1,keysize),dom=duplicate_record,err=write_err)new_rec$
       else
          writerecord(new_file,key=k$,dom=duplicate_record,err=write_err)new_rec$
       endif
    else
       writerecord(new_file,dom=duplicate_record,err=write_err)new_rec$
    endif

    po_det$=new_datafile$
    po_file! = po_files!.get(po_det$)
    if po_file! <> null() and cvs(old_rec.customer_nbr$,3)<>""then gosub convert_po_files
        
    goto next_rec

duplicate_record:

    if cvs(new_datafile$,2)="glm-06" or cvs(new_datafile$,2)="adm-19" or cvs(new_datafile$,2)="opm-14" goto next_rec
    dupe_rec$="Y"
    call stbl("+DIR_SYP")+"bac_key_template.bbj",table_alias$,"",key_temp1$,rd_table_chans$[all],rd_stat$
    if len(key_temp1$)>0
        dim dupe_key$:key_temp1$
        dupe_key$=new_rec$
        dupe_key$=field(dupe_key$)
    endif    
    print (err_dev)"There are duplicate records being written to "+new_datafile$+" with a key template of"
    if len(key_temp1$)>0
        print (err_dev)"   "+key_temp1$
        print (err_dev)"   and values of "+dupe_key$
        print (err_dev)"   This key will be ignored."
    else
        print (err_dev)"   No key could be determined. The record value is"
        print (err_dev)"   "+new_rec$
        print (err_dev)"   This record will be ignored."
    endif
    goto next_rec

eof:
    rem --- Payroll Daily Entry new Header file
    if cvs(new_datafile$,11)="pre-02" then
        gosub header_pre02_recs
    endif

    rem --- Initialize glm-08 (GLM_BUDGETMASTER.REVISION_SRC)
    if cvs(datafile$,8)="glm-08" then
        gosub init_glm08
    endif

    key_temp$=""

    if old_file then
       close (old_file)
       old_file=0
    endif
    if new_file then
       close (new_file)
       new_file=0
    endif
    if copy_sys01 then
       goto read_normalize_xref
    endif
    goto next_ddm03

rem ' Auto-resize on error
write_err:
    if err<>1 then escape
    fid$=fid(new_file)
    new_file$=fid$(9)
    print "*** Resizing ",new_file$," ... ",
    temp_file$=new_file$+".bak"
    erase temp_file$,err=*next
    fid$=fid$(1,8)+temp_file$
    fid$(7,2)=bin(len(new_rec$),2); rem ' new record size
    fin$=fin(new_file)(86)
    keyed = asc(and(fid$(1,1),$06$)) or asc(and(fid$(1,1),$08$)) or fid$(1,1)=$0d$
    multi = keyed and asc(fid$(2,1))=0
    single = keyed and !(multi)
    if multi then
       file fid$,fin$
    else
       file fid$
    endif
    close (new_file)
    open (new_file)new_file$
    lock (new_file)
    temp_file=unt
    open (temp_file)temp_file$
    lock (temp_file)
    while 1
        readrecord(new_file,end=*break)rec$
        if single then
           k$=keyp(new_file)
           writerecord(temp_file,key=k$)rec$
        else
           writerecord(temp_file)rec$
        endif
    wend
    close (new_file)
    close (temp_file)
    erase new_file$
    rename temp_file$ to new_file$
    open (new_file)new_file$
    lock (new_file)
    print "(resized)"
    goto write_record

end_read_loop:
    if old_dir then
       close (old_dir)
       old_dir=0
    endif
    if normalize_sys01 then
       print "   sys-01 will be normalized to different parameter files"
       print (log_dev)"sys-01"
       print (log_dev)"   sys-01 will be normalized to different parameter files"
       file_normalization=unt
       open (file_normalization)filedir$+"file_normalization"
       copy_sys01=1
read_normalize_xref:
       read(file_normalization,end=end_read_normalize_xref)file_normalization$
       if file_normalization$="" then
          goto read_normalize_xref
       endif
       ddm03_key$=cvs(file_normalization$(9,6),version_cvs)+file_normalization$(15,1)
       datafile$=ddm03_key$(1,6)
       goto next_ddm03
end_read_normalize_xref:
       close (file_normalization)
       normalize_sys01=0
       goto end_read_loop
    endif; rem ' normalize_sys01

rem --- Now go populate masks and sequences as needed
    if do_make_masks then gosub make_masks
	if do_sequence_numbers then gosub set_sequence_numbers
    
    if temp_vend_ok$="N"
        rd_meter_data$="Temporary vendor records found not ported. See error file."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter
        print "   Temporary vendor records found. See err file"
        print (err_dev)"Temporary vendor records found without a valid stbl("+$22$+"+DP_TEMP_VEND"+$22$+")."
        print (err_dev)"Those records were excluded from the DataPort."
        print (err_dev)"Please set this variable up in the config file and rerun the DataPort."
    endif

    if len(blank_fields$)>0
        rd_meter_data$="Blank values for elements in primary keys exist. See error file for details."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter        
        print "   Blank values for elements in primary keys exist. See error file for details."
    endif
    while len(blank_fields$)>0
        print (err_dev)"Element "+blank_fields$(1,pos($0a$=blank_fields$)-1)+" has a blank value with no translation specified."
        print (err_dev)"  Consider converting to a non-blank value and re-running DataPort."
        blank_fields$=blank_fields$(pos($0a$=blank_fields$)+1)
    wend

    if dupe_rec$="Y" 
        rd_meter_data$="Duplicate keys exist in the ported data. See error file for details."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter        
        print "   Duplicate keys exist in the ported data. See error file for details."
    endif

    if invalid_date$="Y" 
        rd_meter_data$="Invalid dates exist in the ported data. See error file for details."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter        
        print "   Invalid dates exist in the ported data. See error file for details."
    endif

rem --- Populate sequence record(s)

    if len(firms$)>0
        for firms=1 to len(firms$) step 2
            dim ads_sequences$:fattr(ads_sequences$)
            ads_sequences.firm_id$=firms$(firms,2)
            ads_sequences.seq_id$=pad("INTERNAL_SEQ_NO",16)
            ads_sequences.description$="Internal Sequence Number"
            ads_sequences.active$="Y"
            ads_sequences.seq_no_increment=1
            ads_sequences.seq_mask$="000000000000"
            ads_sequences.seq_query_new$="N"
            ads_sequences.asc_comp_id$="01007514"
            ads_sequences.asc_prod_id$="AD"
            readrecord(ads_sequences_dev,key=ads_sequences.firm_id$+ads_sequences.seq_id$,dom=*next)ads_sequences$
            if ads_sequences.seq_last_used<1000 ads_sequences.seq_last_used=1000
            ads_sequences$=field(ads_sequences$)
            writerecord(ads_sequences_dev)ads_sequences$
        next firms    
    endif
    
rem --- Initialize *s_params.post_to_gl with SYM-04's post_gl flag

    gosub init_params_post_gl

rem --- Verify master file records exist for used translations
    if used_translations!.size() then
        gosub verify_translation_master
    endif

rem --- On successful complete, initialize conversion control file data/adx_conversionCtrl
    fileReader!=new FileReader(convCtrlIniFile!)
    convCtrl!=new Properties()
    convCtrl!.load(fileReader!)
    fileReader!.close()
    
    convCtrl!.setProperty("data_source","DataPort")
    convCtrl!.setProperty("start_version",str(source_version:"00.00"))
    convCtrl!.setProperty("modules",upgradedModules$)
    
    rem --- Get version of this Addon installation
    redim adm_modules$
    readrecord(adm_modules_dev,key=pad("01007514AD",11),dom=*next)adm_modules$
    end_version$=cvs(adm_modules.version_id$,3)
    convCtrl!.setProperty("end_version",end_version$)
    
    rem --- Put initialized conversion control file in target data directory
    convCtrlFilePath$=destin_folder$
    convCtrlFileName$=convCtrlFilePath$+"/adx_conversionCtrl"
    convCtrlFile!=new File(convCtrlFileName$)
    rem --- Create path/file if doesn't exist
    if !convCtrlFile!.exists() then
        convCtrlPath!=new File(convCtrlFilePath$)
        if !convCtrlPath!.exists() then
            convCtrlPath!.mkdirs()
        endif
        convCtrlFile!.createNewFile()
    endif
    fileWriter!=new FileWriter(convCtrlFile!)
    convCtrl!.store(fileWriter!,"Addon Conversion Control File")
    fileWriter!.close()
    print(log_dev)"Initialized ",convCtrlFileName$

rem --- On successful completion, create conversion marker files in target directory for all file dependency groups that were ported
    fileDependencies!=new HashMap()
    convCtrlKeys!=convCtrl!.stringPropertyNames()
    versionsVect!=BBjAPI().makeVector()
    versionsVect!.addAll(java.util.Arrays.asList(convCtrl!.getProperty("conversion_versions").split(";")))
    for i=0 to versionsVect!.size()-1
        version$=versionsVect!.getItem(i)
        iter!=convCtrlKeys!.iterator()
        while iter!.hasNext()
            key$=iter!.next()
            if pos(version$+"_"=key$)=1 then
                fileDependencies!.put(convCtrl!.getProperty(key$),version$)
            endif
        wend
    next i
    requiredFiles!=BBjAPI().makeVector()
    keyIter!=fileDependencies!.keySet().iterator()
    while keyIter!.hasNext()
        rem --- Create marker file for each dependency where all the files exist
        create_marker=1
        requiredFiles!.clear()
        key!=keyIter!.next()
        requiredFiles!.addAll(java.util.Arrays.asList(key!.split(";")))
        version$=fileDependencies!.get(key!)
        for i=0 to requiredFiles!.size()-1
            file!=new File(destin_folder$+"/"+requiredFiles!.get(i))
            if !file!.exists() then
                create_marker=0
                break
            endif
        next i
        if create_marker then
            markerFile!=new File(destin_folder$+"/v"+version$+"_"+requiredFiles!.get(0))
            markerFile!.createNewFile()
    
            print(log_dev)"Created conversion marker file: ",markerFile!.getAbsoluteFile()
        endif
    wend
    
done: rem --- Done    

    rem --- Enable disabled triggers
    if disabledTriggers!.size() then
        iter!=disabledTriggers!.iterator()
        while iter!.hasNext()
            trigger$=iter!.next()
            aAdminTriggers!=aAdminBase!.getTriggers(trigger$)
            aAdminTriggers!.setBoolean(BBjAdminTriggers.ENABLED, 1)
            aAdminTriggers!.commit()
            print "Enabled triggers for: ",trigger$
            print (log_dev)"Enabled triggers for: ",trigger$
        wend
    endif

    print (log_dev)$0a$+"DataPort completed"
    print (log_dev)"DataPort End Time: "+DATE(0:"%Mz/%Dz/%Y %hz:%mz:%sz")+" "

    print (err_dev)$0a$+"DataPort completed"
    print (err_dev)"DataPort End Time: "+DATE(0:"%Mz/%Dz/%Y %hz:%mz:%sz")+" "

    rd_meter_data$="DataPort End Time: "+DATE(0:"%Mz/%Dz/%Y %hz:%mz:%sz")
    rd_meter_proc_recs=rd_meter_total_recs
    rd_meter_action$="LST-END"
    gosub disp_meter

    release

write_opcMessage_recs: rem --- Build v17.10 opc_message file from v6 ARM-10G records

    rem --- Translate message_code as necessary
    translate_value!=field_translations!.get("MESSAGE_CODE."+cvs(old_rec.message_code$,3))
    if translate_value!<>null() then old_rec.message_code$=str(translate_value!)

    rem --- Write opc_mesage "header" info (prior to v17.10 was opm-04 (OPC_MSG_HDR) record)
    rec=5
    if old_rec.record_id$="1" then
        rec=0
        redim opcMessage$
        opcMessage.firm_id$=old_rec.firm_id$
        opcMessage.message_code$=old_rec.message_code$
        opcMessage.code_desc$=old_rec.code_desc$
        opcMessage.code_inactive$=""
        opcMessage.memo_1024$=""
        opcMessage$=field(opcMessage$)
        writerecord(opcMessage_trgt)opcMessage$
    endif

    rem --- Write opc_message "detail"/memo info, (prior to v17.10 was opm-14(OPC_MSG_DET) records)
    redim opcMessage$
    opcMessage_key$=old_rec.firm_id$+old_rec.message_code$
    readrecord(opcMessage_trgt,key=opcMessage_key$,dom=*next)opcMessage$
    if opcMessage.firm_id$+opcMessage.message_code$=opcMessage_key$ then
        memo$=""
        write_recs=0
        for i=5 to 1 step -1
            message_text$=cvs(field(old_rec$,"MESSAGE_TEXT_"+str(i:"00")),2)
            rem --- Skip trailing blank message lines
            if message_text$<>"" or write_recs then
                write_recs=1
                memo$=message_text$+$0A$+memo$
            endif
        next i
        opcMessage.memo_1024$=opcMessage.memo_1024$+memo$
        opcMessage$=field(opcMessage$)
        writerecord(opcMessage_trgt)opcMessage$
    endif

    return

write_ars_custdflt_recs: rem --- Write ARS_CUSTDFLT for ARS-01A records

    rem --- Init ARS_CUSTDFLT record
    redim ars_custdflt_rec$
    ars_custdflt_rec.firm_id$=old_rec.firm_id$
    ars_custdflt_rec.record_id_d$="D"
    ars_custdflt_rec.disc_code$=old_rec.disc_code$
    ars_custdflt_rec.territory$=old_rec.territory$
    ars_custdflt_rec.slspsn_code$=old_rec.slspsn_code$
    ars_custdflt_rec.tax_code$=old_rec.tax_code$
    ars_custdflt_rec.ar_terms_code$=old_rec.terms_code$
    ars_custdflt_rec.ar_dist_code$=old_rec.dist_code$
    ars_custdflt_rec.inv_hist_flg$="Y"; rem --- Detail invoice history is always retained now

    rem --- Translate ARS_CUSTDFLT codes listed in field_translations file
    tmp$=fattr(ars_custdflt_rec$,"")
    tmp=pos($0A$=tmp$,1,0)
    dim tmp_fieldlist$:"name["+str(tmp)+"]:c(20*)"
    tmp_fieldlist$=tmp$

    for field=1 to tmp
        value$=field(ars_custdflt_rec$,tmp_fieldlist.name$[field])
        translate_value!=field_translations!.get(tmp_fieldlist.name$[field]+"."+cvs(value$,3))
        if translate_value!<>null()
            translate_value$=str(translate_value!)
            field ars_custdflt_rec$,tmp_fieldlist.name$[field]=translate_value$
        endif
    next field

    rem --- Write ARS_CUSTDFLT record
    ars_custdflt_rec$=field(ars_custdflt_rec$)
    writerecord(ars_custdflt_dev)ars_custdflt_rec$

    return

write_gls_calendar_recs: rem --- Write GLS_CALENDAR for GLS-01A records

    rem --- Init GLS_CALENDAR record
    redim gls_calendar_rec$
    gls_calendar_rec.firm_id$=old_rec.firm_id$
    rem --- Convert 2-char YY to 4-char YYYY
    value$=old_rec.current_year$
    gosub fix_yymmdd_date
    gls_calendar_rec.year$=value$
    gls_calendar_rec.total_pers$=old_rec.total_pers$
    gls_calendar_rec.cal_start_date$=""
    total_pers=num(gls_calendar_rec.total_pers$)
    for i=1 to total_pers
        period_name$="period_name_"+str(i:"00")
        field gls_calendar_rec$,period_name$=field(old_rec$,period_name$)
    next i
    for i=1 to total_pers
        abbr_name$="abbr_name_"+str(i:"00")
        field gls_calendar_rec$,abbr_name$=field(old_rec$,abbr_name$)
    next i
    for i=1 to total_pers
        rem --- Convert packed MD to unpacked MMDD 
        per_ending$="per_ending_"+str(i:"00")
        value$=field(old_rec$,per_ending$)
        if len(cvs(value$,3)) then
            value$=str(asc(value$)-32:"00")+str(mod(asc(value$(2))-32,100):"00")
        else
            value$=pad("",4)
        endif
        field gls_calendar_rec$,per_ending$=value$
    next i
    for i=1 to total_pers
        locked_flag$="locked_flag_"+str(i:"00")
        field gls_calendar_rec$,locked_flag$=field(old_rec$,locked_flag$)
    next i
    for i=1 to total_pers
        rem --- Convert packed YMD to unpacked YYYYMMDD
        locked_date$="locked_date_"+str(i:"00")
        value$=field(old_rec$,locked_date$)
        if len(cvs(value$,3)) then
            value$=str(asc(value$)-32+1900+add_year)+str(asc(value$(2))-32:"00")+str(mod(asc(value$(3))-32,100):"00")
        else
            value$=pad("",8)
        endif
        field gls_calendar_rec$,locked_date$=value$
    next i

    rem --- Calculate calendar start date based on last date of last period.
    calendar_year=num(gls_calendar_rec.year$)-1
    begdate$=field(gls_calendar_rec$,"per_ending_"+str(total_pers:"00"))
    if begdate$(1,4)<gls_calendar_rec.per_ending_01$ then calendar_year=calendar_year+1
    dd$=begdate$(3,2)
    rem --- Adjust last day of February for leap year
    if begdate$(1,2)="02" then
        if begdate$(3,2)="29" and !Calendar!.isLeapYear(calendar_year) then begdate$(3,2)="28"
        if begdate$(3,2)="28" and Calendar!.isLeapYear(calendar_year) then begdate$(3,2)="29"
    endif
    julian=jul(calendar_year,num(begdate$(1,2)),num(begdate$(3,2)))+1
    begdate$=date(julian:"%Yl%Mz%Dz")
    gls_calendar_rec.cal_start_date$=begdate$(5)

    rem --- Write GLS_CALENDAR record
    gls_calendar_rec$=field(gls_calendar_rec$)
    writerecord(gls_calendar_dev)gls_calendar_rec$

    return

make_masks: rem --- Port masks from SYS-01 to Barista's ads_masks

    if len(firms$)=0 then return
    masks_dev=unt
    open (masks_dev)stbl("+DIR_BRA")+"ads_masks.dat"
    masks_dev2=unt
    open (masks_dev2)stbl("+DIR_BRA")+"ads_masks.dat"
    comp_id$=stbl("+AON_APPCOMPANY")

    rem --- Get comp_id for PR/PRB module
    pr_comp_id$=""
    pr_prod_id$=""
    read(adm_modules_dev,key="",dom=*next)
    while 1
        readrecord(adm_modules_dev,end=*break)adm_modules$
        if pos(adm_modules.asc_prod_id$="PR PRB",3)=0 then continue
        pr_comp_id$=adm_modules.asc_comp_id$
        pr_prod_id$=adm_modules.asc_prod_id$
        break
    wend
    if pr_comp_id$="" then pr_comp_id$=comp_id$
    if pr_prod_id$="" then pr_prod_id$="PR "

    rem --- Get ads_masks template and key template

    readrecord(ddm_table_tpls,key=pad("ADS_MASKS",16))ddm_table_tpls$
    dim ads_masks$:ddm_table_tpls.template$
    call stbl("+DIR_SYP")+"bac_key_template.bbj","ADS_MASKS","PRIMARY",key_fieldlist$,rd_table_chans$[all],rd_stat$
    dim ads_masks_key$:key_fieldlist$

    rem --- Do the Masks

    old_file=unt
    open (old_file)source_folder$+"/"+cvs("sys-01",version_cvs)
    
    for firms=1 to len(firms$) step 2
        firm_id$=firms$(firms,2)
        
        rem --- Port existing masks from source SYS-01
        read(old_file,key=firm_id$,dom=*next)
        while 1
            read(old_file,end=*break)a0$,a1$,a2$,a3$,a4$,a5$,a6$,a7$,a8$,a9$,a10$

            if pos(firm_id$=a0$)<>1 then break
            if len(a0$)<>6 and pos("BM00"=a0$)<>3 continue
            if num(a0$(1,2),err=*continue)=0 continue
            if a0$(5,2)<>"00" continue
            if pos(a0$(3,2)="APARBMGLIVPRSFWO",2)=0 continue
            if a0$(3,2)="WO" a0$(3,2)="SF";rem convert v7 Shop Floor to v6 Work Orders
            if a0$(3,2)<>"BM"
                if pos(a0$(3,2)="IVWO",2)=0
                    dim ads_masks$:fattr(ads_masks$)
                    ads_masks.firm_id$=a0$(1,2)
                    if a0$(3,2)="PR" then
                        ads_masks.asc_comp_id$=pr_comp_id$
                        ads_masks.asc_prod_id$=pr_prod_id$
                    else
                        ads_masks.asc_comp_id$=comp_id$
                        ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                    endif
                    ads_masks.dd_mask_type$="I"
                    id$=cvs(a5$,3)
                    if len(id$)>0
                        for id=1 to len(id$)
                            if a0$(3,2)="GL"
                                if id$(id,1)="#" id$(id,1)="0"
                            endif
                            if pos(a0$(3,2)="ARAPPR",2)
                                if id$(id,1)="0" id$(id,1)="U"
                            endif
                        next id
                    endif
                    a5$=id$
                    ads_masks.dd_attr_mski$=cvs(a5$,3)
                    ads_masks.dd_attr_msko$=cvs(a5$,3)
                    ads_masks$=field(ads_masks$)
                    if cvs(a1$,3)<>"" or cvs(a5$,3)<>""
                        writerecord(masks_dev)ads_masks$
                    endif    
                endif    
    
                dim ads_masks$:fattr(ads_masks$)
                ads_masks.firm_id$=a0$(1,2)
                if a0$(3,2)="PR" then
                    ads_masks.asc_comp_id$=pr_comp_id$
                    ads_masks.asc_prod_id$=pr_prod_id$
                else
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                endif
                ads_masks.dd_mask_type$="A"
                ads_masks.dd_attr_mski$=cvs(a6$,3)
                ads_masks.dd_attr_msko$=cvs(a6$,3)
                ads_masks$=field(ads_masks$)
                if cvs(a6$,3)<>""
                    writerecord(masks_dev)ads_masks$
                endif
    
                dim ads_masks$:fattr(ads_masks$)
                ads_masks.firm_id$=a0$(1,2)
                if a0$(3,2)="PR" then
                    ads_masks.asc_comp_id$=pr_comp_id$
                    ads_masks.asc_prod_id$=pr_prod_id$
                else
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                endif
                ads_masks.dd_mask_type$="U"
                ads_masks.dd_attr_mski$=cvs(a7$,3)
                ads_masks.dd_attr_msko$=cvs(a7$,3)
                ads_masks$=field(ads_masks$)
                if cvs(a7$,3)<>""
                    writerecord(masks_dev)ads_masks$
                endif    
    
                dim ads_masks$:fattr(ads_masks$)
                ads_masks.firm_id$=a0$(1,2)
                if a0$(3,2)="PR" then
                    ads_masks.asc_comp_id$=pr_comp_id$
                    ads_masks.asc_prod_id$=pr_prod_id$
                else
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                endif
                ads_masks.dd_mask_type$="R"
                if a0$(3,2)="GL"
                    ads_masks.dd_mask_type$="P"
                endif
                if a0$(3,2)="IV"
                    ads_masks.dd_mask_type$="C"
                endif
                ads_masks.dd_attr_mski$=cvs(a8$,3)
                ads_masks.dd_attr_msko$=cvs(a8$,3)
                ads_masks$=field(ads_masks$)
                if cvs(a8$,3)<>""
                    writerecord(masks_dev)ads_masks$
                endif    
    
                if a0$(3,2)="IV"
                    dim ads_masks$:fattr(ads_masks$)
                    ads_masks.firm_id$=a0$(1,2)
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                    ads_masks.dd_mask_type$="P"
                    ads_masks.dd_attr_mski$=cvs(a9$,3)
                    ads_masks.dd_attr_msko$=cvs(a9$,3)
                    ads_masks$=field(ads_masks$)
                    if cvs(a9$,3)<>""
                        writerecord(masks_dev)ads_masks$
                    endif
    
                        dim ads_masks$:fattr(ads_masks$)
                    ads_masks.firm_id$=a0$(1,2)
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                    ads_masks.dd_mask_type$="V"
                    ads_masks.dd_attr_mski$=cvs(a10$,3)
                    ads_masks.dd_attr_msko$=cvs(a10$,3)
                    ads_masks$=field(ads_masks$)
                    if cvs(a10$,3)<>""
                        writerecord(masks_dev)ads_masks$
                    endif    
                endif
            else
                dim ads_masks$:fattr(ads_masks$)
                ads_masks.firm_id$=a0$(1,2)
                if a0$(3,2)="PR" then
                    ads_masks.asc_comp_id$=pr_comp_id$
                    ads_masks.asc_prod_id$=pr_prod_id$
                else
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                endif
                ads_masks.dd_mask_type$="H"
                ads_masks.dd_attr_mski$=cvs(a1$,3)
                ads_masks.dd_attr_msko$=cvs(a1$,3)
                ads_masks$=field(ads_masks$)
                if cvs(a1$,3)<>""
                    writerecord(masks_dev)ads_masks$
                endif    
    
                dim ads_masks$:fattr(ads_masks$)
                ads_masks.firm_id$=a0$(1,2)
                if a0$(3,2)="PR" then
                    ads_masks.asc_comp_id$=pr_comp_id$
                    ads_masks.asc_prod_id$=pr_prod_id$
                else
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                endif
                ads_masks.dd_mask_type$="M"
                ads_masks.dd_attr_mski$=cvs(a2$,3)
                ads_masks.dd_attr_msko$=cvs(a2$,3)
                ads_masks$=field(ads_masks$)
                if cvs(a2$,3)<>""
                    writerecord(masks_dev)ads_masks$
                endif   
    
                dim ads_masks$:fattr(ads_masks$)
                ads_masks.firm_id$=a0$(1,2)
                if a0$(3,2)="PR" then
                    ads_masks.asc_comp_id$=pr_comp_id$
                    ads_masks.asc_prod_id$=pr_prod_id$
                else
                    ads_masks.asc_comp_id$=comp_id$
                    ads_masks.asc_prod_id$=pad(a0$(3,2),3)
                endif
                ads_masks.dd_mask_type$="O"
                ads_masks.dd_attr_mski$=cvs(a3$,3)
                ads_masks.dd_attr_msko$=cvs(a3$,3)
                ads_masks$=field(ads_masks$)
                if cvs(a3$,3)<>""
                    writerecord(masks_dev)ads_masks$
                endif
            endif
        end_mask:
        wend
    
        rem --- Initialize masks new to Barista-Addon
        
        rem --- AD: U - Units         <== use IV U mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=comp_id$
        ads_masks_key.asc_prod_id$=pad("IV",3)
        ads_masks_key.dd_mask_type$="U"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("AD",3)
            ads_masks.dd_mask_type$="U"
            writerecord(masks_dev)ads_masks$
        endif
    
        rem --- AP: U - Units         <== use IV U mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=comp_id$
        ads_masks_key.asc_prod_id$=pad("IV",3)
        ads_masks_key.dd_mask_type$="U"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("AP",3)
            ads_masks.dd_mask_type$="U"
            writerecord(masks_dev)ads_masks$
        endif
    
        rem --- AR: Q - Quantity      <== use demo data mask

        rem --- BM: C - Cost          <== use IV C mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=comp_id$
        ads_masks_key.asc_prod_id$=pad("IV",3)
        ads_masks_key.dd_mask_type$="C"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("BM",3)
            ads_masks.dd_mask_type$="C"
            writerecord(masks_dev)ads_masks$
        endif
    
        rem --- BM: R - Rate          <== use demo data mask

        rem --- BM: U - Units         <== use IV U mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=comp_id$
        ads_masks_key.asc_prod_id$=pad("IV",3)
        ads_masks_key.dd_mask_type$="U"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("BM",3)
            ads_masks.dd_mask_type$="U"
            writerecord(masks_dev)ads_masks$
        endif

        rem --- GL: % - Percent       <== use demo data mask

        rem --- IV: % - Percent       <== use demo data mask

        rem --- SF: C - Cost          <== use IV C mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=comp_id$
        ads_masks_key.asc_prod_id$=pad("IV",3)
        ads_masks_key.dd_mask_type$="C"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("SF",3)
            ads_masks.dd_mask_type$="C"
            writerecord(masks_dev)ads_masks$
        endif

        rem --- SF: H - Hours         <== use BM H mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=comp_id$
        ads_masks_key.asc_prod_id$=pad("BM",3)
        ads_masks_key.dd_mask_type$="H"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("SF",3)
            ads_masks.dd_mask_type$="H"
            writerecord(masks_dev)ads_masks$
        endif

        rem --- SF: I - ID            <== use PR I mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=pr_comp_id$
        ads_masks_key.asc_prod_id$=pr_prod_id$
        ads_masks_key.dd_mask_type$="I"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("SF",3)
            ads_masks.dd_mask_type$="I"
            writerecord(masks_dev)ads_masks$
        endif

        rem --- SF: M - Mtl Factor    <== use BM M mask if present, otherwise use demo data mask
        dim ads_masks_key$:fattr(ads_masks_key$)
        ads_masks_key.firm_id$=firm_id$
        ads_masks_key.asc_comp_id$=comp_id$
        ads_masks_key.asc_prod_id$=pad("BM",3)
        ads_masks_key.dd_mask_type$="M"
        dim ads_masks$:fattr(ads_masks$)
        readrecord(masks_dev2,key=ads_masks_key$,dom=*next)ads_masks$
        if cvs(ads_masks.dd_attr_mski$,2)<>"" or cvs(ads_masks.dd_attr_msko$,2)<>"" then
            ads_masks.asc_prod_id$=pad("SF",3)
            ads_masks.dd_mask_type$="M"
            writerecord(masks_dev)ads_masks$
        endif

        rem --- SF: % - Percent       <== use demo data mask
    
    next firms
    
    return

set_sequence_numbers:
rem --- Update ads_sequences with sequence numbers from ars(ops)-10N, aps-10N, pos-10N.
rem --- This routine assumes an ads_sequences record for each sequence number id (CUSTOMER_ID, VENDOR_ID, AR_INV_NO, etc.) exists,
rem --- i.e., it doesn't have enough info to create an ads_sequences record from scratch,
rem --- although flat file sequence_no could probably be modified to contain all we need if this becomes an issue.

    dim sequence_no$:sequence_no_tpl$
    dim sequence_no[4]
    sequence_iterator!=sequence_no!.keySet().iterator()
    while sequence_iterator!.hasNext()
        sequence_id$ = str(sequence_iterator!.next())
        sequence_no$=sequence_no!.get(sequence_id$)
        
        old_file=unt
        if source_version=6
            old_file$=cvs(sequence_no.v6_file$,version_cvs+2)
            old_element_no=num(sequence_no.v6_element_no$)
        else
            old_file$=cvs(sequence_no.v7_file$,version_cvs+2)
            old_element_no=num(sequence_no.v7_element_no$)
        endif
        if seq_no_files!.get(cvs(old_file$,11))<>"y" then continue; rem --- Skip if this file wasn't processed
        open (old_file,err=*continue)source_folder$+"/"+old_file$

        if len(firms$)>0
            for firms=1 to len(firms$) step 2
                if pos("CUSTOMER_ID"=sequence_id$)=1
                    param_file=unt                    
                    open (param_file,err=*continue)source_folder$+"/"+cvs("sys-01",version_cvs)
                    read (param_file,key=firms$(firms,2)+"AR00",dom=*continue)*,*,*,sys01_3$
                    if sys01_3$(13,1)<>"Y" then continue
                endif            
                read (old_file,key=firms$(firms,2)+"N",dom=*continue)*,sequence_no[all]              
                dim ads_sequences$:fattr(ads_sequences$)
                ads_sequences.firm_id$=firms$(firms,2)
                ads_sequences.seq_id$=pad(sequence_id$,16)                             
                readrecord(ads_sequences_dev,key=ads_sequences.firm_id$+ads_sequences.seq_id$,dom=*continue)ads_sequences$
                ads_sequences.seq_last_used=sequence_no[old_element_no]
                if cvs(ads_sequences.description$,3)<>""
                    ads_sequences$=field(ads_sequences$)         
                    writerecord(ads_sequences_dev)ads_sequences$
                endif
            next firms    
        endif 
        close (old_file,err=*next)
        
    wend

    return

init_params_post_gl: rem --- Initialize *s_params.post_to_gl with SYM-04's post_gl flag

    rem --- Open source sym-04
    sym04_dev=unt
    open (sym04_dev)source_folder$+"/"+cvs("sym-04",version_cvs)
    
    rem --- Loop thru modules that can post to gl
    modules$="APARBMGLIVOPPOPRSF"
    for i=1 to len(modules$) step 2
        mod$=modules$(i,2)

        rem --- Read this module's sym-04 record, skip if not found
        v6mod$=mod$
        if v6mod$="SF" then v6mod$="WO"
        dim r0$(2),r1$(64)
        find(sym04_dev,key=v6mod$,dom=*continue)r0$(1),r1$(1)
        if r0$<>v6mod$ then continue
        
        rem --- Open this module's destination *s_params, skip if not found
        skip_module=1
        param_file$=cvs(mod$,8)+"s_params"
        param_dev=unt
        open (param_dev,err=*next)destin_folder$+"/"+param_file$; skip_module=0
        if skip_module then continue

        rem --- Get *s_params template for this module
        findrecord(ddm_table_tpls,key=pad(cvs(param_file$,4),16))ddm_table_tpls$
        dim param$:ddm_table_tpls.template$

        rem --- Loop thru all firms in this module's *s_params updating *s_params.post_to_gl
        read(param_dev,key="",dom=*next)
        while 1
            key$=key(param_dev,end=*break)
            readrecord(param_dev)param$
            param.post_to_gl$=r1$(46,1)
            param$=field(param$)         
            writerecord(param_dev,key=key$)param$
            endif
        wend
        close(param_dev)
        
    next i
    close(sym04_dev)

    return
    
convert_po_files:

    po_hdr$=po_file!
    rem --- open hdr file - 
    rem --- Open manually, because we want to open in our target dir, 
    rem --- which isn't necessarily the data dir specified in ddm_tables.
    rem --- So use open_tables to just return actual file name and template for the alias,
    rem --- then open using destin_folder$

    num_files=1
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]=po_hdr$,rd_open_opts$[1]="TA"
    gosub open_tables
    po_hdr_dev=unt
    po_hdr_file$=rd_open_tables$[1]
    dim po_hdr_rec$:rd_open_tpls$[1]
    po_hdr_file$=po_hdr_file$(max(pos("/"=po_hdr_file$,-1,1),pos("\"=po_hdr_file$,-1,1))+1)
    open (po_hdr_dev,err=hdr_open_err)destin_folder$+"/"+po_hdr_file$

    rem --- get key template and load it w/ key fields from detail rec
    call stbl("+DIR_SYP")+"bac_key_template.bbj",po_hdr$,"",key_fieldlist$,rd_table_chans$[all],rd_stat$
    key_fields=pos(","=key_fieldlist$,1,0)+1            
    dim key_fields$:"name["+str(key_fields)+"]:c(20*)"
    dim po_hdr_key$:key_fieldlist$
    key_fields$=fattr(po_hdr_key$,"")
    for xwk=1 to key_fields
        field po_hdr_key$,key_fields.name$[xwk] = field(new_rec$,key_fields.name$[xwk])
    next xwk
    
    rem --- read header and update cust/order/shipto w/ data from old detail rec, set dropship flag
    read record (po_hdr_dev,key=po_hdr_key$)po_hdr_rec$
    if cvs(po_hdr_rec.customer_id$,3)<>"" and cvs(po_hdr_rec.customer_id$,3)<>cvs(old_rec.customer_nbr$,3)
        rem --- put out log entry that the po (req, po or receipt) refers to more than one cust/order
        print (err_dev)"There appears to be more than one customer order referenced in "+po_hdr_file$+" key "+po_hdr_key$+"."
        print (err_dev)"Replacing customer "+po_hdr_rec.customer_id$+" with "+old_rec.customer_nbr$+"."
    endif
    po_hdr_rec.dropship$="Y"
    po_hdr_rec.customer_id$=old_rec.customer_nbr$
    po_hdr_rec.order_no$=old_rec.order_number$
    po_hdr_rec.shipto_no$=old_rec.shipto_nbr$
    po_hdr_rec$=field(po_hdr_rec$)
    
    rem --- now read cust ship-to or cust mast and fill in po hdr's dropship address fields
    if cvs(old_rec.shipto_nbr$,3)=""
        arm_custmast_dev=unt
        dim rec$:arm_custmast_tpl$
        addr_file$="arm-01"
        open (arm_custmast_dev,err=addr_open_err)destin_folder$+"/"+addr_file$
        while 1
            read record (arm_custmast_dev,key=po_hdr_rec.firm_id$+po_hdr_rec.customer_id$,dom=*break)rec$
            po_hdr_rec.ds_name$=rec.customer_name$            
            gosub fill_dropship_address
            break
        wend
        close (arm_custmast_dev)       
    else
        if num(cvs(old_rec.shipto_nbr$,3))>0 and num(cvs(old_rec.shipto_nbr$,3))<99
            arm_custship_dev=unt
            dim rec$:arm_custship_tpl$
            addr_file$="arm-03"
            open (arm_custship_dev,err=addr_open_err)destin_folder$+"/"+addr_file$
            while 1
                read record (arm_custship_dev,key=po_hdr_rec.firm_id$+po_hdr_rec.customer_id$+po_hdr_rec.shipto_no$,dom=*break)rec$
                po_hdr_rec.ds_name$=rec.name$
                gosub fill_dropship_address
                break
            wend
            close (arm_custship_dev)          
        else
            if num(cvs(old_rec.shipto_nbr$,3))=99
                rem --- read opt-01/31 for manual (99) shipto
                opt_invhdr_dev=unt
                dim opt_invhdr$:opt_invhdr_tpl$
                addr_file$="opt-01"
                open (opt_invhdr_dev,err=addr_open_err)destin_folder$+"/"+addr_file$
                opt_invship_dev=unt
                dim rec$:opt_invship_tpl$
                addr_file$="opt-31"
                open (opt_invship_dev,err=addr_open_err)destin_folder$+"/"+addr_file$
                while 1
                    trip_key$=po_hdr_rec.firm_id$+opt_invhdr.ar_type$+po_hdr_rec.customer_id$+po_hdr_rec.order_no$
                    read(opt_invhdr_dev,key=trip_key$,dom=*next)opt_invhdr$
                    opt_invhdr_key$=key(opt_invhdr_dev,end=*break)
                    if pos(trip_key$=opt_invhdr_key$)<>1 then break
                    read record (opt_invhdr_dev)opt_invhdr$
                    read record (opt_invship_dev,key=po_hdr_rec.firm_id$+po_hdr_rec.customer_id$+opt_invhdr.ar_inv_no$,knum="AO_CUST_INV",dom=*break)rec$
                    po_hdr_rec.ds_name$=rec.name$            
                    gosub fill_dropship_address
                    break
                wend
                close (opt_invhdr_dev)
                close (opt_invship_dev)
            else
                rem --- invalid shipto
                po_hdr_rec.addr_line_1$="Unknown address"
            endif
        endif
    endif
    write record (po_hdr_dev)po_hdr_rec$
    
return

    hdr_open_err:
    
    rd_meter_data$="PO header file" + po_hdr_file$ + "not found in destination directory. Dropship info not converted."
    rd_meter_total_recs=vectPort!.size()
    rd_meter_action$="LST"
    gosub disp_meter
    print (err_dev)"Processing PO detail file " + new_datafile$ + ", record: " + old_rec$+"."+$0a$+"Cannot convert dropship fields because corresponding PO header file not found."
    
return

    addr_open_err:
    
    rd_meter_data$="File" + addr_file$ + "not found in destination directory. Dropship address not converted."
    rd_meter_total_recs=vectPort!.size()
    rd_meter_action$="LST"
    gosub disp_meter
    print (err_dev)"Processing PO detail file " + new_datafile$ + ", record: " + old_rec$+"."+$0a$+"Cannot insert dropship address in header file because file "+addr_file$+" not found."

return

fill_dropship_address:

    po_hdr_rec.ds_addr_line_1$=rec.addr_line_1$
    po_hdr_rec.ds_addr_line_2$=rec.addr_line_2$
    po_hdr_rec.ds_addr_line_3$=rec.addr_line_3$
    po_hdr_rec.ds_addr_line_4$=rec.addr_line_4$
    po_hdr_rec.ds_city$=rec.city$
    po_hdr_rec.ds_state_cd$=rec.state_code$
    po_hdr_rec.ds_zip_code$=rec.zip_code$
    
return

get_new_template:
    new_datafile$=cvs(datafile$,8)
    file_id$=ddm03.file_name$+ddm03.record_id$
	if len(cvs(file_id$,2))=7
		file_id$=cvs(file_id$(1,6),7)+file_id$(7,1)
	else
		file_id$=cvs(file_id$,7)
	endif
    file_id! = file_xref!.get(cvs(file_id$,3))
    if file_id!<>null() then
       rem ' found mapping for filename+record id
       file_id$ = str(file_id!)
       if len(file_id$)>7 then
          rem ' Longer new-style filename
          new_datafile$ = cvs(file_id$,8)
       else
          rem ' Drop the record id from the old-style filename
          new_datafile$=cvs(file_id$(1,len(file_id$)-1),8)
       endif
    else
       file_id! = file_xref!.get(cvs(datafile$,7))
       if file_id!<>null() then
          rem ' found mapping for filename
          file_id$ = str(file_id!)+ddm03.record_id$
          new_datafile$ = cvs(str(file_id!),8)
       endif
    endif

    table_alias! = file_alias!.get(cvs(file_id$,7))
    table_alias$ = iff(table_alias!=null(),"",cvs(str(table_alias!),3))
    print "Table Alias: ",table_alias$
    if table_alias$="" then
       print "   Data File "+file_id$+" will not be ported"
       print (err_dev)"   Data File "+file_id$+" will not be ported"
       goto eof
    endif

    call stbl("+DIR_SYP")+"bac_key_template.bbj",table_alias$,"",key_temp$,rd_table_chans$[all],rd_stat$
    while pos(",reserved_str:c(1)"=key_temp$)<>0
        key_temp$=key_temp$(1,pos(",reserved_str:c(1)"=key_temp$)-1)+key_temp$(pos(",reserved_str:c(1)"=key_temp$)+18)
    wend
    if cvs(key_temp$,2)<>""
        dim key_temp$:key_temp$
        key_temp$=fattr(key_temp$,"")
    endif

    findrecord(ddm_table_tpls,key=pad(table_alias$,16),dom=check_for_definition)ddm_table_tpls$
    dim new_rec$:ddm_table_tpls.template$
    new_fields$=$0a$+fattr(new_rec$,"")

    new_file=unt
    open (new_file,err=check_for_definition)destin_folder$+"/"+new_datafile$
    lock (new_file)
    a=msgbox(new_datafile$+" already exists in destination folder. Do you want to overwrite data?",3+32+65536,"DataPort")
    if a=6 then
       close (new_file)
       erase destin_folder$+"/"+new_datafile$
       goto check_for_definition
    endif
    if a=7 then
       goto eof; rem ' No-Skip
    endif
    if a=2 then
       goto eoj; rem ' Cancel-Exit
    endif
return; rem ' get_new_template

check_for_definition:
    new_file=0
    find (ddm_tables,key=pad(table_alias$,16),dom=undefined_file)
    call stbl("+DIR_SYP")+"bac_create_table.bbj",table_alias$,destin_folder$+"/"+new_datafile$,rd_table_chans$[all],"CREATE",status$
    if len(status$) then
       print "*** ERROR ***: Couldn't create "+new_datafile$+$0a$+status$
       print (err_dev)"*** ERROR ***: Couldn't create "+new_datafile$+$0a$+status$
       goto eof
    endif
    print "   New file "+new_datafile$+" created in folder "+destin_folder$
    print (log_dev)"   New file "+new_datafile$+" created"

    new_file=unt
    open (new_file)destin_folder$+"/"+new_datafile$
    lock (new_file,err=*next)
    xfid$=xfid(new_file)
    xfin$=$$
    keysize=dec(xfid$(2,4))
    if keysize then
       call stbl("+DIR_SYP")+"bac_create_xfin.bbj",table_alias$,xfin$,rd_table_chans$[all],status$
       if len(status$) then
          escape
       endif
    endif

    rem --- see if this table has any new fields (i.e., that weren't in v6 (or Payroll))
    rem --- if so, set default value based on new_field_defaults flat file
    rem --- new_rec$ only dim'd once/file, so setting the values once here should carry into every record
    if source_version=6 or pos(";"+cvs(new_datafile$,11)+";"=";prc_taxcode;prc_contcode;prc_taxtable;prc_w2box;prt-01;prt-05;prt-11;prt-21;prt-31;")>0 then
        new_rec_fields!=new_field_defaults!.keySet().iterator()
        while new_rec_fields!.hasNext()
            new_rec_field$ = str(new_rec_fields!.next())
            if pos(cvs(table_alias$,3)=new_rec_field$)=1
                new_rec_value$=new_field_defaults!.get(new_rec_field$)
                new_rec_fieldname$=new_rec_field$(pos("."=new_rec_field$)+1)
                field new_rec$,new_rec_fieldname$=new_rec_value$
            endif
        wend
    
        rem --- If new_field_defaults not set for Payroll year, will need to use Current Year from source Payroll parameters.
        if pos(";"+cvs(new_datafile$,11)+";"=";prc_taxcode;prc_contcode;prc_taxtable;prc_w2box;prt-01;prt-05;prt-11;prt-21;prt-31;")>0 then
            default_pr_year$=new_rec.year$
        endif
    endif
return

undefined_file:
    print "   Data File "+file_id$+" will not be ported"
    print (err_dev)"   Data File "+file_id$+" will not be ported"
    goto eof
return

set_new_fieldname:
    new_fieldname$=""

    if old_fieldname$="RESERVED_STR" then
       old_fieldname$="RESERVED_STR_02"
    endif

    if pos("RESERVED_STR_0"=old_fieldname$) or pos("RESERVED_NUM"=old_fieldname$) then
       new_fieldname$=""
       return
    endif

    if pos($0a$+old_fieldname$+$0a$=new_fields$) then
       new_fieldname$=old_fieldname$
    endif

    if new_fieldname$<>"" and new_fieldname$<>"RESERVED_STR" then
       return
    endif

    if source_version=6 and table_alias$="IVC_PRODCODE" and old_fieldname$="RESERVED_STR" then
       new_fieldname$="SA_LEVEL"
       goto check_fieldlist
    endif

    if pos("INV_DUE_DATE_"=old_fieldname$) then
       new_fieldname$="INV_DUE_BYDT_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("SLS_INV_DATE_"=old_fieldname$) then
       new_fieldname$="SLS_INV_BYDT_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("INV_OH_DATE_"=old_fieldname$) then
       new_fieldname$="INV_OH_BYDT_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("NBR_INV_DUE_"=old_fieldname$) then
       new_fieldname$="NO_INV_DUE_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("NBR_ORD_SHIP_"=old_fieldname$) then
       new_fieldname$="NO_ORD_SHIP_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("NBR_LNS_REC_"=old_fieldname$) then
       new_fieldname$="NO_LNS_REC_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("NBR_INV_DATE_"=old_fieldname$) then
       new_fieldname$="NO_INV_BYDT_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("NBR_INV_BYDT_"=old_fieldname$) then
       new_fieldname$="NO_INV_BYDT_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    if pos("EARN_DEDUCT_"=old_fieldname$) then
       new_fieldname$="EARNDEDUCT_"+old_fieldname$(len(old_fieldname$)-1)
       goto check_fieldlist
    endif

    keyval$ = table_alias$+"."+old_fieldname$
    new_fieldname! = field_file_xref!.get(keyval$)
    if new_fieldname! = null() then
       new_fieldname$ = ""
    else
       new_fieldname$=str(new_fieldname!)
       goto check_fieldlist
    endif

    keyval$=old_fieldname$
    new_fieldname! = field_name_xref!.get(keyval$)
    if new_fieldname! = null() then
       new_fieldname$ = ""
       return
    else
       new_fieldname$=str(new_fieldname!)
    endif

check_fieldlist:

    if pos($0a$+new_fieldname$+$0a$=new_fields$)=0 then
       new_fieldname$=""
    endif

return; rem ' set_new_fieldname

reformat_address:

    if addr_lines=5 then
       address$=pad(old_rec.addr_line_1$,24)+pad(old_rec.addr_line_2$,24)+pad(old_rec.addr_line_3$,24)+pad(old_rec.addr_line_4$,24)+pad(old_rec.addr_line_5$,24)
    endif
    if addr_lines=4 then
       address$=pad(old_rec.addr_line_1$,24)+pad(old_rec.addr_line_2$,24)+pad(old_rec.addr_line_3$,24)+pad(old_rec.addr_line_4$,24)
    endif
    if addr_lines=3 then
       address$=pad(old_rec.addr_line_1$,24)+pad(old_rec.addr_line_2$,24)+pad(old_rec.addr_line_3$,24)
    endif
    dim state$(2),city$(24)
    states$="ALAKAZARCACOCTDEDCFLGAHIIDILINIAKSKYLAMEMDMAMIMNMSMOMTNENVNHNJNMNYNCNDOHOKORPAPRRISCSDTNTXUTVTVAVIWAWVWIWY"
    states$=states$+"ABBCMBNBNLNSNTNUONPEQCSKYT"; rem ' Canadian province/territory codes
    if len(address$) and len(address$)/24 then
       for x=len(address$) to 1 step -24
           string1$=cvs(address$(x-23,24),2)
           if string1$="" then
              continue
           endif
           if cvs(state$,2)<>"" and cvs(city$,2)="" then
              if pos(" "=string1$)=0 then
                 city$(1)=string1$,address$(x-23,24)=""
                 break
              endif
           endif
           if len(string1$)<2 then
              city$(1)=string1$,address$(x-23,24)=""
              break
           endif
           if len(string1$)=2 then
              break
           endif
           if len(string1$)>2 and string1$(len(string1$)-2,1)<>" " then
              city$(1)=""
              break
           endif
           string2$=string1$(len(string1$)-1,2)
           if pos(string2$=states$,2)=0 then
              city$(1)=""
              break
           endif
           state$=string2$
           if len(string1$)=2 then
              if x>1 then
                 city$(1)=address$(x-47,24)
                 address$(x-47,48)=""
              endif
           else
              city$(1)=address$(x-23,len(string1$)-3)
              address$(x-23,24)=""
           endif
           string1$=cvs(city$,2)
           if len(string1$) and string1$(len(string1$),1)="," then
              string1$=string1$(1,len(string1$)-1)
           endif
           city$(1)=string1$
           if cvs(city$,2)<>"" then break
       next x
    endif

    if city$="" or state$="" then
       print(err_dev)"Error copying address fields"
    endif

    new_rec.addr_line_1$=address$(1,24)
    new_rec.addr_line_2$=address$(25,24)
    if pos("ADDR_LINE_3"=fattr(new_rec$))<>0 then new_rec.addr_line_3$=address$(49,24)
    if addr_lines=5 then
       new_rec.addr_line_4$=address$(73,24)
    endif
    new_rec.city$=city$
    new_rec.state_code$=state$
return; rem ' reformat_address

init_glm08: rem --- Initialize glm-08 (GLM_BUDGETMASTER.REVISION_SRC)
    if !new_file then return
    glm08_dev=new_file

    rem --- Initialize reserverd (non-user defined) budget revision codes in GLM_BUDGETMASTER (glm-08)
    read(glm08_dev,key="",dom=*next)
    while 1
        readrecord(glm08_dev,end=*break)new_rec$
        old_rev_src$=cvs(new_rec.revision_src$,2)
        if pos(old_rev_src$="0;2;4") then
            new_rec.revision_src$=old_rev_src$+"A"+new_rec.amt_or_units$
        endif
        if pos(old_rev_src$="1;3;5") then
            new_rec.revision_src$=old_rev_src$+"B"+new_rec.amt_or_units$
        endif
        new_rec$=field(new_rec$)         
        writerecord(glm08_dev)new_rec$
    wend

    rem --- Initialize user defined budget revision codes in GLM_BUDGETMASTER (glm-08)
    read(glm08_dev,key="",dom=*next)
    while 1
        readrecord(glm08_dev,end=*break)new_rec$
        dim next_rec$:fattr(new_rec$)
        next_rec$=new_rec$
        
        gosub get_revision_src
        
        if pos(next_rec.revision_src$(1,1)="0;1;2;3;4;5") then new_rec.revision_src$=next_rec.revision_src$
        new_rec$=field(new_rec$)         
        writerecord(glm08_dev)new_rec$
    wend

    return

get_revision_src: rem --- Get reserverd (non-user defined) budget revision code GLM_BUDGETMASTER.REVISION_SRC
    if pos(next_rec.revision_src$(1,1)=" ;0;1;2;3;4;5")=0 then
        old_rev_src$=next_rec.revision_src$(1,1)
        
        dim next_rec$:fattr(new_rec$)
        readrecord(glm08_dev,key=new_rec.firm_id$+old_rev_src$+new_rec.amt_or_units$,dom=*next)next_rec$

        gosub get_revision_src
    endif

    return

init_update_glm06: rem --- Initialize and update glm-06 and adm-09/19/39
    rem ---     Input: firms$

    rem --- Open files in Barista-Addon's data directory
    num_files=4
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="ADM_AUDITCONTROL",rd_open_opts$[1]="OTA"; rem --- glm-06
    rd_open_tables$[2]="ADM_PROCMASTER",rd_open_opts$[2]="OTA"; rem --- adm-09
    rd_open_tables$[3]="ADM_PROCDETAIL",rd_open_opts$[3]="OTA"; rem --- adm-19
    rd_open_tables$[4]="ADM_PROCTABLES",rd_open_opts$[4]="OTA"; rem --- adm-39
    
    gosub open_tables
    
    glm06_dev=num(rd_open_chans$[1]);dim glm06$:rd_open_tpls$[1]
    adm09_dev=num(rd_open_chans$[2]);dim adm09$:rd_open_tpls$[2]
    adm19_dev=num(rd_open_chans$[3]);dim adm19$:rd_open_tpls$[3]
    adm39_dev=num(rd_open_chans$[4]);dim adm39$:rd_open_tpls$[4]

    rem --- Create and open new files in target directory
    table_alias$="ADM_AUDITCONTROL",new_datafile$="glm-06",file_id$=new_datafile$
    erase destin_folder$+"/"+cvs(new_datafile$,version_cvs),err=*next
    gosub check_for_definition
    glm06_trgt=new_file

    table_alias$="ADM_PROCMASTER",new_datafile$="adm-09",file_id$=new_datafile$
    erase destin_folder$+"/"+cvs(new_datafile$,version_cvs),err=*next
    gosub check_for_definition
    adm09_trgt=new_file

    table_alias$="ADM_PROCDETAIL",new_datafile$="adm-19",file_id$=new_datafile$
    erase destin_folder$+"/"+cvs(new_datafile$,version_cvs),err=*next
    gosub check_for_definition
    adm19_trgt=new_file

    table_alias$="ADM_PROCTABLES",new_datafile$="adm-39",file_id$=new_datafile$
    erase destin_folder$+"/"+cvs(new_datafile$,version_cvs),err=*next
    gosub check_for_definition
    adm39_trgt=new_file

    rem --- Initialize glm-06 and adm-09/19/39 from Barista-Addon's data directory
    if len(firms$)>0
        glm06_initialized=0
        adm09_initialized=0
        adm19_initialized=0
        adm39_initialized=0
        for firms=1 to len(firms$) step 2
            this_firm$=firms$(firms,2); rem --- new install may not be using this firm yet
            
            glm06_firm$=""
            read(glm06_dev,key=glm06_firm$,dom=*next)
            while 1
                rem --- Find first non-blank firm
                glm06_key$=key(glm06_dev,err=*break)
                if len(glm06_key$)>=2 then
                    glm06_firm$=glm06_key$(1,2)
                    if cvs(glm06_firm$,2)<>"" then break
                endif
                read(glm06_dev)
            wend
            read(glm06_dev,key=glm06_firm$,dom=*next)
            while 1
                readrecord(glm06_dev,err=*break)glm06$
                if glm06.firm_id$<>glm06_firm$ then break
                glm06.firm_id$=this_firm$
                glm06.lstuse_date$=""
                glm06.lstuse_time$=""
                glm06.lstupd_date$=""
                glm06.lstupd_time$=""
                glm06.audit_number=0
                glm06$=field(glm06$)
                writerecord(glm06_trgt)glm06$
                glm06_initialized=1
            wend

            read(adm09_dev,key="",err=*next)
            while 1
                readrecord(adm09_dev,err=*break)adm09$
                adm09.firm_id$=this_firm$
                adm09$=field(adm09$)
                writerecord(adm09_trgt)adm09$
                adm09_initialized=1
            wend
            
            read(adm19_dev,key="",err=*next)
            while 1
                readrecord(adm19_dev,err=*break)adm19$
                adm19.firm_id$=this_firm$
                adm19$=field(adm19$)
                writerecord(adm19_trgt)adm19$
                adm19_initialized=1
            wend
            
            read(adm39_dev,key="",err=*next)
            while 1
                readrecord(adm39_dev,err=*break)adm39$
                adm39.firm_id$=this_firm$
                adm39$=field(adm39$)
                writerecord(adm39_trgt)adm39$
                adm39_initialized=1
            wend
        next firms    

        if !glm06_initialized then
            print "*** ERROR ***: Couldn't initialize ADM_AUDITCONTROL (glm-06)"
            print (err_dev)"*** ERROR ***: Couldn't initialize ADM_AUDITCONTROL (glm-06)"
        endif
        if !adm09_initialized then
            print "*** ERROR ***: Couldn't initialize ADM_PROCMASTER (adm-09)"
            print (err_dev)"*** ERROR ***: Couldn't initialize ADM_PROCMASTER (adm-09)"
        endif
        if !adm19_initialized then
            print "*** ERROR ***: Couldn't initialize ADM_PROCDETAIL (adm-19)"
            print (err_dev)"*** ERROR ***: Couldn't initialize ADM_PROCDETAIL (adm-19)"
        endif
        if !adm39_initialized then
            print "*** ERROR ***: Couldn't initialize ADM_PROCTABLES (adm-39)"
            print (err_dev)"*** ERROR ***: Couldn't initialize ADM_PROCTABLES (adm-39)"
        endif
    endif

    rem --- Update glm-06 and adm-09/19/39 from source files
    if len(firms$)>0
        rem --- Open files in source directory
        glm06_src=unt
        open (glm06_src)source_folder$+"/"+cvs("glm-06",version_cvs)
        port_sym04=0
        sym04_src=unt
        open (sym04_src,err=*next)source_folder$+"/"+cvs("sym-04",version_cvs); port_sym04=1
        port_sym06=0
        sym06_src=unt
        open (sym06_src,err=*next)source_folder$+"/"+cvs("sym-06",version_cvs); port_sym06=1
        port_sym09=0
        sym09_src=unt
        open (sym09_src,err=*next)source_folder$+"/"+cvs("sym-09",version_cvs); port_sym09=1
        port_sym19=0
        sym19_src=unt
        open (sym19_src,err=*next)source_folder$+"/"+cvs("sym-19",version_cvs); port_sym19=1
        port_sym39=0
        sym39_src=unt
        open (sym39_src,err=*next)source_folder$+"/"+cvs("sym-39",version_cvs); port_sym39=1
        if !port_sym04 then
            print "*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-04",version_cvs)
            print (err_dev)"*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-04",version_cvs)
        endif
        if !port_sym06 then
            print "*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-06",version_cvs)
            print (err_dev)"*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-06",version_cvs)
        endif
        if !port_sym09 then
            print "*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-09",version_cvs)
            print (err_dev)"*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-09",version_cvs)
        endif
        if !port_sym19 then
            print "*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-19",version_cvs)
            print (err_dev)"*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-19",version_cvs)
        endif
        if !port_sym39 then
            print "*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-39",version_cvs)
            print (err_dev)"*** ERROR ***: Couldn't find source file "+source_folder$+"/"+cvs("sym-39",version_cvs)
        endif

        rem --- read posting_controls into hash map
        posting_controls! = new java.util.HashMap()
        dim post_ctl$:"old_proc:c(10*=124),prog:c(8*=124),new_proc:c(16*=124),seq_nums:c(3*=)"
        post_ctl_xref = unt
        open (post_ctl_xref)filedir$+"posting_controls_xref"
        while 1
           read (post_ctl_xref,end=*break)post_ctl$
           if len(post_ctl$) then
              keyval$ = post_ctl.old_proc$+"."+cvs(post_ctl.prog$,version_cvs)
              dataVec! = BBjAPI().makeVector()
              dataVec!.addItem(post_ctl.new_proc$)
              dataVec!.addItem(cvs(post_ctl.seq_nums$,2))
              posting_controls!.put(keyval$,dataVec!)
           endif
        wend
        close (post_ctl_xref)

        rem --- read processes and process_details into hash maps
        processes! = new java.util.HashMap()
        process_details! = new java.util.HashMap()
        dim proc_pgm$:"old_proc:c(10*=124),prog:c(8*=124),new_proc:c(16*=124),seq_nums:c(3*=)"
        proc_pgm_xref = unt
        open (proc_pgm_xref)filedir$+"process_pgms_xref"
        while 1
           read (proc_pgm_xref,end=*break)proc_pgm$
           if len(proc_pgm$) then
              keyval$ = proc_pgm.old_proc$
              dataval$ = proc_pgm.new_proc$
              processes!.put(keyval$,dataval$)
              keyval$ = proc_pgm.old_proc$+"."+cvs(proc_pgm.prog$,version_cvs)
              dataVec! = BBjAPI().makeVector()
              dataVec!.addItem(proc_pgm.new_proc$)
              dataVec!.addItem(cvs(proc_pgm.seq_nums$,2))
              process_details!.put(keyval$,dataVec!)
           endif
        wend
        close (proc_pgm_xref)

        rem --- read process_files into hash maps
        process_files! = new java.util.HashMap()
        dim proc_file$:"old_proc:c(10*=124),file:c(6*=124),new_proc:c(16*=124),seq_num:c(3*=)"
        proc_file_xref = unt
        open (proc_file_xref)filedir$+"process_files_xref"
        while 1
           read (proc_file_xref,end=*break)proc_file$
           if len(proc_file$) then
              keyval$ = proc_file.old_proc$+"."+cvs(proc_file.file$,version_cvs)
              dataVec! = BBjAPI().makeVector()
              dataVec!.addItem(proc_file.new_proc$)
              dataVec!.addItem(cvs(proc_file.seq_num$,2))
              process_files!.put(keyval$,dataVec!)
           endif
        wend
        close (proc_file_xref)

        glm06_updated=0
        adm09_updated=0
        adm19_updated=0
        adm39_updated=0
        for firms=1 to len(firms$) step 2
            this_firm$=firms$(firms,2)
            
            read(glm06_src,key=this_firm$,err=*next)
            while 1
                dim glm06_0$(20),glm06_1$(63),glm06_2$(30)
                read(glm06_src,err=*break)glm06_0$(1),glm06_1$(1),glm06_2$(1),glm06_3$,glm06_0
                if glm06_0$(1,2)<>this_firm$ then break
                old_proc$=glm06_0$(3,10)
                prog$=glm06_0$(13,8)                               
                if posting_controls!.containsKey(old_proc$+"."+prog$) then
                    rem --- Maps to new standard glm-06 record
                    dataVec!=cast(BBjVector,posting_controls!.get(old_proc$+"."+prog$))
                    new_proc$=dataVec!.getItem(0)
                    seq_nums$=dataVec!.getItem(1)
                    while len(seq_nums$)>=3
                        rem --- Old process can map to more than one instance of new process, re 'WO Time Sh'
                        seq$=seq_nums$(1,3)
                        seq_nums$=iff(pos(":"=seq_nums$)=0,"",seq_nums$(1+pos(":"=seq_nums$)))
                        readrecord(glm06_trgt,key=this_firm$+new_proc$+seq$,dom=*continue)glm06$
                        glm06.journal_id$=glm06_1$(1,2)
                        value$=glm06_1$(11,6)
                        gosub fix_yymmdd_date
                        glm06.lstuse_date$=value$
                        glm06.lstuse_time$=glm06_1$(17,4)
                        value$=glm06_1$(21,6)
                        gosub fix_yymmdd_date
                        glm06.lstupd_date$=value$
                        glm06.lstupd_time$=glm06_1$(27,4)
                        glm06.gl_audit$=glm06_1$(37,1)
                        glm06.detail_level$=glm06_1$(39,1)
                        glm06.prt_gl_sum$=glm06_1$(40,1)
                        glm06.gl_post_memo$=glm06_2$
                        glm06.audit_number=glm06_0
                        glm06$=field(glm06$)
                        writerecord(glm06_trgt)glm06$
                        glm06_updated=1
                    wend
                else
                    rem --- Does not map to new standard glm-06 record
                    dim glm06$:fattr(glm06$)
                    glm06.firm_id$=this_firm$
                    glm06.process_id$=old_proc$
                    rem glm06.sequence_no$=???
                    glm06.process_alias$=""
                    glm06.process_program$=prog$
                    glm06.journal_id$=glm06_1$(1,2)
                    value$=glm06_1$(11,6)
                    gosub fix_yymmdd_date
                    glm06.lstuse_date$=value$
                    glm06.lstuse_time$=glm06_1$(17,4)
                    value$=glm06_1$(21,6)
                    gosub fix_yymmdd_date
                    glm06.lstupd_date$=value$
                    glm06.lstupd_time$=glm06_1$(27,4)
                    glm06.file_name$=glm06_1$(31,6)
                    glm06.gl_audit$=glm06_1$(37,1)
                    glm06.gl_work_file$=glm06_1$(38,1)
                    glm06.detail_level$=glm06_1$(39,1)
                    glm06.prt_gl_sum$=glm06_1$(40,1)
                    glm06.gl_post_memo$=glm06_2$
                    glm06.audit_number=glm06_0
                    
                    rem --- Get next sequence_no for this new record
                    seq_no=1
                    while seq_no<1000
                        read(glm06_trgt,key=glm06.firm_id$+glm06.process_id$+str(seq_no:"000"),dom=*break)
                        seq_no=seq_no+1
                    wend
                    if seq_no>999 then seq_no=0
                    glm06.sequence_no$=str(seq_no:"000")

                    glm06$=field(glm06$)
                    writerecord(glm06_trgt)glm06$
                    glm06_updated=1
                endif
            wend

            if port_sym09 then
                read(sym09_src,key="",err=*next)
                while 1
                    dim sym09_0$(10),sym09_1$(30),sym09_2$(23)
                    read(sym09_src,err=*break)sym09_0$(1),sym09_1$(1),sym09_2$(1)
                    old_proc$=sym09_0$
                    if cvs(old_proc$,2)="" then continue
                    dim sym04_1$(64)
                    if port_sym04 then
                        read(sym04_src,key=sym09_2$(1,2),dom=*next)*,sym04_1$
                    endif
                    dim sym06_2$(16)
                    if port_sym06 then
                        read(sym06_src,key=this_firm$,dom=*next)*,*,sym06_2$
                    endif
                    if processes!.containsKey(old_proc$) then
                        rem --- Maps to new standard adm-09 record
                        new_proc$=cast(BBjString,processes!.get(old_proc$))
                        readrecord(adm09_trgt,key=this_firm$+new_proc$,dom=*continue)adm09$
                    else
                        rem --- Does not map to new standard adm-09 record
                        dim adm09$:fattr(adm09$)
                        adm09.firm_id$=this_firm$
                        adm09.process_id$=pad(old_proc$,16)
                        adm09.description$=sym09_1$
                        adm09.process_mod_id$=sym09_2$(1,2)
                        adm09.allow_batch$=" "
                    endif
                    rem --- BATCH_ENTRY is 'Y' only if it's 'Y' in SYM-04 and SYM-06 and SYM-09
                    if sym04_1$(47,1)="Y" and sym06_2$(2,1)="Y" and sym09_2$(3,1)="Y" then
                        adm09.batch_entry$="Y"
                    else
                        adm09.batch_entry$="N"
                    endif                    
                    adm09$=field(adm09$)
                    writerecord(adm09_trgt)adm09$
                    adm09_updated=1
                wend
            endif

            if port_sym19 then
                read(sym19_src,key="",err=*next)
                while 1
                    dim sym19_0$(18),sym19_1$(30),sym19_2$(11)
                    read(sym19_src,err=*break)sym19_0$(1),sym19_1$(1),sym19_2$(1)
                    old_proc$=sym19_0$(1,10)
                    if cvs(old_proc$,2)="" then continue
                    prog$=sym19_0$(11,8)                               
                    if process_details!.containsKey(old_proc$+"."+prog$) then
                        rem --- Maps to new standard adm-19 record
                        dataVec!=cast(BBjVector,process_details!.get(old_proc$+"."+prog$))
                        new_proc$=dataVec!.getItem(0)
                        seq_nums$=dataVec!.getItem(1)
                        while len(seq_nums$)>=3
                            rem --- Old process can map to more than one instance of new process, re 'WO Time Sh'
                            seq$=seq_nums$(1,3)
                            seq_nums$=iff(pos(":"=seq_nums$)=0,"",seq_nums$(1+pos(":"=seq_nums$)))
                            readrecord(adm19_trgt,key=this_firm$+new_proc$+seq$,dom=*continue)adm19$
                            adm19.new_batch$=sym19_2$(1,1)
                            adm19$=field(adm19$)
                            writerecord(adm19_trgt)adm19$
                            adm19_updated=1
                        wend
                    else
                        rem --- Does not map to new standard adm-19 record
                        dim adm19$:fattr(adm19$)
                        adm19.firm_id$=this_firm$
                        adm19.process_id$=pad(old_proc$,16)
                        rem adm19.sequence_no$=???
                        adm19.dd_table_alias$=""
                        adm19.program_name$=prog$
                        adm19.description$=sym19_1$
                        adm19.new_batch$=sym19_2$(1,1)
                    
                        rem --- Get next sequence_no for this new record
                        seq_no=1
                        while seq_no<1000
                            read(adm19_trgt,key=adm19.firm_id$+adm19.process_id$+str(seq_no:"000"),dom=*break)
                            seq_no=seq_no+1
                        wend
                        if seq_no>999 then seq_no=0
                        adm19.sequence_no$=str(seq_no:"000")

                        adm19$=field(adm19$)
                        writerecord(adm19_trgt)adm19$
                        adm19_updated=1
                    endif
                wend
            endif

            if port_sym39 then
                read(sym39_src,key="",err=*next)
                while 1
                    dim sym39_0$(16)
                    read(sym39_src,err=*break)sym39_0$(1)
                    old_proc$=sym39_0$(1,10)
                    if cvs(old_proc$,2)="" then continue
                    file$=sym39_0$(11,6)                               
                    if process_files!.containsKey(old_proc$+"."+file$) then
                        rem --- Maps to new standard adm-39 record
                        rem --- Don't change new standard adm-39 record
                        adm39_updated=1
                    else
                        if pos(cvs("GLW-",version_cvs)=file$)=1 then
                            rem --- Do not copy old v6 GLW-* records forward to new adm-39
                        else
                            rem --- Does not map to new standard adm-39 record
                            dim adm39$:fattr(adm39$)
                            adm39.firm_id$=this_firm$
                            adm39.process_id$=pad(old_proc$,16)
                            rem adm39.sequence_no$=???
                            adm39.dd_table_alias$=pad(" ",16)
                            adm39.dd_file_name$=pad(file$,30)
                        
                            rem --- Get next sequence_no for this new record
                            seq_no=1
                            while seq_no<1000
                                read(adm39_trgt,key=adm39.firm_id$+adm39.process_id$+str(seq_no:"000"),dom=*break)
                                seq_no=seq_no+1
                            wend
                            if seq_no>999 then seq_no=0
                            adm39.sequence_no$=str(seq_no:"000")

                            adm39$=field(adm39$)
                            writerecord(adm39_trgt)adm39$
                            adm39_updated=1
                        endif
                    endif
                wend
            endif
        next firms
        
        if glm06_updated then
            print "   Copied data from "+cvs("glm-06",version_cvs)
            print(log_dev)"   Copied data from "+cvs("glm-06",version_cvs)
        else
            print "*** ERROR ***: Data not copied from "+cvs("glm-06",version_cvs)
            print (err_dev)"*** ERROR ***: Data not copied from "+cvs("glm-06",version_cvs)
        endif
        if port_sym09 and adm09_updated then
            print "   Copied data from "+cvs("sym-09",version_cvs)
            print(log_dev)"   Copied data from "+cvs("sym-09",version_cvs)
        else
            print "*** ERROR ***: Data not copied from "+cvs("sym-09",version_cvs)
            print (err_dev)"*** ERROR ***: Data not copied from "+cvs("sym-09",version_cvs)
        endif
        if port_sym19 and adm19_updated then
            print "   Copied data from "+cvs("sym-19",version_cvs)
            print(log_dev)"   Copied data from "+cvs("sym-19",version_cvs)
        else
            print "*** ERROR ***: Data not copied from "+cvs("sym-19",version_cvs)
            print (err_dev)"*** ERROR ***: Data not copied from "+cvs("sym-19",version_cvs)
        endif
        if port_sym39 and adm39_updated then
            print "   Copied data from "+cvs("sym-39",version_cvs)
            print(log_dev)"   Copied data from "+cvs("sym-39",version_cvs)
        else
            print "*** ERROR ***: Data not copied from "+cvs("sym-39",version_cvs)
            print (err_dev)"*** ERROR ***: Data not copied from "+cvs("sym-39",version_cvs)
        endif
    endif
    
    rem --- Close files opened in source directory for this subroutine
    close(glm06_src)
    if port_sym04 then close(sym04_src)
    if port_sym06 then close(sym06_src)
    if port_sym09 then close(sym09_src)
    if port_sym19 then close(sym19_src)
    if port_sym39 then close(sym39_src)
    return

init_apm01_memo1024: rem --- Build APM_VENDMAST.MEMO_1024 (apm-01) from apm-09 (APM_VENDCMTS) Vendor Comments

    rem --- Get current record template for APM_VENDMAST
    num_files=1
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="APM_VENDMAST",rd_open_opts$[1]="T"
    
    gosub open_tables
    
    dim apmVendMast$:rd_open_tpls$[1]

    rem --- Open destination apm-01 (APM_VENDMAST) that has already been ported
    apmVendMast_found=0
    apmVendMast_dev=unt
    open (apmVendMast_dev,err=*next)destin_folder$+"/apm-01"; apmVendMast_found=1
    if apmVendMast_found then
        print "   Building APM_VENDMAST (apm-01) MEMO_1024 field from "+cvs("apm-09",version_cvs)+" (APM_VENDCMTS) table"
        print(log_dev)"   Building APM_VENDMAST (apm-01) MEMO_1024 field from "+cvs("apm-09",version_cvs)+" (APM_VENDCMTS) table"

        rem --- Open and lock source apm-09 (APM_VENDCMTS)
        apm09_dev=unt
        open (apm09_dev)source_folder$+"/"+cvs("apm-09",version_cvs)
        lock (apm09_dev)

        rem --- Build new APM_VENDMAST.MEMO_1024 field from apm-09 (APM_VEMDCMTS) table
        currentFirm$=""
        currentVendor$=""
        memo$=""
        read(apm09_dev,key="",dom=*next)
        while 1
            apm09_key$=key(apm09_dev,end=*break)
            readrecord(apm09_dev)apm09_rec$
            if apm09_rec$(1,8)<>currentFirm$+currentVendor$ then
                record_found=0
                readrecord(apmVendMast_dev,key=currentFirm$+currentVendor$,dom=*next)apmVendMast$; record_found=1
                if record_found then
                    apmVendMast.memo_1024$=memo$
                    apmVendMast$=field(apmVendMast$)
                    writerecord(apmVendMast_dev)apmVendMast$
                endif
                memo$=""
                currentVendor$=apm09_rec$(3,6)
                if apm09_rec$(1,2)<>currentFirm$ then
                    currentFirm$=apm09_rec$(1,2)
                endif
            endif
            memo$=memo$+cvs(apm09_rec$(12,48),2)+$0A$; rem --- must trim all trailing $00$ from record
        wend
        record_found=0
        readrecord(apmVendMast_dev,key=currentFirm$+currentVendor$,dom=*next)apvmVendMast$; record_found=1
        if record_found then
            apmVendMast.memo_1024$=memo$
            apmVendMast$=field(apmVendMast$)
            writerecord(apmVendMast_dev)apmVendMast$
        endif
        
        close (apmVendMast_dev,err=*next)
        unlock (apm09_dev,err=*next)
        close (apm09_dev,err=*next)
    else
        rd_meter_data$="Vendor Master file apm-01 not found in destination directory. Vendor Comments ("+cvs("apm-09",version_cvs)+") could not be ported."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter
        print (err_dev)"Vendor Master file apm-01 not found in destination directory."+$0a$+"Vendor Comments ("+cvs("apm-09",version_cvs)+") could not be ported."
    endif

    return

init_arm01_memo1024: rem --- Build ARM_CUSTMAST.MEMO_1024 (arm-01) from arm-05 (ARM_CUSTCMTS) Customer Comments

    rem --- Get current record template for ARM_CUSTMAST
    num_files=1
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="ARM_CUSTMAST",rd_open_opts$[1]="T"
    
    gosub open_tables
    
    dim armCustMast$:rd_open_tpls$[1]

    rem --- Open destination arm-01 (ARM_CUSTMAST) that has already been ported
    armCustMast_found=0
    armCustMast_dev=unt
    open (armCustMast_dev,err=*next)destin_folder$+"/arm-01"; armCustMast_found=1
    if armCustMast_found then
        print "   Building ARM_CUSTMAST (arm-01) MEMO_1024 field from "+cvs("arm-05",version_cvs)+" (ARM_CUSTCMTS) table"
        print(log_dev)"   Building ARM_CUSTMAST (arm-01) MEMO_1024 field from "+cvs("arm-05",version_cvs)+" (ARM_CUSTCMTS) table"

        rem --- Open and lock source arm-05 (ARM_CUSTCMTS)
        arm05_dev=unt
        open (arm05_dev)source_folder$+"/"+cvs("arm-05",version_cvs)
        lock (arm05_dev)

        rem --- Build new ARM_CUSTMAST.MEMO_1024 field from arm-05 (ARM_CUSTCMTS) table
        currentFirm$=""
        currentCustomer$=""
        memo$=""
        read(arm05_dev,key="",dom=*next)
        while 1
            arm05_key$=key(arm05_dev,end=*break)
            readrecord(arm05_dev)arm05_rec$
            if arm05_rec$(1,8)<>currentFirm$+currentCustomer$ then
                record_found=0
                readrecord(armCustMast_dev,key=currentFirm$+currentCustomer$,dom=*next)armCustMast$; record_found=1
                if record_found then
                    armCustMast.memo_1024$=memo$
                    armCustMast$=field(armCustMast$)
                    writerecord(armCustMast_dev)armCustMast$
                endif
                memo$=""
                currentCustomer$=arm05_rec$(3,6)
                if arm05_rec$(1,2)<>currentFirm$ then
                    currentFirm$=arm05_rec$(1,2)
                endif
            endif
            memo$=memo$+cvs(arm05_rec$(12,48),2)+$0A$; rem --- must trim all trailing $00$ from record
        wend
        record_found=0
        readrecord(armCustMast_dev,key=currentFirm$+currentCustomer$,dom=*next)armCustMast$; record_found=1
        if record_found then
            armCustMast.memo_1024$=memo$
            armCustMast$=field(armCustMast$)
            writerecord(armCustMast_dev)armCustMast$
        endif
        
        close (armCustMast_dev,err=*next)
        unlock (arm05_dev,err=*next)
        close (arm05_dev,err=*next)
    else
        rd_meter_data$="Customer Master file arm-01 not found in destination directory. Customer Comments ("+cvs("arm-05",version_cvs)+") could not be ported."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter
        print (err_dev)"Customer Master file arm-01 not found in destination directory."+$0a$+"Customer Comments ("+cvs("arm-05",version_cvs)+") could not be ported."
    endif

    return

init_bmm01_memo1024: rem --- Build BMM_BILLMAST.MEMO_1024 (bmm-01) from bmm-09 (BMM_BILLCMTS) BOM Comments

    rem --- Get current record template for BMM_BILLMAST
    num_files=1
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="BMM_BILLMAST",rd_open_opts$[1]="T"
    
    gosub open_tables
    
    dim bmmBillMast$:rd_open_tpls$[1]

    rem --- Open destination bmm-01 (BMM_BILLMAST) that has already been ported
    bmmBillMast_found=0
    bmmBillMast_dev=unt
    open (bmmBillMast_dev,err=*next)destin_folder$+"/bmm-01"; bmmBillMast_found=1
    if bmmBillMast_found then
        print "   Building BMM_BILLMAST (bmm-01) MEMO_1024 field from "+cvs("bmm-09",version_cvs)+" (BMM_BILLCMTS) table"
        print(log_dev)"   Building BMM_BILLMAST (bmm-01) MEMO_1024 field from "+cvs("bmm-09",version_cvs)+" (BMM_BILLCMTS) table"

        rem --- Open and lock source bmm-09 (BMM_BILLCMTS)
        bmm09_dev=unt
        open (bmm09_dev)source_folder$+"/"+cvs("bmm-09",version_cvs)
        lock (bmm09_dev)

        rem --- Build new BMM_BILLMAST.MEMO_1024 field from bmm-09 (BMM_BILLCMTS) table
        currentFirm$=""
        currentBill$=""
        memo$=""
        read(bmm09_dev,key="",dom=*next)
        while 1
            bmm09_key$=key(bmm09_dev,end=*break)
            readrecord(bmm09_dev)bmm09_rec$
            if bmm09_rec$(1,22)<>currentFirm$+currentBill$ then
                record_found=0
                readrecord(bmmBillMast_dev,key=currentFirm$+currentBill$,dom=*next)bmmBillMast$; record_found=1
                if record_found then
                    bmmBillMast.memo_1024$=memo$
                    bmmBillMast$=field(bmmBillMast$)
                    writerecord(bmmBillMast_dev)bmmBillMast$
                endif
                memo$=""
                currentBill$=bmm09_rec$(3,20)
                if bmm09_rec$(1,2)<>currentFirm$ then
                    currentFirm$=bmm09_rec$(1,2)
                endif
            endif
            memo$=memo$+cvs(bmm09_rec$(26,48),2)+$0A$; rem --- must trim all trailing $00$ from record
        wend
        record_found=0
        readrecord(bmmBillMast_dev,key=currentFirm$+currentBill$,dom=*next)bmmBillMast$; record_found=1
        if record_found then
            bmmBillMast.memo_1024$=memo$
            bmmBillMast$=field(bmmBillMast$)
            writerecord(bmmBillMast_dev)bmmBillMast$
        endif
        
        close (bmmBillMast_dev,err=*next)
        unlock (bmm09_dev,err=*next)
        close (bmm09_dev,err=*next)
    else
        rd_meter_data$="BOM Master file bmm-01 not found in destination directory. BOM Comments ("+cvs("bmm-09",version_cvs)+") could not be ported."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter
        print (err_dev)"BOM Master file bmm-01 not found in destination directory."+$0a$+"BOM Comments ("+cvs("bmm-09",version_cvs)+") could not be ported."
    endif

    return

init_glm01_memo1024: rem --- Build GLM_ACCT.MEMO_1024 (glm-01) from glm-09 (GLM_ACCTCOMMENTS) Accounts Comments

    rem --- Get current record template for GLM_ACCT
    num_files=1
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="GLM_ACCT",rd_open_opts$[1]="T"
    
    gosub open_tables
    
    dim glmAcct$:rd_open_tpls$[1]

    rem --- Open destination glm-01 (GLM_ACCT) that has already been ported
    glmAcct_found=0
    glmAcct_dev=unt
    open (glmAcct_dev,err=*next)destin_folder$+"/glm-01"; glmAcct_found=1
    if glmAcct_found then
        print "   Building GLM_ACCT (glm-01) MEMO_1024 field from "+cvs("glm-09",version_cvs)+" (GLM_ACCTCOMMENTS) table"
        print(log_dev)"   Building GLM_ACCT (glm-01) MEMO_1024 field from "+cvs("glm-09",version_cvs)+" (GLM_ACCTCOMMENTS) table"

        rem --- Open and lock source glm-09 (GLM_ACCTCOMMENTS)
        glm09_dev=unt
        open (glm09_dev)source_folder$+"/"+cvs("glm-09",version_cvs)
        lock (glm09_dev)

        rem --- Build new GLM_ACCT.MEMO_1024 field from glm-09 (GLM_ACCTCOMMENTS) table
        currentFirm$=""
        currentAccount$=""
        memo$=""
        read(glm09_dev,key="",dom=*next)
        while 1
            glm09_key$=key(glm09_dev,end=*break)
            readrecord(glm09_dev)glm09_rec$
            if glm09_rec$(1,12)<>currentFirm$+currentAccount$ then
                record_found=0
                readrecord(glmAcct_dev,key=currentFirm$+currentAccount$,dom=*next)glmAcct$; record_found=1
                if record_found then
                    glmAcct.memo_1024$=memo$
                    glmAcct$=field(glmAcct$)         
                    writerecord(glmAcct_dev)glmAcct$
                endif
                memo$=""
                currentAccount$=glm09_rec$(3,10)
                if glm09_rec$(1,2)<>currentFirm$ then
                    currentFirm$=glm09_rec$(1,2)
                endif
            endif
            memo$=memo$+cvs(glm09_rec$(16,48),2)+$0A$; rem --- must trim all trailing $00$ from record
        wend
        record_found=0
        readrecord(glmAcct_dev,key=currentFirm$+currentAccount$,dom=*next)glmAcct$; record_found=1
        if record_found then
            glmAcct.memo_1024$=memo$
            glmAcct$=field(glmAcct$)         
            writerecord(glmAcct_dev)glmAcct$
        endif
        
        close (glmAcct_dev,err=*next)
        unlock (glm09_dev,err=*next)
        close (glm09_dev,err=*next)
    else
        rd_meter_data$="GL Account Master file glm-01 not found in destination directory. Account Comments ("+cvs("glm-09",version_cvs)+") could not be ported."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter
        print (err_dev)"GL Account Master file glm-01 not found in destination directory."+$0a$+"Account Comments ("+cvs("glm-09",version_cvs)+") could not be ported."
    endif

    return

init_ivm01_memo1024: rem --- Build IVM_ITEMMAST.MEMO_1024 (ivm-01) from ivm-09 (IVM_ITEMCMTS) Item Comments

    rem --- Get current record template for IVM_ITEMMAST
    num_files=1
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="IVM_ITEMMAST",rd_open_opts$[1]="T"
    
    gosub open_tables
    
    dim ivmItemMast$:rd_open_tpls$[1]

    rem --- Open destination ivm-01 (IVM_ITEMMAST) that has already been ported
    ivmItemMast_found=0
    ivmItemMast_dev=unt
    open (ivmItemMast_dev,err=*next)destin_folder$+"/ivm-01"; ivmItemMast_found=1
    if ivmItemMast_found then
        print "   Building IVM_ITEMMAST (ivm-01) MEMO_1024 field from "+cvs("ivm-09",version_cvs)+" (IVM_ITEMCMTS) table"
        print(log_dev)"   Building IVM_ITEMMAST (ivm-01) MEMO_1024 field from "+cvs("ivm-09",version_cvs)+" (IVM_ITEMCMTS) table"

        rem --- Open and lock source ivm-09 (IVM_ITEMCMTS)
        ivm09_dev=unt
        open (ivm09_dev)source_folder$+"/"+cvs("ivm-09",version_cvs)
        lock (ivm09_dev)

        rem --- Build new IVM_ITEMMAST.MEMO_1024 field from ivm-09 (IVM_ITEMCMTS) table
        currentFirm$=""
        currentItem$=""
        memo$=""
        read(ivm09_dev,key="",dom=*next)
        while 1
            ivm09_key$=key(ivm09_dev,end=*break)
            readrecord(ivm09_dev)ivm09_rec$
            if ivm09_rec$(1,22)<>currentFirm$+currentItem$ then
                record_found=0
                readrecord(ivmItemMast_dev,key=currentFirm$+currentItem$,dom=*next)ivmItemMast$; record_found=1
                if record_found then
                    ivmItemMast.memo_1024$=memo$
                    ivmItemMast$=field(ivmItemMast$)         
                    writerecord(ivmItemMast_dev)ivmItemMast$
                endif
                memo$=""
                currentItem$=ivm09_rec$(3,20)
                if ivm09_rec$(1,2)<>currentFirm$ then
                    currentFirm$=ivm09_rec$(1,2)
                endif
            endif
            memo$=memo$+cvs(ivm09_rec$(26,48),2)+$0A$; rem --- must trim all trailing $00$ from record
        wend
        record_found=0
        readrecord(ivmItemMast_dev,key=currentFirm$+currentItem$,dom=*next)ivmItemMast$; record_found=1
        if record_found then
            ivmItemMast.memo_1024$=memo$
            ivmItemMast$=field(ivmItemMast$)         
            writerecord(ivmItemMast_dev)ivmItemMast$
        endif
        
        close (ivmItemMast_dev,err=*next)
        unlock (ivm09_dev,err=*next)
        close (ivm09_dev,err=*next)
    else
        rd_meter_data$="Inventory Item Master file ivm-01 not found in destination directory. Item Comments ("+cvs("ivm-09",version_cvs)+") could not be ported."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter
        print (err_dev)"Inventory Item Master file ivm-01 not found in destination directory."+$0a$+"Item Comments ("+cvs("ivm-09",version_cvs)+") could not be ported."
    endif

    return

init_sfe01_memo1024: rem --- Build SFE_WOMASTR.MEMO_1024 (sfe-01) from woe-07 (SFE_WOCOMNT (sfe-07)) Work Order Comments

    rem --- Get current record template for SFE_WOMASTR
    num_files=1
    dim rd_open_tables$[1:num_files],rd_open_opts$[1:num_files],rd_open_chans$[1:num_files],rd_open_tpls$[1:num_files]
    rd_open_tables$[1]="SFE_WOMASTR",rd_open_opts$[1]="T"
    
    gosub open_tables
    
    dim sfeWOMastr$:rd_open_tpls$[1]

    rem --- Open destination sfe-01 (SFE_WOMASTR) that has already been ported
    sfeWOMastr_found=0
    sfeWOMastr_dev=unt
    open (sfeWOMastr_dev,err=*next)destin_folder$+"/sfe-01"; sfeWOMastr_found=1
    if sfeWOMastr_found then
        print "   Building SFE_WOMASTR (sfe-01) MEMO_1024 field from "+cvs("woe-07",version_cvs)+" (SFE_WOCOMNT) table"
        print(log_dev)"   Building SFE_WOMASTR (sfe-01) MEMO_1024 field from "+cvs("woe-07",version_cvs)+" (SFE_WOCOMNT) table"

        rem --- Open and lock source woe-07 (SFE_WOCOMNT (sfe-07))
        woe07_dev=unt
        open (woe07_dev)source_folder$+"/"+cvs("woe-07",version_cvs)
        lock (woe07_dev)

        rem --- Build new SFE_WOMASTR.MEMO_1024 field from woe-07 (SFE_WOCOMNT (sfe-07)) table
        currentFirm$=""
        currentLocation$=""
        currentWO$=""
        memo$=""
        read(woe07_dev,key="",dom=*next)
        while 1
            woe07_key$=key(woe07_dev,end=*break)
            readrecord(woe07_dev)woe07_rec$
            if woe07_rec$(1,11)<>currentFirm$+currentLocation$+currentWO$ then
                record_found=0
                readrecord(sfeWOMastr_dev,key=currentFirm$+currentLocation$+currentWO$,dom=*next)sfeWOMastr$; record_found=1
                if record_found then
                    sfeWOMastr.memo_1024$=memo$
                    sfeWOMastr$=field(sfeWOMastr$)         
                    writerecord(sfeWOMastr_dev)sfeWOMastr$
                endif
                memo$=""
                currentFirm$=woe07_rec$(1,2)
                currentLocation$=woe07_rec$(3,2)
                currentWO$=woe07_rec$(5,7)
            endif
            memo$=memo$+cvs(woe07_rec$(16,60),2)+$0A$; rem --- must trim all trailing $00$ from record
        wend
        record_found=0
        readrecord(sfeWOMastr_dev,key=currentFirm$+currentLocation$+currentWO$,dom=*next)sfeWOMastr$; record_found=1
        if record_found then
            sfeWOMastr.memo_1024$=memo$
            sfeWOMastr$=field(sfeWOMastr$)
            writerecord(sfeWOMastr_dev)sfeWOMastr$
        endif
        
        close (sfeWOMastr_dev,err=*next)
        unlock (woe07_dev,err=*next)
        close (woe07_dev,err=*next)
    else
        rd_meter_data$="Work Order Entry file sfe-01 not found in destination directory. Work Order Comments ("+cvs("woe-07",version_cvs)+") could not be ported."
        rd_meter_total_recs=vectPort!.size()
        rd_meter_action$="LST"
        gosub disp_meter
        print (err_dev)"Work Order Entry file sfe-01 not found in destination directory."+$0a$+"Work Order Comments ("+cvs("woe-07",version_cvs)+") could not be ported."
    endif

    return

split_header_detail_recs: rem --- Split old header/detail files into separate files
    rem --- Do not alter v6/v7 file so it can still be used as-is on existing v6/v7 system.
    rem --- Copy v6/v7 file to temporary v6/v7 header and detail files.
    rem ---     Input: datafile$
    rem ---     Input: split_record_id$
    rem ---     Input: header_keys_end_with$
    rem ---     Input: split_temp_file$
    rem ---     Input: split_temp_file_desc$
    rem ---     Input: header_file
    
    rem --- Write ddm-03 record for temporary file
    dim split_ddm03$:fattr(ddm03$)
    split_ddm03.file_name$=cvs(split_temp_file$,version_cvs)
    split_ddm03.record_id$=split_record_id$
    split_ddm03.description$=split_temp_file_desc$
    split_ddm03$=field(split_ddm03$)         
    writerecord(ddm03,key=split_ddm03.file_name$+split_ddm03.record_id$)split_ddm03$

    rem --- Copy ddm-04 records for datafile to ddm-04 records for the temporary file    
    dim split_ddm04$:fattr(ddm04$)
    read(ddm04,key=datafile$+split_record_id$,dom=*next)
    while 1
        ddm04_key$=key(ddm04,end=*break)
        if pos(datafile$+split_record_id$=ddm04_key$)<>1 then break
        readrecord(ddm04,key=ddm04_key$)split_ddm04$
        split_ddm04.file_name$=cvs(split_temp_file$,version_cvs)
        split_ddm04$=field(split_ddm04$)         
        writerecord(ddm04,key=split_ddm04.file_name$+split_ddm04.record_id$+split_ddm04.layout_seq$)split_ddm04$
        
        rem --- Reset file pointer to get next ddm-04 record for datafile
        read(ddm04,key=ddm04_key$)
    wend

    rem --- Create temporary file
    split_file=unt
    open (split_file)source_folder$+"/"+datafile$
    lock (split_file)
    fid$=fid(split_file)
    fid$=fid$(1,8)+source_folder$+"/"+cvs(split_temp_file$,version_cvs)
    fin$=fin(split_file)(86)
    keyed = asc(and(fid$(1,1),$06$)) or asc(and(fid$(1,1),$08$)) or fid$(1,1)=$0d$
    multi = keyed and asc(fid$(2,1))=0
    single = keyed and !(multi)
    erase source_folder$+"/"+cvs(split_temp_file$,version_cvs),err=*next
    if multi then
       file fid$,fin$
    else
       file fid$
    endif
    
    rem --- Copy datafile to temporary header and detail files
    temp_file=unt
    open (temp_file)source_folder$+"/"+cvs(split_temp_file$,version_cvs)
    lock (temp_file)
    read(split_file,key="",dom=*next)
    while 1
        split_key$=key(split_file,end=*break)
        readrecord(split_file)rec$
        if pos(cvs(datafile$,8)="woe-11;woe-21;woe-31",7)
            rem --- Second string in woe-11/21/31 detail record is written short.
            rem --- Need to pad it to correct length in order to port trailing numerics correctly.
            pos_2nd_0a=pos($0a$=rec$,1,2)
            if pos_2nd_0a<49 then
                rec$(pos_2nd_0a)=pad("",49-pos_2nd_0a)+rec$(pos_2nd_0a)
            endif
        endif
        end_pos=len(split_key$)-len(header_keys_end_with$)+1
        if (header_file and pos(header_keys_end_with$=split_key$,-1)=end_pos) or 
:       (!header_file and pos(header_keys_end_with$=split_key$,-1)<>end_pos) then
            rem --- Correct record type for this temporary file
            if single then
               writerecord(temp_file,key=split_key$)rec$
            else
               writerecord(temp_file)rec$
            endif
        endif
    wend
    close (split_file)
    close (temp_file)

    rem --- Append temporary file to vectPort!
    vectPort!.addItem(cvs(split_temp_file$,version_cvs))
    return

verify_translation_master: rem --- Verify master file records exist for used translations
    usedIter!=used_translations!.keySet().iterator()
    while usedIter!.hasNext()
        fieldname$=usedIter!.next()
        
        rem --- Open master file
        masterfile$=code_masterfile!.get(fieldname$)
        filename$=cvs(masterfile$,3)
        if filename$="" then continue
        key_field$=""
        record_id$=""
        xpos=pos(";"=filename$)
        if xpos then
            if pos("["=filename$(xpos+1))=1 then
                rem --- Get key field name
                key_field$=filename$(xpos+2)
                key_field$=key_field$(1,len(key_field$)-1)
                table_id$=cvs(filename$(1,xpos-1),4)
                if file_xref!.get(cvs(filename$(1,xpos-1),4))<>null() then
                    table_id$=cast(BBjString,file_xref!.get(cvs(filename$(1,xpos-1),4)),err=*next)
                endif
                call stbl("+DIR_SYP")+"bac_key_template.bbj",table_id$,"PRIMARY",key_fieldlist$,rd_table_chans$[all],rd_stat$
                if rd_stat$<>"" then
                        print (err_dev)"Primary key template for master file "+filename$+" not found for translation of "+fieldname$
                endif
            else
                rem --- Get record ID
                record_id$=filename$(xpos+1)
            endif
            filename$=filename$(1,xpos-1)
        endif
        master_found=0
        master_chan=unt
        open (master_chan,err=*next)destin_folder$+"/"+filename$;master_found=1
        if !master_found then
            print (err_dev)"Master file "+filename$+" not found for translation of "+fieldname$
            continue
        endif

        rem --- Get value used for translation
        transIter!=field_translations!.keySet().iterator()
        while transIter!.hasNext()
            transKey$=transIter!.next()
            if fieldname$+"."=transKey$<>1 then continue
            transValue$=field_translations!.get(transKey$)
       
            rem --- Check each firm for translation master record
            firmSet!=cast(java.util.TreeSet,used_translations!.get(fieldname$))
            firmIter!=firmSet!.iterator()
            while firmIter!.hasNext()
                firm$=firmIter!.next()
                if key_field$=""then
                    rem --- Use record ID for this master file record
                    record$=""
                    readrecord(master_chan,key=firm$+record_id$+transValue$,dom=*next)record$
                    if cvs(record$,2)="" then
                        print (err_dev)"Translation for "+fieldname$+"="+transValue$+ " not found in "+filename$+" for firm "+firm$
                    endif
                else
                    rem --- Use key field for this master file record
                    previous_key_field_value$=""
                    read(master_chan,key=firm$,knum="PRIMARY",dom=*next)
                    while 1
                        dim key_tpl$:key_fieldlist$
                        key_tpl$=key(master_chan,end=*break)
                        if key_tpl.firm_id$<>firm$ then break
                        read(master_chan) 
                        fieldvalue$=field(key_tpl$,fieldname$)
                        key_field_value$=key_tpl$(1,len(key_tpl$)-len(fieldvalue$))
                        key_field_value$=key_field_value$(3)
                        
                        if previous_key_field_value$=key_field_value$ then continue
                        previous_key_field_value$=key_field_value$
                        
                        record$=""
                        readrecord(master_chan,key=firm$+key_field_value$+transValue$,dom=*next,end=*break)record$
                        if cvs(record$,2)="" then
                            print (err_dev)"Translation for "+fieldname$+"="+transValue$+ " not found in "+filename$+" for firm "+firm$
:                           +" and "+key_field$+"="+key_field_value$
                        endif
                    wend
                endif
            wend
        wend
        
        rem --- Close master file
        close(master_chan)
    wend
    return
    
split_prm10w_recs: rem --- Split old w2Box Detail File into separate files
    prc_w2box_trgt=new_file;rem "Header Table"
    prc_w2box_tpl$=fattr(new_rec$)

    rem --- Create and open new files in target directory
    table_alias$="PRC_W2BOX_CONT",new_datafile$="prc_w2box_cont",file_id$=new_datafile$
    erase destin_folder$+"/"+new_datafile$,err=*next
    gosub check_for_definition
    prc_w2box_cont_trgt=new_file
    findrecord(ddm_table_tpls,key=pad(table_alias$,16),dom=*next)ddm_table_tpls$
    prc_w2box_cont_tpl$=ddm_table_tpls.template$

    rem --- Create and open new files in target directory
    table_alias$="PRC_W2BOX_DDCT",new_datafile$="prc_w2box_ddct",file_id$=new_datafile$
    erase destin_folder$+"/"+new_datafile$,err=*next
    gosub check_for_definition
    prc_w2box_ddct_trgt=new_file
    findrecord(ddm_table_tpls,key=pad(table_alias$,16),dom=*next)ddm_table_tpls$
    prc_w2box_ddct_tpl$=ddm_table_tpls.template$

    rem --- Create and open new files in target directory
    table_alias$="PRC_W2BOX_EARN",new_datafile$="prc_w2box_earn",file_id$=new_datafile$
    erase destin_folder$+"/"+new_datafile$,err=*next
    gosub check_for_definition
    prc_w2box_earn_trgt=new_file
    findrecord(ddm_table_tpls,key=pad(table_alias$,16),dom=*next)ddm_table_tpls$
    prc_w2box_earn_tpl$=ddm_table_tpls.template$

    rem --- Create and open new files in target directory
    table_alias$="PRC_W2BOX_TAX",new_datafile$="prc_w2box_tax",file_id$=new_datafile$
    erase destin_folder$+"/"+new_datafile$,err=*next
    gosub check_for_definition
    prc_w2box_tax_trgt=new_file
    findrecord(ddm_table_tpls,key=pad(table_alias$,16),dom=*next)ddm_table_tpls$
    prc_w2box_tax_tpl$=ddm_table_tpls.template$

    while 1
        readrecord(old_file,end=*break)old_rec$
        if field(old_rec$,record_id_field$,err=*continue)<>"W" then continue
        if pos(old_rec.firm_id$=firms$,2)=0 then continue

        dim  prc_w2box$:prc_w2box_tpl$
        field_name$="FIRM_ID"
        field prc_w2box$,field_name$=field(old_rec$,field_name$)

        field_name$="RECORD_ID_W"
        field prc_w2box$,field_name$=field(old_rec$,field_name$)

        rem --- If new_field_defaults not set for Payroll year, use Current Year from source Payroll parameters.
        if cvs(new_rec.year$,2)="" then
            if prYearMap!.containsKey(old_rec.firm_id$) then
                w2year$=prYearMap!.get(old_rec.firm_id$)
            endif
        else
            w2year$=new_rec.year$
        endif

        field prc_w2box$,"year"=w2year$

        field_name$="W2_BOX_ID"
        field prc_w2box$,field_name$=field(old_rec$,field_name$)

        rem "Box Renumbers after 2012"
        box_num=0 
        box_num=num(prc_w2box.w2_box_id$,err=*next)
        if box_num > 9 then
            prc_w2box.w2_box_id$=str(box_num-1:"00")
        endif

        field_name$="W2_BXLN_ID"
        field prc_w2box$,field_name$=field(old_rec$,field_name$)

        field_name$="W2_STATE_ID"
        field prc_w2box$,field_name$=field(old_rec$,field_name$)

        field prc_w2box$,"W2_LOCAL_ID"=field(old_rec$,"W2_LOC_CODE")
        prc_w2box$=field(prc_w2box$)
        writerecord(prc_w2box_trgt)prc_w2box$

        for x=1 to 10
            datatype$=cvs(field(old_rec$,"w2_code_type_"+str(x:"00")),2)
        
            switch (BBjAPI().TRUE)
                case datatype$ = "A"
                    rem "Earnings"
                    dim prc_w2box_earn$:prc_w2box_earn_tpl$
                    prc_w2box_earn.firm_id$=prc_w2box.firm_id$
                    prc_w2box_earn.record_id_w$=prc_w2box.record_id_w$
                    prc_w2box_earn.year$=prc_w2box.year$
                    prc_w2box_earn.w2_box_id$=prc_w2box.w2_box_id$
                    prc_w2box_earn.w2_bxln_id$=prc_w2box.w2_bxln_id$
                    prc_w2box_earn.w2_state_id$=prc_w2box.w2_state_id$
                    prc_w2box_earn.w2_local_id$=prc_w2box.w2_local_id$
                    prc_w2box_earn.pay_code$=field(old_rec$,"w2_code_id_"+str(x:"00"))
                    prc_w2box_earn.operator$=field(old_rec$,"w2_code_oper_"+str(x:"00"))
                    prc_w2box_earn.multiplier=nfield(old_rec$,"w2_code_mult_"+str(x:"00"))
                    prc_w2box_earn$=field(prc_w2box_earn$)
                    writerecord(prc_w2box_earn_trgt)prc_w2box_earn$
                    break
                case datatype$ = "B"
                    rem "Deductions"
                    dim prc_w2box_ddct$:prc_w2box_ddct_tpl$
                    prc_w2box_ddct.firm_id$=prc_w2box.firm_id$
                    prc_w2box_ddct.record_id_w$=prc_w2box.record_id_w$
                    prc_w2box_ddct.year$=prc_w2box.year$
                    prc_w2box_ddct.w2_box_id$=prc_w2box.w2_box_id$
                    prc_w2box_ddct.w2_bxln_id$=prc_w2box.w2_bxln_id$
                    prc_w2box_ddct.w2_state_id$=prc_w2box.w2_state_id$
                    prc_w2box_ddct.w2_local_id$=prc_w2box.w2_local_id$

                    prc_w2box_ddct.deduct_code$=field(old_rec$,"w2_code_id_"+str(x:"00"))
                    prc_w2box_ddct.operator$=field(old_rec$,"w2_code_oper_"+str(x:"00"))
                    prc_w2box_ddct.multiplier=nfield(old_rec$,"w2_code_mult_"+str(x:"00"))
                    prc_w2box_ddct$=field(prc_w2box_ddct$)
                    writerecord(prc_w2box_ddct_trgt)prc_w2box_ddct$
                    break
                case datatype$ = "C"
                    rem "Contributions"
                    dim prc_w2box_cont$:prc_w2box_cont_tpl$
                    prc_w2box_cont.year$=prc_w2box.year$
                    prc_w2box_cont.w2_box_id$=prc_w2box.w2_box_id$
                    prc_w2box_cont.w2_bxln_id$=prc_w2box.w2_bxln_id$
                    prc_w2box_cont.w2_state_id$=prc_w2box.w2_state_id$
                    prc_w2box_cont.w2_local_id$=prc_w2box.w2_local_id$

                    prc_w2box_cont.contrib_code$=field(old_rec$,"w2_code_id_"+str(x:"00"))
                    prc_w2box_cont.operator$=field(old_rec$,"w2_code_oper_"+str(x:"00"))
                    prc_w2box_cont.multiplier=nfield(old_rec$,"w2_code_mult_"+str(x:"00"))
                    temp_flag$="GBA"
                    prc_w2box_cont.src_column$=temp_flag$(nfield(old_rec$,"w2_amt_type_"+str(x:"00")),1)
                    prc_w2box_cont$=field(prc_w2box_cont$)
                    writerecord(prc_w2box_cont_trgt)prc_w2box_cont$
                    break
                case datatype$ = "D"
                    rem "Taxes"
                    dim prc_w2box_tax$:prc_w2box_tax_tpl$
                    prc_w2box_tax.firm_id$=prc_w2box.firm_id$
                    prc_w2box_tax.record_id_w$=prc_w2box.record_id_w$
                    prc_w2box_tax.year$=prc_w2box.year$
                    prc_w2box_tax.w2_box_id$=prc_w2box.w2_box_id$
                    prc_w2box_tax.w2_bxln_id$=prc_w2box.w2_bxln_id$
                    prc_w2box_tax.w2_state_id$=prc_w2box.w2_state_id$
                    prc_w2box_tax.w2_local_id$=prc_w2box.w2_local_id$

                    prc_w2box_tax.tax_code$=field(old_rec$,"w2_code_id_"+str(x:"00"))
                    prc_w2box_tax.operator$=field(old_rec$,"w2_code_oper_"+str(x:"00"))
                    prc_w2box_tax.multiplier=nfield(old_rec$,"w2_code_mult_"+str(x:"00"))
                    temp_flag$="GTA"
                    prc_w2box_tax.src_column$=temp_flag$(nfield(old_rec$,"w2_amt_type_"+str(x:"00")),1)
                    prc_w2box_tax$=field(prc_w2box_tax$)
                    writerecord(prc_w2box_tax_trgt)prc_w2box_tax$
                    break
            swend
        next x
    wend
    return

header_pre02_recs: rem --- Create Header for pre02_recs"
    pre_02_trgt=new_file;rem "Header Table"
    pre_02_tpl$=fattr(new_rec$)

    rem --- Create and open new Header files in target directory
    table_alias$="PRE_DAYTIME_HDR",new_datafile$="pre_daytime_hdr",file_id$=new_datafile$
    erase destin_folder$+"/"+new_datafile$,err=*next
    gosub check_for_definition
    pre_02hdr_trgt=new_file
    findrecord(ddm_table_tpls,key=pad(table_alias$,16),dom=*next)ddm_table_tpls$
    pre_02hdr_tpl$=ddm_table_tpls.template$

    read(pre_02_trgt,key="",dom=*next)
    dim pre02$:pre_02_tpl$
    while 1
       readrecord(pre_02_trgt,end=*break)pre02$
       dim pre_02hdr$:pre_02hdr_tpl$
       pre_02hdr.firm_id$=pre02.firm_id$
       pre_02hdr.batch_no$=pre02.batch_no$
       pre_02hdr.employee_no$=pre02.employee_no$
       pre_02hdr.pay_period$="W"
       pre_02hdr$=field(pre_02hdr$)
       writerecord(pre_02hdr_trgt)pre_02hdr$
    wend
    return
    
fix_yymmdd_date: rem --- Convert yy dates to yyyy, and yymmdd dates to yyyymmdd
    rem ---     Input/Out: value$
    if len(cvs(value$,3)) then
        value$=fnyy_yy21$(value$)
        if asc(value$)>=65 then
            value$=str(200+asc(value$)-65)+value$(2)
        else
            value$="19"+value$
        endif
        value$=str(num(value$(1,4),err=*next)+add_year)+value$(5)
    else
        value$=pad("",8)
    endif
    return

disp_meter:rem --- Display Progress Meter

    call stbl("+DIR_SYP")+"bam_prog_bar.bbj",rdSysGUI!,rdForm!,rdMeterWin!,rd_meter_title$,rd_meter_total_recs,rd_meter_proc_recs,rd_meter_data$,rd_meter_action$

    return


open_tables:rem --- Open Tables

    call stbl("+DIR_SYP")+"bac_open_tables.bbj",
:       rd_open_beg,
:       rd_open_end,
:       rd_open_tables$[all],
:       rd_open_opts$[all],
:       rd_open_chans$[all],
:       rd_open_tpls$[all],
:       rd_table_chans$[all],
:       rd_open_batch,
:       rd_open_status$

    if rd_open_status$<>""
        rd_msg_id$="ENTRY_OPEN_ERROR"
        dim rd_msg_tokens$[1]
        rd_msg_tokens$[1]=rd_open_status$
        gosub disp_message
        release
    endif

    return

disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",rd_msg_id$,rd_msg_tokens$[all],rd_msg_opt$,rd_table_chans$[all]
    return

eoj:
   a=msgbox("Exit from DataPort?",4+32+256,"DataPort")
   if a=6 then
      release
   endif
   if a=7 then
      a=msgbox("Continuing DataPort..")
      goto eof
   endif
   return

rem --- Convert 2-Char Year to 21st Century 2-Char Year
    def fnyy_yy21$(q$)
        q3$=" ABCDE56789ABCDEFGHIJ"
        q$(1,1)=q3$(pos(q$(1,1)=" 0123456789ABCDEFGHIJ"))
        return q$
    fnend

std_error: rem --- Standard error handler (18Nov2014)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem /**
rem  * TriggerFilter Class to filter *.trigger files.
rem  */
class public TriggerFilter implements java.io.FilenameFilter

    method public boolean accept(File dir!, String file!)
        if file!.endsWith(".trigger")
            methodret Boolean.valueOf("true")
        else
            methodret Boolean.valueOf("false")
        endif
    methodend
    
classend
