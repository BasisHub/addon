rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem before write
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr throw_error

rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	aon_dir$=stbl("+V6DATA",err=std_exit)
	wkdir$=stbl("+DIR_BRD",err=std_exit)

rem --- Open v6 IV ivm-10 file

	ivm10_dev=unt
	open (ivm10_dev,err=throw_error) aon_dir$+"IVM-10"

	ivm10_rec$="firm_id:c(2),rec_code:c(1),wh_id:c(2),name:c(24),add1:c(24),add2:c(24),"
	ivm10_rec$=ivm10_rec$+"add3:c(24),add4:c(24),phone:c(10),zip:c(9),reserved:c(16*=10)"
	dim ivm10_rec$:ivm10_rec$

rem --- Set data in output record

	tpl_dev=unt
	open (tpl_dev,err=throw_error) wkdir$+"ddm_table_tpls.dat"
	read (tpl_dev,key=pad("IVC_WHSECODE",16),dom=throw_error) *,*,iv_rec$
	dim iv_rec$:iv_rec$
	
	iv_rec$=td!.getWriteBuffer()
	
	ivm10_rec.firm_id$=iv_rec.firm_id$
	ivm10_rec.rec_code$="C"
	ivm10_rec.wh_id$=iv_rec.warehouse_id$
	ivm10_rec.name$=iv_rec.short_name$
	ivm10_rec.add1$=iv_rec.addr_line_1$
	ivm10_rec.add2$=iv_rec.addr_line_2$
	ivm10_rec.add3$=iv_rec.addr_line_3$
	ivm10_rec.add4$=iv_rec.city$ + iv_rec.state_code$
	ivm10_rec.phone$=iv_rec.phone_no$
	ivm10_rec.zip$=iv_rec.zip_code$
	ivm10_rec$=field(ivm10_rec$)
	write record (ivm10_dev,key=ivm10_rec.firm_id$+ivm10_rec.rec_code$+ivm10_rec.wh_id$) ivm10_rec$+$0a$

	goto std_exit

throw_error:

	throw lst(pgm(tcb(5))) , err
	goto std_exit
	
std_exit: