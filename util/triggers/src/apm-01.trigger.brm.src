rem before remove
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr throw_error

rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	vend_key$=td!.getKey()
	
	aon_dir$=stbl("+V6DATA",err=std_exit)
	wkdir$=stbl("+DIR_BRD",err=std_exit)

rem --- Open v6 AP Vendor Master and Cross Reference tables

	apm01_dev=unt
	open (apm01_dev,err=throw_error) aon_dir$+"APM-01"
	apm04_dev=unt
	open (apm04_dev,err=throw_error) aon_dir$+"APM-04"

	apm01_rec$="firm_id:C(2),vendor_id:c(6*=10),name:c(30),add1:c(24),add2:c(24),add3:c(24),"
	apm01_rec$=apm01_rec$+"zip_code:c(9),phone:c(10),ext:c(4),contact:c(20),alt_sequence:c(10),"
	apm01_rec$=apm01_rec$+"date_open:c(3),hold_flag:c(1),fed_no:c(15),vend_1099:c(1),"
	apm01_rec$=apm01_rec$+"vend_acct:c(10),fax:c(10),fob:c(15),ship_via:c(15),reserved:c(20*=10)"
	dim apm01_rec$:apm01_rec$
	apm04a_rec$="firm_id:c(2),code:c(1),alt_seq:c(10),vendor_id:c(6)"
	dim apm04a_rec$:apm04a_rec$
	apm04b_rec$="firm_id:c(2),code:c(1),zip_code:c(9),vendor_id:c(6)"
	dim apm04b_rec$:apm04b_rec$

rem --- Read old vendor record"

	read record (apm01_dev,key=vend_key$,dom=*next) apm01_rec$

rem --- Remove records

	remove (apm01_dev,key=vend_key$,dom=*next)
	remove (apm04_dev,key=apm01_rec.firm_id$+"A"+apm01_rec.alt_sequence$+apm01_rec.vendor_id$,dom=*next)
	remove (apm04_dev,key=apm01_rec.firm_id$+"B"+apm01_rec.zip_code$+apm01_rec.vendor_id$,dom=*next)
	
	goto std_exit

throw_error:

	throw lst(pgm(tcb(5))) , err
	goto std_exit
	
std_exit: