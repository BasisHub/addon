rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem before write
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr throw_error

rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	iv_data$=td!.getWriteBuffer()
	
	aon_dir$=stbl("+V6DATA",err=std_exit)
	wkdir$=stbl("+DIR_BRD",err=std_exit)
	
rem --- Open v6 IV Master file and Sort files and set v6 template

	ivm01_dev=unt
	open (ivm01_dev,err=throw_error) aon_dir$+"IVM-01"
	ivm02_dev=unt
	open (ivm02_dev,err=throw_error) aon_dir$+"IVM-02"
	ivm03_dev=unt
	open (ivm03_dev,err=throw_error) aon_dir$+"IVM-03"

	ivm01_rec$="firm_id:c(2),iv_item_id:c(20*=10),iv_desc:c(60*=10),prod_type:c(3),"
	ivm01_rec$=ivm01_rec$+"uos:c(2),uop:c(2),taxable:c(1),buyer:c(3),vendor_id:c(6),"
	ivm01_rec$=ivm01_rec$+"sa_lev:c(1),lot_ser:c(1),inventoried:c(1),item_class:c(3),"
	ivm01_rec$=ivm01_rec$+"stock_lev:c(1),abc_code:c(1),eoq_code:c(1),ord_pnt_code:c(1),"
	ivm01_rec$=ivm01_rec$+"ss_code:c(1),lst_rct_date:c(3),lst_blt_date:c(3),lst_sale_date:c(3),"
	ivm01_rec$=ivm01_rec$+"lst_iss_date:c(3),lst_adj_date:c(3),lst_xin_date:c(3),lst_xout_date:c(3),"
	ivm01_rec$=ivm01_rec$+"lst_ret_date:c(3),item_type:c(3),reserved_01:c(7*=10),upc:c(20),"
	ivm01_rec$=ivm01_rec$+"bar_code:c(20*=10),as_code:c(1),as_item:c(20*=10),reserved_02:c(1*=10),"
	ivm01_rec$=ivm01_rec$+"gl_iv_acct:c(10),gl_cogs_acct:c(10),gl_adj_acct:c(10),gl_purch_acct:c(10*=10),"
	ivm01_rec$=ivm01_rec$+"conv_fact:n(1*=10),weight:n(1*=10),msrp:n(1*=10),max_qty:n(1*=10),"
	ivm01_rec$=ivm01_rec$+"ord_pnt:n(1*=10),saf_stk:n(1*=10),eoq:n(1*=10),lead_time:n(1*=10),"
	ivm01_rec$=ivm01_rec$+"res_01:n(1*=10),res_02:n(1*=10),res_03:n(1*=10),res_04:n(1*=10),res_05:n(1*=10)"
	dim ivm01_rec$:ivm01_rec$
	
rem --- get the Barista template and populate from WriteBuffer
	tpl_dev=unt
	open (tpl_dev,err=throw_error) wkdir$+"ddm_table_tpls.dat"
	read (tpl_dev,key=pad("IVM_ITEMMAST",16),dom=throw_error) *,*,ivm_after_read$
	dim ivm01_after_rec$:ivm_after_read$
	ivm01_after_rec$=iv_data$
	
rem --- Remove v6 sort record

	while 1
		read record (ivm01_dev,key=iv_data$(1,22),dom=*break) ivm01_rec$

rem --- Remove non-warehouse cross references
		if ivm01_rec.prod_type$<>ivm01_after_rec.product_type$
			ivm03_rec$="firm_id:c(2),rec_id:c(1),prod_type:c(3),item_id:c(20)"
			dim ivm03_rec$:ivm03_rec$
			ivm03_rec.firm_id$=ivm01_rec.firm_id$
			ivm03_rec.rec_id$="A"
			ivm03_rec.prod_type$=ivm01_rec.prod_type$
			ivm03_rec.item_id$=ivm01_rec.iv_item_id$
			remove (ivm03_dev,key=ivm03_rec$,dom=*next)
			ivm03_rec.prod_type$=ivm01_after_rec.product_type$
			ivm03_rec$=field(ivm03_rec$)
			write record(ivm03_dev,key=ivm03_rec$)ivm03_rec$
		endif

		if ivm01_rec.iv_desc$<>ivm01_after_rec.item_desc$
			ivm03_rec$="firm_id:c(2),rec_id:c(1),iv_desc:c(20),item_id:c(20)"
			dim ivm03_rec$:ivm03_rec$
			ivm03_rec.firm_id$=ivm01_rec.firm_id$
			ivm03_rec.rec_id$="E"
			ivm03_rec.iv_desc$=ivm01_rec.iv_desc$
			ivm03_rec.item_id$=ivm01_rec.iv_item_id$
			remove (ivm03_dev,key=ivm03_rec$,dom=*next)
			ivm03_rec.iv_desc$=ivm01_after_rec.item_desc$
			ivm03_rec$=field(ivm03_rec$)
			write record(ivm03_dev,key=ivm03_rec$)ivm03_rec$
		endif

		if ivm01_rec.bar_code$<>ivm01_after_rec.bar_code$
			ivm03_rec$="firm_id:c(2),rec_id:c(1),bar_code:c(20),item_id:c(20)"
			dim ivm03_rec$:ivm03_rec$
			ivm03_rec.firm_id$=ivm01_rec.firm_id$
			ivm03_rec.rec_id$="I"
			ivm03_rec.bar_code$=ivm01_rec.bar_code$
			ivm03_rec.item_id$=ivm01_rec.iv_item_id$
			remove (ivm03_dev,key=ivm03_rec$,dom=*next)
			ivm03_rec.bar_code$=ivm01_after_rec.bar_code$
			ivm03_rec$=field(ivm03_rec$)
			write record(ivm03_dev,key=ivm03_rec$)ivm03_rec$
		endif
		
		if ivm01_rec.item_class$<>ivm01_after_rec.item_class$
			ivm03_rec$="firm_id:c(2),rec_id:c(1),item_class:c(3),item_id:c(20)"
			dim ivm03_rec$:ivm03_rec$
			ivm03_rec.firm_id$=ivm01_rec.firm_id$
			ivm03_rec.rec_id$="J"
			ivm03_rec.item_class$=ivm01_rec.item_class$
			ivm03_rec.item_id$=ivm01_rec.iv_item_id$
			remove (ivm03_dev,key=ivm03_rec$,dom=*next)
			ivm03_rec.item_class$=ivm01_after_rec.item_class$
			ivm03_rec$=field(ivm03_rec$)
			write record(ivm03_dev,key=ivm03_rec$)ivm03_rec$
		endif
		
		if ivm01_rec.item_type$<>ivm01_after_rec.item_type$
			ivm03_rec$="firm_id:c(2),rec_id:c(1),item_type:c(3),item_id:c(20)"
			dim ivm03_rec$:ivm03_rec$
			ivm03_rec.firm_id$=ivm01_rec.firm_id$
			ivm03_rec.rec_id$="T"
			ivm03_rec.item_type$=ivm01_rec.item_type$
			ivm03_rec.item_id$=ivm01_rec.iv_item_id$
			remove (ivm03_dev,key=ivm03_rec$,dom=*next)
			ivm03_rec.item_type$=ivm01_after_rec.item_type$
			ivm03_rec$=field(ivm03_rec$)
			write record(ivm03_dev,key=ivm03_rec$)ivm03_rec$
		endif

		if ivm01_rec.upc$<>ivm01_after_rec.upc_code$
			ivm03_rec$="firm_id:c(2),rec_id:c(1),upc:c(20),item_id:c(20)"
			dim ivm03_rec$:ivm03_rec$
			ivm03_rec.firm_id$=ivm01_rec.firm_id$
			ivm03_rec.rec_id$="U"
			ivm03_rec.upc$=ivm01_rec.upc$
			ivm03_rec.item_id$=ivm01_rec.iv_item_id$
			remove (ivm03_dev,key=ivm03_rec$,dom=*next)
			ivm03_rec.upc$=ivm01_after_rec.upc_code$
			ivm03_rec$=field(ivm03_rec$)
			write record(ivm03_dev,key=ivm03_rec$)ivm03_rec$
		endif
	
rem --- Get and remove warehouse cross references for this item

		read(ivm03_dev,key=ivm01_rec.firm_id$+"G"+ivm01_rec.iv_item_id$,dom=*next)
		while 1
			ivm03_key$=key(ivm03_dev,end=*break)
			read(ivm03_dev,end=*break)
			if pos(ivm01_rec.firm_id$+"G"+ivm01_rec.iv_item_id$=ivm03_key$)<>1 break
			ivm02_rec$="firm_id:c(2),wh_id:c(2),item_id:c(20*=10),loc:c(10),cycle:c(2),"
			ivm02_rec$=ivm02_rec$+"dist_code:c(2),sel_phy:c(1),lst_phy:c(3),buyer:c(3),vend_id:c(6),"
			ivm02_rec$=ivm02_rec$+"lst_rct_date:c(3),lst_blt_date:c(3),lst_sale_date:c(3),"
			ivm02_rec$=ivm02_rec$+"lst_iss_date:c(3),lst_adj_date:c(3),lst_xin_date:c(3),lst_xout_date:c(3),"
			ivm02_rec$=ivm02_rec$+"lst_ret_date:c(3),cur_price_cd:c(2),prior_price_cd:c(2),abc_code:c(1),"
			ivm02_rec$=ivm02_rec$+"eoq_code:c(1),ord_pnt_code:c(1),ss_code:c(1),spec_ord:c(1),reserved_01:c(4*=10),"
			ivm02_rec$=ivm02_rec$+"reserved_02:c(1*=10),qty_oh:n(1*=10),qty_oo:n(1*=10),qty_com:n(1*=10),"
			ivm02_rec$=ivm02_rec$+"max_qty:n(1*=10),ord_pnt:n(1*=10),ss:n(1*=10),eoq:n(1*=10),lead:n(1*=10),"
			ivm02_rec$=ivm02_rec$+"phys_cnt:n(1*=10),cur_price:n(1*=10),pri_price:n(1*=10),unit_cost:n(1*=10),"
			ivm02_rec$=ivm02_rec$+"avg_cost:n(1*=10),std_cost:n(1*=10),repl_cost:n(1*=10),land_cost:n(1*=10),"
			ivm02_rec$=ivm02_rec$+"lst_pch_cost:n(1*=10),res_num_01:n(1*=10),res_num_02:n(1*=10),"
			ivm02_rec$=ivm02_rec$+"res_num_03:n(1*=10),res_num_04:n(1*=10)"
			dim ivm02_rec$:ivm02_rec$
			read record(ivm02_dev,key=ivm03_key$(1,2)+ivm03_key$(24,2)+ivm03_key$(4,20),dom=*continue) ivm02_rec$

			if ivm01_rec.prod_type$<>ivm01_after_rec.product_type$
				ivm03_rec$="firm_id:c(2),rec_id:c(1),wh_id:c(2),prod_type:c(3),item_id:c(20)"
				dim ivm03_rec$:ivm03_rec$
				ivm03_rec.firm_id$=ivm01_rec.firm_id$
				ivm03_rec.rec_id$="C"
				ivm03_rec.wh_id$=ivm02_rec.wh_id$
				ivm03_rec.prod_type$=ivm01_prod_type$
				ivm03_item_id$=ivm02_item_id$
				remove (ivm03_dev,key=ivm03_rec$,dom=*next)
				ivm03_rec.prod_type$=ivm01_after_rec.product_type$
				ivm03_rec$=field(ivm03_rec$)
				write record(ivm03_dev,key=ivm03_rec$)ivm03_rec$

				read(ivm03_dev,key=ivm01_rec.firm_id$+"H"+ivm01_rec.iv_item_id$,dom=*next)
				while 1
					ivm03h_key$=key(ivm03_dev,end=*break)
					if pos(ivm01_rec.firm_id$+"H"+ivm01_rec.iv_item_id$=ivm03h_key$)<>1 break
					read(ivm03_dev)
					x$=ivm01_rec.firm_id$+"F"+ivm02_rec.buyer$+ivm03h_key$(24,6)+ivm01_rec.prod_type$+ivm02_rec.item_id$+ivm02_rec.wh_id$
					remove (ivm03_dev,key=x$,dom=*next)
					x$=ivm01_rec.firm_id$+"F"+ivm02_rec.buyer$+ivm03h_key$(24,6)+ivm01_after_rec.product_type$+ivm02_rec.item_id$+ivm02_rec.wh_id$	
					write record(ivm03_dev,key=x$)x$
					break
				wend
			endif
			break
		wend
		break
	wend
rem --- Write v6 master record

	ivm01_rec.firm_id$=ivm01_after_rec.firm_id$
	ivm01_rec.iv_item_id$=ivm01_after_rec.item_id$
	ivm01_rec.iv_desc$=ivm01_after_rec.item_desc$
	ivm01_rec.prod_type$=ivm01_after_rec.product_type$
	ivm01_rec.uos$=ivm01_after_rec.unit_of_sale$
	ivm01_rec.uop$=ivm01_after_rec.purchase_um$
	ivm01_rec.taxable$=ivm01_after_rec.taxable_flag$
	ivm01_rec.buyer$=ivm01_after_rec.buyer_code$
	ivm01_rec.vendor_id$=ivm01_after_rec.vendor_id$
	ivm01_rec.sa_lev$=ivm01_after_rec.sa_level$
	ivm01_rec.lot_ser$=ivm01_after_rec.lotser_item$
	ivm01_rec.inventoried$=ivm01_after_rec.inventoried$
	ivm01_rec.item_class$=ivm01_after_rec.item_class$
	ivm01_rec.stock_lev$=ivm01_after_rec.stock_level$
	ivm01_rec.abc_code$=ivm01_after_rec.abc_code$
	ivm01_rec.eoq_code$=ivm01_after_rec.eoq_code$
	ivm01_rec.ord_pnt_code$=ivm01_after_rec.ord_pnt_code$
	ivm01_rec.ss_code$=ivm01_after_rec.saf_stk_code$
	ivm01_rec.lst_rct_date$=fnpackdate$(ivm01_after_rec.lstrec_date$)
	ivm01_rec.lst_blt_date$=fnpackdate$(ivm01_after_rec.lstblt_date$)
	ivm01_rec.lst_sale_date$=fnpackdate$(ivm01_after_rec.lstsal_date$)
	ivm01_rec.lst_iss_date$=fnpackdate$(ivm01_after_rec.lstiss_date$)
	ivm01_rec.lst_adj_date$=fnpackdate$(ivm01_after_rec.lstadj_date$)
	ivm01_rec.lst_xin_date$=fnpackdate$(ivm01_after_rec.lstxin_date$)
	ivm01_rec.lst_xout_date$=fnpackdate$(ivm01_after_rec.lstxot_date$)
	ivm01_rec.lst_ret_date$=fnpackdate$(ivm01_after_rec.lstret_date$)
	ivm01_rec.item_type$=ivm01_after_rec.item_type$
	ivm01_rec.upc$=ivm01_after_rec.upc_code$
	ivm01_rec.bar_code$=ivm01_after_rec.bar_code$
	ivm01_rec.as_code$=ivm01_after_rec.alt_sup_flag$
	ivm01_rec.as_item$=ivm01_after_rec.alt_sup_item$
	ivm01_rec.gl_iv_acct$=ivm01_after_rec.gl_inv_acct$
	ivm01_rec.gl_cogs_acct$=ivm01_after_rec.gl_cogs_acct$
	ivm01_rec.gl_adj_acct$=ivm01_after_rec.gl_inv_adj$
	ivm01_rec.gl_purch_acct$=ivm01_after_rec.gl_pur_acct$
	ivm01_rec.conv_fact=ivm01_after_rec.conv_factor
	ivm01_rec.weight=ivm01_after_rec.weight
	ivm01_rec.msrp=ivm01_after_rec.msrp
	ivm01_rec.max_qty=ivm01_after_rec.maximum_qty
	ivm01_rec.ord_pnt=ivm01_after_rec.order_point
	ivm01_rec.saf_stk=ivm01_after_rec.safety_stock
	ivm01_rec.eoq=ivm01_after_rec.eoq
	ivm01_rec.lead_time=ivm01_after_rec.lead_time

	write record (ivm01_dev,key=iv_data$(1,22)) ivm01_rec$

	goto std_exit

rem --- Functions

	rem --- Pack CCYYMMDD into a 3 character version 6 date format
	def fnpackdate$(q$)
		if len(q$)<> 8 or cvs(q$,3)=""
			return "   "
		endif
		m1$=chr(num(q$(5,2))+32)
		d1$=chr(num(q$(7,2))+32)
		if q$(1,2)="19"
			y1$=chr(num(q$(3,2))+32)
		else
			y1$=chr(num(q$(3,2))+132)
		endif
		q1$=y1$+m1$+d1$
		return q1$
	fnend
	
throw_error:

	throw lst(pgm(tcb(5))) , err
	goto std_exit
	
std_exit: