rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem before remove
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr trigger_error

rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	gl_key$=td!.getKey()
	
	aon_dir$=stbl("+V6DATA",err=std_exit)

rem --- Open v6 G/L Master file and Sort files

	glm01_dev=unt
	open (glm01_dev,err=trigger_error) aon_dir$+"GLM-01"
	glm04_dev=unt
	open (glm04_dev,err=trigger_error) aon_dir$+"GLM-04"

	glm01_rec$="firm_id:c(2),gl_account:c(10*=10),gl_acct_desc:c(35),gl_acct_type:c(1),"
	glm01_rec$=glm01_rec$+"detail_flag:c(1),reserved_1:c(3*=10)"
	dim glm01_rec$:glm01_rec$

rem --- Get and remove v6 records
	
	read record (glm01_dev,key=gl_key$) glm01_rec$

	glm04_rec$="firm_id:c(2),rec_id:c(1),gl_acct_desc:c(35),gl_account:c(10)"
	dim glm04_rec$:glm04_rec$

	glm04_rec.firm_id$=glm01_rec.firm_id$
	glm04_rec.rec_id$="A"
	glm04_rec.gl_acct_desc$=glm01_rec.gl_acct_desc$
	glm04_rec.gl_account$=glm01_rec.gl_account$
	glm04_rec$=field(glm04_rec$)
	remove (glm04_dev,key=glm04_rec$,dom=*next)

	remove (glm01_dev,key=gl_key$,dom=*next)

	goto std_exit

trigger_error:

    rd_err_text$="", err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
std_exit: