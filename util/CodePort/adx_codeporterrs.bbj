rem adx - CodePort Program Conversion Utility (Error/Warnings Overlay)
rem Program adx_codeporterrs.bbj v8.0.0 12Oct2007
rem 
rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Feb2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+
rem 

rem --- Initializations
    dim errors$[999],filerefs$[999],recrefs$[999]
    let errors=0,warnings=0,filerefs=0,recrefs=0,needed=1
    let channels=0,offset=0,inserted=0,total=numlines
    let header$=" Errors, Warnings and Conversion Information "
    let header=len(header$)

rem --- Any underfined functions?
    gosub startline
    gosub function_errors

rem --- Any reference errors?
    gosub startline
    gosub reference_errors

rem --- List errors (if any)
    let line=8
    if errors then gosub list_errors
    let inserted=numlines-total

rem --- Any reset verbs removed?
    if resets then gosub list_resets
    let inserted=numlines-total

rem --- Any cross reference files used?
    gosub startline
    gosub xreffile_check
    if filerefs then gosub list_xreffiles
    let inserted=numlines-total

rem --- Any cross-reference records replace by secondary indices used?
    gosub startline
    gosub xrefrecord_check
    if recrefs then gosub list_xrefrecs
    let inserted=numlines-total

rem --- Insert list of removed IOLIST's
    gosub startline
    if iolists then gosub list_iolists
    let inserted=numlines-total

rem --- Any miscellaneous channel references that need to be listed?
    for x=1 to iolists
        if pos(": iolist"=iolists$[x])=0 then let channels=channels+1
    next x
    if channels=0 then goto remove_blanklines
    if needed then gosub header
    let line=(line+iolists)-channels

rem --- Insert list of channel references
    let description$="Inserting list of channel references..."
    gosub display_step
    let line=line+1
    gosub insert_line
    let lines$[line]="rem --- The following channel references have been identified:"
    let line=line+1
    gosub insert_line
    let line=line+1
    gosub insert_line
    for x=iolists to 1 step -1
        if pos(": iolist"=iolists$[x]) then continue
        let x$=iolists$[x]
        if pos("CodePort"=iolists$[x]) then goto codeport_line
        let p=pos(" (Line"=x$),l=num(x$(p+6,4))+(numlines-channel_lines)
        let x$=x$(1,p-1)
        let x$=fnerror$(x$,l-inserted)
        codeport_line: 
        gosub insert_line
        let lines$[line]="rem --- "+x$
    next x
    gosub display_done

rem --- Remove any extra blank lines
remove_blanklines: 
    if inserted then gosub header
    let description$="Removing extraneous blank lines..."
    let action=14
    gosub modify_code

rem --- Adjust line number references as needed
    let offset=numlines-total
    if offset=0 then goto done
    let description$="Adjusting line number references..."
    gosub display_step
    gosub startline
    for x=1 to startline
        let rec$=cvs(lines$[x],2)
        let p=pos("(Line "=rec$)
        if p=0 then continue
        let x$=rec$(p+6)
        let rec$=rec$(1,p-1)
        let l=num(x$(1,4))
        let l=l+offset
        let lines$[x]=rec$+"(Line "+str(l:"0000")+")"
    next x
    gosub display_done

rem --- Program converted/Write new source file
done: 
    gosub write_src

rem --- Update log file
    let message$="Program "+filename$+" converted to v8.x standards"
    gosub display_message
    let errors$="No",warnings$="No"
    if errors then let errors$=str(errors)
    if warnings then let warnings$=str(warnings)
    let message$=str(original)+" Lines in / "+str(numlines)+" Lines Out"
    gosub display_message
    let message$=errors$+" Error(s) encountered / "+warnings$+" Warnings issued"
    gosub display_message
    let message$="New program text file is "+srcfile$
    gosub display_message
    rem --- Close files, delete work files and run next overlay
    gosub close_files
    gosub remove_files
    run "adx_codeportfile.bbj",err=the_end

rem --- Check for function errors
function_errors: 
    let description$="Checking for function errors..."
    gosub display_step
    for x=startline to numlines
        let rec$=cvs(lines$[x],1)
        if pos("rem"=rec$)=1 then goto next_function_error
        if pos("fnend"=rec$)=1 then goto next_function_error
        if pos("def fn"=rec$) then goto next_function_error
        next_function_check: 
        let p=pos("fn"=rec$)
        if p=0 then goto next_function_error
        let rec$=rec$(p);p=pos("("=rec$)
        if p=0 then goto next_function_error else x$=rec$(1,p-1),rec$=rec$(p)
        if pos(x$=functions$) then goto next_function_check
        let errors=errors+1,errors$[errors]=fnerror$("Undefined function "+x$,x)
        goto next_function_check
        next_function_error: 
    next x
    gosub display_done
return 

rem --- Reference errors
reference_errors: 
    let description$="Checking for miscellaneous errors..."
    gosub display_step
    for x=startline to numlines
        let rec$=lines$[x]
        let p=pos(sysref$=rec$),b=pos(".bbx"=rec$),s=pos("sys01"=rec$)
        if s=0 then goto check_admin
        let x$=rec$(s),l=len(x$),x$=x$(1,min(12,l)),errors=errors+1
        let errors$[errors]=fnerror$("v7.x sys-01 reference ["+x$+"]",x)
        check_admin: 
        if p=0 then goto check_bbx
        let x$=rec$(p),l=len(x$),x$=x$(1,min(12,l)),errors=errors+1
        let errors$[errors]=fnerror$("v7.x administrator reference ["+x$+"]",x)
        check_bbx: 
        if b=0 then goto next_reference_error
        let x$=rec$(b-7,12),errors=errors+1
        let errors$[errors]=fnerror$("BBx reference ["+x$+"]",x)
        next_reference_error: 
    next x
    gosub display_done
return 

rem --- Insert list of errors
list_errors: 
    let description$="Listing errors encountered during conversion..."
    gosub display_step
    if needed then gosub header
    let line=line+1
    gosub insert_line
    let lines$[line]="rem --- The following error(s) were encountered during the conversion:"
    let line=line+1
    gosub insert_line
    let line=line+1
    gosub insert_line
    for x=1 to errors
        let rec$=errors$[x]
        let lines$[line]="rem --- "+rec$
        let line=line+1
        gosub insert_line
    next x
    gosub display_done
return 

rem --- Check for reference to cross-reference files
xreffile_check: 
    let description$="Checking for references to cross-reference files..."
    gosub display_step
    for x=startline to numlines
        let rec$=lines$[x],p=pos("_dev"=rec$)
        if p=0 then continue
        let p=p-5
        if p<1 then continue
        let x$=rec$(p,5)
        if pos(x$=xreffile$,7)=0 then continue
        let filerefs=filerefs+1,warnings=warnings+1,x$=x$(1,3)+"-"+x$(4,2)
        let filerefs$[filerefs]=fnerror$("Cross-reference file "+x$+" referenced",x-inserted)
    next x
    gosub display_done
return 

rem --- Insert list of xref files being referenced
list_xreffiles: 
    let description$="Listing cross-reference files being used..."
    gosub display_step
    if needed then gosub header
    let line=line+1
    gosub insert_line
    let lines$[line]="rem --- The following cross-reference files are being referenced:"
    let line=line+1
    gosub insert_line
    let line=line+1
    gosub insert_line
    for x=1 to filerefs
        let rec$=filerefs$[x]
        let lines$[line]="rem --- "+rec$
        let line=line+1
        gosub insert_line
    next x
    gosub display_done
return 

rem --- Call adc_codeportconv.bbx to perform the requested action
modify_code: 
    gosub display_step
    call "adc_codeportconv.bbj",action,lines$[all],options$[all],numlines
    gosub display_done
return 

rem --- Check for reference to cross-reference records
xrefrecord_check: 
    let l=len(xrefrecs$)
    let description$="Checking for references to cross-reference records..."
    gosub display_step
    for x=10 to numlines
        let rec$=lines$[x]
        for z=1 to l step 7
            let xrefrecord$=cvs(xrefrecs$(z,7),2)
            let p=pos(xrefrecord$=rec$)
            if p=0 then continue
            let recrefs=recrefs+1,warnings=warnings+1,x$=xrefrecord$,y$=xrefindx$(z,6)
            let recrefs$[recrefs]=fnerror$("Cross-reference record "+x$+" replaced by index on "+y$,x-inserted)
        next z
    next x
    gosub display_done
return 

rem --- Insert list of xref records being referenced
    list_xrefrecs: 
    let description$="Listing cross-reference records being used..."
    gosub display_step
    if needed then gosub header
    let line=line+1
    gosub insert_line
    let lines$[line]="rem --- The following cross-reference records are being referenced:"
    let line=line+1
    gosub insert_line
    let line=line+1
    gosub insert_line
    for x=1 to recrefs
        let rec$=recrefs$[x]
        let lines$[line]="rem --- "+rec$
        let line=line+1
        gosub insert_line
    next x
    gosub display_done
return 

rem --- Insert list of previously removed IOLIST's
list_iolists: 
    let description$="Inserting list of removed IOLIST's..."
    gosub display_step
    if needed then gosub header
    let line=line+1
    gosub insert_line
    let lines$[line]="rem --- The following IOLIST's were removed from this program:"
    let line=line+1
    gosub insert_line
    let line=line+1
    gosub insert_line
    for x=iolists to 1 step -1
        if pos(": iolist"=iolists$[x])=0 then continue
        gosub insert_line
        let lines$[line]="rem --- "+iolists$[x]
    next x
    gosub display_done
return 

rem --- Insert list of previously removed reset verbs"
list_resets: 
    let description$="Inserting list of removed reset verbs..."
    gosub display_step
    if needed then gosub header
    let line=line+1
    gosub insert_line
    let lines$[line]="rem --- The following reset verbs were removed from this program:"
    let line=line+1
    gosub insert_line
    let line=line+1
    gosub insert_line
    for x=1 to resets
        let rec$=resets$[x]
        let p=pos(" (Line"=rec$)
        let rec$=rec$(1,p-1)
        let lines$[line]="rem --- "+rec$
        let line=line+1
        gosub insert_line
    next x
    let warnings=warnings+resets
    gosub display_done
return 

rem --- Display range of LINES$ (debugging tool)
listlines: 
    for x=b to e
        print "(",x:"0000","): ",lines$[x]
    next x
    escape 
return 

rem --- Find seterr/setesc line
startline: 
    let startline=0
    for x=1 to numlines
        let rec$=lines$[x]
        if pos("seterr"=rec$) then let startline=x
        if pos("setesc"=rec$) then let startline=x
        if startline then break
    next x
return 

rem --- Display message
display_message: 
    print fndatestamp$(0),message$
    print (log_dev)fndatestamp$(0),message$,cr$
return 

rem --- Display step
display_step: 
    let message$=description$
    gosub display_message
return 

rem --- Display step completion
display_done: 
    let message$=description$+"completed"
    gosub display_message
return 

rem --- List final version of program in the v8.x source file
write_src: 
    let srcfile$=pgmdir$+newname$+src$
    erase srcfile$,err=open_srcfile
    open_srcfile: 
    let src_dev=unt
    string srcfile$
    open (src_dev)srcfile$
    for x=1 to numlines
        print (src_dev)lines$[x]
    next x
return 

rem --- Close any open channels
close_files: 
    close_txt: close (txt_dev,err=close_src)
    close_src: close (src_dev,err=close_files_exit)
    close_files_exit: 
return 

rem --- Delete work files
remove_files: 
    if keepfiles then goto remove_files_exit
    remove_tmp: erase tmpfile$,err=remove_txt
    remove_txt: erase txtfile$,err=remove_files_exit
    remove_files_exit: 
return 

rem --- Header/footer
header: 
    let line=line+1
    gosub insert_line
    if header=0 then let line=line+1; gosub insert_line
    let x$="rem "+fill(80,"-"),p=int((84-header)/2)
    if header then let x$(p,header)=header$
    let lines$[line]=x$
    if header then let line=line+1; gosub insert_line
    let header$="",header=0,needed=0
return 

rem --- Insert a blank line at LINE
insert_line: 
    let numlines=numlines+1
    for z=numlines to line+1 step -1
        let lines$[z]=lines$[z-1]
    next z
    let lines$[line]=""
return 

rem --- Functions
    def fnerror$(q$,q)=q$+" (Line "+str(q:"0000")+")"
    def fndatestamp$(q)=date(0:"%Mz/%Dz/%Y %hz:%mz:%sz")+" "

rem --- Exit utility
the_end: 
    run "adx_codeportexit.bbj",err=finished
finished: 
    end