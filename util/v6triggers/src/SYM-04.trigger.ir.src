rem instead of read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr throw_error
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	ky$=td!.getKey()
	
	myns!=BBjAPI().getGlobalNamespace()
	wkdir$ = myns!.getValue("work_dir")
	x$=stbl("+AON_APPCOMPANY",myns!.getValue("aon_appcompany"))
	data_dir$=myns!.getValue("v6data")
	
rem --- Template for the SYM-04 record being caught

	intemp$="system_id:c(2*=10),sys_desc:c(35),multi_firms:c(1),firm_id:c(2),sys_installed:c(1),"
	intemp$=intemp$+"version:c(6),post_gl:c(1),batch_flag:c(1),reserved_1:c(17*=10),"
	intemp$=intemp$+"data_path:c(50*=10),prog_path:c(50*=10),res_num_01:n(1*=10),res_num_02:n(1*=10),"
	intemp$=intemp$+"res_num_03:n(1*=10)"
	dim inRec$:intemp$
	
rem --- Open Barista Modules file

	modules_dev=unt
	open (modules_dev,err=throw_error)wkdir$+"sys/data/adm_modules.dat"
	tpl_dev=unt
	open (tpl_dev,err=throw_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("ADM_MODULES",16),dom=throw_error) *,*,modules$
	dim modules$:modules$
	
rem --- Get the data

	read record (modules_dev,key=stbl("+AON_APPCOMPANY")+pad(ky$,3),dom=throw_error) modules$
	inRec.system_id$ = modules.asc_prod_id$
	inRec.sys_desc$ = modules.sys_desc$
	inRec.multi_firms$ = modules.multi_firm$
	inRec.firm_id$ = modules.company_id$
	inRec.sys_installed$ = modules.sys_install$
	inRec.version$ = "6.0.0"
	inRec.post_gl$ = modules.post_gl$
	inRec.batch_flag$ = modules.batch_entry$
	inRec.data_path$ = data_dir$
	inRec.prog_path$ = modules.program_path$

	td!.setReadBuffer(inRec$)

	goto std_exit

throw_error:

	throw lst(pgm(tcb(5))) , err
	goto std_exit
	
rem --- Functions

	rem --- fnyy$ returns a 2 character year (A0-E9) from a 4 character year
	def fnyy$(q1$)
		q3$=" ABCDE56789ABCDEFGHIJ"
		q1$(1,1)=q3$(pos(q1$(1,1)=" 0123456789ABCDEFGHIJ"))
		return q1$
	fnend
	
std_exit: