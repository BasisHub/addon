rem after write
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr trigger_error

rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	myns!=BBjAPI().getGlobalNamespace()
	wkdir$ = myns!.getValue("work_dir")
	gldir$ = myns!.getValue("gldata")

rem --- Template for the GLM-06 record being caught

	glm06$="firm_id:c(2),process_id:c(10),program_name:c(8*=10),journal_id:c(2),reserved_1:c(8),"
	glm06$=glm06$+"lstuse_date:c(6),lstuse_time:c(4),lstupd_date:c(6),lstupd_time:c(4),file_name:c(6),"
	glm06$=glm06$+"gl_audit:c(1),gl_work_file:c(1),detail_flag:c(1),prt_gl_sum:c(1),reserved_2:c(23*=10),"
	glm06$=glm06$+"gl_post_memo:c(30*=10),dealer_str:c(1*=10),cur_adt_nbr:n(1*=10),res_num_1:n(1*=10),"
	glm06$=glm06$+"res_num_2:n(1*=10),res_num_3:n(1*=10),res_num_4:n(1*=10),dlr_num_1:n(1*=10),"
	glm06$=glm06$+"dlr_num_2:n(1*=10)"
	dim inRec$:glm06$

	inRec$=td!.getWriteBuffer()
	inRec$=field(inRec$)

	if cvs(inRec.process_id$,3)<>"PR Chk Reg" and cvs(inRec.process_id$,3)<>"PR Distrib"
		goto std_exit
	endif

rem --- Open Barista Audit Control file

	glm06_dev=unt
	open (glm06_dev,err=trigger_error)gldir$+"glm-06"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("ADM_AUDITCONTROL",16),dom=trigger_error) *,*,glm06_tpl$
	dim glm06_tpl$:glm06_tpl$
	
rem --- Assign the data

	glm06_tpl.firm_id$ = inRec.firm_id$
	glm06_tpl.process_id$ = inRec.process_id$
	glm06_tpl.sequence_no$ = "001"
	glm06_tpl.process_alias$ = ""
	if cvs(inRec.process_id$,3)="PR Chk Reg" then glm06_tpl.process_program$ = "prr_chkregister.aon"
    if cvs(inRec.process_id$,3)="PR Distrib" then glm06_tpl.process_program$ = "prr_gldistrib.aon"
	glm06_tpl.journal_id$ = inRec.journal_id$
	glm06_tpl.lstuse_date$ = fnyear$(inRec.lstuse_date$(1,2))+inRec.lstuse_date$(3)
	glm06_tpl.lstuse_time$ = inRec.lstuse_time$
	glm06_tpl.lstupd_date$ = fnyear$(inRec.lstupd_date$(1,2))+inRec.lstupd_date$(3)
	glm06_tpl.lstupd_time$ = inRec.lstupd_time$
	if cvs(inRec.process_id$,3)="PR Chk Reg" then glm06_tpl.file_name$ = "glw-67"
    if cvs(inRec.process_id$,3)="PR Distrib" then glm06_tpl.file_name$ = "glw-66"
	glm06_tpl.gl_audit$ = inRec.gl_audit$
	glm06_tpl.gl_work_file$ = inRec.gl_work_file$
	glm06_tpl.detail_level$ = inRec.detail_flag$
	glm06_tpl.prt_gl_sum$ = inRec.prt_gl_sum$
	glm06_tpl.gl_post_memo$ = inRec.gl_post_memo$
	glm06_tpl.audit_number = inRec.cur_adt_nbr

rem --- write the data

	glm06_tpl$=field(glm06_tpl$)
	write record(glm06_dev)glm06_tpl$
	close(glm06_dev)
	
	goto std_exit

trigger_error:

    rd_err_text$="", err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
rem --- Functions

	rem --- fnyear$ returns a 4 character year from a 2 character semi-packed date
	def fnyear$(q1$)
		if q1$(1,1)>="A"
			century$="20"
		else
			century$="19"
		endif
		q3$=" 01234567890123456789"
		q1$(1,1)=q3$(pos(q1$(1,1)=" 0123456789ABCDEFGHIJ"))
		q1$=century$+q1$
		return q1$
	fnend
	
std_exit: