rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem instead of read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr trigger_error
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	ky$=td!.getKey()
	
	myns!=BBjAPI().getGlobalNamespace()
	wkdir$ = myns!.getValue("work_dir")
	gldir$ = myns!.getValue("gldata")

rem --- Template for the GLM-03 record being caught

 	intemp$="firm_id:c(2),journal_id:c(2),code_desc:c(20),permit_je:c(1),post_yr_end:c(1),"
	intemp$=intemp$+"post_locked:c(1),reserved_str1:c(5*=10),reserved_str2:c(1*=10),"
	intemp$=intemp$+"reserved_1:n(7*=10),reserved_2:n(7*=10),reserved_3:n(7*=10)"
	dim inRec$:intemp$
	
rem --- Open Barista Journal ID Code table

	glm03_dev=unt
	open (glm03_dev,err=trigger_error)gldir$+"glm-03"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("GLC_JOURNALCODE",16),dom=trigger_error) *,*,journal$
	dim journal$:journal$
	
rem --- Get the data

	read record (glm03_dev,key=ky$,dom=trigger_error) journal$
	inRec.firm_id$ = journal.firm_id$
	inRec.journal_id$ = journal.journal_id$
	inRec.code_desc$ = journal.code_desc$
	inRec.permit_je$ = journal.permit_je$
	inRec.post_yr_end$ = journal.post_yr_end$
	inRec.post_locked$ = journal.post_locked$
	inRec.reserved_str1$ = journal.reserved_str$
	inRec.reserved_str2$ = journal.dealer_str$
	inRec.reserved_1 = journal.dealer_num_01
	inRec.reserved_2 = journal.dealer_num_02
	inRec.reserved_3 = journal.dealer_num_03

	td!.setReadBuffer(inRec$)

	goto std_exit

trigger_error:

    rd_err_text$="", err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
std_exit: