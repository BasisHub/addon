rem after read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr trigger_error

rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	myns!=BBjAPI().getGlobalNamespace()
	wkdir$ = myns!.getValue("work_dir")
	prdir$ = myns!.getValue("prdata")

rem --- Template for the PRM-02 record being caught

	intemp$="firm_id:c(2),employee_nbr:c(9),title_code:c(2*=10),std_rate:n(7*),std_hrs:n(7*)"
	dim inRec$:intemp$

	inRec$=td!.getWriteBuffer()
	inRec$=field(inRec$)
	
rem --- Open Barista Payroll Pay Rate File

	prm02_dev=unt
	open (prm02_dev,err=trigger_error)prdir$+"prm-02"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("PRM_EMPLPAY",16),dom=trigger_error) *,*,prm_emplpay$
	dim prm_emplpay$:prm_emplpay$
	
rem --- Assign the data

	prm_emplpay.firm_id$ = inRec.firm_id$
	prm_emplpay.employee_no$ = inRec.employee_nbr$
	prm_emplpay.title_code$ = inRec.title_code$
	prm_emplpay.std_rate = inRec.std_rate
	prm_emplpay.std_hrs = inRec.std_hrs

rem --- write the data

	prm_emplpay$=field(prm_emplpay$)
	write record(prm02_dev)prm_emplpay$
	close(prm02_dev)
	
	goto std_exit

trigger_error:

    rd_err_text$="", err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
rem --- Functions

	rem --- fndate$ returns an 8 character year from a 3 character packed date
	def fndate$(q1$)
		if len(cvs(q1$,3))=3
			q2$=str(asc(q1$)-32+1900)+str(asc(q1$(2))-32:"00")+str(mod(asc(q1$(3))-32,100):"00")
		else
			q2$=pad("",8)
		endif
		while 1
			if date(jul(num(q2$(1,4)),num(q2$(5,2)),num(q2$(7,2)),err=*next),err=*next)>"" break
			q2$=fill(8)
			break
		wend
		return q2$
	fnend
	
std_exit: