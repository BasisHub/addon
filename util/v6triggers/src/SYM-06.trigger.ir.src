rem AddonSoftware
rem Copyright BASIS International Ltd.
rem 
rem instead of read
rem This trigger assumes that it is being used in a non-modified Barista and Addon
rem Version 6 environment. If there have been any modifications to these systems,
rem be sure to thoroughly examine this trigger and test changes.

	setopts $080872202c2e$

	seterr trigger_error
	
rem ---  Get the Trigger object from the BBj File System

    td! = BBjAPI().getFileSystem().getTriggerData()

	ky$=td!.getKey()
	
	myns!=BBjAPI().getGlobalNamespace()
	wkdir$ = myns!.getValue("work_dir")
	
rem --- Template for the SYM-06 record being caught

	intemp$="firm_id:c(2*=10),firm_name:c(40*=10),help_enabled:c(1),batch_flag:c(1),reserved:c(14*=10)"
	dim inRec$:intemp$
	
rem --- Open Barista Firms file

	firms_dev=unt
	open (firms_dev,err=trigger_error)wkdir$+"sys/data/adm_firms.dat"
	tpl_dev=unt
	open (tpl_dev,err=trigger_error) wkdir$+"sys/data/ddm_table_tpls.dat"
	read (tpl_dev,key=pad("ADM_FIRMS",16),dom=trigger_error) *,*,firms$
	dim firms$:firms$
	
rem --- Get the data

	read record (firms_dev,key=pad(ky$,2),dom=trigger_error) firms$
	inRec.firm_id$ = firms.company_id$
	inRec.firm_name$ = firms.firm_name$
	inRec.help_enabled$ = firms.help_flag$
	inRec.batch_flag$ = firms.batch_entry$

	td!.setReadBuffer(inRec$)

	goto std_exit

trigger_error:

    rd_err_text$="", err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
	
std_exit: