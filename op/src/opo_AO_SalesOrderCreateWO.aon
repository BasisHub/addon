rem ==========================================================================
rem --- opo_AO_SalesOrderCreateWO.aon 
rem --- AO_SalesOrderCreateWO class (SalesOrderCreateWO superclass)
rem --- OP superclass that creates planned Work Orders for items (BOMs) ordered
rem --- in Order Entry.
rem --- 
rem --- AddonSoftware Version 17.0
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem ==========================================================================

rem /**
rem  * This class creates planned Work Orders for items (BOMs) ordered
rem  * in Order Entry.
rem  */

rem --- Use statements and declares

    use ::ado_func.src::func
    use ::bbtranslator.bbj::BBTranslator
    use java.util.ArrayList
    use java.util.HashMap
    use java.util.TreeMap

    class public AO_SalesOrderCreateWO
        field protected BBjString customerId$
        field protected BBjNumber devAdsMasks = -1
        field protected BBjNumber devBmcOpCodes = -1
        field protected BBjNumber devBmmBillMast = -1
        field protected BBjNumber devIvmItemMast = -1
        field protected BBjNumber devIvmItemWhse = -1
        field protected BBjNumber devIvsParams = -1
        field protected BBjNumber devOpeOrdDet = -1
        field protected BBjNumber devOpsParams = -1
        field protected BBjNumber devSfcWOTypeCd = -1
        field protected BBjNumber devSfeWOMastr = -1
        field protected BBjNumber devSfeWOMatl = -1
        field protected BBjString firmId$
        field protected HashMap isnWOMap! = new HashMap()
        field protected HashMap isnWOMap_initial! = new HashMap()
            rem  isnWOMap! structure
                rem  Key = SO detail line ISN
                rem  Value = woVect! BBjVector
                    rem  woVect! index = 0 (ASKED): Has operater been asked?
                        rem  0 = false, operator has NOT been asked
                        rem  1 = true, operator has been asked
                    rem  woVect! index = 1 (CREATE_WO): Create WO?
                        rem  0 = false, do NOT create WO
                        rem  1 = true, do create WO
                    rem  woVect! index = 2 (SOLINE_NO): SO detail line number (not ISN)
                    rem  woVect! index = 3 (SOLINE_ITEM): SO detail line item
                    rem  woVect! index = 4 (SOLINE_ITEMDESC): SO detail line item description
                    rem  woVect! index = 5 (SOLINE_WHSE): SO detail line warehouse ID
                    rem  woVect! index = 6 (SOLINE_SHIPQTY): SO detail line ship quantity
                    rem  woVect! index = 7 (SOLINE_PREV_SHIPQTY): SO detail line previous ship quantity 
                    rem  woVect! index = 8 (SOLINE_SHIPDATE): SO detail line ship date
                    rem  woVect! index = 9 (WO_NO): WO number when linked WO exists
                    rem  woVect! index = 10 (WO_SCHPRODQTY): WO scheduled production quantity when linked WO exists
                    rem  woVect! index = 11 (WO_ESTCMPDATE): WO estimated completion date when linked WO exists
                    rem  woVect! index = 12 (WO_TYPE): WO Type when linked WO exists
                    rem  woVect! index = 13 (WARNINGS): Warnings
        field public BBjString maskIvU$
        field public BBjString maskSfU$
        field protected BBjString orderNo$
        field protected BBjString tplAdsMasks$
        field protected BBjString tplBmcOpCodes$
        field protected BBjString tplBmmBillMast$
        field protected BBjString tplIvmItemMast$
        field protected BBjString tplIvmItemWhse$
        field protected BBjString tplIvsParams$
        field public BBjString tplOpeOrdDet$
        field protected BBjString tplOpsParams$
        field protected BBjString tplSfcWOTypeCd$
        field protected BBjString tplSfeWOMastr$
        field protected BBjString tplSfeWOMatl$
        field protected BBTranslator Translate!
        field protected BBjString userId$
        field public BBjNumber Warn = 0
        field protected BBjString woCategory$
        field protected BBjString woType$

        rem --- Constants for woVect! indexes
        field public static BBjNumber ASKED = 0
        field public static BBjNumber CREATE_WO = 1
        field public static BBjNumber SOLINE_NO = 2
        field public static BBjNumber SOLINE_ITEM = 3
        field public static BBjNumber SOLINE_ITEMDESC = 4
        field public static BBjNumber SOLINE_WHSE = 5
        field public static BBjNumber SOLINE_SHIPQTY = 6
        field public static BBjNumber SOLINE_PREV_SHIPQTY = 7
        field public static BBjNumber SOLINE_SHIPDATE = 8
        field public static BBjNumber WO_NO = 9
        field public static BBjNumber WO_SCHPRODQTY = 10
        field public static BBjNumber WO_ESTCMPDATE = 11
        field public static BBjNumber WO_TYPE = 12
        field public static BBjNumber WARNINGS = 13

        rem /**
        rem  * Constructor.
        rem  *
        rem  * @param BBjString firmId$
        rem  * @param BBjString customerId$
        rem  * @param BBjString orderNo$
        rem  */
        method public AO_SalesOrderCreateWO(BBjString firmID$, BBjString customerId$, BBjString orderNo$)
            def fnget_dev1(tmp0$)=num(rd_table_chans$[0,0](pos(pad(tmp0$,17)=rd_table_chans$[0,0],20)+17,3))
            seterr AO_SalesOrderCreateWO_error

            rem --- Initialize field variables
            #firmId$ = firmId$
            #customerId$ = customerId$
            #orderNo$ = orderNo$

            rem --- Open needed files          
            num_files=13
            dim open_tables$[1:num_files], open_opts$[1:num_files], open_chans$[1:num_files], open_tpls$[1:num_files]
            open_tables$[1] ="ADS_MASKS",     open_opts$[1] = "OTA[1]"
            open_tables$[2] ="BMC_OPCODES",   open_opts$[2] = "OTA[1]"
            rem open_tables$[3] ="",  open_opts$[3] = ""
            open_tables$[4] ="BMM_BILLMAST",  open_opts$[4] = "OTA[1]"
            open_tables$[5] ="IVM_ITEMMAST",  open_opts$[5] = "OTA[1]"
            open_tables$[6] ="IVM_ITEMWHSE",  open_opts$[6] = "OTA[1]"
            open_tables$[7] ="IVS_PARAMS",    open_opts$[7] = "OTA[1]"
            open_tables$[8] ="OPE_ORDDET",    open_opts$[8] = "OTA[1]"
            open_tables$[9] ="OPS_PARAMS",    open_opts$[9] = "OTA[1]"
            open_tables$[10] ="SFC_WOTYPECD", open_opts$[10] = "OTA[1]"
            rem open_tables$[11] ="",  open_opts$[11] = ""
            open_tables$[12] ="SFE_WOMASTR",  open_opts$[12] = "OTA[1]"
            open_tables$[13] ="SFE_WOMATL",   open_opts$[13] = "OTA[1]"
        
            rem --- Note that rd_table_chans$[all] is NOT being shared/passed.
            call stbl("+DIR_SYP")+"bac_open_tables.bbj",open_beg,open_end,open_tables$[all],open_opts$[all],open_chans$[all],open_tpls$[all],rd_table_chans$[all],open_batch,open_status$

            if open_status$ = "" then
            rem --- Make sure ddm_tables and ddm_table_tpls get closed
            devDdmTables=fnget_dev1("DDM_TABLES")
            devDdmTableTpls=fnget_dev1("DDM_TABLE_TPLS")
            if devDdmTables >= 0 then close(devDdmTables,err=*next)
            if devDdmTableTpls >= 0 then close(devDdmTableTpls,err=*next)

                rem --- Hold on to channels and record templates
                #devAdsMasks = num(open_chans$[1])
                #devBmcOpCodes = num(open_chans$[2])
                #devBmmBillMast = num(open_chans$[4])
                #devIvmItemMast = num(open_chans$[5])
                #devIvmItemWhse = num(open_chans$[6])
                #devIvsParams = num(open_chans$[7])
                #devOpeOrdDet = num(open_chans$[8])
                #devOpsParams = num(open_chans$[9])
                #devSfcWOTypeCd = num(open_chans$[10])
                #devSfeWOMastr = num(open_chans$[12])
                #devSfeWOMatl = num(open_chans$[13])

                #tplAdsMasks$ = open_tpls$[1]
                #tplBmcOpCodes$ = open_tpls$[2]
                #tplBmmBillMast$ = open_tpls$[4]
                #tplIvmItemMast$ = open_tpls$[5]
                #tplIvmItemWhse$ = open_tpls$[6]
                #tplIvsParams$= open_tpls$[7]
                #tplOpeOrdDet$ = open_tpls$[8]
                #tplOpsParams$ = open_tpls$[9]
                #tplSfcWOTypeCd$ = open_tpls$[10]
                #tplSfeWOMastr$ = open_tpls$[12]
                #tplSfeWOMatl$ = open_tpls$[13]
            else
                rem --- Throw error
                err_num=err
                err_num=num(open_status$,err=*next)
                seterr 0
                throw "["+pgm(-2)+"] Constructor failed to open files: "+open_status$,err_num
            endif

            rem --- Get User ID
            userId$ ="?????"
            userId$ = stbl("+USER_ID",err=*next)
            #userId$ = userId$

            rem --- Get needed IV params
            dim ivsParamsRec$:#tplIvsParams$
            readrecord(#devIvsParams,key=#firmId$+"IV00")ivsParamsRec$
            func.setLen1(num(ivsParamsRec.desc_len_01$))
            func.setLen2(num(ivsParamsRec.desc_len_02$))
            func.setLen3(num(ivsParamsRec.desc_len_03$))

            rem --- Get needed OP params
            dim opsParamsRec$:#tplOpsParams$
            readrecord(#devOpsParams,key=#firmId$+"AR00")opsParamsRec$
            #woType$ = opsParamsRec.op_create_wo_typ$
            dim sfcWOTypeCdRec$:#tplSfcWOTypeCd$
            readrecord(#devSfcWOTypeCd,key=#firmId$+"A"+#woType$,dom=*next)sfcWOTypeCdRec$
            #woCategory$ = sfcWOTypeCdRec.wo_category$

            rem --- Get IV Unit mask
            dim adsMasksRec$:#tplAdsMasks$
            readrecord(#devAdsMasks,key=#firmId$+"01007514"+"IV "+"U",dom=*next)adsMasksRec$
            #maskIvU$=cvs(adsMasksRec.dd_attr_msko$,2)

            rem --- Get SF Unit mask
            dim adsMasksRec$:#tplAdsMasks$
            readrecord(#devAdsMasks,key=#firmId$+"01007514"+"SF "+"U",dom=*next)adsMasksRec$
            #maskSfU$=cvs(adsMasksRec.dd_attr_msko$,2)

            rem --- Get Translate! object
            rdTransSpaceKey$=stbl("+PROPS_NAME")+"_"+stbl("+USER_LOCALE")+"_BBTranslator"
            #Translate!=cast(BBTranslator,BBjAPI().getGroupNamespace().getValue(rdTransSpaceKey$,err=*next))
            if #Translate!=null()
                #Translate!=BBTranslator.getInstance(stbl("+PROPS_NAME"),stbl("+USER_LOCALE"),null(),stbl("+PROPS_PATH"))
                BBjAPI().getGroupNamespace().setValue(rdTransSpaceKey$,#Translate!)
            endif

            methodret
            
AO_SalesOrderCreateWO_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Creates a new woVect! BBjVector for the given SO detail line and adds it to the isnWOMap! HashMap
        rem  * if its not already in isnWOMap!. Returns the new or existing woVect!.
        rem  * 
        rem  * @param BBjString rowDataIn$
        rem  * @param BBjString item_description$
        rem  *
        rem  * @return BBjVector woVect!
        rem  */
        method public BBjVector addSODetailLine(BBjString rowDataIn$, BBjString item_description$)
            seterr addSODetailLine_error
            dim rowData$:#tplOpeOrdDet$
            rowData$=rowDataIn$

            rem --- Return existing rowVect! for given SO detail line, or create a new one.
            if #isnWOMap!.containsKey(rowData.internal_seq_no$) then
                rem --- Return existing rowVect! with updated ship quantity and date
                woVect!=#isnWOMap!.get(rowData.internal_seq_no$)
                woVect!.setItem(#SOLINE_SHIPQTY, rowData.qty_shipped)
                woVect!.setItem(#SOLINE_SHIPDATE, rowData.est_shp_date$)
            else
                rem --- Create a new rowVect! for given SO detail line
                woVect! = BBjAPI().makeVector()
                woVect!.addItem(0);                     rem --- index 0: false, operator has NOT been asked 
                woVect!.addItem(0);                     rem --- index 1: false, do NOT create WO
                woVect!.addItem(rowData.line_no$);      rem --- index 2
                woVect!.addItem(rowData.item_id$);      rem --- index 3
                woVect!.addItem(item_description$);     rem --- index 4
                woVect!.addItem(rowData.warehouse_id$); rem --- index 5
                woVect!.addItem(rowData.qty_shipped);   rem --- index 6
                woVect!.addItem(0);                     rem --- index 7
                woVect!.addItem(rowData.est_shp_date$); rem --- index 8
                woVect!.addItem("");                    rem --- index 9
                woVect!.addItem("");                    rem --- index 10
                woVect!.addItem("");                    rem --- index 11
                woVect!.addItem("");                    rem --- index 12
                woVect!.addItem("");                    rem --- index 13

                rem --- Add new woVect! to isnWOMap! HashMap
                #isnWOMap!.put(rowData.internal_seq_no$,woVect!)
            endif

            methodret woVect!
            
addSODetailLine_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Adds a Work Order comment to MEMO_1024 field for the given Work Order.
        rem  * 
        rem  * @param BBjString wo_no$
        rem  * @param BBjString cmnt$
        rem  */
        method public void addWOCmnt(BBjString wo_no$, BBjString cmnt$)
            seterr addWOCmnt_error

            rem --- Add audit info for this comment
            auditInfo$="["+date(0:"%Yd-%Mz-%Dz@%Hz:%mz:%sz")+" "+#userId$+"] "
            cmnt$=auditInfo$+cmnt$

            rem --- Add comment to MEMO_1024 field for this Work Order
            dim sfeWOMastrRec$:#tplSfeWOMastr$
            readrecord(#devSfeWOMastr,key=#firmId$+"  "+wo_no$,knum="PRIMARY",dom=*next)sfeWOMastrRec$
            if cvs(sfeWOMastrRec.wo_no$,2)<>"" then
                spacer$=" "
                if cvs(sfeWOMastrRec.memo_1024$,2)="" then spacer$=""
                sfeWOMastrRec.memo_1024$=sfeWOMastrRec.memo_1024$+spacer$+cmt$
                writerecord(#devSfeWOMastr)sfeWOMasterRec$
            endif

            methodret
            
addWOCmnt_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Updates woVect! SOLINE_SHIPDATE and WARNINGS for the new Estimated Ship Date.
        rem  * Warns if the new Estimated Ship Date for the Sales Order detail line is after the
        rem  * linked Work Orders Estimated Completion Date, and asks operator for approval to
        rem  * continue. 
        rem  * 
        rem  * @param BBjString isn$
        rem  * @param BBjString newEstShpDate$
        rem  * 
        rem  * @return boolean  1=true: ship date change approved
        rem  *                 0=false: ship date change not approved
        rem  */
        method public boolean adjustEstShpDate(BBjString isn$, BBjString newEstShpDate$)
            seterr adjustEstShpDate_error
            true_false=Boolean.valueOf("true")

            if #isnWOMap!.containsKey(isn$) then
                woVect! = #isnWOMap!.get(isn$)
                wo_no$ = woVect!.getItem(#WO_NO)
                estCmpDate$ = woVect!.getItem(#WO_ESTCMPDATE)
                if cvs(wo_no$,2)<>"" then
                    newShpDate$=date(jul(newEstShpDate$,"%Yd%Mz%Dz"):stbl("+DATE_MASK"))

                    if newEstShpDate$ < estCmpDate$ then
                        rem --- Warn the new estimated ship date is before the linked Work Order's estmated completion date. 
                        rem --- Ask if they want to continue with changing the estimated ship date. 
                        cmpDate$=date(jul(estCmpDate$,"%Yd%Mz%Dz"):stbl("+DATE_MASK"))
                        msg_id$="OP_EST_SHIP_DATE_Q"
                        dim msg_tokens$[4]
                        msg_tokens$[1]=newShpDate$
                        msg_tokens$[2]=wo_no$
                        msg_tokens$[3]=cmpDate$
                        msg_tokens$[4]=cvs(woVect!.getItem(#SOLINE_ITEM),2)
                        call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
                        
                        if msg_opt$<>"Y" then true_false=Boolean.valueOf("false")
                    endif

                    rem --- Update warnings and WO Comments for new estimated ship date.
                    if true_false=Boolean.valueOf("true") then
                        rem --- Update late date warning
                        warning$=woVect!.getItem(#WARNINGS)
                        lateDate$=#Translate!.getTranslation("AON_SCH_PROD_DATE_LATE")+". "
                        warningPos=pos(lateDate$=warning$)
                        if warningPos then
                            if newEstShpDate$ >= estCmpDate$ then
                                rem --- Remove existing late date warning
                                warning$=warning$(1,warningPos-1)+warning$(warningPos+len(lateDate$))
                            endif
                        else
                            if newEstShpDate$ < estCmpDate$ then
                                rem --- Add late date warning
                                warning$=warning$+lateDate$
                            endif
                        endif
                        woVect!.setItem(#WARNINGS,warning$)

                        rem --- Update WO Comments
                        dim sfeWOMastrRec$:#tplSfeWOMastr$
                        readrecord(#devSfeWOMastr,key=#firmId$+"  "+wo_no$,knum="PRIMARY",dom=*next)sfeWOMastrRec$
                        if pos(sfeWOMastrRec.wo_status$="OPQ") then
                            oldEstShpDate$ = woVect!.getItem(#SOLINE_SHIPDATE)
                            oldShpDate$=date(jul(oldEstShpDate$,"%Yd%Mz%Dz"):stbl("+DATE_MASK"))
                            cmnt$=#Translate!.getTranslation("AON_LINKED")+" "+#Translate!.getTranslation("AON_SALES")+" "+#Translate!.getTranslation("AON_ORDER")
                            cmnt$=cmnt$+" "+#orderNo$+" "
                            cmnt$=cmnt$+#Translate!.getTranslation("AON_EST")+" "+#Translate!.getTranslation("AON_SHIP_DATE")
                            cmnt$=cmnt$+" "+oldShpDate$+" "
                            cmnt$=cmnt$+#Translate!.getTranslation("AON_CHANGED_TO")
                            cmnt$=cmnt$+" "+newShpDate$
                            #addWOCmnt(wo_no$,cmnt$)
                        endif
                    endif
                endif

                rem --- Use new ship date
                if true_false=Boolean.valueOf("true") then woVect!.setItem(#SOLINE_SHIPDATE, newEstShpDate$)
            endif

            methodret true_false
            
adjustEstShpDate_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Updates woVect! SOLINE_SHIPQTY and WARNINGS for the new Quantity Shipped, and WO_SCHPRODQTY for
        rem  * linked planned Work Orders. Warns if the quantity needed for the Sales Order detail line's new
        rem  * Quantity Shipped is greater than the linked Work Orders Scheduled Production Quantity, and asks
        rem  * operator for approval to continue.
        rem  * 
        rem  * Calculates quantity needed AFTER the inventory committed quantity has been updated for the quantity ordered. 
        rem  * 
        rem  * @param BBjString isn$
        rem  * @param BBjNumber newQtyShipped
        rem  * 
        rem  * @return boolean  1=true: ship quantity change approved
        rem  *                 0=false: ship quantity not approved
        rem  */
        method public boolean adjustQtyShipped(BBjString isn$, BBjNumber newQtyShipped)
            seterr adjustQtyShipped_2_error

            methodret #adjustQtyShipped(isn$, newQtyShipped, 1)
            
adjustQtyShipped_2_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Updates woVect! SOLINE_SHIPQTY and WARNINGS for the new Quantity Shipped, and WO_SCHPRODQTY for
        rem  * linked planned Work Orders. Warns if the quantity needed for the Sales Order detail line's new
        rem  * Quantity Shipped is greater than the linked Work Orders Scheduled Production Quantity, and asks
        rem  * operator for approval to continue.
        rem  * 
        rem  * Can calculate quantity needed either BEFORE (ivCommitted=0) or AFTER (ivCommitted=1) the inventory committed
        rem  * quantity has been updated for the quantity ordered. 
        rem  * 
        rem  * @param BBjString isn$
        rem  * @param BBjNumber newQtyShipped
        rem  * @param BBjNumber ivCommitted
        rem  * 
        rem  * @return boolean  1=true: ship quantity change approved
        rem  *                 0=false: ship quantity not approved
        rem  */
        method public boolean adjustQtyShipped(BBjString isn$, BBjNumber newQtyShipped, BBjNumber ivCommitted)
            seterr adjustQtyShipped_1_error
            true_false=Boolean.valueOf("true")

            if #isnWOMap!.containsKey(isn$) then
                woVect! = #isnWOMap!.get(isn$)
                wo_no$ = woVect!.getItem(#WO_NO)
                if cvs(wo_no$,2)<>"" then

                    rem --- Calculate the quantity needed to fill the Sales Order detail line shipped quantity
                    prevQtyShipped=woVect!.getItem(#SOLINE_SHIPQTY)
                    woVect!.setItem(#SOLINE_PREV_SHIPQTY,prevQtyShipped)
                    qtyNeeded=#calculateQtyNeeded(woVect!,newQtyShipped,prevQtyShipped,isn$,ivCommitted)

                    rem --- Warn if the quantity needed for the new ship quantity is greater than linked Work Order's scheduled production quantity. 
                    rem --- And, ask if they want to continue with changing the ship quantity. 
                    schProdQty=woVect!.getItem(#WO_SCHPRODQTY)
                    if qtyNeeded > schProdQty then
                        rem --- Check for existing material requirements for this WO
                        sfeWomastrKey$=#firmId$+"  "+wo_no$
                        read(#devSfeWOMatl,key=sfeWomastrKey$,dom=*next)
                        sfeWomatlKey$=key(#devSfeWOMatl,end=*next)
                        if pos(sfeWomastrKey$=sfeWomatlKey$)=1 then
                            rem --- Material requirements alreay exist
                            msg_id$="OP_MAT_REQS_EXIST"
                            dim msg_tokens$[4]
                            msg_tokens$[1]=cvs(str(qtyNeeded:#maskIvU$),3)
                            msg_tokens$[2]=cvs(str(newQtyShipped:#maskIvU$),3)
                            msg_tokens$[3]=wo_no$
                            msg_tokens$[4]=cvs(str(schProdQty:#maskSfU$),3)
                            call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
                            
                            true_false=Boolean.valueOf("false")
                        else
                            rem --- No material requirements yet
                            msg_id$="OP_SHIP_QTY_Q"
                            dim msg_tokens$[6]
                            msg_tokens$[1]=cvs(str(qtyNeeded:#maskIvU$),3)
                            msg_tokens$[2]=cvs(str(newQtyShipped:#maskIvU$),3)
                            msg_tokens$[3]=wo_no$
                            msg_tokens$[4]=cvs(str(schProdQty:#maskSfU$),3)
                            msg_tokens$[5]=cvs(str(prevQtyShipped:#maskIvU$),3)
                            msg_tokens$[6]=cvs(woVect!.getItem(#SOLINE_ITEM),2)
                            call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
                            
                            if msg_opt$<>"Y" then true_false=Boolean.valueOf("false")
                        endif
                    endif
                    
                    rem --- Update warnings and WO Comments for new ship quantity.
                    if true_false=Boolean.valueOf("true") then
                        rem --- Update quantity short warning
                        warning$=woVect!.getItem(#WARNINGS)
                        shortQty$=#Translate!.getTranslation("AON_SCH_PROD_QTY_SHORT")+". "
                        warningPos=pos(shortQty$=warning$)
                        if warningPos then
                            if qtyNeeded <= schProdQty then
                                rem --- Remove existing quantity short warning
                                warning$=warning$(1,warningPos-1)+warning$(warningPos+len(shortQty$))
                            endif
                        else
                            if qtyNeeded > schProdQty then
                                rem --- Add quantity short warning
                                warning$=shortQty$+warning$
                            endif
                        endif
                        woVect!.setItem(#WARNINGS,warning$)

                        rem --- Update WO Comments
                        dim sfeWOMastrRec$:#tplSfeWOMastr$
                        extractrecord(#devSfeWOMastr,key=#firmId$+"  "+wo_no$,knum="PRIMARY",dom=*next)sfeWOMastrRec$
                        if sfeWOMastrRec.wo_status$="P" then
                            sfeWOMastrRec.sch_prod_qty = #calculateSchProdQty(woVect!, newQtyShipped, isn$, ivCommitted)
                            writerecord(#devSfeWOMastr)sfeWOMastrRec$
                            woVect!.setItem(#WO_SCHPRODQTY,sfeWOMastrRec.sch_prod_qty)
                        endif
                        if pos(sfeWOMastrRec.wo_status$="OQ") then
                            cmnt$=#Translate!.getTranslation("AON_LINKED")+" "+#Translate!.getTranslation("AON_SALES")+" "+#Translate!.getTranslation("AON_ORDER")
                            cmnt$=cmnt$+" "+#orderNo$+" "
                            cmnt$=cmnt$+#Translate!.getTranslation("AON_SHIPPED")+" "+#Translate!.getTranslation("AON_QUANTITY")
                            cmnt$=cmnt$+" "+cvs(str(prevQtyShipped:#maskIvU$),3)+" "
                            cmnt$=cmnt$+#Translate!.getTranslation("AON_CHANGED_TO")
                            cmnt$=cmnt$+" "+cvs(str(newQtyShipped:#maskIvU$),3)
                            #addWOCmnt(wo_no$,cmnt$)
                        endif
                        read(#devSfeWOMastr,end=*next)
                    endif
                endif

                rem --- Use new ship quantity
                if true_false=Boolean.valueOf("true") then woVect!.setItem(#SOLINE_SHIPQTY,newQtyShipped)
            endif

            methodret true_false
            
adjustQtyShipped_1_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Calculates the Work Order estimated completion date for the woVect! BBjVector from
        rem  * isnWOMap! HashMap. Issues warning and adds a Work Order comment (SFE_WOCOMNT record) if the Sales Order 
        rem  * detail line estimated ship date is BEFORE the estimated completion date.
        rem  * 
        rem  * @param BBjVector woVect!
        rem  * @param BBjString estShpDate$
        rem  * 
        rem  * @return BBjString estCmpDate$
        rem  */
        method protected BBjString calculateEstCmpDate(BBjVector woVect!, BBjString estShpDate$)
            seterr calculateEstCmpDate_error

            estCmpDate$=""
            dim ivmItemWhseRec$:#tplIvmItemWhse$
            itemId$=woVect!.getItem(#SOLINE_ITEM)
            whsId$=woVect!.getItem(#SOLINE_WHSE)
            readrecord(#devIvmItemWhse,key=#firmId$+itemId$+whsId$,dom=*next)ivmItemWhseRec$
            call "adc_daydates.aon",stbl("+SYSTEM_DATE"),estCmpDate$,ivmItemWhseRec.lead_time
            if estCmpDate$<>"" and  estShpDate$<estCmpDate$
                shpDate$=date(jul(estShpDate$,"%Yd%Mz%Dz"):stbl("+DATE_MASK"))
                cmpDate$=date(jul(estCmpDate$,"%Yd%Mz%Dz"):stbl("+DATE_MASK"))
                wo_no$=woVect!.getItem(#WO_NO)
                cmnt$=#Translate!.getTranslation("AON_LINKED")+" "+#Translate!.getTranslation("AON_SALES")+" "+#Translate!.getTranslation("AON_ORDER")
                cmnt$=cmnt$+" "+#orderNo$+" "
                cmnt$=cmnt$+#Translate!.getTranslation("AON_EST")+" "+#Translate!.getTranslation("AON_SHIP_DATE")
                cmnt$=cmnt$+" "+shpDate$+" "
                cmnt$=cmnt$+#Translate!.getTranslation("AON_IS_BEFORE")+" "+#Translate!.getTranslation("AON_WORK_ORDER")+" "+#Translate!.getTranslation("AON_EST")
                cmnt$=cmnt$+" "+#Translate!.getTranslation("AON_COMPLETION")+" "+#Translate!.getTranslation("AON_DATE")
                cmnt$=cmnt$+" "+cmpDate$
                #addWOCmnt(wo_no$,cmnt$)
                
                #Warn=1
                msg_id$="OP_WO_CMPDATE_LATE"
                dim msg_tokens$[4]
                msg_tokens$[1]=wo_no$
                msg_tokens$[2]=cmpDate$
                msg_tokens$[3]=shpDate$
                msg_tokens$[4]=cvs(itemId$,2)
                call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
            endif

            methodret estCmpDate$
            
calculateEstCmpDate_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Calculates the quantity needed to fill the given ship quantity for the given woVect! BBjVector from
        rem  * isnWOMap! HashMap.
        rem  * 
        rem  * @param BBjVector woVect!
        rem  * @param BBjNumber qtyShipped
        rem  * @param BBjNumber prevQtyShipped
        rem  * @param BBjString isn$
        rem  * @param BBjNumber ivCommitted
        rem  * 
        rem  * @return BBjNumber qtyNeeded
        rem  */
        method public BBjNumber calculateQtyNeeded(BBjVector woVect!, BBjNumber qtyShipped, BBjNumber prevQtyShipped, BBjString isn$, BBjNumber ivCommitted)
            seterr calculateQtyNeeded_error
            qtyNeeded=0

            rem --- Verify woVect! has a linked WO
            if woVect!<>null() then
                rem --- Calculate the quantity needed to fill the Sales Order detail line ship quantity
                    whsId$=woVect!.getItem(#SOLINE_WHSE)
                    itemId$=woVect!.getItem(#SOLINE_ITEM)
                    dim ivmItemWhseRec$:#tplIvmItemWhse$
                    readrecord(#devIvmItemWhse,key=#firmId$+whsId$+itemId$,dom=*next)ivmItemWhseRec$
                    qtyOnHand=ivmItemWhseRec.qty_on_hand
                    qtyOnOrder=ivmItemWhseRec.qty_on_order
                
                rem --- Get sch prod qty from planned WOs
                plannedQty=0
                dim sfeWOMastrRec$:#tplSfeWOMastr$
                read(#devSfeWOMastr,key=#firmId$+whsId$+itemId$,knum="AO_WH_ITM_LOC_WO",dom=*next)
                while 1
                    sfeWOMastr_key$=key(#devSfeWOMastr,end=*break)
                    if pos(#firmId$+whsId$+itemId$=sfeWOMastr_key$)<>1 then break
                    readrecord(#devSfeWOMastr)sfeWOMastrRec$
                    if sfeWOMastrRec.wo_status$<>"P" then continue
                    rem --- Don't count WOs not linked to a Sals Order because quantity has not been committed in inventory.
                    if cvs(sfeWOMastrRec.customer_id$+sfeWOMastrRec.order_no$,2)="" and num(sfeWOMastrRec.sls_ord_seq_ref$)=0 then continue
                    plannedQty=plannedQty+sfeWOMastrRec.sch_prod_qty
                wend

                rem --- Back out planned or on order qty for this WO
                wo_no$ = woVect!.getItem(#WO_NO)
                if cvs(wo_no$,2)<>"" then
                    dim sfeWOMastrRec$:#tplSfeWOMastr$
                    readrecord(#devSfeWOMastr,key=#firmId$+"  "+wo_no$,knum="PRIMARY",dom=*next)sfeWOMastrRec$
                    backoutQty=sfeWOMastrRec.sch_prod_qty
                    if sfeWOMastrRec.wo_status$="O" then
                        rem --- Open (released) WO
                        qtyOnOrder=qtyOnOrder-backoutQty
                    endif
                    if sfeWOMastrRec.wo_status$="P" then
                        rem --- Planned WO
                        plannedQty=plannedQty-backoutQty
                    endif
                endif
                    
                rem --- Total qty on hand + qty on order + planned sch prod qty
                    wipQty=qtyOnHand+qtyOnOrder+plannedQty

                    rem --- Inventory committed quantity may already include this ship quantity
                dim rowData$:#tplOpeOrdDet$
                rowData$=#getSODetailRow(isn$)
                if ivCommitted and rowData.commit_flag$="Y" then
                    rem --- Ship quantity is already included in ivmItemWhseRec.qty_commit
                        committedQty=ivmItemWhseRec.qty_commit+(prevQtyShipped-qtyShipped)
                    else
                        committedQty=ivmItemWhseRec.qty_commit-prevQtyShipped
                    endif
                
                rem --- Qty available is WIP qty minus committed qty
                availableQty=wipQty-committedQty
                
                rem --- Just want qty needed for current ship qty, so ignore existing shortage. 
                    if availableQty<0 then availableQty=0
                
                rem --- Quantity needed is the ship quantiy minus the available qty (can't be negative).
                qtyNeeded=qtyShipped-availableQty
                if qtyNeeded<0 then qtyNeeded=0
            endif

            methodret qtyNeeded
            
calculateQtyNeeded_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Calculates the Work Order scheduled production quantity for the woVect! BBjVector from
        rem  * isnWOMap! HashMap. Uses the largest between the item's Bill Of Materials standard lot size
        rem  * and the quantity needed to fill the Sales Order detail line shipped quantity.
        rem  * 
        rem  * Calculates quantity needed AFTER the inventory committed quantity has been updated for the quantity ordered. 
        rem  * 
        rem  * @param BBjVector woVect!
        rem  * @param BBjNumber qtyShipped
        rem  * @param BBjString isn$
        rem  * 
        rem  * @return BBjNumber schProdQty
        rem  */
        method protected BBjNumber calculateSchProdQty(BBjVector woVect!, BBjNumber qtyShipped, BBjString isn$)
            seterr calculateSchProdQty_2_error

            methodret #calculateSchProdQty(woVect!, qtyShipped, isn$, 1)
            
calculateSchProdQty_2_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Calculates the Work Order scheduled production quantity for the woVect! BBjVector from
        rem  * isnWOMap! HashMap. Uses the largest between the item's Bill Of Materials standard lot size
        rem  * and the quantity needed to fill the Sales Order detail line shipped quantity.
        rem  * 
        rem  * Can calculate quantity needed either BEFORE (ivCommitted=0) or AFTER (ivCommitted=1) the inventory committed
        rem  * quantity has been updated for the quantity ordered. 
        rem  * 
        rem  * @param BBjVector woVect!
        rem  * @param BBjNumber qtyShipped
        rem  * @param BBjString isn$
        rem  * @param BBjNumber ivCommitted
        rem  * 
        rem  * @return BBjNumber schProdQty
        rem  */
        method protected BBjNumber calculateSchProdQty(BBjVector woVect!, BBjNumber qtyShipped, BBjString isn$, BBjNumber ivCommitted)
            seterr calculateSchProdQty_1_error
            schProdQty=0

            rem --- Get item's Bill Of Materials standard lot size
            itemId$=woVect!.getItem(#SOLINE_ITEM)
            dim bmmBillMastRec$:#tplBmmBillMast$
            readrecord(#devBmmBillMast,key=#firmId$+itemId$,dom=*next)bmmBillMastRec$
            stdLotSize=bmmBillMastRec.std_lot_size

            rem --- Calculate the quantity needed to fill the Sales Order detail line shipped quantity
            prevQtyShipped=woVect!.getItem(#SOLINE_PREV_SHIPQTY)
            qtyNeeded=#calculateQtyNeeded(woVect!,qtyShipped,prevQtyShipped,isn$,ivCommitted)

            rem --- Return the largest quantity
            if qtyNeeded>stdLotSize then
                schProdQty=qtyNeeded
            else
                schProdQty=stdLotSize
            endif

            methodret schProdQty
            
calculateSchProdQty_1_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Determines whether or not the given SO detail line is a valid candidate for creating a Work Order.
        rem  * Returns the item description for valid candidates. Otherwise returns blank (empty) string.
        rem  * Note: Do NOT use this method if the SO is a Quote.
        rem  * Note: Do NOT use this method if the SO is on Credit Hold.
        rem  * 
        rem  * Calculates quantity needed AFTER the inventory committed quantity has been updated for the quantity ordered. 
        rem  * 
        rem  * @param BBjString rowDataIn$
        rem  *
        rem  * @return BBjString itemDescription$
        rem  */
        method public BBjString canCreateWO(BBjString rowDataIn$)
            seterr canCreateWO_2_error

            methodret #canCreateWO(rowDataIn$, 1)
            
canCreateWO_2_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Determines whether or not the given SO detail line is a valid candidate for creating a Work Order.
        rem  * Returns the item description for valid candidates. Otherwise returns blank (empty) string.
        rem  * Note: Do NOT use this method if the SO is a Quote.
        rem  * Note: Do NOT use this method if the SO is on Credit Hold.
        rem  * 
        rem  * Can calculate quantity needed either BEFORE (ivCommitted=0) or AFTER (ivCommitted=1) the inventory committed
        rem  * quantity has been updated for the quantity ordered. 
        rem  * 
        rem  * @param BBjString rowDataIn$
        rem  * @param BBjNumber ivCommitted
        rem  *
        rem  * @return BBjString itemDescription$
        rem  */
        method public BBjString canCreateWO(BBjString rowDataIn$, BBjNumber ivCommitted)
            seterr canCreateWO_1_error
            dim rowData$:#tplOpeOrdDet$
            rowData$=rowDataIn$
            valid=1

            rem --- Sales Order detail line shipped quantity must be positive.
            if rowData.qty_shipped<0 then valid=0 

            rem --- Sales Order detail line item must be a Bill of Materials, and NOT a Phantom Bill
            if valid then
                dim bmmBillMastRec$:#tplBmmBillMast$
                readrecord(#devBmmBillMast,key=#firmId$+rowData.item_id$,dom=*next)bmmBillMastRec$
                if bmmBillMastRec.firm_id$+bmmBillMastRec.bill_no$<>#firmId$+rowData.item_id$ then valid=0
                if bmmBillMastRec.phantom_bill$="Y" then valid=0
            endif

            rem --- Sales Oder detail line item must be an active item, and must NOT be a superseded item.
            if valid then
                dim ivmItemMastRec$:#tplIvmItemMast$
                readrecord( #devIvmItemMast,key=#firmId$+rowData.item_id$,dom=*next)ivmItemMastRec$
                if ivmItemMastRec.firm_id$+ivmItemMastRec.item_id$<>#firmId$+rowData.item_id$ then valid=0
                if ivmItemMastRec.item_inactive$="Y" then valid=0
                if ivmItemMastRec.alt_sup_flag$="S" then valid=0
            endif
            
            rem --- Check ship quantity against quantity available + on order + planned
            if valid then
                dim sfeWOMastrRec$:#tplSfeWOMastr$
                read(#devSfeWOMastr,key=#firmId$+#customerId$+#orderNo$+rowData.internal_seq_no$,knum="AO_CST_ORD_LINE",dom=*next)
                readrecord(#devSfeWOMastr,end=*next)sfeWOMastrRec$
                if sfeWOMastrRec.firm_id$+sfeWOMastrRec.customer_id$+sfeWOMastrRec.order_no$+sfeWOMastrRec.sls_ord_seq_ref$ <> 
:               #firmId$+#customerId$+#orderNo$+rowData.internal_seq_no$ then
                    rem --- Sales Order detail line shipped quantity must be positive and greater than the quantity available + on order + planned.
                    rem --- Need a temporary woVect! to calculated qty needed for this detail record
                    tempWoVect!=#addSODetailLine(rowData$, "temporary item description")
                    rem --- Remove temporary woVect! from isnWOMap! HashMap
                    #isnWOMap!.remove(rowData.internal_seq_no$)
                    qtyNeeded=#calculateQtyNeeded(tempWoVect!,rowData.qty_shipped,0,rowData.internal_seq_no$,ivCommitted)

                    if qtyNeeded <= 0 then valid=0
                endif
            endif

            if valid then
                rem --- Valid candidate for creating a WO, return formatted item description.
                itemDescription$=func.displayDesc(ivmItemMastRec.item_desc$)
            else
                rem --- NOT a valid candidate for creating a WO, return a blank/empty BBjString
                itemDescription$=""
            endif

            methodret itemDescription$
            
canCreateWO_1_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Creates planned Work Order (SFE_WOMASTR record) for given Sale Order detail grid row
        rem  * internal sequence number (isn) and the correponging woVect! BBjVector from isnWOMap! HashMap.
        rem  * 
        rem  * @param BBjString isn$
        rem  * @param BBjVector woVect!
        rem  */
        method protected void createWO(BBjString isn$, BBjVector woVect!)
            seterr createWO_error

            rem --- Get next WO number
            call stbl("+DIR_SYP")+"bas_sequences.bbj","WO_NO",next_wo_no$,rd_table_chans$[all],"QUIET"

            rem --- Get Bill comments
            comments$=""
            dim bmmBillMastRec$:#tplBmmBillMast$
            read(#devBmmBillMast,key=#firmId$+woVect!.getItem(#SOLINE_ITEM),dom=*next)bmmBillMastRec$
            if bmmBillMastRec.firm_id$+bmmBillMastRec.bill_no$=#firmId$+woVect!.getItem(#SOLINE_ITEM) then
                comments$=comments$+cvs(bmmBillMastRec.memo_1024$,2)
            endif

            rem --- Get the item's IVM_ITEMMAST record
            dim ivmItemMastRec$:#tplIvmItemMast$
            readrecord(#devIvmItemMast,key=#firmId$+woVect!.getItem(#SOLINE_ITEM),dom=*next)ivmItemMastRec$

            rem --- Get the item's BMM_BILLMAST record
            dim bmmBillMastRec$:#tplBmmBillMast$
            readrecord(#devBmmBillMast,key= #firmId$+woVect!.getItem(#SOLINE_ITEM),dom=*next)bmmBillMastRec$

            rem --- Initialize and write new SFE_WOMASTR record for planned Work Order
            dim sfeWOMastrRec$:#tplSfeWOMastr$
            sfeWOMastrRec.firm_id$=#firmId$
            sfeWOMastrRec.wo_location$=""
            sfeWOMastrRec.wo_no$=next_wo_no$
            woVect!.setItem(#WO_NO,sfeWOMastrRec.wo_no$)
            sfeWOMastrRec.wo_type$=#woType$
            woVect!.setItem(#WO_TYPE,sfeWOMastrRec.wo_type$)
            sfeWOMastrRec.wo_category$=#woCategory$
            sfeWOMastrRec.wo_status$ ="P"
            sfeWOMastrRec.customer_id$=#customerId$
            sfeWOMastrRec.order_no$=#orderNo$
            sfeWOMastrRec.sls_ord_seq_ref$=isn$
            sfeWOMastrRec.unit_measure$=bmmBillMastRec.unit_measure$
            sfeWOMastrRec.bill_rev$=bmmBillMastRec.bill_rev$
            sfeWOMastrRec.warehouse_id$=woVect!.getItem(#SOLINE_WHSE)
            sfeWOMastrRec.item_id$=woVect!.getItem(#SOLINE_ITEM)
            sfeWOMastrRec.opened_date$=date(0:"%Yd%Mz%Dz")
            sfeWOMastrRec.eststt_date$=date(0:"%Yd%Mz%Dz")
            sfeWOMastrRec.estcmp_date$=#calculateEstCmpDate(woVect!,cast(BBjString,woVect!.getItem(#SOLINE_SHIPDATE)))
            woVect!.setItem(#WO_ESTCMPDATE,sfeWOMastrRec.estcmp_date$)
            sfeWOMastrRec.act_st_date$=""
            sfeWOMastrRec.lstact_date$=""
            sfeWOMastrRec.closed_date$=""
            sfeWOMastrRec.description_01$=""
            sfeWOMastrRec.description_02$=""
            sfeWOMastrRec.drawing_no$=bmmBillMastRec.drawing_no$
            sfeWOMastrRec.drawing_rev$=bmmBillMastRec.drawing_rev$
            sfeWOMastrRec.complete_flg$=""
            sfeWOMastrRec.recalc_flag$=""
            if ivmItemMastRec.lotser_item$="Y" and ivmItemMastRec.inventoried$="Y" then
                sfeWOMastrRec.lotser_item$="Y"
            else
                sfeWOMastrRec.lotser_item$=""
            endif
            sfeWOMastrRec.priority$="5"
            sfeWOMastrRec.sched_flag$="M"
            sfeWOMastrRec.forecast$=""
            sfeWOMastrRec.cls_inp_date$=""
            sfeWOMastrRec.lock_ref_num$="N"
            sfeWOMastrRec.sch_prod_qty=#calculateSchProdQty(woVect!,cast(BBjNumber,woVect!.getItem(#SOLINE_SHIPQTY)),isn$)
            woVect!.setItem(#WO_SCHPRODQTY,sfeWOMastrRec.sch_prod_qty)
            sfeWOMastrRec.qty_cls_todt=0
            sfeWOMastrRec.cls_cst_todt=0
            sfeWOMastrRec.cls_inp_qty=0
            sfeWOMastrRec.closed_cost=0
            sfeWOMastrRec.est_yield=bmmBillMastRec.est_yield
            sfeWOMastrRec.memo_1024$=comments$
            writerecord(#devSfeWOMastr)sfeWOMastrRec$

            methodret
            
createWO_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Creates Work Orders (SFE_WOMASTR records) for woVect! BBjVectors in isnWOMap! HashMap
        rem  * that have CREATE_WO element set true, or unlinks existing Work Orders CREATE_WO is set false.
        rem  * 
        rem  */
        method public void createWOs()
            seterr createWOs_error

            if #isnWOMap!.size() then
                isnKeys!=#isnWOMap!.keySet()
                iter!=isnKeys!.iterator()
                while iter!.hasNext()
                    isn$=iter!.next()
                    woVect!=#isnWOMap!.get(isn$)
                    wo_no$=woVect!.getItem(#WO_NO)
                    if woVect!.getItem(#CREATE_WO) then
                        rem --- Create planned WO if one doesn't exist already
                        if cvs(wo_no$,2)="" then #createWO(isn$, woVect!)
                    else
                        rem --- Unlink existing WO
                        if cvs(wo_no$,2)<>"" then #removeLinkedWO(isn$)
                    endif
                wend
            endif

            methodret
            
createWOs_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Returns the Sales Order detail record (OPT_INVDET) for the given internal sequence number (isn),
        rem  * and the currrent customer and order.
        rem  * 
        rem  * @param BBjString isn$
        rem  * 
        rem  * @return BBjString rowData$
        rem  */
        method public BBjString getSODetailRow(BBjString isn$)
            seterr getSODetailRow_error
            dim rowData$:#tplOpeOrdDet$

            thisKey$=#firmId$+rowData.ar_type$+#customerId$+#orderNo$+rowData.ar_inv_no$+isn$
            readrecord(#devOpeOrdDet,key=thisKey$,dom=*next)rowData$
            
            methodret rowData$
            
getSODetailRow_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Returns an ArrayList of woVect! BBjVectors in the isnWOMap! HashMap sorted by the Sales Order detail
        rem  * grid line number instead of the grid row's internal sequence number (isn).
        rem  * 
        rem  * @return ArrayList woList!
        rem  */
        method public ArrayList getWOList()
            seterr getWOList_error
            woList!=new java.util.ArrayList()

            rem --- Sort by line_no instead of isn
            lineNoTree!=new java.util.TreeMap()
            isnKeys!=#isnWOMap!.keySet()
            iter!=isnKeys!.iterator()
            while iter!.hasNext()
                isn$=iter!.next()
                woVect!=#isnWOMap!.get(isn$)
                lineNoTree!.put(woVect!.getItem(#SOLINE_NO),woVect!)
            wend
            
            rem --- Put sorted woVect! in an ArrayList
            lineNoKeys!=lineNoTree!.keySet()
            iter!=lineNoKeys!.iterator()
            while iter!.hasNext()
                lineNo$=iter!.next()
                woVect!=lineNoTree!.get(lineNo$)
                woList!.add(woVect!)
            wend
            
            methodret woList!
            
getWOList_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Gets the woVect! BBjVector from isnWOMap! HashMap for the given Sale Order detail line internal
        rem  * sequence number (isn). Return null() if isnWOMap! does not contain the isn key.
        rem  * 
        rem  * @param BBjString isn$
        rem  * 
        rem  * @return BBjVector woVect!
        rem  */
        method public BBjVector getWOVect(BBjString isn$)
            seterr getWOVect_error
            woVect! = null()

            if #isnWOMap!.containsKey(isn$) then woVect! = #isnWOMap!.get(isn$)

            methodret woVect!
            
getWOVect_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Initializes isnWOMap! HashMap with Sale Order detail lines that are candidates for creating Work Orders, and
        rem  * includes information on Work Orders that have already been created, i.e. linked to one of the Sales Order detail
        rem  * lines.
        rem  * Note: Do NOT use this method if the SO is a Quote.
        rem  * Note: Do NOT use this method if the SO is on Credit Hold.
        rem  * 
        rem  * @param BBjVector gridRowVect!
        rem  */
        method public void initIsnWOMap(BBjVector gridRowVect!)
            seterr initIsnWOMap_error
            isnWOMap! = new HashMap()

            dim rowData$:#tplOpeOrdDet$
            iter!=gridRowVect!.iterator()
            while iter!.hasNext()
                rowData$=iter!.next()
                itemDescription$=#canCreateWO(rowData$)
                if itemDescription$="" then continue
                woVect! = #addSODetailLine(rowData$, itemDescription$)

                rem --- Add Work Order info to woVect! if there are existing linked WO
                dim sfeWOMastrRec$:#tplSfeWOMastr$
                read(#devSfeWOMastr,key=#firmId$+#customerId$+#orderNo$+rowData.internal_seq_no$,knum="AO_CST_ORD_LINE",dom=*next)
                readrecord(#devSfeWOMastr,end=*next)sfeWOMastrRec$
                if sfeWOMastrRec.firm_id$+sfeWOMastrRec.customer_id$+sfeWOMastrRec.order_no$+sfeWOMastrRec.sls_ord_seq_ref$ = 
:               #firmId$+#customerId$+#orderNo$+rowData.internal_seq_no$ then
                    woVect!.setItem(#CREATE_WO,1); rem --- true, do create WO
                    woVect!.setItem(#WO_NO,sfeWOMastrRec.wo_no$)
                    woVect!.setItem(#WO_SCHPRODQTY,sfeWOMastrRec.sch_prod_qty)
                    woVect!.setItem(#WO_ESTCMPDATE,sfeWOMastrRec.estcmp_date$)
                    woVect!.setItem(#WO_TYPE,sfeWOMastrRec.wo_type$)

                    rem --- Calculate the quantity needed to fill the Sales Order detail line shipped quantity
                    qtyShipped=woVect!.getItem(#SOLINE_SHIPQTY)
                    prevQtyShipped=woVect!.getItem(#SOLINE_PREV_SHIPQTY)
                    qtyNeeded=#calculateQtyNeeded(woVect!,qtyShipped,prevQtyShipped,rowData.internal_seq_no$,1)

                    rem --- Warn if sch_prod_qty isnt enough
                    warning$=""
                    if sfeWOMastrRec.sch_prod_qty<qtyNeeded then
                        warning$=#Translate!.getTranslation("AON_SCH_PROD_QTY_SHORT")+". "
                    endif 
                    rem --- Warn if estcmp_date$ isnt soon enough
                    if sfeWOMastrRec.estcmp_date$>woVect!.getItem(#SOLINE_SHIPDATE) then
                        warning$=warning$+#Translate!.getTranslation("AON_SCH_PROD_DATE_LATE")+". "
                    endif
                    woVect!.setItem(#WARNINGS,warning$)
                endif
            wend

            rem --- Save copy of initialized isnWOMap! so we can tell later if it's been changed
            isnWOMap_initial! = new HashMap()
            keys!=#isnWOMap!.keySet()
            iter!=keys!.iterator()
            while iter!.hasNext()
                isn$=iter!.next()
                woVect!=#isnWOMap!.get(isn$)
                tmpVect!=BBjAPI().makeVector()
                for i=0 to woVect!.size()-1
                    tmpVect!.addItem(woVect!.getItem(i))
                next i
                #isnWOMap_initial!.put(isn$,tmpVect!)
            wend

            methodret
            
initIsnWOMap_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Reports whether or not the isnWOMap! HashMap has changed since it was intialized in the
        rem  * initIsnWOMap() method.
        rem  * 
        rem  * @return boolean  1=true: #isnWOMap! has changed
        rem  *                 0=false: #isnWOMap! has NOT changed
        rem  */
        method public boolean isChanged()
            seterr isChanged_error

            methodret !#isnWOMap!.equals(#isnWOMap_initial!)
            
isChanged_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Removes existing planned (or quote) Work Order, or link to existing open (released) Work Order,
        rem  * for the given Sales Order detail line internal sequence number (isn).
        rem  * 
        rem  * @param BBjString isn$
        rem  */
        method protected void removeLinkedWO(BBjString isn$)
            seterr removeLinkedWO_error

            woVect!=#isnWOMap!.get(isn$)
            wo_no$=woVect!.getItem(#WO_NO)
            dim sfeWOMastrRec$:#tplSfeWOMastr$
            extractrecord(#devSfeWOMastr,key=#firmId$+"  "+wo_no$,knum="PRIMARY",dom=*next)sfeWOMastrRec$
            if sfeWOMastrRec.wo_status$="P" then
                rem --- Remove existing Planned WO
                remove(#devSfeWOMastr,key=#firmId$+"  "+wo_no$,err=*next)
            endif
            if pos(sfeWOMastrRec.wo_status$="OQ") then
                rem --- Unlink existing Open (Released) WO
                cmnt$=#Translate!.getTranslation("AON_REMOVED")+" "+#Translate!.getTranslation("AON_SALES")+" "+#Translate!.getTranslation("AON_LINK")
                cmnt$=cmnt$+" "+sfeWOMastrRec.customer_id$+":"+sfeWOMastrRec.order_no$+":"+sfeWOMastrRec.sls_ord_seq_ref$+" "
                cmnt$=cmnt$+#Translate!.getTranslation("AON_FOR_")+#Translate!.getTranslation("AON_ITEM")
                cmnt$=cmnt$+" "+sfeWOMastrRec.item_id$
                #addWOCmnt(wo_no$,cmnt$)
                sfeWOMastrRec.customer_id$=""
                sfeWOMastrRec.order_no$=""
                sfeWOMastrRec.sls_ord_seq_ref$=""
                writerecord(#devSfeWOMastr,err=*next)sfeWOMastrRec$
            endif
            if sfeWOMastrRec.wo_status$="C" then
                rem --- Make sure the extracted record is released
                read(#devSfeWOMastr,err=*next)
            endif

            methodret
            
removeLinkedWO_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Updates CREATE_WO in all woVect! in isnWOMap! to either true (create WO) or false (do NOT create WO)
        rem  * based on the input value (1=true, 0=false).
        rem  * 
        rem  * @param BBjNumber true_false
        rem  */
        method public void setCreateWO(BBjNumber true_false)
            seterr setCreateWO_error

            mapKeys!=#isnWOMap!.keySet()
            iter!=mapKeys!.iterator()
            while iter!.hasNext()
                isn$=iter!.next()
                woVect! = #isnWOMap!.get(isn$)
                woVect!.setItem(#CREATE_WO, true_false)
            wend

            methodret
            
setCreateWO_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Removes existing woVect! BBjVector from isnWOMap! HashMap for the given SO detail line internal
        rem  * sequence number (isn). Asks operator approval before removing a woVect! BBjVector with an existing
        rem  * linked Work Order.
        rem  * 
        rem  * @param BBjString isn$
        rem  * 
        rem  * @return boolean  1=true: woVect! removed
        rem  *                 0=false: woVect! not removed, operator didn't approve
        rem  */
        method public boolean unlinkWO(BBjString isn$)
            seterr unlinkWO_error
            true_false=Boolean.valueOf("true")

            if #isnWOMap!.containsKey(isn$)
                woVect! = #isnWOMap!.get(isn$)
                wo_no$ = woVect!.getItem(#WO_NO)
                if cvs(wo_no$,2)<>"" then
                    rem --- Warn there is an existing WO linked to this Sales Order detail line.
                    rem --- Ask if they want to continue with the delete.
                    msg_id$="OP_DELETE_WO_LINK_Q"
                    dim msg_tokens$[2]
                    msg_tokens$[1]=wo_no$
                    msg_tokens$[2]=cvs(woVect!.getItem(#SOLINE_ITEM),2)
                    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
                    
                    if msg_opt$="Y" then
                        #removeLinkedWO(isn$)
                        #isnWOMap!.remove(isn$)
                    else
                        true_false=Boolean.valueOf("false")
                    endif
                else
                    rem --- No existing linked WO so just remove isn from isnWOMap!
                    #isnWOMap!.remove(isn$)
                endif
            endif

            methodret true_false
            
unlinkWO_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Removes all existings woVect! BBjVectors from isnWOMap! HashMap for the Sales Order.
        rem  * Asks operator approval before removing woVect! BBjVectors if there are any existing
        rem  * linked Work Order.
        rem  * 
        rem  * @return boolean  1=true: all woVect! removed
        rem  *                 0=false: no woVect! removed, operator didn't approve
        rem  */
        method public boolean unlinkWOs()
            seterr unlinkWOs_error
            true_false=Boolean.valueOf("true")

            if #isnWOMap!.size() > 0 
                linkedIsn! = BBjAPI().makeVector()
                isnKeys! = #isnWOMap!.keySet()
                iter! = isnKeys!.iterator()
                while iter!.hasNext()
                    isn$ = iter!.next()
                    woVect! = #isnWOMap!.get(isn$)
                    wo_no$ = woVect!.getItem(#WO_NO)
                    if cvs(wo_no$,2)<>"" then linkedIsn!.addItem(isn$)
                wend
                if linkedIsn!.size() then
                    rem --- Warn there is an existing WO linked to this Sales Order detail line.
                    rem --- Ask if they want to continue with the delete.
                    msg_id$="OP_DEL_LINKED_WOS_Q"
                    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
                    
                    if msg_opt$="Y" then
                        iter!=linkedIsn!.iterator()
                        while iter!.hasNext()
                            isn$ = iter!.next()
                            #removeLinkedWO(isn$)
                        wend
                    else
                        true_false=Boolean.valueOf("false")
                    endif
                endif
            endif

            methodret true_false
            
unlinkWOs_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Updates SOLINE_NO in all woVect! in isnWOMap! with the CURRENT Sales Order detail grid line numbers.
        rem  * This is needed to sort the woVect!'s in isnWOMap! in the same order as the Sales Order detail grid rows. 
        rem  * 
        rem  * @param BBjVector gridRowVect!
        rem  */
        method public void updateLineNo(BBjVector gridRowVect!)
            seterr updateLineNo_error
            dim rowData$:#tplOpeOrdDet$

            rem --- Loop thru gridRowVect! to get current line_no for updating isns woVect!
            iter!=gridRowVect!.iterator()
            while iter!.hasNext()
                rowData$=iter!.next()
                if #isnWOMap!.containsKey(rowData.internal_seq_no$) then
                    woVect! = #isnWOMap!.get(rowData.internal_seq_no$)
                    woVect!.setItem(#SOLINE_NO, rowData.line_no$)
                endif
            wend

            methodret
            
updateLineNo_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Provides the number of existing and potential Work Order links for this Sales Order.
        rem  * 
        rem  * @return BBjNumber Number of existing and potential WO links for this SO
        rem  */
        method public BBjNumber woCount()
            seterr woCount_error

            methodret #isnWOMap!.size()
            
woCount_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Close all open channels and connections.
        rem  */
        method public void close()
            seterr close_error

            rem --- Note that even though tables were opened using bac_open_tables.bbj, rd_table_chans$[all] is NOT being shared/passed.
            if #devAdsMasks >= 0 then close(#devAdsMasks,err=*next)
            if #devBmcOpCodes >= 0 then close(#devBmcOpCodes,err=*next)
            if #devBmmBillMast >= 0 then close(#devBmmBillMast,err=*next)
            if #devIvmItemMast >= 0 then close(#devIvmItemMast,err=*next)
            if #devIvmItemWhse >= 0 then close(#devIvmItemWhse,err=*next)
            if #devIvsParams >= 0 then close(#devIvsParams,err=*next)
            if #devOpeOrdDet >= 0 then close(#devOpeOrdDet,err=*next)
            if #devOpsParams >= 0 then close(#devOpsParams,err=*next)
            if #devSfcWOTypeCd >= 0 then close(#devSfcWOTypeCd,err=*next)
            if #devSfeWOMastr >= 0 then close(#devSfeWOMastr,err=*next)
            if #devSfeWOMatl >= 0 then close(#devSfeWOMatl,err=*next)
 
            methodret
            
close_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            if err_num=252 then
                E!=BBjAPI().getLastBBjException()
                rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
                if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
            endif
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Cleanup on object's destruction and garbage collection.
        rem  */
        method protected void finalize()

            rem --- Close all open channels and connections
            #close()

            methodret
        methodend
	
classend
