rem ==========================================================================
rem --- A set of utility methods to help create PDF printer forms
rem --- Program ado_pdf.src v8.0.0 17Jun2009
rem --- Copyright 2008-2009, BASIS International, Ltd.
rem ==========================================================================

	use java.awt.Font

    class public PDFHelper
    
        field public Font DefaultFont!
        field public BBjColor DefaultTextColor!
        field public BBjColor DefaultBackgroundColor!
        field public BBjAPI myBBjAPI! = BBjAPI()
        field private BBjInt LEFT_JUSTIFY% = 0
        field private BBjInt RIGHT_JUSTIFY% = 1
        field private BBjInt CENTER_JUSTIFY% = 2
    
    rem --- New object
    
        method public PDFHelper()
            #setDefaultColors()
        methodend
    
        method public PDFHelper(Font p_font!)
            #DefaultFont! = p_font!
            #setDefaultColors()
        methodend
        
        method public PDFHelper(Font p_font!, BBjColor p_text_color!, BBjColor p_background_color!)
            #DefaultFont! = p_font!
            #DefaultTextColor! = p_text_color!
            #DefaultBackgroundColor! = p_background_color!
        methodend
        
        method private void setDefaultColors()
            #DefaultTextColor! = #myBBjAPI!.makeColor("BLACK")
            #DefaultBackgroundColor! = #myBBjAPI!.makeColor("WHITE")
        methodend
        
    rem --- Access the constants
    
        method public BBjInt LEFT_JUSTIFY()
            methodret #LEFT_JUSTIFY%
        methodend
    
        method public BBjInt RIGHT_JUSTIFY()
            methodret #RIGHT_JUSTIFY%
        methodend
    
        method public BBjInt CENTER_JUSTIFY()
            methodret #CENTER_JUSTIFY%
        methodend
        
    rem --- Set text; this is the full form
    
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjString p_text$, Font p_font!, BBjInt p_just%, BBjColor p_background_color!, BBjColor p_text_color!, BBjFormPage p_page!)
            declare BBjFormParagraph para!
            declare Font font!
            
            para! = p_page!.newParagraph()
            
            if p_font! = null() then 
                font! = #DefaultFont!
            else
                font! = p_font!
            endif
            
            if p_w% and p_h% then 
                para!.setSize(p_w%, p_h%)
            else
                height% = p_h%
                width%  = p_w%
                if p_h% = 0 then point_size% = font!.getSize(), height% = point_size%
                if p_w% = 0 then width% = len(p_text$) * point_size%
                para!.setSize(width%, height%)
            endif
            
            para!.setPosition(p_x%, p_y%)
            
            if p_just% = 1 then 
                para!.setHorizontalAlignment(para!.RIGHT_JUSTIFIED)
            else
                if p_just% = 2 then 
                    para!.setHorizontalAlignment(para!.CENTER_JUSTIFIED)
                endif
            endif
                    
            para!.setFont(font!)
            para!.setText(p_text$)
            
            if p_background_color! <> null() then 
                para!.setBackgroundColor(p_background_color!)
            else
                para!.setBackgroundColor(#DefaultBackgroundColor!)
            endif
            
            if p_text_color! <> null() then 
                para!.setTextColor(p_text_color!)
            else
                para!.setTextColor(#DefaultTextColor!)
            endif
            
            p_page!.add(para!)
            
            methodret p_page!
        methodend

    rem --- These are the setText() method with different signatures
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjString p_text$, Font p_font!, BBjInt p_just%, BBjColor p_background_color!, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, p_w%, p_h%, p_text$, p_font!, p_just%, p_background_color!, null(), p_page!)
        methodend
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjString p_text$, BBjInt p_just%, BBjColor p_background_color!, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, p_w%, p_h%, p_text$, null(), p_just%, p_background_color!, null(), p_page!)
        methodend
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjString p_text$, Font p_font!, BBjInt p_just%, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, p_w%, p_h%, p_text$, p_font!, p_just%, null(), null(), p_page!)
        methodend
                
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjString p_text$, BBjColor p_background_color!, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, p_w%, p_h%, p_text$, null(), 0, p_background_color!, null(), p_page!)
        methodend
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjString p_text$, BBjInt p_just%, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, p_w%, p_h%, p_text$, null(), p_just%, null(), null(), p_page!)
        methodend
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjString p_text$, BBjColor p_background_color!, BBjColor p_text_color!, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, 0, 0, p_text$, null(), 0, p_background_color!, p_text_color!, p_page!)
        methodend
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjString p_text$, Font p_font!, BBjColor p_background_color!, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, 0, 0, p_text$, p_font!, 0, p_background_color!, null(), p_page!)
        methodend

        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjString p_text$, BBjColor p_background_color!, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, 0, 0, p_text$, null(), 0, p_background_color!, null(), p_page!)
        methodend
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjString p_text$, Font p_font!, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, 0, 0, p_text$, p_font!, 0, null(), null(), p_page!)
        methodend
        
        method public BBjFormPage setText(BBjInt p_x%, BBjInt p_y%, BBjString p_text$, BBjFormPage p_page!)
            methodret #setText(p_x%, p_y%, 0, 0, p_text$, null(), 0, null(), null(), p_page!)
        methodend

        method public BBjFormPage setText(BBjInt p_x%, BBjNumber p_y, BBjString p_text$, BBjFormPage p_page!)
            methodret #setText(p_x%, int(p_y), 0, 0, p_text$, null(), 0, null(), null(), p_page!)
        methodend
        
    rem --- Draw a box
       
        method public BBjFormPage drawBox(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjColor p_fill!, BBjColor p_line!, BBjFormPage p_page!)
            declare BBjFormFrame frame!
            
            frame! = p_page!.newFrame()
            frame!.setPosition(p_x%, p_y%)
            frame!.setWidth(p_w%)
            frame!.setHeight(p_h%)
            
            if p_fill! <> null() then 
                frame!.setFillColor(p_fill!)
            endif
            
            if p_line! <> null() then 
                frame!.setLineColor(p_line!)
            endif

            p_page!.add(frame!)
            
            methodret p_page!
        methodend
                
        method public BBjFormPage drawBox(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjColor p_fill!, BBjFormPage p_page!)
            methodret #drawBox(p_x%, p_y%, p_w%, p_h%, p_fill!, null(), p_page!)
        methodend

        method public BBjFormPage drawBox(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjFormPage p_page!)
            methodret #drawBox(p_x%, p_y%, p_w%, p_h%, null(), null(), p_page!)
        methodend
    
    rem --- Draw a line
    
        method public BBjFormPage drawLine(BBjInt p_x1%, BBjInt p_y1%, BBjInt p_x2%, BBjInt p_y2%, BBjFormPage p_page!)
            declare BBjFormLine line!
            
            line! = p_page!.newLine()
            line!.setPosition(p_x1%, p_y1%)
            line!.setEndPoint(p_x2%, p_y2%)
            p_page!.add(line!)
            
            methodret p_page!
        methodend
        
        method public BBjFormPage drawVLine(BBjInt p_x%, BBjInt p_y%, BBjInt p_h%, BBjFormPage p_page!)
            methodret #drawLine(p_x%, p_y%, p_x%, p_y% + p_h%, p_page!)
        methodend
        
        method public BBjFormPage drawHLine(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjFormPage p_page!)
            methodret #drawLine(p_x%, p_y%, p_x% + p_w%, p_y%, p_page!)
        methodend
        
    rem --- Set Logo
    
        method public BBjFormPage setLogo(BBjInt p_x%, BBjInt p_y%, BBjInt p_w%, BBjInt p_h%, BBjString p_logofile$, BBjFormPage p_page!)
            declare BBjFormImage logo!
            
            logo! = p_page!.newImage()
            start_block = 1
            
            if start_block then
                logo!.setURL(p_logofile$,err=*endif)
                logo!.setPosition(p_x%, p_y%)
                logo!.setWidth(p_w%)
                logo!.setHeight(p_h%)
                p_page!.add(logo!)
            endif
            
            methodret p_page!
        methodend

    
    classend

rem ==========================================================================    
rem --- Testing
rem ==========================================================================    

    rem use java.awt.Font
    use java.lang.Float
    use javax.print.attribute.HashPrintRequestAttributeSet
    use javax.print.attribute.standard.MediaPrintableArea
    use javax.print.attribute.standard.MediaSizeName
    use javax.print.attribute.standard.OrientationRequested

rem --- Init PDF printing 

    declare Font textfont!
    declare Font boldfont!
    declare Font italicfont!
    declare Font largefont!
    declare BBjAPI myBBjAPI!
    declare BBjColor white!
    declare BBjColor black!
    declare BBjColor light_gray!
    declare BBjColor very_light_gray!

    textfont!   = new Font("Times New Roman", Font.PLAIN, 10)
    boldfont!   = new Font("Arial", Font.BOLD, 10)
    italicfont! = new Font("Times New Roman", Font.ITALIC, 10)
    largefont!  = new Font("Arial", Font.BOLD, 26)
    
    myBBjAPI! = BBjAPI()
    white! = myBBjAPI!.makeColor("WHITE")
    black! = myBBjAPI!.makeColor("BLACK")
    light_gray! = myBBjAPI!.makeColor("LTGRAY")
    very_light_gray! = myBBjAPI!.makeColor(224, 224, 224)
    
rem --- Setup PDF printer and print area   

    declare HashPrintRequestAttributeSet requestAttributes!
    declare MediaPrintableArea a!
    declare Float x!
    declare Float y!
    declare Float w!
    declare Float h!

    requestAttributes! = new HashPrintRequestAttributeSet()
    requestAttributes!.add(MediaSizeName.NA_LETTER)
    requestAttributes!.add(OrientationRequested.PORTRAIT)

    units = MediaPrintableArea.INCH
    x! = new Float(0.5).floatValue()
    y! = new Float(0.5).floatValue()
    w! = new Float(8.0).floatValue()
    h! = new Float(10.5).floatValue()
    a! = new MediaPrintableArea(x!,y!,w!,h!,units)

    requestAttributes!.add(a!)

rem --- Create PDF printer and set attributes 

    declare BBjPrinter myBBjPrinter!
    declare BBjForm docPrint!
    
    myBBjPrinter! = myBBjAPI!.getBBjPrinter(1)
    myBBjPrinter!.setAttributes(requestAttributes!)
    docPrint! = myBBjPrinter!.getForm(0)
    
rem --- Setup form page and PDF helper object
	
	declare BBjFormPage page!
    declare PDFHelper pdf!

    page! = docPrint!.createPage()
    page!.setRightMargin(18)
    page!.setLeftMargin(18)
    page!.setTopMargin(18)
    page!.setBottomMargin(18)
    
    pdf! = new PDFHelper(textfont!)

rem --- Boxes and lines

    page! = pdf!.drawBox(18, 18, 540, 720, page!); rem x, y, w, h
    page! = pdf!.drawBox(300, 300, 100, 100, very_light_gray!, page!); rem with background color and border
    page! = pdf!.drawBox(300, 450, 100, 100, very_light_gray!, very_light_gray!, page!); rem no border, light gray patch
    page! = pdf!.drawVLine(100, 18, 720, page!); rem x, y, height
    page! = pdf!.drawHLine(18, 100, 540, page!); rem x, y, width
    page! = pdf!.drawLine(18, 100, 558, 738, page!); rem x1, y1, x2, y2
    
rem --- Text
    
    page! = pdf!.setText(100, 100, "This is a test at (100, 100)", page!)
    page! = pdf!.setText(0, 0, "This is a test at (0, 0)", page!)
    page! = pdf!.setText(18+2, 18+2, "TLH Corner", page!)
    page! = pdf!.setText(18+2, 18+2, 540-4, 0, "This is a Heading", largefont!, pdf!.CENTER_JUSTIFY(), black!, white!, page!)
    page! = pdf!.setText(18+2, 50, 100-18-4, 0, "Halfway", boldfont!, pdf!.CENTER_JUSTIFY(), page!)
    page! = pdf!.setText(18+2, 90-2, 100-18-4, 0, "Bottom", italicfont!, pdf!.RIGHT_JUSTIFY(), page!)
    page! = pdf!.setText(100, 120, "Light gray background", light_gray!, page!)
    page! = pdf!.setText(100, 140, "Black and White Inverse", black!, white!, page!)
    page! = pdf!.setText(100, 160, pad("Light Gray Bar", 172), very_light_gray!, page!)
    page! = pdf!.setText(100, 180, fill(183), very_light_gray!, page!)
    page! = pdf!.setText(100, 180, "Text on top", very_light_gray!, page!)
    page! = pdf!.setText(100, 200, pad("Padded Centered Text", 147, "C"), boldfont!, very_light_gray!, page!)
    page! = pdf!.setText(100, 220, fill(183), very_light_gray!, page!)
    page! = pdf!.setText(100, 220, 440, 0, "Centered Text on Top", boldfont!, pdf!.CENTER_JUSTIFY(), very_light_gray!, page!)
    page! = pdf!.setText(100, 240, 440, 0, "Centered Text, light gray", boldfont!, pdf!.CENTER_JUSTIFY(), very_light_gray!, page!)
    
rem --- Display

    docPrint!.preview()
    release

    end
    