rem ==========================================================================
rem --- A set of utility methods to help create PDF printer forms
rem --- Program ado_pdf.src v8.0.0 17Jun2009
rem --- Copyright 2008-2009, BASIS International, Ltd.
rem ==========================================================================

	use java.awt.Font

    class public PDFHelper
    
        field public Font DefaultFont!
        field public BBjColor DefaultTextColor!
        field public BBjColor DefaultBackgroundColor!
        field private BBjInt LEFT_JUSTIFY% = 0
        field private BBjInt RIGHT_JUSTIFY% = 1
        field private BBjInt CENTER_JUSTIFY% = 2
        field public BBjFormPage Page!
    
    rem --- New object
    
        method public PDFHelper()
            #setDefaultColors()
        methodend
    
        method public PDFHelper(Font p_font!)
            #DefaultFont! = p_font!
            #setDefaultColors()
        methodend
        
        method public PDFHelper(Font p_font!, BBjColor p_text_color!, BBjColor p_background_color!)
            #DefaultFont! = p_font!
            #DefaultTextColor! = p_text_color!
            #DefaultBackgroundColor! = p_background_color!
        methodend
        
        method private void setDefaultColors()
            #DefaultTextColor! = BBjAPI().makeColor("BLACK")
            #DefaultBackgroundColor! = BBjAPI().makeColor("WHITE")
        methodend
        
    rem --- Access the constants
    
        method public BBjInt LEFT_JUSTIFY()
            methodret #LEFT_JUSTIFY%
        methodend
    
        method public BBjInt RIGHT_JUSTIFY()
            methodret #RIGHT_JUSTIFY%
        methodend
    
        method public BBjInt CENTER_JUSTIFY()
            methodret #CENTER_JUSTIFY%
        methodend
        
    rem --- Set text; this is the full form
    
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjString p_text$, Font p_font!, BBjInt p_just%, BBjColor p_background_color!, BBjColor p_text_color!)
        
            p_x% = int(p_x)
            p_y% = int(p_y)
            p_w% = int(p_w)
            p_h% = int(p_h)
        
            if #Page! = null() then
                throw "PDFHelper: setText(): You must setPage() before using this method", 257
            endif
        
            declare BBjFormParagraph para!
            declare Font font!
            
            para! = #Page!.newParagraph()
            
            if p_font! = null() then 
                font! = #DefaultFont!
            else
                font! = p_font!
            endif
            
            if p_w% and p_h% then 
                para!.setSize(p_w%, p_h%)
            else
                height% = p_h%
                width%  = p_w%
                if p_h% = 0 then point_size% = font!.getSize(), height% = point_size%
                if p_w% = 0 then width% = len(p_text$) * point_size%
                para!.setSize(width%, height%)
            endif
            
            para!.setPosition(p_x%, p_y%)
            
            if p_just% = #RIGHT_JUSTIFY% then 
                para!.setHorizontalAlignment(para!.RIGHT_JUSTIFIED)
            else
                if p_just% = #CENTER_JUSTIFY% then 
                    para!.setHorizontalAlignment(para!.CENTER_JUSTIFIED)
                endif
            endif
                    
            para!.setFont(font!)
            para!.setText(p_text$)
            
            if p_background_color! <> null() then 
                para!.setBackgroundColor(p_background_color!)
            else
                para!.setBackgroundColor(#DefaultBackgroundColor!)
            endif
            
            if p_text_color! <> null() then 
                para!.setTextColor(p_text_color!)
            else
                para!.setTextColor(#DefaultTextColor!)
            endif
            
            #Page!.add(para!)
            
        methodend

    rem --- These are the setText() method with different signatures
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjString p_text$, Font p_font!, BBjInt p_just%, BBjColor p_background_color!)
            #setText(p_x, p_y, p_w, p_h, p_text$, p_font!, p_just%, p_background_color!, null())
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjString p_text$, BBjInt p_just%, BBjColor p_background_color!)
            #setText(p_x, p_y, p_w, p_h, p_text$, null(), p_just%, p_background_color!, null())
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjString p_text$, Font p_font!, BBjInt p_just%)
            #setText(p_x, p_y, p_w, p_h, p_text$, p_font!, p_just%, null(), null())
        methodend
                
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjString p_text$, BBjColor p_background_color!)
            #setText(p_x, p_y, p_w, p_h, p_text$, null(), 0, p_background_color!, null())
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjString p_text$, BBjInt p_just%)
            #setText(p_x, p_y, p_w, p_h, p_text$, null(), p_just%, null(), null())
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjString p_text$, BBjColor p_background_color!, BBjColor p_text_color!)
            #setText(p_x, p_y, 0, 0, p_text$, null(), 0, p_background_color!, p_text_color!)
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjString p_text$, Font p_font!, BBjColor p_background_color!)
            #setText(p_x, p_y, 0, 0, p_text$, p_font!, 0, p_background_color!, null())
        methodend

        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjString p_text$, BBjColor p_background_color!)
            #setText(p_x, p_y, 0, 0, p_text$, null(), 0, p_background_color!, null())
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjString p_text$, Font p_font!, BBjInt p_just%)
            #setText(p_x, p_y, 0, 0, p_text$, p_font!, p_just%, null(), null())
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjString p_text$, Font p_font!)
            #setText(p_x, p_y, 0, 0, p_text$, p_font!, 0, null(), null())
        methodend
        
        method public void setText(BBjNumber p_x, BBjNumber p_y, BBjString p_text$)
            #setText(p_x, p_y, 0, 0, p_text$, null(), 0, null(), null())
        methodend

    rem --- Draw a box
       
        method public void drawBox(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjColor p_fill!, BBjColor p_line!)
        
            p_x% = int(p_x)
            p_y% = int(p_y)
            p_w% = int(p_w)
            p_h% = int(p_h)
        
            if #Page! = null() then
                throw "PDFHelper: drawBox(): You must setPage() before using this method", 257
            endif
        
            declare BBjFormFrame frame!
            
            frame! = #Page!.newFrame()
            frame!.setPosition(p_x%, p_y%)
            frame!.setWidth(p_w%)
            frame!.setHeight(p_h%)
            
            if p_fill! <> null() then 
                frame!.setFillColor(p_fill!)
            endif
            
            if p_line! <> null() then 
                frame!.setLineColor(p_line!)
            endif

            #Page!.add(frame!)
            
        methodend
                
        method public void drawBox(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjColor p_fill!)
            #drawBox(p_x, p_y, p_w, p_h, p_fill!, null())
        methodend

        method public void drawBox(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h)
            #drawBox(p_x, p_y, p_w, p_h, null(), null())
        methodend
    
    rem --- Draw a line
    
        method public void drawLine(BBjNumber p_x1, BBjNumber p_y1, BBjNumber p_x2, BBjNumber p_y2)
            
            p_x1% = int(p_x1)
            p_y1% = int(p_y1)
            p_x2% = int(p_x2)
            p_y2% = int(p_y2)

            if #Page! = null() then
                throw "PDFHelper: drawLine(): You must setPage() before using this method", 257
            endif
       
            declare BBjFormLine line!
            
            line! = #Page!.newLine()
            line!.setPosition(p_x1%, p_y1%)
            line!.setEndPoint(p_x2%, p_y2%)
            #Page!.add(line!)
            
        methodend
        
        method public void drawVLine(BBjNumber p_x, BBjNumber p_y, BBjNumber p_h)
            #drawLine(p_x, p_y, p_x, p_y + p_h)
        methodend
        
        method public void drawHLine(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w)
            #drawLine(p_x, p_y, p_x + p_w, p_y)
        methodend
        
    rem --- Set Logo
    
        method public void setLogo(BBjNumber p_x, BBjNumber p_y, BBjNumber p_w, BBjNumber p_h, BBjString p_logofile$)
        
            p_x% = int(p_x)
            p_y% = int(p_y)
            p_w% = int(p_w)
            p_h% = int(p_h)

            if #Page! = null() then
                throw "PDFHelper: setLogo(): You must setPage() before using this method", 257
            endif
        
            declare BBjFormImage logo!
            
            logo! = #Page!.newImage()
            start_block = 1
            
            if start_block then
                logo!.setURL(p_logofile$,err=*endif)
                logo!.setPosition(p_x%, p_y%)
                logo!.setWidth(p_w%)
                logo!.setHeight(p_h%)
                #Page!.add(logo!)
            endif
            
        methodend

    
    classend

rem ==========================================================================    
rem --- Testing
rem ==========================================================================    

    rem use java.awt.Font
    use java.lang.Float
    use javax.print.attribute.HashPrintRequestAttributeSet
    use javax.print.attribute.standard.MediaPrintableArea
    use javax.print.attribute.standard.MediaSizeName
    use javax.print.attribute.standard.OrientationRequested

rem --- Init PDF printing 

    declare Font textfont!
    declare Font boldfont!
    declare Font italicfont!
    declare Font largefont!
    declare BBjColor white!
    declare BBjColor black!
    declare BBjColor light_gray!
    declare BBjColor very_light_gray!

    textfont!   = new Font("Times New Roman", Font.PLAIN, 10)
    boldfont!   = new Font("Arial", Font.BOLD, 10)
    italicfont! = new Font("Times New Roman", Font.ITALIC, 10)
    largefont!  = new Font("Arial", Font.BOLD, 26)
    
    white!           = BBjAPI().makeColor("WHITE")
    black!           = BBjAPI().makeColor("BLACK")
    light_gray!      = BBjAPI().makeColor("LTGRAY")
    very_light_gray! = BBjAPI().makeColor(224, 224, 224)
    
rem --- Setup PDF printer and print area   

    declare HashPrintRequestAttributeSet requestAttributes!
    declare MediaPrintableArea a!
    declare Float x!
    declare Float y!
    declare Float w!
    declare Float h!

    requestAttributes! = new HashPrintRequestAttributeSet()
    requestAttributes!.add(MediaSizeName.NA_LETTER)
    requestAttributes!.add(OrientationRequested.PORTRAIT)

    units = MediaPrintableArea.INCH
    x! = new Float(0.5).floatValue()
    y! = new Float(0.5).floatValue()
    w! = new Float(8.0).floatValue()
    h! = new Float(10.5).floatValue()
    a! = new MediaPrintableArea(x!,y!,w!,h!,units)

    requestAttributes!.add(a!)

rem --- Create PDF printer and set attributes 

    declare BBjPrinter myBBjPrinter!
    declare BBjForm docPrint!
    
    myBBjPrinter! = BBjAPI().getBBjPrinter(1)
    myBBjPrinter!.setAttributes(requestAttributes!)
    docPrint! = myBBjPrinter!.getForm(0)
    
rem --- Setup form page and PDF helper object
	
	declare BBjFormPage page!
    declare PDFHelper pdf!

    page! = docPrint!.createPage()
    page!.setRightMargin(18)
    page!.setLeftMargin(18)
    page!.setTopMargin(18)
    page!.setBottomMargin(18)
    
    pdf! = new PDFHelper(textfont!)

rem --- Boxes and lines

    pdf!.setPage(page!)
    pdf!.drawBox(18, 18, 540, 720); rem x, y, w, h
    pdf!.drawBox(300, 300, 100, 100, very_light_gray!); rem with background color and border
    pdf!.drawBox(300, 450, 100, 100, very_light_gray!, very_light_gray!); rem no border, light gray patch
    pdf!.drawVLine(100, 18, 720); rem x, y, height
    pdf!.drawHLine(18, 100, 540); rem x, y, width
    pdf!.drawLine(18, 100, 558, 738); rem x1, y1, x2, y2
    
rem --- Text
    
    pdf!.setText(100, 100, "This is a test at (100, 100)")
    pdf!.setText(0, 0, "This is a test at (0, 0)")
    pdf!.setText(18+2, 18+2, "TLH Corner")
    pdf!.setText(18+2, 18+2, 540-4, 0, "This is a Heading", largefont!, pdf!.CENTER_JUSTIFY(), black!, white!)
    pdf!.setText(18+2, 50, 100-18-4, 0, "Halfway", boldfont!, pdf!.CENTER_JUSTIFY())
    pdf!.setText(18+2, 90-2, 100-18-4, 0, "Bottom", italicfont!, pdf!.RIGHT_JUSTIFY())
    pdf!.setText(100, 120, "Light gray background", light_gray!)
    pdf!.setText(100, 140, "Black and White Inverse", black!, white!)
    pdf!.setText(100, 160, pad("Light Gray Bar", 172), very_light_gray!)
    pdf!.setText(100, 180, fill(183), very_light_gray!)
    pdf!.setText(100, 180, "Text on top", very_light_gray!)
    pdf!.setText(100, 200, pad("Padded Centered Text", 147, "C"), boldfont!, very_light_gray!)
    pdf!.setText(100, 220, fill(183), very_light_gray!)
    pdf!.setText(100, 220, 440, 0, "Centered Text on Top", boldfont!, pdf!.CENTER_JUSTIFY(), very_light_gray!)
    pdf!.setText(100, 240, 440, 0, "Centered Text, light gray", boldfont!, pdf!.CENTER_JUSTIFY(), very_light_gray!)
    
rem --- Display

    docPrint!.preview()
    stop
    release

    end
    