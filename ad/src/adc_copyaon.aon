rem ==========================================================================
rem --- adc_copyaon.aon 
rem --- Call to copy aon directory and sub-directories from one location to another,
rem --- with or without data files.
rem --- 
rem --- AddonSoftware Version 10.0
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved
rem ==========================================================================


rem --- OLDLOCATION$ : Location of source aon directory being copied (Input)
rem --- NEWLOCATION$ : Location of target aon directory (Input)
rem --- DEMODATA%    : 0=Do not copy demo data/1=Copy demo data (Input)
rem --- HELP%        : 0=Do not copy AddonHelp.jar/1=Copy AddonHelp.jar (Input)
rem --- RDFORM!      : Parent BBjTopLevelWindow object, the form (Input)
rem --- WAIT4OK%     : Wait for user's OK when done, 0=don't wait/1=wait (Input)
rem --- STATUS       : Status code/0=No error (Returned)

    declare BBjTopLevelWindow rdForm!

    seterr std_error
    setesc std_error
    enter oldLocation$,newLocation$,demoData%,help%,rdForm!,wait4ok%,status

rem --- Initializations

    status=0

rem --- Recursively copy oldLocation to newLocation

    declare CopyAonDir utility!
    utility! = new CopyAonDir(rdForm!, wait4ok%)
    utility!.copyAonDirectory(oldLocation$, newLocation$, demoData%, help%)
    
done: rem --- All done
    
    goto std_exit
   
rem #include std_error.src

std_error: rem --- Standard error handler (18Nov2014)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)

    exit

rem #endinclude std_exit.src

rem /**
rem  * Utility Class for recursively copying aon directory, skipping specific
rem  * pre-defined directories and files.
rem  */
    use java.io.File
	use java.util.Iterator
    use java.util.Vector
    use ::bbtranslator.bbj::BBTranslator
    use ::ado_file.src::FileObject

    class public CopyAonDir
        field private Vector skipDirs! = new Vector()
        field private BBjInt wait4ok% = 0
        field private BBjNumber BLOCKSIZE = 2^20
        field private BBjNumber log_dev = 0
        field private BBjNumber meter_proc_recs = 0
        field private BBjNumber meter_total_recs = 3000
        field private BBjString dir_syp$=stbl("+DIR_SYP")
        field private BBjSysGui sysGUI!
        field private BBjTopLevelWindow form!
        field private BBjTopLevelWindow meter!


        rem /**
        rem  * Constructor.
        rem  *
        rem  * @param BBjTopLevelWindow rdForm!
        rem  * @param BBjInt wait4ok
        rem  */
        method public CopyAonDir(BBjTopLevelWindow rdForm!, BBjInt wait4ok%)
            seterr method_error
            #sysGUI! = BBjAPI().getSysGui()
            #form! = rdForm!
            #wait4ok% = wait4ok%
            
            rem --- don't copy these directories, skip them
            rem --- CVS
            rem --- .svn
            rem --- callpoints
            rem --- data/arc
            rem --- data/def
            rem --- help/WebHelp
            sep$ = File.separator
            #skipDirs!.add(sep$+"CVS")
            #skipDirs!.add(sep$+".svn")
            #skipDirs!.add(sep$+"callpoints")
            #skipDirs!.add(sep$+"data"+sep$+"arc")
            #skipDirs!.add(sep$+"data"+sep$+"def")
            #skipDirs!.add(sep$+"help"+sep$+"WebHelp")
            methodret
            
method_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Recursively copy aon directory.
        rem  *
        rem  * Recursively copies aon directory for old location to new location, 
        rem  * skipping specific pre-defined directories and files.
        rem  *
        rem  * @param BBjString oldLocation$
        rem  * @param BBjString newLocation$
        rem  * @param BBjInt demoData
        rem  * @param BBjInt helpFile
        rem  */
        method public void copyAonDirectory(BBjString oldLocation$, BBjString newLocation$, BBjInt demoData%, BBjInt helpFile%)
            seterr method_error
            if demoData% then #meter_total_recs = #meter_total_recs + 500
            #meter! = #createProgressMeter()
            #openLog(demoData%, oldLocation$, newLocation$)

            #copyAonDirectory(new File(oldLocation$), new File(newLocation$), demoData%, helpFile%)

			if !demoData% then

				rem --- When not using demo data, remove inventory item images (except addon_logo32.png)
				declare Iterator iter!
				declare File imagesDir!
				declare Vector imagesFiles!
				imagesDir! = new File(newLocation$ + "/images")
				imagesFiles! = new Vector(java.util.Arrays.asList(imagesDir!.listFiles()))
				iter! = imagesFiles!.iterator()
				while iter!.hasNext()
					aFile! = cast(File, iter!.next())
				
					rem --- Remove inventory item image (except addon_logo32.png)
					if aFile!.getName()<>"addon_logo32.png" then
						aFile!.delete()
						#incrementProgressMeter("removed: "+aFile!.getPath())
						#log("REMOVED: " + aFile!.getPath())
					endif
				wend

                rem --- When not using demo data, remove ads_masks~*.xml files except don't remove ads_masks~99*.xml files
                rem --- Now that Barista's Create Sync File Backup utility only backs up changes, all the ads_masks~*.xml
                rem --- files are needed to initialize default values on a new system when the values weren't changed on
                rem --- the old system. The Install Wizard, via Set Up New Firm utility adc_firmsetup.aon, handles removing
                rem --- demo ads_masks~*.xml files and creating new ones.
				rem declare File syncDir!
				rem declare Vector masksFiles!
				rem syncDir! = new File(newLocation$ + "/data/sync")
				rem masksFiles! = new Vector(java.util.Arrays.asList(syncDir!.listFiles(new MasksFilter())))
				rem iter! = masksFiles!.iterator()
				rem while iter!.hasNext()
				rem 	aFile! = cast(File, iter!.next())
				rem 	aFile!.delete()
				rem 	#incrementProgressMeter("removed: "+aFile!.getPath())
				rem 	#log("REMOVED: " + aFile!.getPath())
				rem wend

				rem --- When not using demo data, remove ads_sequences~*.xml files except dont' remove ads_sequences~99*.xml files
                rem --- Now that Barista's Create Sync File Backup utility only backs up changes, all the ads_sequences~*.xml
                rem --- files are needed to initialize default values on a new system when the values weren't changed on
                rem --- the old system. The Install Wizard, via Set Up New Firm utility adc_firmsetup.aon, handles removing
                rem --- demo ads_sequences~*.xml files and creating new ones.
				rem declare Vector sequencesFiles!
				rem sequencesFiles! = new Vector(java.util.Arrays.asList(syncDir!.listFiles(new SequencesFilter())))
				rem iter! = sequencesFiles!.iterator()
				rem while iter!.hasNext()
				rem 	aFile! = cast(File, iter!.next())
				rem 	aFile!.delete()
				rem 	#incrementProgressMeter("removed: "+aFile!.getPath())
				rem 	#log("REMOVED: " + aFile!.getPath())
				rem wend
	
				rem --- When not using demo data, copy requiredFiles!
				declare Vector requiredFiles!
				requiredFiles! = new Vector()
				requiredFiles!.add("adm_rptctl")
				requiredFiles!.add("adm-09"); rem --- adm_procmaster
				requiredFiles!.add("adm-19"); rem --- adm_procdetail
				requiredFiles!.add("adm-39"); rem --- adm_proctables
				requiredFiles!.add("glm-06"); rem --- adm_auditcontrol
				requiredFiles!.add("glm-03"); rem --- glc_journalcode
				requiredFiles!.add("glm-18"); rem --- glm_recordtypes
				requiredFiles!.add("ivc_whsecode")
				requiredFiles!.add("opm_pointofsale")
				iter! = requiredFiles!.iterator()
				while iter!.hasNext()
					aFile! = new File(newLocation$ + "/data", cast(String, iter!.next()))

					rem --- Copy files
					sourceChan=unt
					open(sourceChan,isz=-1)oldLocation$ + "/data/"+aFile!.getName()
					targetChan=unt
					open(targetChan,isz=-1,mode="O_CREATE,O_TRUNC")aFile!.getPath()
					while 1
						readrecord(sourceChan,siz=#BLOCKSIZE,end=*break)record$
						writerecord(targetChan)record$
					wend
					close(sourceChan)
					close(targetChan)
					#incrementProgressMeter(oldLocation$ + "/data/"+aFile!.getName())
					#log("COPIED: " + oldLocation$ + "/data/"+aFile!.getName())
				wend
			endif
			
            #closeLog()
            #destroyProgressMeter()
            methodret
            
method_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Recursively copy aon directory.
        rem  *
        rem  * Recursively copies aon directory from source File to target File, 
        rem  * skipping specific pre-defined directories and files.
        rem  *
        rem  * @param File source
        rem  * @param File target
        rem  * @param BBjNumber demoData
        rem  * @param BBjInt helpFile
        rem  */
        method private void copyAonDirectory(File source!, File target!, BBjInt demoData%, BBjInt helpFile%)
            seterr method_error
    
            rem -- recursively process directories and copy files
            if(source!.isDirectory()) then
            
                rem --- skip this directory?
                if(#skipDirectory(source!))then
                    #log("SKIPPED: " + source!.getPath())
                    methodret
                endif
            
                rem --- create new directory if it doesn't exist
                FileObject.makeDirs(target!)

                rem --- get list of what is in the source dir, and process it
                declare Vector fileVector!
                fileVector! = new Vector(java.util.Arrays.asList(source!.list()))
                if(fileVector!.size()>0) then
                    for i=0 to fileVector!.size()-1
                        #copyAonDirectory(new File(source!, cast(BBjString, fileVector!.get(i))), 
:                                         new File(target!, cast(BBjString, fileVector!.get(i))), 
:                                         demoData%, helpFile%)
                    next i
                endif
                
            else
            
                rem --- skip data files in data directory if not including demo data
                if(!demoData%) then
                    parent$ = source!.getParent()
                    if(cast(BBjString, parent$).endsWith(cast(BBjString, File.separator + "data"))) then
                        #log("SKIPPED: " + source!.getPath())
                        methodret
                    endif
                endif
            
                rem --- skip AddonHelp.jar?
                if(!helpFile%) then
                    if(cast(BBjString, source!.getPath()).endsWith(cast(BBjString, "AddonHelp.jar"))) then
                        #log("SKIPPED: " + source!.getPath())
                        methodret
                    endif
                endif
                
                rem --- skip if source and target are the same
                if !FileObject.isSameFile(source!.getPath(),target!.getPath()) then
                    rem --- copy file
                    rem --- Can't use java.nio.files.Files::copy() method because of err 252 on Win machines when BBj has the source file open.
                    #incrementProgressMeter(source!.getPath())
                    sourceModTime!=source!.lastModified()
                    sourceChan=unt
                    open(sourceChan,isz=-1)source!.getPath()
                    targetChan=unt
                    open(targetChan,isz=-1,mode="O_CREATE,O_TRUNC")target!.getPath()
                    while 1
                        readrecord(sourceChan,siz=#BLOCKSIZE,end=*break)record$
                        writerecord(targetChan)record$
                    wend
                    close(sourceChan)
                    close(targetChan)
                    target!.setLastModified(sourceModTime!)
                endif
                #log("COPIED: " + source!.getPath())
               
            endif
            methodret
            
method_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Skip copy of some aon directories.
        rem  *
        rem  * @param File sourceDirectory
        rem  *
        rem  * @return BBjInt (1=true  false=0)
        rem  */
        method private BBjInt skipDirectory(File source!)
            seterr method_error
            skipDir% = 0
            
            if(source!.isDirectory()) then
                path$ = source!.getPath()
                for i=0 to #skipDirs!.size()-1
                    if(cast(BBjString, path$).endsWith(cast(BBjString, #skipDirs!.get(i)))) then
                        skipDir% = 1
                        break
                    endif
                next i
            endif
            
            methodret skipDir%
            
method_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Create progress meter.
        rem  *
        rem  * @return BBjTopLevelWindow meterWin!
        rem  */
        method private BBjTopLevelWindow createProgressMeter()
            seterr method_error

            rem --- meterWin! is not declared in bam_prog_bar.bbj, so can't declare it here
            rem declare BBjTopLevelWindow meterWin!
            
            declare BBTranslator Translate!
            rdTransSpaceKey$=stbl("+PROPS_NAME")+"_"+stbl("+USER_LOCALE")+"_BBTranslator"
            Translate!=cast(BBTranslator,BBjAPI().getGroupNamespace().getValue(rdTransSpaceKey$,err=*next))
            if Translate!=null()
                Translate!=BBTranslator.getInstance(stbl("+PROPS_NAME"),stbl("+USER_LOCALE"),null(),stbl("+PROPS_PATH"))
                BBjAPI().getGroupNamespace().setValue(rdTransSpaceKey$,Translate!)
            endif
            meter_title$=Translate!.getTranslation("AON_COPYING")+"..."
            #meter_proc_recs=0
            meter_text$=""
            meter_action$="WIN-LST"
            if #wait4ok% then meter_action$=meter_action$+"-OK"

            call #dir_syp$+"bam_prog_bar.bbj",
:               #sysGUI!,
:               #form!,
:               meterWin!,
:               meter_title$,
:               #meter_total_recs,
:               #meter_proc_recs,
:               meter_text$,
:               meter_action$
            methodret meterWin!
            
method_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Increment progress meter.
        rem  *
        rem  * @param File sourceDirectory
        rem  */
        method private void incrementProgressMeter(BBjString meter_text$)
            #meter_proc_recs=#meter_proc_recs+1
            meter_action$="MTR-LST"

            call #dir_syp$+"bam_prog_bar.bbj",
:               #sysGUI!,
:               #form!,
:               #meter!,
:               meter_title$,
:               #meter_total_recs,
:               #meter_proc_recs,
:               meter_text$,
:               meter_action$
        methodend

        rem /**
        rem  * Destroy progress meter.
        rem  */
        method private void destroyProgressMeter()

            #meter_proc_recs=#meter_total_recs
            meter_action$="LST-END"

            call #dir_syp$+"bam_prog_bar.bbj",
:               #sysGUI!,
:               #form!,
:               #meter!,
:               meter_title$,
:               #meter_total_recs,
:               #meter_proc_recs,
:               meter_text$,
:               meter_action$
        methodend

        rem /**
        rem  * Open log file.
        rem  *
        rem  * @param BBjInt demoData
        rem  * @param BBjString oldLocation$
        rem  * @param BBjString newLocation$
        rem  */
        method private void openLog(BBjInt demoData%, BBjString oldLocation$, BBjString newLocation$)
            seterr method_error
            logDir$ = newLocation$ + "/logs"
            appDir$ = newLocation$
            
            rem --- create logs directory at new location
            FileObject.makeDirs(new File(logDir$))

            rem --- create and open log file
            log$ = logDir$+"/copyaon_"+DATE(0:"%Yd%Mz%Dz")+"_"+DATE(0:"%Hz%mz")+".txt"
            erase log$,err=*next
            string log$
            #log_dev=unt
            open(#log_dev)log$
            
            rem --- write log header info
            print(#log_dev)"Copyaon log started: " + date(0:"%Yd-%Mz-%Dz@%Hz:%mz:%sz")
            print(#log_dev)"Old source location: " + oldLocation$
            print(#log_dev)"New target location: " + newLocation$
            if demoData%
                print(#log_dev)"Demo data included."
            else
                print(#log_dev)"Demo data not included."
            endif
            print(#log_dev)
            methodret
            
method_error:rem --- Method error trap/handler
            rd_err_text$="", err_num=err
            if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
            call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
            if pos("ESCAPE"=rd_err_act$)<>0 seterr 0;setesc 0
            if pos("RETRY"=rd_err_act$)<>0 retry
            x$=stbl("+THROWN_ERR","TRUE")   
            throw "["+pgm(-2)+"] "+str(tcb(5))+": "+rd_err_text$,err_num
        methodend

        rem /**
        rem  * Write to log file.
        rem  *
        rem  * @param BBjString text$
        rem  */
        method private void log(BBjString text$)
            print(#log_dev)text$
        methodend

        rem /**
        rem  * Close log file.
        rem  */
        method private void closeLog()
            print(#log_dev)"Copyaon log finished: " + date(0:"%Yd-%Mz-%Dz@%Hz:%mz:%sz")
            close(#log_dev)
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter ads_masks~*.xml files, excluding ads_masks~99*.xml files.
rem  */

    class public MasksFilter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_masks~") and !file!.startsWith("ads_masks~99") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter ads_sequences~*.xml files, excluding ads_sequences~99*.xml files.
rem  */

    class public SequencesFilter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_sequences~") and !file!.startsWith("ads_sequences~99") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend
    
    end