rem adc - Clear Records In A File For A Partial Key
rem Program adc_clearpartial v8.0.0 10May2006 (syc_fb)
rem Created by adx_progconv.bbx v1.0.3 (05/10/2006 06:06:43)

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright BASIS International Ltd.  All Rights Reserved.
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

rem --- ACTION$ : Action Code (Input)
rem ---         : N=No error on inability to lock file
rem --- CHANNEL : Channel number of file to remove records from (Input)
rem --- PARTIAL$: Partial key to remove (Input)
rem --- STATUS  : Status code (Returned)

    seterr std_error
    setesc std_error
    enter action$,channel,partial$,status

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Open/Lock Files

    error=-1
    locked=0
    fid$=fid(channel,err=std_exit)
    lock (channel,err=nolock); locked=1

nolock: rem --- Unable to lock target file

    if !locked and pos("N"=action$)=0 then
    	error=err
        status=999
        msg_id$="AD_NOLOCK"
        gosub disp_message
        goto std_exit
    endif

rem --- Position file (and remove record if an exact match)

    partial_found=0
    read (channel,key=partial$,err=*next); partial_found=1
    
rem --- Remove record if an exact match to "partial" key
    if partial_found then remove (channel,key=partial$)

rem --- Remove all records with the specified partial key

    while 1
          k$=key(channel,end=*break)
          if pos(partial$=k$)<>1 break
          remove (channel,key=k$)
    wend

rem --- All done

    goto std_exit
    
disp_message:rem --- Display Message Dialog

	call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,rd_table_chans$[all]

	return
    
rem #include std_error.src

std_error: rem --- Standard error handler (22Feb2013)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)

    unlock (channel,err=*next)
rem    if pos("N"=action$)=0 and error>=0 then exit error
rem --- rem'd the above because it causes control to go directly
rem ---		to the seterr routine in the calling program, rather than returning to
rem ---		the call line, where we can check the status flag and do accordingly
    exit

rem #endinclude std_exit.src

    end
