rem --- adx_aondashboard.aon
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- Addon dashboards

	use ::dashboard/dashboard.bbj::Dashboard
	use ::dashboard/dashboard.bbj::DashboardCategory
	use ::dashboard/dashboard.bbj::DashboardWidget
	use ::dashboard/dashboard.bbj::DashboardWidgetFilter
	use ::dashboard/dashboard.bbj::DashboardWidgetLink
	use ::dashboard/dashboard.bbj::DashboardWindow
	use ::dashboard/widget.bbj::Widget
	use ::dashboard/widget.bbj::ChartWidget
	use ::dashboard/widget.bbj::ImageWidget
	use ::dashboard/widget.bbj::GridWidget
	use ::dashboard/widget.bbj::BarChartWidget
	use ::dashboard/widget.bbj::StackedBarChartWidget
	use ::dashboard/widget.bbj::StackedPercentageBarChartWidget
	use ::dashboard/widget.bbj::LineChartWidget
	use ::dashboard/widget.bbj::AreaChartWidget	
	use ::dashboard/widget.bbj::PieChartWidget
	use ::dashboard/widget.bbj::JasperViewerWidget
	use ::reporting/bbjasper/bbjasper.bbj::BBJasperReport
	use ::BBUtils.bbj::BBUtils
    use ::bbtranslator.bbj::BBTranslator

	use java.awt.Color
	use java.awt.Font
	use java.util.HashMap
	use java.util.LinkedHashMap

	seterr std_error
	setesc std_error
	
rem --- if BUI, need to call bas_process_beg

	if info(3,6)="5"
		tmp_cnt=0
		while tmp_cnt<argc()
			tmp$=argv(tmp_cnt)
			if pos("-u"=tmp$) then user_id$=cvs(tmp$(3),4); break
			tmp_cnt=tmp_cnt+1
		wend

        if user_id$=""
            use ::sys/prog/bao_security.bbj::Security
            user_id$=Security.getURLUser()
        endif

		if user_id$<>""
			call stbl("+DIR_SYP")+"bas_process_beg.bbj",user_id$,table_chans$[all]
		else
            wait 2
			release
		endif

		if rd_session_pfx$="" then
			rd_session_pfx$="barista_"+str(dec(info(3,1)):"0000000000000000")
			rd_temp_stbl$=stbl("+SESSION_PREFIX",rd_session_pfx$)
		endif
	endif

rem --- initialization
	
	gosub get_sysinfo
    if info(3,6)="5" then gosub get_translate_object
	gosub get_masks	
	gosub get_connectstring
	gosub resolve_paths
	gosub init_prog_meter
	gosub get_installed_modules
	
rem --- create dashboard, tabs, widgets

	gosub create_dashboard
	gosub create_acct_tab_and_widgets
	gosub create_sales_tab_and_widgets
	gosub create_mfg_tab_and_widgets
	
rem --- now create/show the Dashboard Window
 
	aonDashboardWindow! = new DashboardWindow(aonDashboard!)
	progress!.setValue("+process_task",task_id$+"^D^")

	aonDashboardWindow!.doModal()

	goto std_exit

	release
	

rem =========================================================================================================================
rem  create dashboard, accounting tab, and widgets
rem  this section defines the accounting category/tab, and the several widgets it contains
rem =========================================================================================================================
	
rem ==============================================
create_dashboard:
rem ==============================================

	declare Dashboard aonDashboard!

	aonDashboard! = new Dashboard("Addon",Translate!.getTranslation("AON_ADDONSOFTWARE_DASHBOARD"))

	return

	
rem ==============================================
create_acct_tab_and_widgets:
rem ==============================================

	declare DashboardCategory   acctDashboardCategory!

	acctDashboardCategory! = aonDashboard!.addDashboardCategory("Accounting",Translate!.getTranslation("AON_ACCOUNTING"))

rem ==============================================
rem --- create a yearly GL totals grid
rem ==============================================

	dashboard_menu_id$="ADD_GLINCTOT_GRD"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("GL")="Y"
		name$="GLINCTOT_GRD"
		title$ = Translate!.getTranslation("AON_GL_INCOME_ACCOUNT_TOTALS")
		previewText$=Translate!.getTranslation("AON_GENERAL_LEDGER_INCOME_ACCOUNT_TOTALS_BY_YEAR_DISPLAYED_IN_A_GRID")
		previewImage$=preview_path$+"glinctot_grd.png"
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		include_type$="J"; rem "Include Current and Prior Actual summaries; includes GL Record IDs 0, 2
		acct_type$="I"; rem "Income accounts only
		do_coa_join$="N"; rem "Change to Y to include a column for Chart of Accts Category 

		sql$="CALL GLINCTOT_GRD ('"+firm_id$+"', '"+include_type$+"', '"+acct_type$+"', '"+do_coa_join$+"', '"+masks$+"', '"+barista_wd$+"')"

		incomeGridDashboardWidget! = acctDashboardCategory!.addGridDashboardWidget(name$,title$,previewText$,previewImage$,connectString$,sql$)
		gridWidget!=incomeGridDashboardWidget!.getWidget()
		gridWidget!.setColumnAlignment(3,GridWidget.getHORIZONTAL_ALIGNMENT_RIGHT())
		gridWidget!.setColumnAlignment(0,GridWidget.getHORIZONTAL_ALIGNMENT_LEFT())

		rem Create Filters 
		filterName$ = Translate!.getTranslation("AON_INCOME_TOTALS_-_TIME_FRAME")
		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("A",Translate!.getTranslation("AON_CURRENT_YEAR_ACTUAL"))
		filterHashMap!.put("B",Translate!.getTranslation("AON_CURRENT_YEAR_BUDGET"))
		filterHashMap!.put("C",Translate!.getTranslation("AON_PRIOR_YEAR_ACTUAL"))
		filterHashMap!.put("D",Translate!.getTranslation("AON_PRIOR_YEAR_BUDGET"))
		filterHashMap!.put("E",Translate!.getTranslation("AON_NEXT_YEAR_ACTUAL"))
		filterHashMap!.put("F",Translate!.getTranslation("AON_NEXT_YEAR_BUDGET"))
		filterHashMap!.put("G",Translate!.getTranslation("AON_ALL_ACTUAL"))
		filterHashMap!.put("H",Translate!.getTranslation("AON_ALL_BUDGET"))
rem		filterHashMap!.put("I",Translate!.getTranslation("AON_ALL_(ACTUAL_&_BUDGET)"))
		filterHashMap!.put("J",Translate!.getTranslation("AON_CURRENT_&_PRIOR_YEAR_ACTUAL"))
		filterHashMap!.put("K",Translate!.getTranslation("AON_CURRENT_&_PRIOR_YEAR_BUDGET"))

		toolTip$ = Translate!.getTranslation("AON_SELECT_TIME_FRAME_AND_ACTUAL/BUDGET")
		dockLocation = DashboardWidget.getDOCK_LEFT()
		filter! = incomeGridDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.selectFilter("J")
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnGLINCTOT_GRDFilterSelect")
	
		gosub update_meter
		
	endif

rem ==============================================
rem --- create an Income Comparison bar chart
rem ==============================================

	dashboard_menu_id$="ADD_GLCMPINC_BAR"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("GL")="Y"
		name$="GLCMPINC_BAR"
		title$ = Translate!.getTranslation("AON_INCOME_COMPARISON")
		previewText$=Translate!.getTranslation("AON_INCOME_COMPARISON")
		previewImage$=preview_path$+"glcmpinc_bar.png"
		domainTitle$ = ""
		rangeTitle$ = Translate!.getTranslation("AON_$_IN_1000S")
		orientation=BarChartWidget.getORIENTATION_VERTICAL() 
		legend=1
		chartTitle$ = ""
		flat=0
		connectString$=aon_url$

		rem --- params for calling SPROC
		include_type$="A"; rem "Include Current vs Prior years' data; summarized by period (filter values A/0)
		acct_type$="I"; rem "Income accounts only
		do_coa_join$="N"; rem "Change to Y to JOIN w/Chart of Accts to filter by account grouping

		sql$="CALL GLCMPINC_BAR ('"+firm_id$+"', '"+include_type$+"', '"+acct_type$+"', '"+do_coa_join$+"', '"+masks$+"', '"+barista_wd$+"')"

		incomeComparisonBarDashboardWidget! = acctDashboardCategory!.addBarChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,flat,orientation,legend,connectString$,sql$)
		incCmpWidget!=incomeComparisonBarDashboardWidget!.getWidget()
		incCmpWidget!.setFontScalingFactor(0.45)
rem		incCmpWidget!.setChartColors("#00a4d9","#7fbe27",1.0,1.0); rem Colors for By Period
rem		incCmpWidget!.setChartColors("#3cb6df,#4aca75,#91bd5d"); rem supplying three explicit colors due to issue in legend vs chart if range is used (CAH)
		incCmpWidget!.setLabelsInBarChartColor("#000000")
		
		rem Create Filters 
		filterName$ = Translate!.getTranslation("AON_COMPARE")

		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("A",Translate!.getTranslation("AON_CURRENT/PRIOR"))
		filterHashMap!.put("C",Translate!.getTranslation("AON_CURRENT/NEXT"))
		filterHashMap!.put("E",Translate!.getTranslation("AON_CURRENT/PRIOR/NEXT"))
		
		toolTip$ = Translate!.getTranslation("AON_SELECT_BASIS_FOR_COMPARISON")
		dockLocation = DashboardWidget.getDOCK_LEFT()
		filter! = incomeComparisonBarDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.selectFilter("A")
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnGLCMPINC_BARFilterSelect")

		filterName$ = Translate!.getTranslation("AON_PERYEAR")
		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("0",Translate!.getTranslation("AON_BY_PERIOD"))
		filterHashMap!.put("1",Translate!.getTranslation("AON_BY_YEAR"))
		
		toolTip$ = Translate!.getTranslation("AON_SELECT_COMPARISON_FOR_PERIOD_OR_YEAR")
		dockLocation = DashboardWidget.getDOCK_RIGHT()
		filter! = incomeComparisonBarDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.selectFilter("0")
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnGLCMPINC_BARFilterSelect")

		gosub update_meter
	endif

rem ==============================================
rem --- create bank acct balances bar chart
rem ==============================================

	dashboard_menu_id$="ADD_GLBNKBAL_BAR"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("GL")="Y"
		name$="GLBNKBAL_BAR"
		title$ = Translate!.getTranslation("AON_BANK_ACCOUNT_BALANCES")
		previewText$=Translate!.getTranslation("AON_BANK_ACCOUNT_BALANCES_DISPLAYED_IN_A_BAR_CHART")
		previewImage$=preview_path$+"glbnkbal_bar.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = ""
		flat=0 
		orientation=BarChartWidget.getORIENTATION_VERTICAL() 
		legend=0
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		include_type$="A"; rem "Include Current Year Actual totals (no data/period filtering)

		sql$="CALL GLBNKBAL_BAR ('"+firm_id$+"', '"+include_type$+"', '"+masks$+"', '"+barista_wd$+"')"
		
		bankacctBarChartDashboardWidget! = acctDashboardCategory!.addBarChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,flat,orientation,legend,connectString$,sql$)
		acctWidget! = bankacctBarChartDashboardWidget!.getWidget()
		acctWidget!.setDomainLabelAngle(1)
rem		acctWidget!.setChartColors("#71a5d4","#6fd37c",1.0,1.0)
rem		acctWidget!.setChartRangeColors("#71a5d4","#6fd37c",1.0,1.0)

		gosub update_meter
	endif

rem ==============================================
rem --- create bank acct bal ring chart
rem ==============================================

	dashboard_menu_id$="ADD_GLBNKBAL_RNG"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("GL")="Y"
		name$="GLBNKBAL_RNG"
		title$ = Translate!.getTranslation("AON_BANK_BALANCES_RINGCHART")
		previewText$=Translate!.getTranslation("AON_BANK_ACCOUNT_BALANCES_DISPLAYED_IN_A_PIE_CHART")
		previewImage$=preview_path$+"glbnkbal_rng.png"
		chartTitle$ = ""
		flat=1 
		legend=0		
		connectString$=aon_url$
				
		rem --- params for calling SPROC
		include_type$="A"; rem "Include Current Year Actual totals
		min_amt = 600; rem Hardcoded for now

		sql$="CALL GLBNKBAL_RNG ('"+firm_id$+"', '"+include_type$+"', '"+str(min_amt)+"', '"+masks$+"', '"+barista_wd$+"')"

		bankacctRingChartDashboardWidget! = acctDashboardCategory!.addRingChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,flat,legend,connectString$,sql$)
		
		rem Modify the chart
		ringChartWidget! = bankacctRingChartDashboardWidget!.getWidget()
		ringChartWidget!.setExplodeAllSlicesPercent(.025)
		ringChartWidget!.setGradientShadingAmount(.0)
rem		ringChartWidget!.setChartColors(ringChartWidget!.getColorTheme(ChartWidget.getColorThemeGreen()))
		ringChartWidget!.setRingDepth(.55)
	 
		rem Ensure the labels aren't truncated by increasing the padding around the plot by 10%
		ringChartWidget!.setPlotPadding(0.1)

		rem Create Filters 
		filterName$ = Translate!.getTranslation("AON_BANK_BALANCES_RINGCHART_-_TIMEFRAME")
		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("A",Translate!.getTranslation("AON_CURRENT_YEAR"))
		filterHashMap!.put("C",Translate!.getTranslation("AON_PREVIOUS_YEAR"))
		toolTip$ = Translate!.getTranslation("AON_SELECT_TIME_FRAME")
		dockLocation = DashboardWidget.getDOCK_LEFT()
		filter! = bankacctRingChartDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnGLBNKBAL_RNGFilterSelect")
		
		gosub update_meter
	endif

rem ==============================================
rem --- Create an Expense Breakdown Piechart
rem ==============================================

	dashboard_menu_id$="ADD_GLEXPTOT_PIE"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("GL")="Y"
		name$="GLEXPTOT_PIE"
		title$ = Translate!.getTranslation("AON_YTD_EXPENSE_BREAKDOWN")
		previewText$=Translate!.getTranslation("AON_EXPENSE_YEAR_TO_DATE_BREAKDOWN_DISPLAYED_IN_A_PIE_CHART")
		previewImage$=preview_path$+"glexptot_pie.png"
		chartTitle$ = ""
		flat = 0
		legend=0
		numSlices=8
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		include_type$="A"; rem "Include Current Year Actual totals (no data/period filtering)
		acct_type$="E"; rem "Expense accounts only
		do_coa_join$="N"; rem "No Op. Chart of Acocunts is integral part of this SPROC

		sql$="CALL GLEXPTOT_PIE ('"+firm_id$+"', '"+include_type$+"', '"+acct_type$+"', '"+do_coa_join$+"', '"+masks$+"', '"+barista_wd$+"')"

		expPieChartDashboardWidget! = acctDashboardCategory!.addPieChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,flat,legend,connectString$,sql$)
		expPieWidget! = expPieChartDashboardWidget!.getWidget()
rem		expPieWidget!.setChartColors("#860102,#cc2200,#e14900,#fe7600,#ff9c01,#fdce2e")
		expPieWidget!.setGradientShadingAmount(.05)
rem		chartColors! = expPieWidget!.getCustomChartColors()
rem		ChartWidget.adjustColorVectorBrightness(chartColors!, 0.35)
rem		ChartWidget.adjustColorVectorOpacity(chartColors!, 0.75)
rem		expPieWidget!.setCustomChartColors(chartColors!)

        rem Ensure the labels aren't truncated by increasing the padding around the plot by 10%
        expPieWidget!.setPlotPadding(0.1)

		text$ = Translate!.getTranslation("AON_CHART_OF_ACCOUNTS") 
		toolTip$ = Translate!.getTranslation("AON_LAUNCH_QUERY_ON_GL_CHART_OF_ACCOUNTS") 
		dockLocation = DashboardWidget.getDOCK_LEFT()
		browseOnSelect = 0
		link! = expPieChartDashboardWidget!.addLink(url$,text$,toolTip$,dockLocation,browseOnSelect)
		link!.setCallback(DashboardWidgetLink.getON_LINK_SELECT(),pgm(-2) + "::OnGLEXPTOT_PIEAppLink")
		
		gosub update_meter
	endif

	
rem =======================================
rem --- create GL Income vs Expense Area chart
rem =======================================

	dashboard_menu_id$="ADD_GLINCEXP_ARE"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("GL")="Y"
		name$ = "GLINCEXP_ARE"
		title$ = Translate!.getTranslation("AON_INCOME_VS_EXPENSE")
		previewText$=Translate!.getTranslation("AON_COMPARISON_OF_GENERAL_LEDGER_INCOME_AND_EXPENSE_MONTHLY_TOTALS_DISPLAYED_FOR_A_SELECTED_YEAR_IN_AN_AREA_CHART")
		previewImage$=preview_path$+"glincexp_are.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = Translate!.getTranslation("AON_$_IN_1000S")
		flat=1 
		orientation=LineChartWidget.getORIENTATION_VERTICAL() 
		legend=1
		connectString$=aon_url$
		
		rem --- params for calling SPROC

		include_type$ = "A"; rem Default to Current year

		sql$="CALL GLINCEXP_ARE ('"+firm_id$+"', '"+include_type$+"', '"+masks$+"', '"+barista_wd$+"')"

		incExpAAreaChartDashboardWidget! = acctDashboardCategory!.addAreaChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,orientation,legend,connectString$,sql$)
		incExpAWidget! = incExpAAreaChartDashboardWidget!.getWidget()
		
		incExpAWidget!.setFontScalingFactor(0.45)
		incExpAWidget!.setDomainLabelAngle(1)

		rem Create Filters 
		filterName$ = Translate!.getTranslation("AON_COMPARE_INCOME_TO_EXPENSE_TIME_FRAME")
		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("A",Translate!.getTranslation("AON_CURRENT_YEAR"))
		filterHashMap!.put("B",Translate!.getTranslation("AON_NEXT_YEAR"))
		filterHashMap!.put("C",Translate!.getTranslation("AON_PREVIOUS_YEAR"))
		toolTip$ = Translate!.getTranslation("AON_SELECT_TIME_FRAME")
		dockLocation = DashboardWidget.getDOCK_LEFT()
		filter! = incExpAAreaChartDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnGLINCEXP_AREFilterSelect")
	
		gosub update_meter

	endif
	
	return

rem =========================================================================================================================
rem  create sales tab and widgets
rem  this section defines the sales category/tab, and the several widgets it contains
rem =========================================================================================================================
 
rem ==============================================
create_sales_tab_and_widgets:
rem ==============================================
	
	declare DashboardCategory   salesDashboardCategory!
	
	salesDashboardCategory! = aonDashboard!.addDashboardCategory("Sales",Translate!.getTranslation("AON_SALES"))
rem	salesDashboardCategory!.setMinWidgetWidth(500)
rem	salesDashboardCategory!.setMaxWidgetWidth(2000)

rem ==============================================
rem --- create SA Top Customers bar chart
rem ==============================================

	dashboard_menu_id$="ADD_SATOPCST_BAR"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("SA")="Y"
		name$="SATOPCST_BAR"
		title$ = Translate!.getTranslation("AON_TOP_CUSTOMERS")
		previewText$=Translate!.getTranslation("AON_TOP_5_CUSTOMERS_DISPLAYED_IN_A_BAR_CHART")
		previewImage$=preview_path$+"satopcst_bar.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = ""
		flat=0 
		orientation=BarChartWidget.getORIENTATION_VERTICAL() 
		legend=0
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		year$ = proc_date$(7,4)
		num_to_list$ = "5"; rem <<============= HARDCODED =========
	
		sql$="CALL SATOPCST_BAR ('"+firm_id$+"', '"+year$+"', '"+num_to_list$+"', '"+masks$+"', '"+barista_wd$+"')"
		
		topCustsBarChartDashboardWidget! = salesDashboardCategory!.addBarChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,flat,orientation,legend,connectString$,sql$)
		topCustsWidget! = topCustsBarChartDashboardWidget!.getWidget()
		topCustsWidget!.setFontScalingFactor(0.45)
		topCustsWidget!.setDomainLabelAngle(1)
rem		topCustsWidget!.setChartColors("#71a5d4","#6fd37c",1.0,1.0)
rem		topCustsWidget!.setChartRangeColors("#71a5d4","#6fd37c",1.0,1.0)

		gosub update_meter
	endif


rem ==============================================
rem --- create SA Top Customers STACKED bar chart
rem ==============================================

	dashboard_menu_id$="ADD_SATOPCST_SBR"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("SA")="Y"
		name$ = "SATOPCST_SBR"
		title$ = Translate!.getTranslation("AON_PRODUCTS_FOR_TOP_CUSTOMERS")
		previewText$=Translate!.getTranslation("AON_TOP_5_CUSTOMERS_DISPLAYED_IN_A_STACKED_BAR_CHART")
		previewImage$=preview_path$+"satopcst_sbr.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = ""
		flat=0 
		orientation=BarChartWidget.getORIENTATION_HORIZONTAL() 
		legend=1
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		year$ = proc_date$(7,4)
		num_to_list$ = "5"; rem <<============= HARDCODED =========
	
		sql$="CALL SATOPCST_SBR ('"+firm_id$+"', '"+year$+"', '"+num_to_list$+"', '"+masks$+"', '"+barista_wd$+"')"

		topCustsSStackedBarChartDashboardWidget! = salesDashboardCategory!.addStackedBarChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,flat,orientation,legend,connectString$,sql$)
		topCustsSWidget! = topCustsSStackedBarChartDashboardWidget!.getWidget()
		topCustsSWidget!.setFontScalingFactor(0.45)
		topCustsSWidget!.setDomainLabelAngle(1)
rem		topCustsSWidget!.setChartColors("#71a5d4","#6fd37c",1.0,1.0)
rem		topCustsSWidget!.setChartRangeColors("#71a5d4","#6fd37c",1.0,1.0)

		gosub update_meter
	endif	

	
rem ==============================================
rem --- create SA Top Salesreps STACKED bar chart
rem ==============================================

	dashboard_menu_id$="ADD_SATOPREP_SBR"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("SA")="Y"
		name$ = "SATOPREP_SBR"
		title$ = Translate!.getTranslation("AON_PRODUCTS_FOR_TOP_SALESREPS")
		previewText$=Translate!.getTranslation("AON_PRODUCTS_FOR_TOP_SALESREPS_DISPLAYED_IN_A_STACKED_BAR_CHART")
		previewImage$=preview_path$+"satoprep_sbr.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = ""
		flat=0 
		orientation=BarChartWidget.getORIENTATION_HORIZONTAL() 
		legend=1
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		year$ = proc_date$(7,4)
		num_to_list$ = "5"; rem <<============= HARDCODED =========
	
		sql$="CALL SATOPREP_SBR ('"+firm_id$+"', '"+year$+"', '"+num_to_list$+"', '"+masks$+"', '"+barista_wd$+"')"

		toprepSStackedBarChartDashboardWidget! = salesDashboardCategory!.addStackedBarChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,flat,orientation,legend,connectString$,sql$)
		toprepWidget! = toprepSStackedBarChartDashboardWidget!.getWidget()
		
		toprepWidget!.setDomainLabelAngle(1)
		toprepWidget!.setFontScalingFactor(0.45)
rem		toprepWidget!.setChartColors("#71a5d4","#6fd37c",1.0,1.0)
rem		toprepWidget!.setChartRangeColors("#71a5d4","#6fd37c",1.0,1.0)

		gosub update_meter
	endif	

	
rem ==============================================
rem --- create SA Top Salesreps pie chart
rem ==============================================

	dashboard_menu_id$="ADD_SATOPREP_PIE"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("SA")="Y"
		name$ = "SATOPREP_PIE"
		title$ = Translate!.getTranslation("AON_TOP_SALESREPS")
		previewText$=Translate!.getTranslation("AON_TOP_SALESREPS_DISPLAYED_IN_A_PIE_CHART")
		previewImage$=preview_path$+"satoprep_pie.png"
		chartTitle$ = ""
		flat=0 
		legend=0
		numSlices=8
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		year$ = proc_date$(7,4)
		num_to_list$ = "5"; rem <<============= HARDCODED =========
	
		sql$="CALL SATOPREP_PIE ('"+firm_id$+"', '"+year$+"', '"+num_to_list$+"', '"+masks$+"', '"+barista_wd$+"')"

		toprepPPieChartDashboardWidget! = salesDashboardCategory!.addPieChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,flat,legend,connectString$,sql$)
		toprepPWidget! = toprepPPieChartDashboardWidget!.getWidget()

		toprepPWidget!.setFontScalingFactor(0.45)
rem		toprepPWidget!.setChartColors("#71a5d4","#6fd37c",1.0,1.0)
rem		toprepPWidget!.setChartRangeColors("#71a5d4","#6fd37c",1.0,1.0)
rem		toprepPWidget!.setLabelsInPieChartColor("#ffffff")
	
		gosub update_meter
	endif		

	
rem ==============================================
rem --- create SA Salesrep YTD Totals stacked bar chart
rem ==============================================

	dashboard_menu_id$="ADD_SAREPTOT_SBR"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("SA")="Y"
		name$ = "SAREPTOT_SBR"
		title$ = Translate!.getTranslation("AON_PRODUCTS_FOR_SALESREPS")
		previewText$=Translate!.getTranslation("AON_PRODUCTS_FOR_SALESREPS_DISPLAYED_IN_A_STACKED_BAR_CHART")
		previewImage$=preview_path$+"sareptot_sbr.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = Translate!.getTranslation("AON_$_IN_1000S")
		flat=0 
		orientation=BarChartWidget.getORIENTATION_VERTICAL() 
		legend=1
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		year$ = proc_date$(7,4)
		num_to_list$ = "5"; rem <<============= HARDCODED =========
	
		sql$="CALL SAREPTOT_SBR ('"+firm_id$+"', '"+year$+"', '"+num_to_list$+"', '"+masks$+"', '"+barista_wd$+"')"
	
		repsalesSStackedBarChartDashboardWidget! = salesDashboardCategory!.addStackedBarChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,flat,orientation,legend,connectString$,sql$)
		repsalesWidget! = repsalesSStackedBarChartDashboardWidget!.getWidget()
		
		repsalesWidget!.setDomainLabelAngle(1)
		repsalesWidget!.setFontScalingFactor(0.45)
rem		repsalesWidget!.setChartColors("#71a5d4","#6fd37c",1.0,1.0)
rem		repsalesWidget!.setChartRangeColors("#71a5d4","#6fd37c",1.0,1.0)
		repsalesWidget!.setLabelsInBarChartColor("#000000")

		gosub update_meter
	endif		
		
rem =======================================
rem --- create SA SA Top Salesrep line chart
rem =======================================
rem GOTO TMPSKIP_SAREPS
	dashboard_menu_id$="ADD_SATOPREP_LIN"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("SA")="Y"
		name$ = "SATOPREP_LIN"
		title$ = Translate!.getTranslation("AON_TOP_SALESREP_TOTALS")
		previewText$=Translate!.getTranslation("AON_TOP_5_SALESREPS_DISPLAYED_IN_A_LINE_CHART")
		previewImage$=preview_path$+"satoprep_lin.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = ""
		flat=1 
		orientation=LineChartWidget.getORIENTATION_VERTICAL() 
		legend=1
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		year$ = proc_date$(7,4)
		num_to_list$ = "5"; rem <<============= HARDCODED =========. Also Note: SATOPREP_LIN sets a MAX of 5
		include_type$ = "A"; rem Summarize at the rep-level. No others are currently implemented
		
		sql$="CALL SATOPREP_LIN ('"+firm_id$+"', '"+include_type$+"', '"+year$+"', '"+num_to_list$+"', '"+masks$+"', '"+barista_wd$+"')"

		topRepsLLineChartDashboardWidget! = salesDashboardCategory!.addLineChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,flat,orientation,legend,connectString$,sql$)
		topRepsLWidget! = topRepsLLineChartDashboardWidget!.getWidget()
		
		topRepsLWidget!.setFontScalingFactor(0.45)
		topRepsLWidget!.setDomainLabelAngle(1)
rem		topRepsLWidget!.setChartColors("#71a5d4","#6fd37c",1.0,1.0)
rem		topRepsLWidget!.setChartRangeColors("#71a5d4","#6fd37c",1.0,1.0)

		gosub update_meter

	endif
		

		
rem =======================================
rem --- create SA SA Top Salesrep Stacked Area chart
rem =======================================

	dashboard_menu_id$="ADD_SATOPREP_SAR"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("SA")="Y"
		name$ = "SATOPREP_SAR"
		title$ = Translate!.getTranslation("AON_TOP_SALESREPS_PERIOD_TOTALS")
		previewText$=Translate!.getTranslation("AON_TOP_5_SALESREPS'_PERIOD_TOTALS_DISPLAYED_IN_A_STACKED_AREA_CHART")
		previewImage$=preview_path$+"satoprep_sar.png"
		chartTitle$ = ""
		domainTitle$ = ""
		rangeTitle$ = ""
		flat=1 
		orientation=LineChartWidget.getORIENTATION_VERTICAL() 
		legend=1
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		year$ = proc_date$(7,4)
		num_to_list$ = "5"; rem <<============= HARDCODED =========. Also Note: SATOPREP_SAR sets a MAX of 5
		include_type$ = "A"; rem Summarize at the rep-level. No others are currently implemented
		
		sql$="CALL SATOPREP_SAR ('"+firm_id$+"', '"+include_type$+"', '"+year$+"', '"+num_to_list$+"', '"+masks$+"', '"+barista_wd$+"')"

		topRepsSAAreaChartDashboardWidget! = salesDashboardCategory!.addStackedAreaChartDashboardWidget(name$,title$,previewText$,previewImage$,chartTitle$,domainTitle$,rangeTitle$,orientation,legend,connectString$,sql$)
		topRepsSAWidget! = topRepsSAAreaChartDashboardWidget!.getWidget()
		
		topRepsSAWidget!.setFontScalingFactor(0.45)
		topRepsSAWidget!.setDomainLabelAngle(1)
		
		gosub update_meter

	endif	
		
	
rem ==============================================   
rem --- create jasper widget for Sales by Cust Type
rem --- code copied from arr_drillDownSalesReport.aon
rem ==============================================  

	dashboard_menu_id$="ADD_SLSBYCST_JAS"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("OP")="Y"
		call stbl("+DIR_PGM")+"adc_getmask.aon","CUSTOMER_ID","","","",custIdMask$,0,custIdLen
		custIdMaskLen = len(custIdMask$)

rem --- get the accounting periods

		sql$ = "SELECT TOTAL_PERS, CURRENT_PER, CURRENT_YEAR, "
		sql$ = sql$ + "PER_ENDING_01, PER_ENDING_02, PER_ENDING_03, PER_ENDING_04, PER_ENDING_05, PER_ENDING_06, "
		sql$ = sql$ + "PER_ENDING_07, PER_ENDING_08, PER_ENDING_09, PER_ENDING_10, PER_ENDING_11, PER_ENDING_12, PER_ENDING_13 "
		sql$ = sql$ + "FROM GLS_PARAMS "
		sql$ = sql$ + "WHERE FIRM_ID = '" + firm_id$ + "'"

		params! = BBjAPI().createSQLRecordSet(aon_url$,"",sql$)
		periods! = params!.getCurrentRecordData()

rem --- Assign input values to local variables
rem --- ***************** HARD-CODED FOR NOW TO BE CURRENT MONTH/YR

		period$="03"
		year$=proc_date$(7,4)

rem - establish the month 

		period = num(period$)

		firstPerEnding$ = periods!.getFieldValue("PER_ENDING_01")

		switch period
			case 1; ending$ = periods!.getFieldValue("PER_ENDING_01"); break
			case 2; ending$ = periods!.getFieldValue("PER_ENDING_02"); break
			case 3; ending$ = periods!.getFieldValue("PER_ENDING_03"); break
			case 4; ending$ = periods!.getFieldValue("PER_ENDING_04"); break
			case 5; ending$ = periods!.getFieldValue("PER_ENDING_05"); break
			case 6; ending$ = periods!.getFieldValue("PER_ENDING_06"); break
			case 7; ending$ = periods!.getFieldValue("PER_ENDING_07"); break
			case 8; ending$ = periods!.getFieldValue("PER_ENDING_08"); break
			case 9; ending$ = periods!.getFieldValue("PER_ENDING_09"); break
			case 10; ending$ = periods!.getFieldValue("PER_ENDING_10"); break
			case 11; ending$ = periods!.getFieldValue("PER_ENDING_11"); break
			case 12; ending$ = periods!.getFieldValue("PER_ENDING_12"); break
			case 13; ending$ = periods!.getFieldValue("PER_ENDING_13"); break
		swend

		month$ = ending$(1,2)

		name$="SLSBYCST_JAS"
		title$ = Translate!.getTranslation("AON_SALES_BY_CUSTOMER_TYPE")
		previewText$=Translate!.getTranslation("AON_DRILLDOWN_SALES_REPORT_FOR_MONTH/YEAR_DISPLAYED_IN_A_JASPER_VIEWER")
		previewImage$=preview_path$+"slsbycst_jas.png"
		reportFile$ = report_path$+"SalesByCustType.jasper"
		connectString$=aon_url$
		report! = new BBJasperReport(reportFile$,connectString$)

		report!.putParam("FIRM_ID",firm_id$)
		report!.putParam("FIRM_NAME",firm_name$)
		report!.putParam("MONTH",month$)
		report!.putParam("YEAR",year$)
		report!.putParam("CUST_ID_MASK", custIdMask$)
		report!.putParam("CUST_ID_LEN", str(custIdLen))
		report!.putParam("BARISTA_WD",barista_wd$)

		report!.fill()

		salesJasperViewerDashboardWidget! = salesDashboardCategory!.addJasperViewerDashboardWidget(name$,title$,previewText$,previewImage$,report!)
		
		salesJasperViewerWidget!=salesJasperViewerDashboardWidget!.getWidget()
		salesJasperViewerWidget!.setCallback(JasperViewerWidget.getSAVE_MENU_BUTTON_NAME(),BBjMenuButton.ON_BUTTON_PUSH,pgm(-2) + "::OnSLSBYCST_JASFileSave")
		salesJasperViewerWidget!.setCallback(JasperViewerWidget.getSAVE_MENU_ITEM_NAME(),BBjMenuItem.ON_POPUP_ITEM_SELECT,pgm(-2) + "::OnSLSBYCST_JASFileSave")
		salesJasperViewerWidget!.setCallback(JasperViewerWidget.getEMAIL_TOOL_BUTTON_NAME(),BBjToolButton.ON_TOOL_BUTTON_PUSH,pgm(-2) + "::OnSLSBYCST_JASEmail")

		rem Create Filters 
		filterName$ = Translate!.getTranslation("AON_MONTH","Month",1)

		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("01",Translate!.getTranslation("AON_JANUARY","January",1))
		filterHashMap!.put("02",Translate!.getTranslation("AON_FEBRUARY","February",1))
		filterHashMap!.put("03",Translate!.getTranslation("AON_MARCH","March",1))
        filterHashMap!.put("04",Translate!.getTranslation("AON_APRIL","April",1))
        filterHashMap!.put("05",Translate!.getTranslation("AON_MAY","May",1))
        filterHashMap!.put("06",Translate!.getTranslation("AON_JUNE","June",1))
        filterHashMap!.put("07",Translate!.getTranslation("AON_JULY","July",1))
        filterHashMap!.put("08",Translate!.getTranslation("AON_AUGUST","August",1))
        filterHashMap!.put("09",Translate!.getTranslation("AON_SEPTEMBER","September",1))
        filterHashMap!.put("10",Translate!.getTranslation("AON_OCTOBER","October",1))
        filterHashMap!.put("11",Translate!.getTranslation("AON_NOVEMBER","November",1))
        filterHashMap!.put("12",Translate!.getTranslation("AON_DECEMBER","December",1))
		
		toolTip$ = Translate!.getTranslation("AON_SELECT_MONTH","Select reporting month",1)
		dockLocation = DashboardWidget.getDOCK_LEFT()
		filter! = salesJasperViewerDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.selectFilter(month$)
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnSLSBYCST_JASFilterSelect")

		filterName$ = Translate!.getTranslation("AON_YEAR","Year",1)
		filterHashMap! = new LinkedHashMap()
        for xwk=1990 to 2020
            filterHashMap!.put(str(xwk),str(xwk))
		next xwk
		
		toolTip$ = Translate!.getTranslation("AON_SELECT_YEAR","Select reporting year",1)
		dockLocation = DashboardWidget.getDOCK_RIGHT()
		filter! = salesJasperViewerDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.selectFilter(year$)
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnSLSBYCST_JASFilterSelect")
	endif
	
	return

	

rem =========================================================================================================================
rem  create manufacturing tab and widgets
rem  this section defines the mfg category/tab, and the several widgets it contains
rem =========================================================================================================================
 
rem ==============================================
create_mfg_tab_and_widgets:
rem ==============================================
	
	declare DashboardCategory   mfgDashboardCategory!
	
	mfgDashboardCategory! = aonDashboard!.addDashboardCategory("Manufacturing",Translate!.getTranslation("AON_MANUFACTURING"))


rem ==============================================
rem --- create a WOs with SOs grid
rem ==============================================

	dashboard_menu_id$="ADD_SFWOSSOS_GRD"
	gosub get_security
	if allow_widget$="Y" and installMap!.get("OP")="Y" and installMap!.get("SF")="Y"
		name$="SFWOSSOS_GRD"
		title$ = Translate!.getTranslation("AON_WOS_LINKED_TO_SOS")
		previewText$=Translate!.getTranslation("AON_WORK_ORDERS_LINKED_TO_SALES_ORDERS_DISPLAYED_IN_A_GRID")
		previewImage$=preview_path$+"sfwossos_grd.png"
		connectString$=aon_url$
		
		rem --- params for calling SPROC
		wo_include_type$="A"; rem "Default to Open WOs only
		so_include_type$="A"; rem "Default to Open SOs only

		sql$="CALL SFWOSSOS_GRD ('"+firm_id$+"', '"+wo_include_type$+"', '"+so_include_type$+"', '"+masks$+"', '"+barista_wd$+"')"

		woSOGridDashboardWidget! = mfgDashboardCategory!.addGridDashboardWidget(name$,title$,previewText$,previewImage$,connectString$,sql$)
		woSOgridWidget!=woSOGridDashboardWidget!.getWidget()
		woSOgridWidget!.setColumnAlignment(3,GridWidget.getHORIZONTAL_ALIGNMENT_RIGHT())
		woSOgridWidget!.setColumnAlignment(0,GridWidget.getHORIZONTAL_ALIGNMENT_LEFT())

		rem Create Filters 
		filterName$ = Translate!.getTranslation("AON_WO_STATUS")
		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("A",Translate!.getTranslation("AON_OPEN_WOS_ONLY"))
		filterHashMap!.put("B",Translate!.getTranslation("AON_PLANNED_WOS_ONLY"))
		filterHashMap!.put("C",Translate!.getTranslation("AON_QUOTED_WOS_ONLY"))
		filterHashMap!.put("D",Translate!.getTranslation("AON_OPEN_AND_PLANNED_WOS"))
		filterHashMap!.put("E",Translate!.getTranslation("AON_PLANNED_AND_QUOTED_WOS"))
		filterHashMap!.put("F",Translate!.getTranslation("AON_OPEN_AND_QUOTED_WOS"))
		filterHashMap!.put("G",Translate!.getTranslation("AON_ALL_OPEN/PLANNED/QUOTED"))

		toolTip$ = Translate!.getTranslation("AON_SELECT_WO_STATUS")
		dockLocation = DashboardWidget.getDOCK_LEFT()
		filter! = woSOGridDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.selectFilter("A"); rem default to All
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnSFWOSSOS_GRDFilterSelect")
	
	
		filterName$ = Translate!.getTranslation("AON_SO_STATUS")
		filterHashMap! = new LinkedHashMap()
		filterHashMap!.put("A",Translate!.getTranslation("AON_OPEN_SOS_ONLY"))
		filterHashMap!.put("B",Translate!.getTranslation("AON_BACKORDERS_ONLY"))
		filterHashMap!.put("C",Translate!.getTranslation("AON_QUOTES_ONLY"))
		filterHashMap!.put("D",Translate!.getTranslation("AON_SALES_AND_BACKORDERS"))
		filterHashMap!.put("E",Translate!.getTranslation("AON_BACKORDERS_AND_QUOTES"))
		filterHashMap!.put("F",Translate!.getTranslation("AON_SALES_AND_QUOTES"))
		filterHashMap!.put("G",Translate!.getTranslation("AON_ALL_SALES/BACKORDERS/QUOTES"))
		
		toolTip$ = Translate!.getTranslation("AON_SELECT_SO_STATUS")
		dockLocation = DashboardWidget.getDOCK_RIGHT()
		filter! = woSOGridDashboardWidget!.addFilter(filterName$,filterHashMap!,toolTip$,dockLocation)
		filter!.selectFilter("A"); rem default to All
		filter!.setCallback(DashboardWidgetFilter.getON_FILTER_SELECT(),pgm(-2) + "::OnSFWOSSOS_GRDFilterSelect")
	
		gosub update_meter
		
	endif
	
	return

	
	
rem =========================================================================================================================
rem  event handlers
rem  this section contains the event handlers for the widget filters, links and jasper save/email buttons
rem =========================================================================================================================


rem ==================================================================
rem ===                     FILTERS                                ===
rem ==================================================================

		
rem ==============================================   
rem Handle filter select event for GL YrTots grid
OnGLINCTOT_GRDFilterSelect:
rem ==============================================    
rem The filter selection event for the Left filter on the Income Comparison bar graph
rem Get the information from the filter event so we'll have our dashboard widget and the inner widget
rem     The dashboard widget gives us access to the filters and links
rem     The inner widget gives us access to the dataset so that we can provide new data

	customEvent! = BBjAPI().getLastEvent()
	filterSelectEvent! = customEvent!.getObject()
	filterName$ = filterSelectEvent!.getFilterName()
	
	rem You can get the current filter value from the filter selection event
	filterKey$ = filterSelectEvent!.getFilterKey()
	
	rem Get the dashboard widget and inner widget
	incomeGridDashboardWidget! = filterSelectEvent!.getDashboardWidget()
	gridWidget! = incomeGridDashboardWidget!.getWidget()
	
	gosub get_masks
	gosub get_sysinfo
	gosub get_connectstring
	acct_type$="I"; rem "Income accounts only
	do_coa_join$="N"; rem "Change to Y to include a column for Chart of Accts Category
	include_type$=filterKey$

	sql$="CALL GLINCTOT_GRD ('"+firm_id$+"', '"+include_type$+"', '"+acct_type$+"', '"+do_coa_join$+"', '"+masks$+"', '"+barista_wd$+"')"
	
	rem Refresh based on new sql
	gridWidget!.fill(aon_url$,sql$)
	gridWidget!.refresh()

	exit

rem ==============================================   
rem Handle filter select event for Bank Acct
OnGLBNKBAL_RNGFilterSelect:
rem ==============================================    
rem The filter selection event for the Left filter on the Income Comparison bar graph
rem Get the information from the filter event so we'll have our dashboard widget and the inner widget
rem     The dashboard widget gives us access to the filters and links
rem     The inner widget gives us access to the dataset so that we can provide new data

	customEvent! = BBjAPI().getLastEvent()
	filterSelectEvent! = customEvent!.getObject()
	filterName$ = filterSelectEvent!.getFilterName()
	
	rem You can get the current filter value from the filter selection event
	filterKey$ = filterSelectEvent!.getFilterKey()
	filterValue$ = filterSelectEvent!.getFilterValue()
	
	rem Get the dashboard widget and inner widget
	bankacctRingChartDashboardWidget! = filterSelectEvent!.getDashboardWidget()
	ringChartWidget! = bankacctRingChartDashboardWidget!.getWidget()
	
	gosub get_masks
	gosub get_sysinfo
	min_amt=600; rem using same hard-coded as code that initially creates the ring chart (above)
	include_type$=filterKey$

	sql$="CALL GLBNKBAL_RNG ('"+firm_id$+"', '"+include_type$+"', '"+str(min_amt)+"', '"+masks$+"', '"+barista_wd$+"')"
	
	rem Clear out the old data from the inner widget
	ringChartWidget!.clearDataSet()
	
	rem Refresh based on new sql
	ringChartWidget!.setSQL(sql$)
    ringChartWidget!.refresh()
	
	exit

rem ==============================================   
rem Handle filter select event for Bank Acct
OnGLCMPINC_BARFilterSelect:
rem ==============================================    
rem The filter selection event for the Left filter on the Income Comparison bar graph
rem Get the information from the filter event so we'll have our dashboard widget and the inner widget
rem     The dashboard widget gives us access to the filters and links
rem     The inner widget gives us access to the dataset so that we can provide new data

	customEvent! = BBjAPI().getLastEvent()
	filterSelectEvent! = customEvent!.getObject()
	filterName$ = filterSelectEvent!.getFilterName()
	
	rem You can get the current filter value from the filter selection event
	filterKey$ = filterSelectEvent!.getFilterKey()
	filterValue$ = filterSelectEvent!.getFilterValue()
	
	rem Get the dashboard widget and inner widget
	incomeComparisonBarDashboardWidget! = filterSelectEvent!.getDashboardWidget()
	incCmpWidget! = incomeComparisonBarDashboardWidget!.getWidget()
	
    rem Get the current filter values from the dashboard widget, as that way we have access to all of them
    filterLeft! = incomeComparisonBarDashboardWidget!.getDashboardWidgetFilterLeft()
	incCmpWidgetFilterCompareKey$ = filterLeft!.getKey()
	include_type$=incCmpWidgetFilterCompareKey$;rem A,C,E
	
	filterRight! = incomeComparisonBarDashboardWidget!.getDashboardWidgetFilterRight()
	incCmpWidgetFilterCompareKey$ = filterRight!.getKey();rem 0,1
	
	gosub get_masks
	gosub get_sysinfo
	include_type$=chr(asc(include_type$)+num(incCmpWidgetFilterCompareKey$));rem A,B,C,D,E,F
	acct_type$="I"; rem "Income accounts only
	do_coa_join$="N"; rem "Change to Y to JOIN w/Chart of Accts to filter by account grouping

	sql$="CALL GLCMPINC_BAR ('"+firm_id$+"', '"+include_type$+"', '"+acct_type$+"', '"+do_coa_join$+"', '"+masks$+"', '"+barista_wd$+"')"
	
	rem Clear out the old data from the inner widget
	incCmpWidget!.clearDataSet()
	
	rem Refresh based on new sql
	incCmpWidget!.setSQL(sql$)
    incCmpWidget!.refresh()
	
	exit
	
rem ==============================================   
rem Handle filter select event for GL Income vs Expense Area Chart
OnGLINCEXP_AREFilterSelect:
rem ==============================================    
rem The filter selection event for the Left filter on the Income Comparison area chart
rem Get the information from the filter event so we'll have our dashboard widget and the inner widget
rem     The dashboard widget gives us access to the filters and links
rem     The inner widget gives us access to the dataset so that we can provide new data

	customEvent! = BBjAPI().getLastEvent()
	filterSelectEvent! = customEvent!.getObject()
	filterName$ = filterSelectEvent!.getFilterName()
	
	rem You can get the current filter value from the filter selection event
	filterKey$ = filterSelectEvent!.getFilterKey()
	filterValue$ = filterSelectEvent!.getFilterValue()
	
	rem Get the dashboard widget and inner widget
	incExpAAreaChartDashboardWidget! = filterSelectEvent!.getDashboardWidget()
	incExpAWidget! = incExpAAreaChartDashboardWidget!.getWidget()
	
	gosub get_masks
	gosub get_sysinfo
	include_type$=filterKey$

		sql$="CALL GLINCEXP_ARE ('"+firm_id$+"', '"+include_type$+"', '"+masks$+"', '"+barista_wd$+"')"
	
	rem Clear out the old data from the inner widget
	incExpAWidget!.clearDataSet()
	
	rem Refresh based on new sql
	incExpAWidget!.setSQL(sql$)
    incExpAWidget!.refresh()
	
	exit
	
rem ==============================================   
rem Handle filter select event for "WOs linked to SOs" grid
OnSFWOSSOS_GRDFilterSelect:
rem ==============================================    
rem The filter selection event for the Left filter on the "WOs linked to SOs" grid
rem Get the information from the filter event so we'll have our dashboard widget and the inner widget
rem     The dashboard widget gives us access to the filters and links
rem     The inner widget gives us access to the dataset so that we can provide new data

	customEvent! = BBjAPI().getLastEvent()
	filterSelectEvent! = customEvent!.getObject()
	filterName$ = filterSelectEvent!.getFilterName()
	
	rem You can get the current filter value from the filter selection event
	filterKey$ = filterSelectEvent!.getFilterKey()
	filterValue$ = filterSelectEvent!.getFilterValue()
	
	rem Get the dashboard widget and inner widget
	woSOGridDashboardWidget! = filterSelectEvent!.getDashboardWidget()
	woSOgridWidget! = woSOGridDashboardWidget!.getWidget()
	
    rem Get the current filter values from the dashboard widget, as that way we have access to all of them
    filterLeft! = woSOGridDashboardWidget!.getDashboardWidgetFilterLeft()
	woSOWidgetFilterCompareKey$ = filterLeft!.getKey()
	wo_include_type$=woSOWidgetFilterCompareKey$;rem A-G
	
	filterRight! = woSOGridDashboardWidget!.getDashboardWidgetFilterRight()
	woSOWidgetFilterCompareKey$ = filterRight!.getKey();rem A-G
	so_include_type$=woSOWidgetFilterCompareKey$;rem A-G
	
	gosub get_masks
	gosub get_sysinfo
	gosub get_connectstring
	
	sql$="CALL SFWOSSOS_GRD ('"+firm_id$+"', '"+wo_include_type$+"', '"+so_include_type$+"', '"+masks$+"', '"+barista_wd$+"')"
	
	rem Refresh based on new sql
	woSOgridWidget!.fill(aon_url$,sql$)	
	woSOgridWidget!.refresh()
	
	exit

rem ==============================================   
rem Handle filter select event for AR Drilldown Jasper widget
OnSLSBYCST_JASFilterSelect:
rem ==============================================    
rem The filter selection event for the filters on the AR Drilldown Jasper widget
rem Get the information from the filter event so we'll have our dashboard widget and the inner widget
rem     The dashboard widget gives us access to the filters and links
rem     The inner widget gives us access to the dataset so that we can provide new data

    customEvent! = BBjAPI().getLastEvent()
    filterSelectEvent! = customEvent!.getObject()
    filterName$ = filterSelectEvent!.getFilterName()
    
    salesJasperViewerDashboardWidget! = filterSelectEvent!.getDashboardWidget()
    salesJasperViewerWidget! = salesJasperViewerDashboardWidget!.getWidget()
    report! = salesJasperViewerWidget!.getReport()
    
    filterLeft! = salesJasperViewerDashboardWidget!.getDashboardWidgetFilterLeft()
    month$ = filterLeft!.getKey()
    report!.putParam("MONTH",month$)
    
    filterRight! = salesJasperViewerDashboardWidget!.getDashboardWidgetFilterRight()
    year$ = filterRight!.getKey()
    report!.putParam("YEAR",year$)
    
    salesJasperViewerWidget!.refresh()
    
    exit

	
	
rem ==================================================================
rem ===                      LINKS                                 ===
rem ==================================================================
	
rem ==============================================   
OnGLEXPTOT_PIEAppLink:
rem ==============================================    

	customEvent! = BBjAPI().getLastEvent()
	linkSelectEvent! = customEvent!.getObject()
	url$ = linkSelectEvent!.getURL();rem not using
	dashboardWidget! = linkSelectEvent!.getDashboardWidget()
	widget! = dashboardWidget!.getWidget()
	
	rdNSAdmin! = BBjAPI().getNamespace("adminobjects", "barista", 1)
	rdAdmin! = rdNSAdmin!.getValue("+bar_admin_" + cvs(stbl("+USER_ID"), 11))
	query_user$=rdAdmin!.getUser()
	query_pass$=rdAdmin!.getPassword()

	myConfig! = BBjAPI().getConfig().getCurrentCommandLineObject()
	myConfig!.setProgramName(stbl("+DIR_SYP")+"bax_launch_task.bbj")
	myConfig!.setProgramArgs("-yQ"+" -u"+query_user$+" -p"+query_pass$+" -qGL_ACCOUNTS")

	tmp=BBjAPI().newBBjSession(myConfig!)

	exit
	

rem ==================================================================
rem ===                     JASPER                                 ===
rem ==================================================================

rem ==============================================    
OnSLSBYCST_JASFileSave:
rem ============================================== 

rem --- retrieve jasper widget

	customEvent! = BBjAPI().getLastEvent()
	jasperViewerWidgetControlEvent! = customEvent!.getObject()
	salesJasperViewerWidget! = jasperViewerWidgetControlEvent!.getWidget()
	saveReport! = salesJasperViewerWidget!.getReport()
	if saveReport!<>null()
	
rem --- get doc path for saved pdf, and report title

		gosub get_sysinfo
		gosub doc_path
		repTitle!=sysinfo.task_desc$
		repTitle$=repTitle!.replace(" ","_")

rem --- set rd_alias_id$

		if rd_alias_id$=""
			rd_alias_id$=pgm(-2)
			rd_alias_id$=rd_alias_id$(max(pos("/"="/"+rd_alias_id$,-1),pos("\"="\"+rd_alias_id$,-1)))
			rd_alias_id$=rd_alias_id$(1,pos("."=rd_alias_id$+".")-1)
		endif    
		rd_alias_id$=cvs(rd_alias_id$,4)
		
rem --- Make Document Archive Record and Get ID

		rep_date$=date(0:stbl("+DATE_MASK"))
		rep_date_stamp$=date(0:"%Yd%Mz%Dz")
		rep_time$=date(0:"%hz:%mz %p")
		rep_time_stamp$=date(0:"%Hz%mz%sz")

		rd_source_alias$=rd_alias_id$
		rd_source_type$="O"
		rd_doc_source$="E"
		rd_doc_ext$="JAS"
		rd_archive_action$="DOC_ID-NOREPRINT"
		gosub document_whse
		rd_archive_action$=""	

		rd_sv_path$=rd_doc_path$
		if rpt_pg>0
			rd_doc_id$=""
			rd_doc_path$=""
			rd_doc_name$=""
			rd_doc_ext$="JAS"
			rd_archive_action$="DOC_ID-NOREPRINT"
			gosub document_whse
			rd_archive_action$=""
		endif
		rpt_pg=rpt_pg+1
		rd_doc_path$=rd_sv_path$
		rd_doc_name$=repTitle$+"_"+rd_doc_id$+"_p"+str(rpt_pg)+".pdf"
		rd_doc_ext$="PDF"
		saveReport!.exportToPDF(BBjAPI().FALSE, rd_doc_path$+rd_doc_name$)
		gosub document_whse
	endif
    
	if email_event then return else exit

rem ============================================== 
OnSLSBYCST_JASEmail:
rem ============================================== 
rem --- Add document to fax/email queue
rem --- force exportToPDF first, in case it hasn't already been saved
  
    rem --- force exportToPDF in case not already saved
	email_event=1
    gosub OnSLSBYCST_JASFileSave
	email_event=0

    if rd_doc_id$<>"" and rd_doc_ext$<>""
        call stbl("+DIR_SYP")+"bac_faxemail_jasper.bbj",rd_doc_id$,rd_doc_ext$,rd_table_chans$[all]
    endif

    exit
	
	
rem =========================================================================================================================
rem  misc. subroutines
rem =========================================================================================================================

rem ==============================================   
init_prog_meter:
rem ============================================== 

	num_recs=5;rem current count of widgets being built
	progress! = bbjAPI().getGroupNamespace()
	progress!.setValue("+process_task",task_id$+"^C^"+sysinfo.task_desc$+"^CNC^"+str(num_recs)+"^")
	curr_rec=0
	
	return
	
rem ==============================================   
resolve_paths:
rem ============================================== 
  
rem --- resolve path to preview image pngs

    preview_path$=stbl("+CUST_IMAGES",err=*next)
	sv_dir$=dsk("")+dir("")
	chdir preview_path$
	preview_path$=dsk("")+dir("")
	chdir sv_dir$

rem --- resolve path to jasper reports

    report_path$=stbl("+DIR_REPORTS",err=*next)
	sv_dir$=dsk("")+dir("")
	chdir report_path$
	report_path$=dsk("")+dir("")
	chdir sv_dir$
	
	return

rem ==============================================   
get_connectstring:
rem ==============================================

    dbserver$="localhost"
    dbsqlport$=":2001" 
    dbserver$=stbl("+DBSERVER",err=*next)
    dbsqlport$=":"+stbl("+DBSQLPORT",err=*next)
    dbssl=num(stbl("+DBSSL",err=*next))
    
    if dbssl
        dbssl$="&ssl=true"
    else
        dbssl$="&ssl=false"
    endif

    url_user$="&user=guest"
    if stbl("!DSUDDB",err=*endif)<>"" then
        url_user$=""
    endif

    dbname$ = stbl("+DBNAME")
    dbname_api$ = stbl("+DBNAME_API")
    if pos("jdbc:apache"=cvs(dbname$,8))=1 then
        url$ = dbname$
    else
        if pos("jdbc:"=cvs(dbname$,8))=1 then			
            url$=dbname$+url_user$+dbssl$
        else
            url$ = "jdbc:basis:"+dbserver$+dbsqlport$+"?database="+dbname_api$+url_user$+dbssl$
        endif
    endif
	
	aon_url$=url$
	
	return

rem ==============================================   
get_sysinfo:
rem ==============================================
 
	sysinfo_tpl$=stbl("+SYSINFO_TPL",err=*next)
	dim sysinfo$:sysinfo_tpl$
	sysinfo$=stbl("+SYSINFO",err=*next)
	user_id$=sysinfo.user_id$
	proc_date$=date(jul(num(sysinfo.system_date$(1,4)),num(sysinfo.system_date$(5,2)),num(sysinfo.system_date$(7,2))):stbl("+DATE_MASK"))
	firm_id$=sysinfo.firm_id$
	firm_name$=sysinfo.firm_name$
	task_description$=cvs(sysinfo.task_desc$,2)
	task_id$=sysinfo.task_id$

	barista_wd$=dsk("")+dir("")
	sproc_dir$=stbl("+DIR_SPROCS",err=*next)
	
	return

rem ==============================================   
get_translate_object:
rem ==============================================
    rem --- Open/Lock files
    files=1,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="adm_modules.dat",options$[1]="S",ids$[1]="ADM_MODULES";rem "S"=System file  
    
    call stbl("+DIR_PGM",err=*next)+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    
    rd_adm_modules=channels[1]; dim rd_adm_modules$:templates$[1]
    readrecord(rd_adm_modules,key=pad(sysinfo.lic_feature$,11),dom=*endif)rd_adm_modules$
    
    if rd_adm_modules.locale_resource$<>"" and rd_adm_modules.locale_path$<>""
        locale_path$=rd_adm_modules.locale_path$
rem        sv_dir$=dsk("")+dir("")
rem        chdir locale_path$
rem        locale_path$=dsk("")+dir("")
rem        chdir sv_dir$
        Translate!=BBTranslator.getInstance(rd_adm_modules.locale_resource$,stbl("+USER_LOCALE"),null(),locale_path$); rem ,err=*endif
        rd_temp_stbl$=stbl("+PROPS_NAME",rd_adm_modules.locale_resource$)
        rd_temp_stbl$=stbl("+PROPS_PATH",locale_path$)
    endif
  
    return

rem ==============================================   
get_masks:
rem ==============================================    

	call stbl("+DIR_PGM")+"adc_getmask.aon","","AR","I","",msk$,0,0
	masks$=masks$+"cust_mask^"+msk$+"|"
	call stbl("+DIR_PGM")+"adc_getmask.aon","","AP","I","",msk$,0,0
	masks$=masks$+"vendor_mask^"+msk$+"|"
	call stbl("+DIR_PGM")+"adc_getmask.aon","","AD","U","",msk$,0,0
	masks$=masks$+"ad_units_mask^"+msk$+"|"
	call stbl("+DIR_PGM")+"adc_getmask.aon","","GL","A","",msk$,0,0
	masks$=masks$+"gl_amt_mask^"+msk$+"|"
	call stbl("+DIR_PGM")+"adc_getmask.aon","","GL","I","",msk$,0,0
	masks$=masks$+"gl_acct_mask^"+msk$+"|"
	
	return

rem ==============================================
get_security:
rem get Barista security settings for this widget
rem ==============================================

	allow_widget$=""
    call stbl("+DIR_SYP")+"bac_getsecurity.bbj",
:       user_id$,
:       dashboard_menu_id$,
:       "",
:       firm_id$,
:       temp_array$[all],
:       access$[all],
:       table_chans$[all],
:       "ACCESS",
:       role_status$

    if role_status$<>"INVALID"
        allow_widget$=iff(pos("Y"=access$[0]),"Y","")
    endif

	return

rem ==============================================   
rem Update progress meter
update_meter:
rem ==============================================

	curr_rec=curr_rec+1
	progress!.setValue("+process_task",task_id$+"^U^"+str(curr_rec)+"^")	
	
	return

rem ==============================================
document_whse:
rem --- Write record to Barista document warehouse
rem ==============================================

    call stbl("+DIR_SYP")+"bac_documents.bbj",
:       rd_doc_id$,
:       rep_date_stamp$,
:       rep_time_stamp$,
:       rd_doc_source$,
:       rd_doc_ext$,
:       rd_doc_path$,
:       rd_source_type$,
:       rd_source_alias$,
:       rd_source_id$,
:       rd_source_ref$,
:       rd_table_chans$[all],
:       rd_archive_action$,
:       rd_doc_name$,
:       rep_title$,
:       rd_doc_keywords$

    return
rem ==============================================
doc_path:rem --- Get Document Path
rem ==============================================

    if rd_doc_path$="" then
        rd_tmp_dir$=stbl("+DOC_DIR_PDF",err=*endif)
        rd_temp_wd$=dsk("")+dir("")
        if pos(":"=rd_tmp_dir$) setdrive rd_tmp_dir$(1,pos(":"=rd_tmp_dir$)-1),err=*next
        chdir rd_tmp_dir$,err=*next
        rd_doc_path$=dsk("")+dir("")
        if pos(":"=rd_temp_wd$) setdrive rd_temp_wd$(1,pos(":"=rd_temp_wd$)-1)
        chdir rd_temp_wd$
    endif

    return

rem ==============================================
get_installed_modules:rem --- which apps installed
rem ==============================================

	installMap! = new HashMap()
	modules$="ADAPARBMGLIVMPOPPOPRSASF"

    dim info$[20]
    for i=1 to len(modules$) step 2
        mod$=modules$(i,2)
        call stbl("+DIR_PGM")+"adc_application.aon",mod$,info$[all]
        installMap!.put(mod$,info$[20])
    next i
	
	return

rem ==============================================
std_error: rem --- Standard error handler
rem ==============================================

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem ==============================================
std_exit: rem --- Standard program end
rem ==============================================

    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release

	

