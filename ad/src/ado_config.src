rem ==========================================================================
rem --- Cofig file class
rem --- Program ado_config.src v8.0.0 13Apr2009
rem --- Copyright 2008-2009, BASIS International, Ltd.
rem ==========================================================================

use ::ado_func.src::func

class public Config

	rem ==========================================================================
	rem --- Properties (fields)
	rem ==========================================================================
	
	field private BBjString ClassName$ = "Config"
	field public BBjString ConfigFileName$
	field public BBjVector ConfigLines!
	field public BBjVector Aliases!
	field public BBjVector AliasBBxNames!

	rem ==========================================================================
	rem --- Constructor
	rem ==========================================================================
	
	method public Config()
		#ConfigFileName$ = BBjAPI().getConfig().getConfigFileName()
		#ConfigLines!    = BBjAPI().makeVector()
		#Aliases!        = BBjAPI().makeVector()
		#AliasBBxNames!  = BBjAPI().makeVector()
		
		config_dev = unt
		open(config_dev, err=config_no_file) #ConfigFileName$
		ignore = mask("", "^[Aa][Ll][Ii][Aa][Ss][ 	]")
		
		while 1
			read (config_dev, end=*break) line$
			#ConfigLines!.addItem(line$)
			
			if mask(cvs(line$, 1+2)) then
				#Aliases!.addItem(cvs(line$, 1+2))
			endif
		wend
		
		methodret
		
	config_no_file:
		throw #ClassName$ +": cannot open " +#ConfigFileName$, 12
		
	methodend
	
	rem --- Alias BBxNames
	method public BBjVector getAliasBBxNames()
	
		if #AliasBBxNames!.size() then
			methodret #AliasBBxNames!
		endif
	
		declare BBjVector bbxNames!
		bbxNames! = BBjAPI().makeVector()
		
		for i=0 to #Aliases!.size()-1
			line$ = str(#Aliases!.getItem(i))
			p  = func.minimumNotZero(pos(" " = line$), pos("	" = line$))
			
			if p then
				line$ = cvs(line$(p+1), 1)
				p  = func.minimumNotZero(pos(" " = line$), pos("	" = line$))
				if p then
					bbxNames!.addItem( cvs(line$(1, p-1), 1+2) )
				endif
			endif
		next i
		
		#AliasBBxNames! = bbxNames!
		methodret bbxNames!
	
	methodend
	
	rem --- Is string a BBx Alias Name?
	method public BBjInt isAlias(BBjString p_name$)
		#getAliasBBxNames()
		found% = 0
		
		for i=0 to #AliasBBxNames!.size()-1
			if p_name$ = str(#AliasBBxNames!.getItem(i)) then
				found% = 1
				break
			endif
		next i
		
		methodret found%
	methodend
	
classend

rem ==========================================================================
rem --- Testing
rem ==========================================================================

	declare Config config!
	config! = new Config()
	declare BBjVector aliases!
	aliases! = BBjAPI().makeVector()

	aliases! = config!.getAliasBBxNames()
	print "Alias BBxNames:"

	for i=0 to aliases!.size()-1
		print aliases!.getItem(i)
	next i
	
	x$ = "PD"
	print x$,
	
	if config!.isAlias(x$) then
		print " is an alias"
	else
		print " is not an alias"
	endif


end	