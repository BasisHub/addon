rem ==========================================================================
rem --- Object representation of an open file and methods to work on it
rem --- Program ado_file.src v8.0.0 01Jan2010
rem --- Copyright 2008-2010, BASIS International, Ltd.
rem ==========================================================================

    use java.util.HashMap

    class public FileObject
    
        field private BBjString fileAlias$
        field private BBjString fileName$
        field private BBjString fullPath$
        field private BBjString fileDesc$
        field private BBjString fileTmpl$
        field private BBjTemplatedString fileRec!
        field private BBjString key$
        field private BBjInt channel%
        field private BBjInt eof% = -1
        field private BBjNumber notFound
        
        field private static HashMap openFiles! = new HashMap()
        
    rem --- Constants
        
        field private static BBjNumber START_BLOCK = 1
        field private static BBjNumber TRUE = 1
        field private static BBjNumber FALSE = 0
        
    rem --- Barista table data
        
        field private static BBjInt ddmTablesChan% = 0
        field private static BBjInt ddmTableTplsChan% = 0
        field private static BBjString baristaDataPath$
    
    rem ==========================================================================
    rem --- Constructor (private, use getInstance)
    rem ==========================================================================
    
        method private FileObject(BBjString p_alias$)
        
            #setBaristaTables()

            found = 0
            alias$ = cvs( p_alias$, 2 )
            
            while !found
            
            rem --- Get table data by alias
            
                if #START_BLOCK then
                    read (#ddmTablesChan%, key=pad(p_alias$, 16), dom=*endif ) *, file_desc$, file_name$, file_path$
                    found = 1
                    #fileName$ = file_name$
                    #fileAlias$ = alias$
                    #fileDesc$ = file_desc$
                    
                    if pos( "[" = file_path$ ) = 1 and pos( "]" = file_path$ ) = len(file_path$) then
                        path$ = ""
                        path$ = stbl( file_path$(2, len(file_path$) - 2), err=*next )
                        #fullPath$ = #addFinalSlash(path$) + #fileName$
                    else
                        #fullPath$ = #addFinalSlash(file_path$) + #fileName$
                    endif
                endif
                
            rem --- If this is a data file name, lookup the alias and retry
                
                if !found then
                    file_name$ = alias$
                    read (#ddmTablesChan%, knum=2, key=pad(file_name$, 30, $00$), dom=*next)
                    eof = 1
                    k$ = key(#ddmTablesChan%, end=*endif)
                    eof = 0
                    
                    if pos(pad(file_name$, 30, $00$) = k$) = 1 then
                        alias$ = cvs( k$(31,16), 2 )
                    else
                        throw "FileObject: could not find """ + file_name$ + """ in ddm_tables.bat", 11
                    endif
                endif
                
                if eof then
                    throw "FileObject: could not find """ + file_name$ + """ in ddm_tables.bat", 11
                endif
            wend
            
        rem --- Get the table template and setup record object
            
            found = 0
            
            if #START_BLOCK then
                read (#ddmTableTplsChan%, key=pad(alias$, 16), dom=*endif ) *, *, file_tmpl$
                found = 1
                #fileTmpl$ = file_tmpl$
            endif
            
            if !found then
                throw "FileObject: could not find template for """ + alias$ + """ in ddm_table_tpls.bat", 11
            endif
            
            #fileRec! = BBjAPI().makeTemplatedString(#fileTmpl$)
            
        methodend
    
    rem ==========================================================================
    rem --- Get instance of object here
    rem ==========================================================================
    
        method public static FileObject getInstance(BBjString p_alias$)
        
            if #openFiles!.containsKey( p_alias$ ) then
                methodret cast( FileObject, #openFiles!.get( p_alias$ ) )
            else
                methodret #openFile( new FileObject( p_alias$ ) )
            endif
            
        methodend
        
    rem ==========================================================================  
    rem --- Set the Barista table channel for ddm_tables, ddm_table_tpls
    rem ==========================================================================
    
        method private void setBaristaTables()
        
            if #baristaDataPath$ = "" then
                path$ = ""
                path$ = stbl("+DIR_BRD", err=*next)
                #baristaDataPath$ = #addFinalSlash(path$)
            endif
            
            if #ddmTablesChan% = 0
                chan = unt
                opened = 0
            
                if #START_BLOCK then
                    open (chan, err=*endif) #baristaDataPath$ + "ddm_tables.dat"
                    opened = 1
                endif
                
                if !opened then
                    throw "FileObject: could not open """ + #baristaDataPath$ + "ddm_tables.dat""", err
                endif
                
                #ddmTablesChan% = int(chan)
            endif        
            
            if #ddmTableTplsChan% = 0
                chan = unt
                opened = 0
            
                if #START_BLOCK then
                    open (chan, err=*endif) #baristaDataPath$ + "ddm_table_tpls.dat"
                    opened = 1
                endif
                
                if !opened then
                    throw "FileObject: could not open """ + #baristaDataPath$ + "ddm_table_tpls.dat""", err
                endif
                
                #ddmTableTplsChan% = int(chan)
            endif        
            
        methodend
        
    rem ==========================================================================   
    rem --- Open file and set channel
    rem ==========================================================================
    
        method private static FileObject openFile(FileObject p_file!)
            
            chan = unt
            opened = 0
            
            if #START_BLOCK then
                open (chan, err=*endif) p_file!.getFullPath()
                opened = 1
            endif
            
            if !opened then
                throw "FileObject: could not open file """ + p_file!.getFullPath() + """", err
            endif
            
            p_file!.setChannel( int(chan) )
            #openFiles!.put( p_file!.getAlias(), p_file! )
            
            methodret p_file!

        methodend
    
    rem ==========================================================================
    rem --- Get a record
    rem ==========================================================================
        
        method public BBjTemplatedString getRecord(BBjString p_key$)
            
            found = 0
            #eof% = -1
            
            if p_key$ = #key$ then
                methodret #fileRec!
            else
                rem the follow syntax is legal but throws a warning when type-checking
                read record (#channel%, key=p_key$, dom=*endif) #fileRec!
                found = 1
            endif
            
            if found then
                methodret #fileRec!
            else
                methodret null()
            endif
            
        methodend

    rem ==========================================================================
    rem --- Get a string field with a key
    rem ==========================================================================
    
        method public BBjString getStringField(BBjString p_key$, BBjString p_field$)
        
            #eof% = -1
            #getRecord(p_key$)
            methodret #getStringField(p_field$)
            
        methodend
        
    rem ==========================================================================
    rem --- Get a string field from current record
    rem ==========================================================================
        
        method public BBjString getStringField(BBjString p_field$)
        
            declare BBjString fld$
        
            if #fileRec! <> null() then
                fld$ = #fileRec!.getFieldAsString(p_field$, err=*endif)
                #notFound = #FALSE
                methodret fld$
            endif
            
            #notFound = #TRUE
            methodret ""
            
        methodend

    rem ==========================================================================
    rem --- Get a number field with a key
    rem ==========================================================================
    
        method public BBjNumber getNumberField(BBjString p_key$, BBjString p_field$)
        
            #eof% = -1
            #getRecord(p_key$)
            #getNumberField(p_field$)
            
        methodend
        
    rem ==========================================================================
    rem --- Get a number field from current record
    rem ==========================================================================
        
        method public BBjNumber getNumberField(BBjString p_field$)
        
            declare BBjNumber fld
        
            if #fileRec! <> null() then
                fld = #fileRec!.getFieldAsNumber(p_field$, err=*endif)
                #notFound = #FALSE
                methodret fld
            endif
            
            #notFound = #TRUE
            methodret 0
            
        methodend
        
    rem ==========================================================================
    rem --- Are we at the end of the file?
    rem ==========================================================================   

        method public BBjInt hasNext()
        
            if #eof% <> -1 then
                methodret !#eof%
            else
                #eof% = 1
            
                if #START_BLOCK then
                    ignore$ = key(#channel%, end=*endif) 
                    #eof% = 0
                endif
                
                methodret !#eof%
            endif
            
        methodend
        
    rem ==========================================================================
    rem --- Partial Read
    rem ==========================================================================   
    
        method public void partialRead(BBjString p_part$)
        
            read (#channel%, key=p_part$, dom=*next)
            #eof% = -1
        
        methodend
        
    rem ==========================================================================
    rem --- Read next record; set EOF
    rem ==========================================================================   
    
        method public BBjTemplatedString readNext()
        
            #eof% = 1
        
            if #START_BLOCK then
                read record (#channel%, end=*endif) #fileRec!
                #eof% = 0
            endif
            
            if #eof% then #fileRec! = null()
            methodret #fileRec!
                
        methodend
    
    rem ==========================================================================
    rem --- Close this file's channel and remove from the open channels list
    rem ==========================================================================
        
        method public void close()
            if #channel% then close(#channel%, err=*next)
            #openFiles!.remove(#fileAlias$)
        methodend
        
    rem ==========================================================================
    rem --- Close all open channels
    rem ==========================================================================
        
        method public static void closeAll()
        
            declare FileObject file!
        
            if #ddmTablesChan% then close( #ddmTablesChan%, err=*next)
            if #ddmTableTplsChan% then close ( #ddmTableTplsChan%, err=*next)
            
            while #openFiles!.keySet().iterator().hasNext()
                alias$ = str( #openFiles!.keySet().iterator().next() )
                file! = cast( FileObject, #openFiles!.get(alias$) )
                file!.close()
                #openFiles!.remove(alias$)
            wend
            
        methodend
        
    rem ==========================================================================
    rem --- Add final slash to path
    rem ==========================================================================
    
        method public BBjString addFinalSlash(BBjString p_path$)
        
            if  p_path$ <> "" and 
:               p_path$( len(p_path$), 1 ) <> "/" and 
:               p_path$( len(p_path$), 1 ) <> "\" 
:           then 
                p_path$ = p_path$ + "/"
            endif
            
            methodret p_path$
            
        methodend
        
    rem ==========================================================================
    rem --- Accessors
    rem ==========================================================================
    
        method public BBjString getAlias()
            methodret #fileAlias$
        methodend
    
        method public BBjString getName()
            methodret #fileName$
        methodend
        
        method public BBjString getFullPath()
            methodret #fullPath$
        methodend
        
        method public BBjString getTmpl()
            methodret #fileTmpl$
        methodend
        
        method public BBjString getDesc()
            methodret #fileDesc$
        methodend
        
        method public BBjTemplatedString getRecord()
            methodret #fileRec!
        methodend
        
        method public BBjInt getChannel()
            methodret #channel%
        methodend
        
        method private void setChannel(BBjInt p_chan%)
            #channel% = p_chan%
        methodend
        
        method public BBjNumber notFound()
            methodret #notFound
        methodend
    
    classend

rem ==========================================================================    
rem --- Testing
rem ==========================================================================    

    use ::ado_file.src::File
    declare FileObject vendMast!
    declare BBjTemplatedString rec!

    vendMast! = FileObject.getInstance("APM_VENDMAST")
    print "File name for ", vendMast!.getAlias(), ": ", vendMast!.getName()
    print "Description: ", vendMast!.getDesc()
    print "Template: ", vendMast!.getTmpl()
    
    firm_id$ = "01"
    vend_id$ = "000100"
    rec! = vendMast!.getRecord(firm_id$ + vend_id$)
    print "Record for " + firm_id$ + vend_id$ + ":"
    print rec!.getString()
    print "Is """, vend_id$, """ missing?", vendMast!.notFound()
    print "Vendor name from rec!: ", rec!.getFieldAsString("vendor_name")
    print "Vendor name from object with key: ", vendMast!.getStringField(firm_id$ + vend_id$, "vendor_name")
    print "Vendor name from ojbect w/o key: " , vendMast!.getStringField("vendor_name")
    print "Try to find ""foo"" ", vendMast!.getStringField("foo")
    print "Is ""foo"" missing?", vendMast!.notFound()
    vend_id$="123456"
    print "Look for vendor """, vend_id$, """"
    rec! = vendMast!.getRecord(firm_id$ + vend_id$)
    print "Is rec! null?", rec! = null()
    print "Is """, vend_id$, """ missing?", vendMast!.notFound()
    
rem --- Test a loop using only the object

    print ""
    rem input "Loop using only the vendMast! object, press <enter> ", *
    
    vendMast!.partialRead(firm_id$)
    
    while vendMast!.readNext() <> null()
        if vendMast!.getStringField("firm_id") <> firm_id$ then break
        print "Vendor: ", vendMast!.getStringField("vendor_id"), ", ", vendMast!.getStringField("vendor_name")
    wend
    
rem --- Another way using rec!

    print ""
    rem input "Loop using the rec! object, press <enter> ", *

    vendMast!.partialRead(firm_id$)
    
    while 1
        rec! = vendMast!.readNext() 
        if vendMast!.hasNext() = 0 then break
        if rec!.getFieldAsString("firm_id") <> firm_id$ then break
        print "Vendor: ", rec!.getFieldAsString("vendor_id"), ", ", rec!.getFieldAsString("vendor_name")
    wend
    
rem --- Yet another way using rec$

    print ""
    rem input "Loop using traditional reads, press <enter> ", *

    dim rec$:vendMast!.getTmpl()
    read (vendMast!.getChannel(), key=firm_id$, dom=*next)
    
    while 1
        read record(vendMast!.getChannel(), end=*break) rec$
        if rec.firm_id$ <> firm_id$ then break
        print "Vendor: ", rec.vendor_id$, ", ", rec.vendor_name$
    wend

rem --- Simple class to list vendors

    class public VendorList
    
        field private BBjString firmId$
        field private FileObject vendMast!
        
        method public VendorList(BBjString p_firm_id$)
        
            #firmId$ = p_firm_id$
            #vendMast! = FileObject.getInstance("APM_VENDMAST")
            
        methodend
        
        method public void listIt()
        
            #vendMast!.partialRead(#firmId$)
            
            while #vendMast!.readNext() <> null()
                if #vendMast!.getStringField("firm_id") <> #firmId$ then break
                print "Vendor: ", #vendMast!.getStringField("vendor_id"), ", ", #vendMast!.getStringField("vendor_name")
            wend
            
        methodend
        
        method public BBjInt getChan()
            methodret #vendMast!.getChannel()
        methodend
    
    classend
    
rem --- now list firm one and two

    declare VendorList firmOne!
    declare VendorList firmTwo!
    
    firmOne! = new VendorList("01")
    firmTwo! = new VendorList("02")
    
    print 'lf',"List firm 01 from object:"
    firmOne!.listIt()
    
    print 'lf',"List firm 02 from object:"
    firmTwo!.listIt()
    
    print "These should be the same:"
    print firmOne!.getChan()
    print firmTwo!.getChan()

    escape
    
    FileObject.closeAll()

    end
    