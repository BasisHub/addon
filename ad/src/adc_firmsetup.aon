rem ==========================================================================
rem --- adc_firmsetup.aon 
rem --- Setup new firm with clean data files for new install, with or without
rem --- demo data.
rem --- 
rem --- AddonSoftware Version 11.0
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved
rem ==========================================================================

rem --- FORMDATA!   : Property object with form data from Barista's Option! object (Input)
rem --- DEMODATA%   : 0=Without demo data/1=With demo data (Input)
rem --- RDFORM!     : Parent BBjTopLevelWindow object, the form (Input)
rem --- WAIT4OK%    : Wait for user's OK when done, 0=don't wait/1=wait (Input)
rem --- STATUS      : Status code/0=No error (Returned)

    use java.io.File
    use java.util.Iterator
    use java.util.TreeMap
    use java.util.Vector
    use ::bbtranslator.bbj::BBTranslator
    use ::ado_file.src::FileObject
    use ::adc_copyaon.aon::CopyAonDir

    seterr std_error
    setesc std_error

    enter formData!,demoData%,rdForm!,wait4ok%,status

rem --- Initializations
    status=0
    BLOCKSIZE = 2^20
    dir_syp$=stbl("+DIR_SYP")
    sysGUI! = BBjAPI().getSysGui()
	declare File aFile!
	declare Iterator iter!

	rem --- Retrieve sysinfo data
    sysinfo_tpl$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_tpl$
    sysinfo$=stbl("+SYSINFO",err=*next)
    systemYear$=sysinfo.system_date$(1,4)
    systemMonth$=sysinfo.system_date$(5,2)

	rem --- Get user inputs
    dataDir$=formData!.getProperty("DATA_LOCATION")
    newFirm$=formData!.getProperty("NEW_FIRM_ID")
    firmName$=formData!.getProperty("FIRM_NAME")
    addrLine1$=formData!.getProperty("ADDR_LINE_1")
    addrLine2$=formData!.getProperty("ADDR_LINE_2")
    city$=formData!.getProperty("CITY")
    stateCode$=formData!.getProperty("STATE_CODE")
    zipCode$=formData!.getProperty("ZIP_CODE")
    countryId$=formData!.getProperty("CNTRY_ID")
    phoneNo$=formData!.getProperty("PHONE_NO")
    federalId$=formData!.getProperty("FEDERAL_ID")
    installType$=formData!.getProperty("INSTALL_TYPE")
    newInstall=num(formData!.getProperty("NEW_INSTALL"))
	
	rem --- Initialize TreeMap with files required for a clean new install
	declare Vector fileVect!
	declare TreeMap requiredFiles!
	if installType$="X" then
	    rem --- Without data
	    fileVect! = CopyAonDir.getRequiredFiles(1); rem --- Only required files listed in readme.htm
	else
	    rem --- With default parameters and codes
        fileVect! = CopyAonDir.getRequiredFiles(3); rem --- Required files listed in readme.htm plus files with firm ZZ default data
	endif
	requiredFiles! = new TreeMap()
	for i=0 to fileVect!.size()-1
	    thisFile$=fileVect!.get(i)
	    requiredFiles!.put(thisFile$,"")
	next i
    
	rem --- Flip directory path separators
    filePath$=dataDir$
    gosub fix_path
    dataDir$=filePath$

	rem --- Get aon directory location from aon/data path
	aonDir$=dataDir$(1, pos("/data"=dataDir$,-1)-1)

    rem --- Get barista directory location from aon directory
    barDir$=aonDir$(1, pos("/aon"=dataDir$,-1)-1)+"/barista"

rem --- Open files
    num_files=1
    dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
    open_tables$[1]="DDM_TABLE_TPLS",open_opts$[1]="OTA"

    gosub open_tables

    ddmTableTpls_dev=num(open_chans$[1]); dim ddmTableTpls$:open_tpls$[1]
            
rem --- Use bax_mount_sel to get rdMountVect! containing hashes of mounted system and backup directory info for use in bax_xmlrec_exp.bbj
    dev_mode$=""
    call stbl("+DIR_SYP")+"bax_mount_sel.bbj",rdMountVect!,table_chans$[all],dev_mode$

rem --- Count files to process so can size progress meter
	declare File dataDir!
	dataDir! = new File(dataDir$)

	rem --- Count requiredFiles!
	meterSize=requiredFiles!.size()

	if cvs(newFirm$,3)<>"" then
        rem --- Add 1 for creating Barista's adm_firms.dat record to new firm
        meterSize=meterSize+1

        rem --- Add 1 for copying Barista's ads_masks.dat firm ZZ records to new firm
        meterSize=meterSize+1

        rem --- Add 1 for copying Barista's ads_sequences.dat firm ZZ records to new firm
        meterSize=meterSize+1
	endif

	if newInstall and !demodata% then
        rem --- Add 2 for removing firms 01 and 02 from Barista's adm_firms.dat file
        meterSize=meterSize+2

        rem --- Add 2 for removing firms 01 and 02 from Barista's ads_masks.dat file
        meterSize=meterSize+2

        rem --- Add 2 for removing firms 01 and 02 from Barista's ads_sequences.dat file
        meterSize=meterSize+2
		
		rem --- Get/count inventory item image in aon/images directory
		declare File imagesDir!
		declare Vector imagesFiles!
		imagesDir! = new File(aonDir$ + "/images")
		if imagesDir!.listFiles()=null() then
		    imagesDir! = new File(aonDir$ + "/image")
		endif
        if imagesDir!.listFiles()<>null() then
    		imagesFiles! = new Vector(java.util.Arrays.asList(imagesDir!.listFiles()))
    		meterSize=meterSize+imagesFiles!.size()
		else
            imagesFiles! = new Vector()
		endif
		
		rem --- Get/count demo data files in aon/data directory
		declare Vector dataFiles!
        declare File syncDir!
        syncDir! = new File(dataDir$ + "/sync")
        if syncDir!.listFiles(new DataFilter())<>null() then
    		dataFiles! = new Vector(java.util.Arrays.asList(syncDir!.listFiles(new DataFilter())))
    		meterSize=meterSize+dataFiles!.size()
		else
		    dataFiles!=new Vector()
		endif
	endif
    
rem --- Start progress meter
    declare BBTranslator Translate!
    rdTransSpaceKey$=stbl("+PROPS_NAME")+"_"+stbl("+USER_LOCALE")+"_BBTranslator"
    Translate!=cast(BBTranslator,BBjAPI().getGroupNamespace().getValue(rdTransSpaceKey$,err=*next))
    if Translate!=null()
        Translate!=BBTranslator.getInstance(stbl("+PROPS_NAME"),stbl("+USER_LOCALE"),null(),stbl("+PROPS_PATH"))
        BBjAPI().getGroupNamespace().setValue(rdTransSpaceKey$,Translate!)
    endif
    meter_title$=Translate!.getTranslation("AON_NEW_FIRM_SETUP")+"..."
    meter_total_recs=meterSize
    meter_proc_recs=0
    meter_text$=""
    meter_action$="WIN-LST"
    if wait4ok% then meter_action$=meter_action$+"-OK"
    gosub disp_meter
    
rem --- Open log file
    location$ = aonDir$
    gosub open_log

rem --- When new install not using demo data, remove demo data files (except requiredFiles!)
	if newInstall and !demodata% then
		print(log_dev)""
		print(log_dev)"remove demo data files"
		iter! = dataFiles!.iterator()
		while iter!.hasNext()
			aFile! = cast(File, iter!.next())
			
			rem --- Skip directories
			if aFile!.isDirectory() then continue
		
			rem --- Remove demo data files (except requiredFiles!)
			if !requiredFiles!.containsKey(aFile!.getName()) then
				aFile!.delete()

				rem --- Increment progress meter
				meter_text$="removed: "+aFile!.getPath()
				meter_proc_recs=meter_proc_recs+1
				meter_action$="MTR-LST"
				gosub disp_meter
				print(log_dev)meter_text$
			endif
		wend
	endif

rem --- When new install not using demo data, remove firms 01 and 02 from Barista's adm_firms.dat file
    admFirms_found=0
    admFirms_dev=unt
    open(admFirms_dev,err=*next)barDir$+"/sys/data/adm_firms.dat"; admFirms_found=1
    if newInstall and !demodata% and admFirms_found then
        findrecord(ddmTableTpls_dev,key=pad(cvs("ADM_FIRMS",4),16),dom=*endif)ddmTableTpls$
        dim admFirms$:ddmTableTpls.dd_table_tpl$
        for i=1 to 2
            firm_id$=str(i:"00")
            print(log_dev)""
            print(log_dev)"remove "+firm_id$+" from Barista's adm_firms.dat file"
            readrecord(admFirms_dev,key=firm_id$,dom=*continue)admFirms$
            remove(admFirms_dev,key=firm_id$)

            rem --- Increment progress meter
            meter_text$="removed firms "+firm_id$+" from adm_firms.dat"
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        next i
    endif

rem --- Create Barista's adm_firms.dat record for new firm
    if cvs(newFirm$,3)<>"" and admFirms_found then 
        print(log_dev)""
        print(log_dev)"create Barista's adm_firms.dat record for new firm "+newFirm$
        findrecord(ddmTableTpls_dev,key=pad(cvs("ADM_FIRMS",4),16),dom=*endif)ddmTableTpls$
        dim admFirms$:ddmTableTpls.dd_table_tpl$
        admFirms.company_id$=newFirm$
        admFirms.firm_name$=firmName$
        admFirms.help_flag$="Y"
        admFirms.batch_entry$="N"
        if pos(newFirm$="0102",2) then
            admFirms.user_modified$="M"
        else
            admFirms.user_modified$="A"
        endif
        writerecord(admFirms_dev)admFirms$

        rem --- When an existing installation, create admin_backup records using bax_xmlrec_exp.bbj
        if !newInstall and rdMountVect!<>null() and rdMountVect!.size()>0 then
            rem --- Create admin_backup records for admin data (adm_firms, ads_masks and ads_sequences) changes
            exp_action$=admFirms.user_modified$
            exp_add_only$=""
            call stbl("+DIR_SYP")+"bax_xmlrec_exp.bbj","ADM_FIRMS",admFirms$,exp_action$,exp_add_only$,dev_mode$,rdMountVect!,table_chans$[all]
        endif

        rem --- Increment progress meter
        meter_text$="created adm_firms.dat record for new firm "+newFirm$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$
    endif
    close(admFirms_dev,err=*next)

rem --- When new install not using demo data, remove firms 01 and 02 from Barista's ads_masks.dat file
    adsMasks_found=0
    adsMasks_dev=unt
    open(adsMasks_dev,err=*next)barDir$+"/sys/data/ads_masks.dat"; adsMasks_found=1
    if newInstall and !demodata% and adsMasks_found then
        findrecord(ddmTableTpls_dev,key=pad(cvs("ADS_MASKS",4),16),dom=*endif)ddmTableTpls$
        dim adsMasks$:ddmTableTpls.dd_table_tpl$
        for i=1 to 2
            firm_id$=str(i:"00")
            print(log_dev)""
            print(log_dev)"remove "+firm_id$+" from Barista's ads_masks.dat file"
            read(adsMasks_dev,key=firm_id$,dom=*next)
            while 1
                masksKey$=key(adsMasks_dev,end=*break)
                if pos(firm_id$=masksKey$)<>1 then break
                readrecord(adsMasks_dev,end=*break)adsMasks$
                remove(adsMasks_dev,key=masksKey$)
            wend

            rem --- Increment progress meter
            meter_text$="removed firms "+firm_id$+" from ads_masks.dat"
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        next i
    endif

rem --- Copy Barista's ads_masks.dat firm ZZ records to new firm
    if cvs(newFirm$,3)<>"" and adsMasks_found then 
        print(log_dev)""
        print(log_dev)"copy Barista's ads_masks.dat firm ZZ records to new firm "+newFirm$
        findrecord(ddmTableTpls_dev,key=pad(cvs("ADS_MASKS",4),16),dom=*endif)ddmTableTpls$
        dim adsMasks$:ddmTableTpls.dd_table_tpl$
        read(adsMasks_dev,key="ZZ",dom=*next)
        while 1
            masksKey$=key(adsMasks_dev,end=*break)
            if pos("ZZ"=masksKey$)<>1 then break
            readrecord(adsMasks_dev)adsMasks$
            adsMasks.firm_id$=newFirm$
            if pos(newFirm$="0102",2) then
                adsMasks.user_modified$="M"
            else
                adsMasks.user_modified$="A"
            endif
            writerecord(adsMasks_dev)adsMasks$
            rem --- Re-set file pointer to next ZZ record
            read(adsMasks_dev,key=masksKey$)

            rem --- When an existing installation, create admin_backup records using bax_xmlrec_exp.bbj
            if !newInstall and rdMountVect!<>null() and rdMountVect!.size()>0 then
                rem --- Create admin_backup records for admin data (adm_firms, ads_masks and ads_sequences) changes
                exp_action$=adsMasks.user_modified$
                exp_add_only$=""
                call stbl("+DIR_SYP")+"bax_xmlrec_exp.bbj","ADS_MASKS",adsMasks$,exp_action$,exp_add_only$,dev_mode$,rdMountVect!,table_chans$[all]
            endif
        wend

        rem --- Increment progress meter
        meter_text$="copied ads_masks.dat firm ZZ records to new firm "+newFirm$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$
    endif
    close(adsMasks_dev,err=*next)

rem --- When new install not using demo data, remove firms 01 and 02 from Barista's ads_sequences.dat file
    adsSequences_found=0
    adsSequences_dev=unt
    open(adsSequences_dev,err=*next)barDir$+"/sys/data/ads_sequences.dat"; adsSequences_found=1
    if newInstall and !demodata% and adsSequences_found then
        findrecord(ddmTableTpls_dev,key=pad(cvs("ADS_SEQUENCES",4),16),dom=*endif)ddmTableTpls$
        dim adsSequences$:ddmTableTpls.dd_table_tpl$
        for i=1 to 2
            firm_id$=str(i:"00")
            print(log_dev)""
            print(log_dev)"remove "+firm_id$+" from Barista's ads_sequences.dat file"
            read(adsSequences_dev,key=firm_id$,dom=*next)
            while 1
                sequencesKey$=key(adsSequences_dev,end=*break)
                if pos(firm_id$=sequencesKey$)<>1 then break
                readrecord(adsSequences_dev,end=*break)adsSequences$
                remove(adsSequences_dev,key=sequencesKey$)
            wend

            rem --- Increment progress meter
            meter_text$="removed firms "+firm_id$+" from ads_sequences.dat"
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        next i
    endif

rem --- Copy Barista's ads_sequences.dat firm ZZ records to new firm
    if cvs(newFirm$,3)<>"" and adsSequences_found then 
        print(log_dev)""
        print(log_dev)"copy Barista's ads_sequences.dat firm ZZ records to new firm "+newFirm$
        findrecord(ddmTableTpls_dev,key=pad(cvs("ADS_SEQUENCES",4),16),dom=*endif)ddmTableTpls$
        dim adsSequences$:ddmTableTpls.dd_table_tpl$
        read(adsSequences_dev,key="ZZ",dom=*next)
        while 1
            sequencesKey$=key(adsSequences_dev,end=*break)
            if pos("ZZ"=sequencesKey$)<>1 then break
            readrecord(adsSequences_dev)adsSequences$
            adsSequences.firm_id$=newFirm$
            if pos(newFirm$="0102",2) then
                adsSequences.user_modified$="M"
            else
                adsSequences.user_modified$="A"
            endif
            writerecord(adsSequences_dev)adsSequences$
            rem --- Re-set file pointer to next ZZ record
            read(adsSequences_dev,key=sequencesKey$)

            rem --- When an existing installation, create admin_backup records using bax_xmlrec_exp.bbj
            if !newInstall and rdMountVect!<>null() and rdMountVect!.size()>0 then
                rem --- Create admin_backup records for admin data (adm_firms, ads_masks and ads_sequences) changes
                exp_action$=adsSequences.user_modified$
                exp_add_only$=""
                call stbl("+DIR_SYP")+"bax_xmlrec_exp.bbj","ADS_SEQUENCES",adsSequences$,exp_action$,exp_add_only$,dev_mode$,rdMountVect!,table_chans$[all]
            endif
        wend

        rem --- Increment progress meter
        meter_text$="copied ads_sequences.dat firm ZZ records to new firm "+newFirm$
        meter_proc_recs=meter_proc_recs+1
        meter_action$="MTR-LST"
        gosub disp_meter
        print(log_dev)meter_text$
    endif
    close(adsSequences_dev,err=*next)

rem --- When new install not using demo data, create admin_backup records using bax_sync_backup.bbj
    if newInstall and !demodata% then
        rem --- Create admin_backup records for admin data (adm_firms, ads_masks and ads_sequences) changes
        print(log_dev)""
        print(log_dev)"create admin_backup records for admin data changes"
    
        rem --- Pass bax_sync_backup.bbj the full path to the installation’s Barista directory
        call stbl("+DIR_SYP")+"bax_sync_backup.bbj","AUTO",barDir$,sysGUI!,rdForm!,null(),table_chans$[all]
    endif

rem --- Process requiredFiles!
    print(log_dev)""
    print(log_dev)"process required data files"
    iter! = requiredFiles!.keySet().iterator()
    while iter!.hasNext()
        aFile! = new File(dataDir!, cast(String, iter!.next()))
        
        rem --- Open file
        file_dev=unt
        open(file_dev,err=*continue)aFile!.getPath()

        rem --- When new install not using demo data, remove firm 01 and 02 data
        if newInstall and !demodata% then
            read(file_dev,key="01",dom=*next)
            while 1
                key$=key(file_dev,end=*break)
                if len(key$)>=2 and pos(key$(1,2)="0102",2) then
                    remove(file_dev,key=key$)
                else
                    read(file_dev,end=*break)
                endif
            wend
        endif
        
        rem --- Copy firm ZZ to new firm, if new firm not blank
        if cvs(newFirm$,3)<>"" then 
            write_dev=unt
            open(write_dev)aFile!.getPath()
            read(file_dev,key="ZZ",dom=*next)
            while 1
                key$=key(file_dev,err=*break)
                if len(key$)>=2 and key$(1,2)="ZZ"
                    readrecord(file_dev,key=key$)record$
                    writerecord(write_dev,key=newFirm$+key$(3),dom=*next)newFirm$+record$(3)
                else
                    read(file_dev,end=*break)
                endif
            wend

            rem --- Handle files with special initialization requirements 
            thisFile$=aFile!.getName()
            findrecord(ddmTableTpls_dev,key=pad(cvs(thisFile$,4),16),dom=*endif)ddmTableTpls$
            dim record$:ddmTableTpls.dd_table_tpl$
            switch (BBjAPI().TRUE)
                case thisFile$ = "ads_compinfo"
                    record.firm_id$=newFirm$
                    record.firm_name$=firmName$
                    record.addr_line_1$=addrLine1$
                    record.addr_line_2$=addrLine2$
                    record.city$=city$
                    record.state_code$=stateCode$
                    record.country_id$=countryId$
                    record.postal_code$=zipCode$
                    record.phone_no$=phoneNo$
                    record.own_tax_id$=federalId$
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "aps_params"
                    findrecord(file_dev,key=newFirm$+"AP00")record$
                    record.current_per$=systemMonth$
                    record.current_year$=systemYear$
                    record.cur_1099_yr$=systemYear$
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "aps_report"
                    record.firm_id$=newFirm$
                    record.ap$="AP"
                    record.sequence_02$="02"
                    record.name$=firmName$
                    record.addr_line_1$=addrLine1$
                    record.addr_line_2$=addrLine2$
                    record.city$=city$
                    record.state_code$=stateCode$
                    record.zip_code$=zipCode$
                    record.phone_no$=phoneNo$
                    record.federal_id$=federalId$
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "ars_params"
                    findrecord(file_dev,key=newFirm$+"AR00")record$
                    record.current_per$=systemMonth$
                    record.current_year$=systemYear$
                    record.lstcom_date$=""
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "ars_report"
                    record.firm_id$=newFirm$
                    record.ar$="AR"
                    record.sequence_02$="02"
                    record.name$=firmName$
                    record.addr_line_1$=addrLine1$
                    record.addr_line_2$=addrLine2$
                    record.city$=city$
                    record.state_code$=stateCode$
                    record.zip_code$=zipCode$
                    record.phone_no$=phoneNo$
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "gls_calendar"
                    read(file_dev,key=newFirm$,dom=*next)
                    while 1
                        thisKey$=key(file_dev,end=*break)
                        if pos(newFirm$=thisKey$)<>1 then break
                        findrecord(file_dev,key=thisKey$)record$
                        remove(file_dev,key=thisKey$,err=*next)
                    wend
                    if record.firm_id$=newFirm$ then
                        for per=1 to 13
                                locked_flag$="locked_flag_"+str(per:"00")
                                field record$,locked_flag$="N"
                                locked_date$="locked_date_"+str(per:"00")
                                field record$,locked_date$=""
                        next per
                    endif
                    record.year$=str(num(systemYear$)-1)
                    writerecord(file_dev)record$
                    record.year$=systemYear$
                    writerecord(file_dev)record$
                    record.year$=str(num(systemYear$)+1)
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "gls_financials"
                    findrecord(file_dev,key=newFirm$+"GL02")record$
                    record.period$=systemMonth$
                    record.year$=systemYear$
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "gls_params"
                    findrecord(file_dev,key=newFirm$+"GL00")record$
                    record.current_per$=systemMonth$
                    record.current_year$=systemYear$
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "ivs_params"
                    findrecord(file_dev,key=newFirm$+"IV00")record$
                    record.current_per$=systemMonth$
                    record.current_year$=systemYear$
                    writerecord(file_dev)record$
                    break
                case thisFile$ = "sfs_params"
                    findrecord(file_dev,key=newFirm$+"SF00")record$
                    record.current_per$=systemMonth$
                    record.current_year$=systemYear$
                    writerecord(file_dev)record$
                    break
                    break
                case default
                    break
            swend

            rem --- Close files
            close(write_dev)
            close(file_dev)
        
            rem --- Increment progress meter
            meter_text$="copied firm ZZ to "+newFirm$+": "+aFile!.getPath()
            meter_proc_recs=meter_proc_recs+1
            meter_action$="MTR-LST"
            gosub disp_meter
            print(log_dev)meter_text$
        endif
    wend
    
done: rem --- All done
    
    rem --- Stop progress meter
    meter_text$=""
    meter_proc_recs=meter_total_recs
    meter_action$="LST-END"
    gosub disp_meter
    
	print(log_dev)""
    print(log_dev)"Firmsetup log finished: " + date(0:"%Yd-%Mz-%Dz@%Hz:%mz:%sz")
    close(log_dev)
    
    close(ddmTableTpls_dev,err=*next)

    goto std_exit

fix_path: rem --- Flip directory path separators

    pos=pos("\"=filePath$)
    while pos
        filePath$=filePath$(1, pos-1)+"/"+filePath$(pos+1)
        pos=pos("\"=filePath$)
    wend
    return
	
disp_meter:rem --- Display Progress Meter

    rem --- sysGUI! is not declared in bam_prog_bar.bbj, so can't declare it here
    rem declare BBjSysGui sysGUI!

    rem --- rdForm! is not declared in bam_prog_bar.bbj, so can't declare it here
    rem declare BBjTopLevelWindow rdForm!

    rem --- meterWin! is not declared in bam_prog_bar.bbj, so can't declare it here
    rem declare BBjTopLevelWindow meterWin!

    call dir_syp$+"bam_prog_bar.bbj",
:       sysGUI!,
:       rdForm!,
:       meterWin!,
:       meter_title$,
:       meter_total_recs,
:       meter_proc_recs,
:       meter_text$,
:       meter_action$

    return

open_log:rem --- Open log file
            
    rem --- create logs directory at location
    filePath$ = location$ + "/logs"
    gosub fix_path
    logDir$ = filePath$
    FileObject.makeDirs(new File(logDir$))

    rem --- create and open log file
    log$ = logDir$+"/firmsetup_"+DATE(0:"%Yd%Mz%Dz")+"_"+DATE(0:"%Hz%mz")+".txt"
    erase log$,err=*next
    string log$
    log_dev=unt
    open(log_dev)log$
    
    rem --- write log header info
    print(log_dev)"Firmsetup log started: " + date(0:"%Yd-%Mz-%Dz@%Hz:%mz:%sz")
    print(log_dev)"Data location: " + dataDir$
    print(log_dev)"New firm ID: " + newFirm$
    if demoData%
        print(log_dev)"With demo data."
    else
        print(log_dev)"Without demo data."
    endif
    print(log_dev)

    return
    
open_tables:rem --- Open Tables

    call stbl("+DIR_SYP")+"bac_open_tables.bbj",
:       open_beg,
:       open_end,
:       open_tables$[all],
:       open_opts$[all],
:       open_chans$[all],
:       open_tpls$[all],
:       table_chans$[all],
:       open_batch,
:       open_status$

    if open_status$<>"" then
        msg_id$="ENTRY_OPEN_ERROR"
        dim msg_tokens$[1]
        msg_tokens$[1]=open_status$
        gosub disp_message
        rdFuncSpace!=bbjAPI().getGroupNamespace().setValue("+build_task","OFF")
        release
    endif

    return
    
disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]

    return
    
rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)

    exit

rem #endinclude std_exit.src

rem /**
rem  * FilenameFilter Class to filter ads_masks~01.xml files.
rem  */

    class public Masks01Filter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_masks~01") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter ads_masks~02.xml files.
rem  */

    class public Masks02Filter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_masks~02") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter ads_masks~ZZ.xml files.
rem  */

    class public MasksZZFilter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_masks~ZZ") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter ads_sequences~01.xml files.
rem  */

    class public Sequences01Filter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_sequences~01") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter ads_sequences~02.xml files.
rem  */

    class public Sequences02Filter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_sequences~02") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter ads_sequences~ZZ.xml files.
rem  */

    class public SequencesZZFilter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if file!.startsWith("ads_sequences~ZZ") and file!.endsWith(".xml")
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend

rem /**
rem  * FilenameFilter Class to filter only files in aon/data, not in sub-directories.
rem  */

    class public DataFilter implements java.io.FilenameFilter

        method public boolean accept(File dir!, String file!)
			if dir!.getName()="data"
				methodret Boolean.valueOf("true")
			else
				methodret Boolean.valueOf("false")
			endif
        methodend
        
    classend
    
    end