rem glu - 13 Period Financial Report Printing (Update Overlay)
rem Program glu_13period v8.0.0 04Jun2006 (glu_eb)
rem Created by adx_progconv.bbx v1.0.3 (06/04/2006 06:37:25)

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright BASIS International Ltd.  All Rights Reserved.
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

rem --- This program compiles the data necessary to produce the
rem --- reports from the Summary Activity (glm-02) file. This 
rem --- program is only run if the user has chosen NOT to reprint
rem --- existing reports. The next overlay is glr_headings, which
rem --- assembles the custom headings for the financial reports.

    setesc std_error
    seterr std_error

rem --- Initializations

    last$=""
    total_recs=0

rem --- Start progress meter

    call pgmdir$+"adc_progress.aon","NC","GLM_ACCT","building","","",0,glm01_dev,1,0,status

rem --- Clear out gle-08

    call stbl("+DIR_PGM")+"adc_clearfile.aon",gle08_dev
    call stbl("+DIR_PGM")+"adc_clearfile.aon",gle09_dev

rem --- Position GL Account Master (glm-01)

    k$=""
    if firm_id$<>"99" k$=firm_id$
    read (glm01_dev,key="",dom=*next)

rem --- Read next GL Account Master (glm-01) record

    while 1
          glm01_key$=key(glm01_dev,end=*break)
          if firm_id$<>"99"
:            if pos(firm_id$=glm01_key$)<>1 break
          read record (glm01_dev) glm01a$
          total_recs=total_recs+1
          if mod(total_recs,milestone)=0 then call stbl("+DIR_PGM")+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status

          rem --- Retrieve prior year Summary Activity (glm-02) record

          dim ta[mp*3],tu[mp*3]
          amount=0
          units=0
          number=0
          this_year$=prior_year$
          gosub summary_activity

          rem --- Fill prior year buckets

          first_col=period+(13-fiscal_pers)
          last_col=fiscal_pers+1
          rem --- Adjustment for different number of periods in fiscal years
          first_col=first_col-(fiscal_pers-prior_year_pers)
          last_col=last_col-(fiscal_pers-prior_year_pers)
          for x=first_col to last_col
              ta[number]=a[x-1]
              tu[number]=u[x-1]
              number=number+1
          next x
          rem --- Retrieve current year Summary Activity (glm-02) record

          this_year$=begyear$
          gosub summary_activity

          rem --- Fill current year buckets

          for x=1 to period
              ta[number]=a[x]
              tu[number]=u[x]
              number=number+1
          next x

          rem --- Set normal sign based on account type

          sign=1
          if pos(glm01a.gl_acct_type$="LCI") sign=-1

          rem --- Update 13 Period Financial Report Amount (gle-08) records

          if firm_id$="99" read (glm21_dev,key=glm01_key$,dom=*next) 
:                     else read (glm11_dev,key=glm01_key$,dom=*next)
          while 1
                if firm_id$="99" k$=key(glm21_dev,end=*break) 
:                           else k$=key(glm11_dev,end=*break)
                if pos(glm01_key$=k$)<>1 break
                if firm_id$="99" read record (glm21_dev,key=k$) glm21a$
:                           else read record (glm11_dev,key=k$) glm11a$
                if firm_id$="99" x$=glm21a.gl_rpt_no$+glm21a.gl_rpt_line$
:                           else x$=glm11a.gl_rpt_no$+glm11a.gl_rpt_line$
                glreport$=x$(1,2)
                line$=x$(3,4)
                if glreport$="00" continue
                if cvs(glreport$,2)="" continue
                if line$="0000" continue
                if cvs(line$,2)="" continue

                rem --- Update 13 Period Financial Report Amount (gle-08) 
                rem --- record for this report

                gosub report_amounts

                gle08a$=field(gle08a$)
                write record (gle08_dev) gle08a$

                rem --- End of report update loop

          wend

          rem --- End of acccount master loop

    wend

rem --- Run report headings overlay

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,0,status
    wait 1; rem --- wait for this meter to clear before displaying next; less confusing to the eye    
    run pgmdir$+"glr_headings.aon",err=std_exit

summary_activity: rem --- Retrieve either current or prior year Summary Activity (glm-02) record

    dim a[mp],u[mp]
    redim glm02a$
    glm02a.firm_id$=glm01a.firm_id$
    glm02a.gl_account$=glm01a.gl_account$
    glm02a.year$=this_year$
    glm02_key$=glm02a.firm_id$+glm02a.gl_account$+glm02a.year$
    find record (glm02_dev,key=glm02_key$,dom=*next) glm02a$
    call pgmdir$+"glc_summary.aon","R",glm02a$,a[all],u[all],status
    return

report_amounts: rem --- Retrieve 13 Period Financial Report Amount (gle-08) record

    if x$<>last$ 
       dim pa[mp],pu[mp]
       last$=x$
       dim gle08a$:fattr(gle08a$)
       gle08a.gl_updt_seq$=""
       gle08_key$=firm_id$+x$
       glm22_key$=gle08_key$
       find record (glm22_dev,key=glm22_key$,dom=*return) glm22a$
       extract record (gle08_dev,key=gle08_key$,dom=*next) gle08a$; rem Advisory Locking
       first=iff(fiscal_pers=13,1,0)
       last=iff(fiscal_pers=13,mp,mp-1)
       delta=iff(fiscal_pers=13,0,1)
       for x=first to last
           pa[x]=nfield(gle08a$,"period_amt_"+str(x+delta:"00"))
           pu[x]=nfield(gle08a$,"period_units_"+str(x+delta:"00"))
       next x
       if gle08a.gl_updt_seq$<>sequence$
          dim pa[mp],pu[mp]
          gle08a.gl_updt_seq$=sequence$
       endif
    endif
    for x=first to last
        pa[x]=pa[x]+ta[x]*sign
        pu[x]=pu[x]+tu[x]*sign
    next x
    for x=first to last
        field gle08a$,"period_amt_"+str(x+delta:"00")=str(pa[x])
        field gle08a$,"period_units_"+str(x+delta:"00")=str(pu[x])
    next x
    gle08a.firm_id$=firm_id$
    if firm_id$="99" gle08a.gl_rpt_no$=glm21a.gl_rpt_no, gle08a.gl_rpt_line$=glm21a.gl_rpt_line$
:      else gle08a.gl_rpt_no$=glm11a.gl_rpt_no$, gle08a.gl_rpt_line$=glm11a.gl_rpt_line$

    return

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next
    release

rem #endinclude std_end.src

    end
