rem glc - Post to Daily Detail File from GL Recap Work File
rem Program glc_update v8.0.0 11May2006

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright BASIS International Ltd.  All Rights Reserved.
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

rem --- GLM01_DEV: GL Account Master file channel (Input)
rem --- GLT04_DEV: Daily Detail file channel (Input)
rem --- GLW11_DEV: GL Recap work file channel (Input)
rem --- STATUS   : Status/0=No Error (Returned)

    seterr std_error
    setesc std_error
    enter glm01_dev,glt04_dev,glw11_dev,status

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$

rem --- Retrieve templates

    files=1,begfile=1,endfile=files
    dim ids$[files],templates$[files]
    ids$[1]="glw-11A:GLW_DAILYDETAIL"
    call pgmdir$+"adc_template.aon",begfile,endfile,ids$[all],templates$[all],status
    if status goto std_exit

rem --- Dimension and store string templates

    dim glw11a$:templates$[1]

rem --- Initializations

    more=1
    status=0
    seqpos=36
    batch_no$=stbl("+BATCH_NO")
    process_id$=stbl("+PROCESS_ID")

rem --- Position file

    read (glw11_dev,key=firm_id$+process_id$+batch_no$,dom=*next)

rem --- Read through the work file and post the records to the daily detail file

    while more
          k$=key(glw11_dev,end=*break)
          if pos(firm_id$+process_id$+batch_no$=k$)<>1 break
          read record(glw11_dev) glw11a$
          call pgmdir$+"glc_glpost.aon",glm01_dev,glt04_dev,notused,glw11a.gl_account$,glw11a.trns_date$,
:                                       glw11a.reference_01$,glw11a.reference_02$,glw11a.reference_03$,
:                                       glw11a.description$,glw11a.trans_amt,glw11a.units,status
    
    remove (glw11_dev,key=k$);rem --- remove each glw-11 rec as it gets written to glt-04 since now using actual glw-11 file for batching.CAH
    
    wend
    
rem --- All done

    goto std_exit

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_exit.src

std_exit: rem --- Standard called program exit (01Mar2006)

    exit

rem #endinclude std_exit.src

    end
