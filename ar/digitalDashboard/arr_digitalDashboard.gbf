[Program]
Creation Date=2007-05-30
Creation Time=6:44:18
Event Set=LEGACY
Generator Program=gb_func
Last Build Date=2012-08-14
Last Build Time=16:17:15
Precision=99
Program Name=arr_digitalDashboard
Remarks=Yes
Resource File=arr_digitalDashboard.arc
Show Forms=All

[Init]
declare BBjSysGui sysgui!
declare BBjTopLevelWindow window!
declare BBjListButton year!
declare BBjFont font!

declare BBjRecordSet rs!
declare BBjRecordData data!

declare BBjRecordSet params!
declare BBjRecordData periods!

declare BBjGroupBox chartGroupBox!
declare BBjProgressBar progressBar!
declare BBjGroupBox progressGroupBox!

declare BBjRecordSet labelRs!
declare BBjRecordData labelData!

REM - Get the window
    sysgui! = BBjAPI().getSysGui()
    window! = cast(BBjTopLevelWindow, sysgui!.getWindow(0))
    
REM - Create the charts
    chartLeft = 180
    chartTop = 110

    newWidth = window!.getWidth()
    newHeight = window!.getHeight()

    chartWidthOffset = 200
    chartHeightOffset = 130
    
    groupBoxWidthOffset = 180
    groupBoxHeightOffset = 90

    chartWidth = newWidth - chartWidthOffset
    chartHeight = newHeight - chartHeightOffset

    groupBoxWidth = newWidth - groupBoxWidthOffset
    groupBoxHeight = newHeight - groupBoxHeightOffset

    progressBar! = window!.addProgressBar(701, 290, 85, 75, 20, 0, 3)
    progressBar!.setVisible(0)

    lineChart1! = window!.addLineChart(505, 15, 530, 135, 90, "", "", 1, 0)
    lineChart1!.setTitle("YTD Sales")
    lineChart1Big! = window!.addLineChart(605, chartLeft, chartTop, chartWidth, chartHeight, "", "", 1, 1)
    lineChart1Big!.setTitle("Year to Date Sales")

    barChart2! = window!.addBarChart(504, 15, 425, 135, 90, "", "", 97, 90, 0, 1, 0); barChart2!.setTitle("Customer Type")
    barChart2Big! = window!.addBarChart(604, chartLeft, chartTop, chartWidth, chartHeight, "", "", 97, 90, 1, 1, 0); barChart2Big!.setTitle("Monthly Sales per Customer Type")

    pieChart2! = window!.addPieChart(503, 15, 320, 135, 90, 0, 1)
    pieChart2!.setTitle("Customer Type")
    pieChart2Big! = window!.addPieChart(603, chartLeft, chartTop, chartWidth, chartHeight, 1, 1)
    pieChart2Big!.setTitle("Monthly Sales per Customer Type")

    barChart1! = window!.addBarChart(502, 15, 215, 135, 90, "", "", 7, 50, 0, 1, 0); barChart1!.setTitle("Salesperson")
    barChart1Big! = window!.addBarChart(602, chartLeft, chartTop, chartWidth, chartHeight, "", "", 7, 50, 1, 1, 0); barChart1Big!.setTitle("Monthly Sales per Salesperson")

    pieChart1! = window!.addPieChart(501, 15, 110, 135, 90, 0, 1); pieChart1!.setTitle("Salesperson")
    pieChart1Big! = window!.addPieChart(601, chartLeft, chartTop, chartWidth, chartHeight, 1, 1); pieChart1Big!.setTitle("Monthly Sales per Salesperson")
    visibleControlID = 601

    chartGroupBox! = cast(BBjGroupBox, window!.getControl("chartGroupBox"))
    progressBarGroupBox!= cast(BBjGroupBox, window!.getControl("progressGroupBox"))
    chartGroupBox!.setSize(groupBoxWidth, groupBoxHeight)
    progressBarGroupBox!.setSize(groupBoxWidth, groupBoxHeight)

REM - Open a JDBC connection

    rem ' ******************************************************
    rem ' build the database URL
    rem ' ******************************************************
    dbserver$=stbl("+DBSERVER",err=*next)
    dbsqlport$=":"+stbl("+DBSQLPORT",err=*next)
    dbssl=num(stbl("+DBSSL",err=*next))
    dbtimeout$="&socket_timeout="+stbl("+DBTIMEOUT")
    
    if dbssl
        dbssl$="&ssl=true"
    else
        dbssl$="&ssl=false"
    endif

    url_user$="&user=guest"
    if stbl("!DSUDDB",err=*endif)<>"" then
        url_user$=""
    endif
	
    dbname$ = stbl("+DBNAME")
    dbname_api$ = stbl("+DBNAME_API")
    if pos("jdbc:apache"=cvs(dbname$,8))=1 then
        url$ = dbname$
    else
	if pos("jdbc:"=cvs(dbname$,8))=1 then			
            url$=dbname$+url_user$
	else
            url$ = "jdbc:basis:"+dbserver$+dbsqlport$+"?database="+dbname_api$+url_user$+dbssl$+dbtimeout$
	endif
    endif

REM - get firm id and firm name
    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$
    firm_name$=sysinfo.firm_name$
    title$ = window!.getTitle()
    title$ = title$ + " - " + firm_name$
    window!.setTitle(title$)

REM - get the accounting periods
    sql$ = "SELECT TOTAL_PERS, CURRENT_PER, CURRENT_YEAR, "
    sql$ = sql$ + "PER_ENDING_01, PER_ENDING_02, PER_ENDING_03, PER_ENDING_04, PER_ENDING_05, PER_ENDING_06, "
    sql$ = sql$ + "PER_ENDING_07, PER_ENDING_08, PER_ENDING_09, PER_ENDING_10, PER_ENDING_11, PER_ENDING_12, PER_ENDING_13 "
    sql$ = sql$ + "FROM GLS_PARAMS "
    sql$ = sql$ + "WHERE FIRM_ID = '" + firm_id$ + "'"

    params! = BBjAPI().createSQLRecordSet(url$,"",sql$)
    periods! = params!.getCurrentRecordData()
    period = num(periods!.getFieldValue("CURRENT_PER"))
    totalPeriods = num(periods!.getFieldValue("TOTAL_PERS"))
    currentYear = num(periods!.getFieldValue("CURRENT_YEAR"))

 REM - Get the Month indicator
    if totalPeriods = 13 then
        window!.getControl("12perSlider").setVisible(0)
        slider! = window!.getControl("13perSlider")
        slider!.setVisible(1)
        window!.getControl("12per01").setVisible(0)
        window!.getControl("12per02").setVisible(0)
        window!.getControl("12per03").setVisible(0)
        window!.getControl("12per04").setVisible(0)
        window!.getControl("12per05").setVisible(0)
        window!.getControl("12per06").setVisible(0)
        window!.getControl("12per07").setVisible(0)
        window!.getControl("12per08").setVisible(0)
        window!.getControl("12per09").setVisible(0)
        window!.getControl("12per10").setVisible(0)
        window!.getControl("12per11").setVisible(0)
        window!.getControl("12per12").setVisible(0)

        window!.getControl("13per01").setVisible(1)
        window!.getControl("13per02").setVisible(1)
        window!.getControl("13per03").setVisible(1)
        window!.getControl("13per04").setVisible(1)
        window!.getControl("13per05").setVisible(1)
        window!.getControl("13per06").setVisible(1)
        window!.getControl("13per07").setVisible(1)
        window!.getControl("13per08").setVisible(1)
        window!.getControl("13per09").setVisible(1)
        window!.getControl("13per10").setVisible(1)
        window!.getControl("13per11").setVisible(1)
        window!.getControl("13per12").setVisible(1)
        window!.getControl("13per13").setVisible(1)

    else
        slider! = window!.getControl("12perSlider")
    fi
    slider!.setValue(period)

    if totalPeriods = 12 then
        for x = 1 to totalPeriods
            switch x
                case 1
                    label$ = periods!.getFieldValue("PER_ENDING_01"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per01").setText(label$)
                    break
                case 2
                    label$ = periods!.getFieldValue("PER_ENDING_02"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per02").setText(label$)
                    break
                case 3
                    label$ = periods!.getFieldValue("PER_ENDING_03"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per03").setText(label$)
                    break
                case 4
                    label$ = periods!.getFieldValue("PER_ENDING_04"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per04").setText(label$)
                    break
                case 5
                    label$ = periods!.getFieldValue("PER_ENDING_05"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per05").setText(label$)
                    break
                case 6
                    label$ = periods!.getFieldValue("PER_ENDING_06"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per06").setText(label$)
                    break
                case 7
                    label$ = periods!.getFieldValue("PER_ENDING_07"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per07").setText(label$)
                    break
                case 8
                    label$ = periods!.getFieldValue("PER_ENDING_08"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per08").setText(label$)
                    break
                case 9
                    label$ = periods!.getFieldValue("PER_ENDING_09"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per09").setText(label$)
                    break
                case 10
                    label$ = periods!.getFieldValue("PER_ENDING_10"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per10").setText(label$)
                    break
                case 11
                    label$ = periods!.getFieldValue("PER_ENDING_11"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per11").setText(label$)
                    break
                case 12
                    label$ = periods!.getFieldValue("PER_ENDING_12"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("12per12").setText(label$)
                    break
            swend
        next x
    else
        for x = 1 to totalPeriods
            switch x
                case 1
                    label$ = periods!.getFieldValue("PER_ENDING_01"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per01").setText(label$)
                    break
                case 2
                    label$ = periods!.getFieldValue("PER_ENDING_02"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per02").setText(label$)
                    break
                case 3
                    label$ = periods!.getFieldValue("PER_ENDING_03"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per03").setText(label$)
                    break
                case 4
                    label$ = periods!.getFieldValue("PER_ENDING_04"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per04").setText(label$)
                    break
                case 5
                    label$ = periods!.getFieldValue("PER_ENDING_05"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per05").setText(label$)
                    break
                case 6
                    label$ = periods!.getFieldValue("PER_ENDING_06"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per06").setText(label$)
                    break
                case 7
                    label$ = periods!.getFieldValue("PER_ENDING_07"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per07").setText(label$)
                    break
                case 8
                    label$ = periods!.getFieldValue("PER_ENDING_08"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per08").setText(label$)
                    break
                case 9
                    label$ = periods!.getFieldValue("PER_ENDING_09"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per09").setText(label$)
                    break
                case 10
                    label$ = periods!.getFieldValue("PER_ENDING_10"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per10").setText(label$)
                    break
                case 11
                    label$ = periods!.getFieldValue("PER_ENDING_11"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per11").setText(label$)
                    break
                case 12
                    label$ = periods!.getFieldValue("PER_ENDING_12"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per12").setText(label$)
                    break
                case 13
                    label$ = periods!.getFieldValue("PER_ENDING_13"); label$ = label$(1,2) + "/" + label$(3,2)
                    window!.getControl("13per13").setText(label$)
                    break
            swend
        next x
    fi

REM - Get the Group box
    groupBox! = window!.getControl(101)

REM - set the year selections
    yearValue = num(date(0:"%Y"))
    yearList$ = str(yearValue) + $0A$
    if yearValue = currentYear then index = 10
    for x = 1 to 10
        yearValue = yearValue - 1
        yearList$ = str(yearValue) + $0A$ + yearList$
        if yearValue = currentYear then index = 10 - x
    next x
    year! = cast(BBjListButton, window!.getControl("year"))
    year!.insertItems(0, yearList$)
    year!.selectIndex(index)

REM - set fonts on labels
    font! = sysgui!.makeFont("Tahoma", 8, sysgui!.PLAIN)
    window!.getControl("12per01").setFont(font!)
    window!.getControl("12per02").setFont(font!)
    window!.getControl("12per03").setFont(font!)
    window!.getControl("12per04").setFont(font!)
    window!.getControl("12per05").setFont(font!)
    window!.getControl("12per06").setFont(font!)
    window!.getControl("12per07").setFont(font!)
    window!.getControl("12per08").setFont(font!)
    window!.getControl("12per09").setFont(font!)
    window!.getControl("12per10").setFont(font!)
    window!.getControl("12per11").setFont(font!)
    window!.getControl("12per12").setFont(font!)

    window!.getControl("13per01").setFont(font!)
    window!.getControl("13per02").setFont(font!)

REM - set font on the year list button
    window!.getControl(116).setFont(font!)

REM - set fonts on the group boxes
    font! = sysgui!.makeFont("Tahoma", 10, sysgui!.BOLD)
    window!.getControl(100).setFont(font!)
    window!.getControl(101).setFont(font!)
    window!.getControl(109).setFont(font!)
    window!.getControl(117).setFont(font!)

REM - create a hashmap for the month names

    declare java.util.HashMap months!
    months! = new java.util.HashMap()
    months!.put("1", "January")
    months!.put("2", "February")
    months!.put("3", "March")
    months!.put("4", "April")
    months!.put("5", "May")
    months!.put("6", "June")
    months!.put("7", "July")
    months!.put("8", "August")
    months!.put("9", "September")
    months!.put("10", "October")
    months!.put("11", "November")
    months!.put("12", "December")


window!.setVisible(1)

gosub UPDATE_GRAPHS

REM - Function to return which control was clicked on given the (x,y) coordinates of the mouse click
DEF FNHITTEST(ptx,pty)
    for i = 501 to 505
        control! = window!.getControl(i)
        x=control!.getX(),y=control!.getY(),w=control!.getWidth(),h=control!.getHeight()
        if ptx>=x and pty>=y and ptx<x+w and pty<y+h then
            return i
        endif
    next i
    return 0
FNEND


[Event Win=101 ID=0 Code=S <WIN_RESIZE> (W101_C0_WIN_RESIZE)]
rem ' Window resized

newWidth = window!.getWidth()
newHeight = window!.getHeight()

chartWidth = newWidth - chartWidthOffset
chartHeight = newHeight - chartHeightOffset
groupBoxWidth = newWidth - groupBoxWidthOffset
groupBoxHeight = newHeight - groupBoxHeightOffset

lineChart1Big!.setSize(chartWidth, chartHeight)
barChart2Big!.setSize(chartWidth, chartHeight)
pieChart2Big!.setSize(chartWidth, chartHeight)
barChart1Big!.setSize(chartWidth, chartHeight)
pieChart1Big!.setSize(chartWidth, chartHeight)

chartGroupBox!.setSize(groupBoxWidth, groupBoxHeight)
progressBarGroupBox!.setSize(groupBoxWidth, groupBoxHeight)

[Event Win=101 ID=0 Code=X <WIN_CLOSE> (W101_C0_WIN_CLOSE)]
rem ' Window Closed
rem ' gb__event! = cast(BBjCloseEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__window! = gb__event!.getControl()

release

[Event Win=101 ID=0 Code=d <MOUSE_DOWN> (W101_C0_MOUSE_DOWN)]
rem ' Mouse button down

rem declare BBjControl gb__control!
gb__event! = cast(BBjMouseDownEvent,bbjapi().getSysGui().getLastEvent())
gb__control! = gb__event!.getControl()

rem ' Mouse button: gb__event!.getButton()
rem ' Mouse location: gb__event!.getX(), gb__event!.getY()
rem ' Shift pressed: gb__event!.isShiftDown()
rem ' Ctrl pressed: gb__event!.isControlDown()
rem ' Alt pressed: gb__event!.isAltDown()

    event!=bbjapi().getSysGui().getLastEvent()
    ptx=event!.getX(),pty=event!.getY()

    controlID = FNHITTEST(ptx,pty)

if controlID > 500 and controlID < 600 then
    visibleControlID = controlID + 100
    control! = window!.getControl(controlID+100)
    control!.setVisible(1)
    for i = 601 to 605
        if i <> controlID + 100 then
            control! = window!.getControl(i)
            control!.setVisible(0)
        endif
    next i 
endif

[Event Win=101 ID=102 Code=p <SCROLL_MOVE> (W101_C102_SCROLL_MOVE)]
rem ' Scroll bar moved

gb__event! = cast(BBjControlScrollEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__control! = gb__event!.getControl()
rem ' Scroll location: gb__event!.getPosition()

if (gb__event!.isAdjusting() = 0) then 
    if slider!.getValue() <> period then 
        gosub UPDATE_GRAPHS
    endif
endif

[Event Win=101 ID=116 Code=N2:19 <LIST_SELECT> (W101_C116_LIST_SELECT)]
rem ' Notify Event - List Control - List Selection (2)
rem ' gb__event! = cast(BBjListSelectEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__control! = gb__event!.getControl()

index = year!.getSelectedIndex()
sYear$ = year!.getItemAt(index)
if sYear$ <> Year$ then
        gosub UPDATE_GRAPHS
endif

[Event Win=101 ID=118 Code=p <SCROLL_MOVE> (W101_C118_SCROLL_MOVE)]
rem ' Scroll bar moved

gb__event! = cast(BBjControlScrollEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__event! = cast(BBjControlScrollEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__control! = gb__event!.getControl()
rem ' Scroll location: gb__event!.getPosition()

if (gb__event!.isAdjusting() = 0) then 
    if slider!.getValue() <> period then 
        gosub UPDATE_GRAPHS
    endif
endif


[Function (UPDATE_GRAPHS)]
rem ' -----------------------------------------------------------------
rem ' UPDATE_GRAPHS
rem ' -----------------------------------------------------------------

UPDATE_GRAPHS:

REM - set wait cursor
    window!.setCursor(3)

    chartGroupBox!.setVisible(0)
    progressBarGroupBox!.setVisible(1)
    progressBar!.setValue(0)
    progressBar!.setVisible(1)

REM - Get the desired month to query upon
    
    gosub set_up_period

REM - clear graphs
    barChart1!.clearData()
    barChart1Big!.clearData()

    pieChart1!.clearData()
    pieChart1Big!.clearData()

    barChart2!.clearData()
    barChart2Big!.clearData()

    pieChart2!.clearData()
    pieChart2Big!.clearData()

    lineChart1!.clearData()
    lineChart1Big!.clearData()

REM - Monthly sales per SalesRep
    declare java.util.ArrayList SalesRepsA!

    SalesRepsA! = new java.util.ArrayList()

    rem ' load the salepserson list for the given period
    sql$ = "SELECT T1.SLSPSN_CODE, T2.CODE_DESC AS SLSPSN_NAME, ROUND(SUM(T1.TOTAL_SALES),0) AS TOTAL "
    sql$ = sql$ + "FROM OPT_INVHDR T1 "
    sql$ = sql$ + "INNER JOIN ARC_SALECODE T2 ON T1.FIRM_ID = T2.FIRM_ID AND T2.RECORD_ID_F = 'F' AND T1.SLSPSN_CODE = T2.SLSPSN_CODE "
    sql$ = sql$ + "WHERE T1.FIRM_ID = '" + firm_id$ + "' AND (T1.INVOICE_DATE BETWEEN '" + monthBeginDate$ + "' AND '" + monthEndDate$ + "') "
    sql$ = sql$ + "GROUP BY T1.SLSPSN_CODE, T2.CODE_DESC "
    sql$ = sql$ + "ORDER BY TOTAL DESC"

    rs! = BBjAPI().createSQLRecordSet(url$,"",sql$)
    progressBar!.setValue(1)

    seriesCount = rs!.getRecordCount()

    if seriesCount > 0 then
        rem ' re add the bar charts with the seriesCount
        barChart1!.destroy()
        barChart1Big!.destroy()

        barChart1! = window!.addBarChart(502, 15, 215, 135, 90, "", "", seriesCount, 50, 0, 1, 0); barChart1!.setTitle("Salesperson")
        barChart1Big! = window!.addBarChart(602, chartLeft, chartTop, chartWidth, chartHeight, "", "", seriesCount, 50, 1, 1, 0, $0010$); barChart1Big!.setTitle("Monthly Sales per Salesperson")

        rem ' manage the visiblity of the charts
        control! = window!.getControl(visibleControlID)
        control!.setVisible(1)

        counter = 0

        while 1
             data! = rs!.getCurrentRecordData()

             if num(data!.getFieldValue("TOTAL")) <> 0 then 

                 SalesRepsA!.add(counter, data!.getFieldValue("SLSPSN_NAME"))

                 barChart1!.setSeriesName(counter, str(counter))
                 barChart1Big!.setSeriesName(counter, cvs(data!.getFieldValue("SLSPSN_NAME"),3) + " - " + str(num(data!.getFieldValue("TOTAL")):"$###,##0.00-"))

                 counter = counter + 1

                 pieChart1!.setSliceValue(str(counter), num(data!.getFieldValue("TOTAL")))
                 pieChart1Big!.setSliceValue(cvs(data!.getFieldValue("SLSPSN_NAME"),3) + " - " + str(num(data!.getFieldValue("TOTAL")):"$###,##0.00-"), num(data!.getFieldValue("TOTAL")))

                 barChart1!.setCategoryName(counter, "")
                 barChart1!.setBarValue(SalesRepsA!.indexOf(data!.getFieldValue("SLSPSN_NAME")),counter,num(data!.getFieldValue("TOTAL")))

                 barChart1Big!.setCategoryName(counter, "")
                 barChart1Big!.setBarValue(SalesRepsA!.indexOf(data!.getFieldValue("SLSPSN_NAME")),counter,num(data!.getFieldValue("TOTAL")))

             fi

             rs!.next(err=*break)
        wend

    fi

    rs!.close()

REM - Monthly sales per Customer type
    declare java.util.ArrayList CustomersA!

    CustomersA! = new java.util.ArrayList()

    rem ' load the customer type list for the given period
    sql$ = "SELECT t2.CUSTOMER_TYPE, t3.CODE_DESC AS CUST_TYPE_DESC, round(sum(t1.TOTAL_SALES),0) as TOTAL "
    sql$ = sql$ + "FROM OPT_INVHDR t1 "
    sql$ = sql$ + "INNER JOIN ARM_CUSTDET t2 ON t1.FIRM_ID = t2.FIRM_ID and t1.CUSTOMER_ID = t2.CUSTOMER_ID "
    sql$ = sql$ + "INNER JOIN ARC_CUSTTYPE t3 on t2.FIRM_ID = t3.FIRM_ID and t3.RECORD_ID_L = 'L' and t2.CUSTOMER_TYPE = t3.CUSTOMER_TYPE "
    sql$ = sql$ + "WHERE t1.FIRM_ID = '" + firm_id$ + "' and (t1.INVOICE_DATE BETWEEN '" + monthBeginDate$ + "' AND '" + monthEndDate$ + "') "
    sql$ = sql$ + "GROUP BY t2.CUSTOMER_TYPE, t3.CODE_DESC ORDER BY TOTAL DESC"
    
    counter = 0

    rs! = BBjAPI().createSQLRecordSet(url$,"",sql$)
    progressBar!.setValue(2)

    seriesCount = rs!.getRecordCount()
 
    if seriesCount > 0 then
        rem ' re add the bar charts with the seriesCount
        barChart2!.destroy()
        barChart2Big!.destroy()

        barChart2! = window!.addBarChart(504, 15, 425, 135, 90, "", "", seriesCount, 90, 0, 1, 0); barChart2!.setTitle("Customer Type")
        barChart2Big! = window!.addBarChart(604, chartLeft, chartTop, chartWidth, chartHeight, "", "", seriesCount, 90, 1, 1, 0, $0010$); barChart2Big!.setTitle("Monthly Sales per Customer Type")

        rem ' manage the visiblity of the charts
        control! = window!.getControl(visibleControlID)
        control!.setVisible(1)
   
        while 1
             rem ' t$ = sqlfetch(chan,err=*BREAK)
             data! = rs!.getCurrentRecordData()         

             if num(data!.getFieldValue("TOTAL")) <> 0 then 

                 CustomersA!.add(counter, data!.getFieldValue("CUST_TYPE_DESC"))

                 barChart2!.setSeriesName(counter, str(counter))
                 barChart2Big!.setSeriesName(counter, data!.getFieldValue("CUST_TYPE_DESC"))

                 counter = counter + 1

                 pieChart2!.setSliceValue(str(counter), num(data!.getFieldValue("TOTAL")))
                 pieChart2Big!.setSliceValue(data!.getFieldValue("CUST_TYPE_DESC"), num(data!.getFieldValue("TOTAL")))

                 barChart2!.setCategoryName(counter, "")
                 barChart2!.setBarValue(CustomersA!.indexOf(data!.getFieldValue("CUST_TYPE_DESC")), counter, num(data!.getFieldValue("TOTAL")))

                 barChart2Big!.setCategoryName(counter, "")
                 barChart2Big!.setBarValue(CustomersA!.indexOf(data!.getFieldValue("CUST_TYPE_DESC")), counter, num(data!.getFieldValue("TOTAL")))
             fi

             rs!.next(err=*break)

        wend
    
    fi

    rs!.close()

REM - Sales Year To Date
    
    dim m[12]

    sql$ = "SELECT SUBSTRING(INVOICE_DATE, 5, 2) as MONTH, SUM(TOTAL_SALES) AS TOTAL FROM OPT_INVHDR "
    sql$ = sql$ + "WHERE FIRM_ID = '" + firm_id$ + "' and (INVOICE_DATE BETWEEN '" + yearStartDate$ + "' AND '" + monthEndDate$ + "') "
    sql$ = sql$ + "GROUP BY SUBSTRING(INVOICE_DATE, 5, 2) ORDER BY MONTH"
    
    rs! = BBjAPI().createSQLRecordSet(url$,"",sql$)
    progressBar!.setValue(3)

    if !(rs!.isEmpty()) then
        while 1
            data! = rs!.getCurrentRecordData()
            rem ' t$ = sqlfetch(chan,err=*BREAK)
            m[num(data!.getFieldValue("MONTH"))] = m[num(data!.getFieldValue("MONTH"))] + num(data!.getFieldValue("TOTAL"))

            rs!.next(err=*break)
        wend

        REM - turn array into a cummulative array instead of a month by month
        for x = 2 to 12
            m[x] = m[x] + m[x-1]
        next x

        lineChart1Big!.setSeriesName(0,"Yearly Sales " + str(m[12]:"$###,###,##0.00-"))
        if totalPeriods = 13 and period = 13 then
            limit = 12
        else
            limit = period
        fi
        for i = 0 to limit 
            lineChart1!.setXYValue(0,i,m[i])
            lineChart1Big!.setXYValue(0,i,m[i])
        next
    fi
    
    rs!.close()

    progressBar!.setVisible(0)
    progressBarGroupBox!.setVisible(0)    
    chartGroupBox!.setVisible(1)
    
REM - set normal cursor
    window!.setCursor(0)

return

[Function (set_up_period)]
rem ' -----------------------------------------------------------------
rem ' set_up_period
rem ' -----------------------------------------------------------------

set_up_period:

    period = slider!.getValue()
    totalPeriods = num(periods!.getFieldValue("TOTAL_PERS"))
    curYear = num(periods!.getFieldValue("CURRENT_YEAR"))
    curPeriod = num(periods!.getFieldValue("CURRENT_PER"))
    
    firstPerEnding$ = periods!.getFieldValue("PER_ENDING_01")
    
    switch period
    	case 1 
    		ending$ = periods!.getFieldValue("PER_ENDING_01") 
    		if totalPeriods = 12 then
    		    starting$ = periods!.getFieldValue("PER_ENDING_12")
    		else
    		    starting$ = periods!.getFieldValue("PER_ENDING_13")
    		fi
    		break
    	case 2; ending$ = periods!.getFieldValue("PER_ENDING_02"); starting$ = periods!.getFieldValue("PER_ENDING_01"); break
    	case 3; ending$ = periods!.getFieldValue("PER_ENDING_03"); starting$ = periods!.getFieldValue("PER_ENDING_02"); break
    	case 4; ending$ = periods!.getFieldValue("PER_ENDING_04"); starting$ = periods!.getFieldValue("PER_ENDING_03"); break
    	case 5; ending$ = periods!.getFieldValue("PER_ENDING_05"); starting$ = periods!.getFieldValue("PER_ENDING_04"); break
    	case 6; ending$ = periods!.getFieldValue("PER_ENDING_06"); starting$ = periods!.getFieldValue("PER_ENDING_05"); break
    	case 7; ending$ = periods!.getFieldValue("PER_ENDING_07"); starting$ = periods!.getFieldValue("PER_ENDING_06"); break
    	case 8; ending$ = periods!.getFieldValue("PER_ENDING_08"); starting$ = periods!.getFieldValue("PER_ENDING_07"); break
    	case 9; ending$ = periods!.getFieldValue("PER_ENDING_09"); starting$ = periods!.getFieldValue("PER_ENDING_08"); break
    	case 10; ending$ = periods!.getFieldValue("PER_ENDING_10"); starting$ = periods!.getFieldValue("PER_ENDING_09"); break
    	case 11; ending$ = periods!.getFieldValue("PER_ENDING_11"); starting$ = periods!.getFieldValue("PER_ENDING_10"); break
    	case 12; ending$ = periods!.getFieldValue("PER_ENDING_12"); starting$ = periods!.getFieldValue("PER_ENDING_11"); break
    	case 13; ending$ = periods!.getFieldValue("PER_ENDING_13"); starting$ = periods!.getFieldValue("PER_ENDING_12"); break
    swend
  
    startMonth = num(starting$(1,2))    
    startDay = num(starting$(3,2))

    endMonth = num(ending$(1,2))
    endDay = num(ending$(3,2))

    firstPerMonth = num(firstPerEnding$(1,2))

    index = year!.getSelectedIndex()
    year = num(year!.getItemAt(index))
   
    rem ' do we have a fiscal year that does not align with the calendar year
    if endMonth < firstPerMonth then 
        endYear = year + 1
    else
        endYear = year
    fi
    if startMonth < firstPerMonth then
        startYear = year + 1
    else
        startYear = year
    fi

    rem ' adjust the start by one day
    if startMonth = 12 and startDay = 31 then
	startDate = jul(startYear - 1, startMonth, startDay)
    else
    	startDate = jul(startYear, startMonth, startDay)
    fi
    startDate = startDate + 1
    startDay = num(date(startDate:"%D"))
    startMonth = num(date(startDate:"%M"))
    startYear = num(date(startDate:"%Y"))
    
    rem ' adjust for leap year
    if startMonth = 2 and startDay = 29 then startMonth = startMonth + 1, startDay = 1
    if endMonth = 2 and endDay = 28 and mod(endYear,4) = 0 then endDay = endDay + 1

    monthBeginDate$ = str(startYear:"0000") + str(startMonth:"00") + str(startDay:"00")
    monthEndDate$ = str(endYear:"0000") + str(endMonth:"00") + str(endDay:"00")
 
    rem ' sett the year start date
    if totalPeriods = 12 then
        starting$ = periods!.getFieldValue("PER_ENDING_12")
    else
        starting$ = periods!.getFieldValue("PER_ENDING_13")
    fi
    yearStartMonth = num(starting$(1,2))
    yearStartDay = num(starting$(3,2))
    yearStartDate = jul(year - 1, yearStartMonth, yearStartDay) + 1
    yearStartDate$ = date(yearStartDate:"%Y%Mz%Dz")
    
return

