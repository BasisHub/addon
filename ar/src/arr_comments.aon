rem apr - Customer Comment Listing
rem Program apr_comments v8.0.0 25Dec2006 (arr_ob)
rem Created by adx_progconv.bbx v1.0.3 (12/25/2006 01:04:09)

rem  +-----------------------------------------+
rem  | AddonSoftware Version 8.0.0 - 01Oct2006 |
rem  |  Copyright (c) 1981-2006 AddonSoftware  |
rem  |          All Rights Reserved            |
rem  +-----------------------------------------+

    setesc std_error
    seterr std_error

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Document initializations

            OutVect!=bbjAPI().getSysGui().makeVector()
            HdrVect!=bbjAPI().getSysGui().makeVector()
            BrkVect!=bbjAPI().getSysGui().makeVector()
            rep_date$=date(0:"%Mz/%Dz/%Yd")
            rep_date_stamp$=date(0:"%Yd%Mz%Dz")
            rep_time$=date(0:"%hz:%mz %p")
            rep_time_stamp$=date(0:"%Hz%mz%sz")
            rep_prog$=pgm(-2)

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$
    
    milestone=50
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)        

rem --- Open/Lock files

    files=7,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="ars_params",ids$[1]="ARS_PARAMS"
    files$[2]="arm-01",ids$[2]="ARM_CUSTMAST"
    files$[3]="arm-02",ids$[3]="ARM_CUSTDET"
    files$[4]="arm-05",ids$[4]="ARM_CUSTCMTS"
    files$[5]="arc_termcode",ids$[5]="ARC_TERMCODE"
    files$[6]="arc_salecode",ids$[6]="ARC_SALECODE"
    files$[7]="arc_terrcode",ids$[7]="ARC_TERRCODE"
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],
:                                   ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit
    ars01_dev=channels[1]
    arm01_dev=channels[2]
    arm02_dev=channels[3]
    arm05_dev=channels[4]
    arc_termcode_dev=channels[5]
    arc_salecode_dev=channels[6]
    arc_terrcode_dev=channels[7]

rem --- Dimension string templates

    dim ars01a$:templates$[1],arm01a$:templates$[2],arm02a$:templates$[3]
    dim arm05a$:templates$[4],arm10a$:templates$[5],arm10f$:templates$[6],arm10h$:templates$[7]

rem --- Assign form input values to local variables

    sequence$=option!.getOptionData("DSP_REPORT_SEQ")
    begsales$=option!.getOptionData("SALESPERSON_1")
    endsales$=option!.getOptionData("SALESPERSON_2")
    begterr$=option!.getOptionData("TERRITORY_1")
    endterr$=option!.getOptionData("TERRITORY_2")

rem --- Retrieve parameter records

    ars01a_key$=firm_id$+"AR00"
    find record (ars01_dev,key=ars01a_key$,err=std_missing_params) ars01a$
    call stbl("+DIR_PGM")+"adc_getmask.aon","CUSTOMER_ID","","","",m0$,0,customer_size
    m8$="(###) ###-####"
    m0=len(m0$)
    m8=len(m8$)

rem --- Column Headings

    dim columns$[2,10]
    columns=2
        columns$[0,0]="Customer",  columns$[0,1]="C",columns$[0,2]="50"
        columns$[1,0]="Contact",   columns$[1,1]="C",columns$[1,2]="20"
        columns$[2,0]="Telephone",columns$[2,1]="C",columns$[2,2]="15"
        
rem --- Initializations

    headings=4
    dim headings$[headings],o[3]
    done=0
    page=0
    width=80
    total=0
    maxlines=59
    moreheaders=1
    moredetail=1
    lines=maxlines+1
    when$=sysinfo.system_date$
    clock$=""
    lastcode$=""
    nf$="(Not on File)"
    keynum=1
    begvalue$=begsales$
    total_recs=0
    
    x$=" By Salesperson"
    if sequence$="T"
       keynum=2
       begvalue$=begterr$
       x$=" By Territory"
       if begterr$="" range$="From First Through " else range$="From "+begterr$+" Through "
       if endterr$="" range$=range$+"Last" else range$=range$+endterr$
    else
       if begsales$="" range$="From First Through " else range$="From "+begsales$+" Through "
       if endsales$="" range$=range$+"Last" else range$=range$+endsales$
    endif
    headings$[0]=sysinfo.firm_name$
    headings$[1]=sysinfo.task_desc$+x$
    headings$[2]=range$
    headings$[4]="[HEADER]"
rem --- Initialize print positions

    o[0]=0
    o[1]=max(m0+1,7)
    o[2]=o[1]+32
    o[3]=width-m8
    cw=min(20,o[3]-o[2]-1)

rem --- Start progress meter and position Customer Master file

    call pgmdir$+"adc_progress.aon","NC","ARM_CUSTMAST","","","",0,arm01_dev,1,meter_num,status
    read (arm02_dev,knum=keynum,key=firm_id$+begvalue$,dom=*next)

rem --- Read next Customer Detail (arm-02) record

    while moreheaders
          arm02_key$=key(arm02_dev,knum=keynum,end=*break)
          if pos(firm_id$=arm02_key$)<>1 break
          read record (arm02_dev,knum=keynum,key=arm02_key$) arm02a$
          total_recs=total_recs+1
          if mod(total_recs,milestone)=0 then call pgmdir$+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status

          if sequence$="S" 
:            if endsales$<>""
:               if arm02a.slspsn_code$>endsales$ break
          if sequence$="T" 
:            if endterr$<>""
:               if arm02a.territory$>endterr$ break

          
          rem --- Retrieve the Customer Master (arm-01) record

          arm01_key$=arm02a.firm_id$+arm02a.customer_id$
          find record (arm01_dev,key=arm01_key$,dom=*continue) arm01a$

          rem --- Initializations for this customer

          dim continued$(width)
          first=1
          call stbl("+DIR_SYP")+"bac_getmask.bbj","T",cvs(arm01a.phone_no$,2),"",phone_mask$
          phone$=cvs(arm01a.phone_no$,2)
          phone$=str(phone$:phone_mask$,err=*next)

          rem --- Position Customer Comments (arm-05) file

          read (arm05_dev,key=arm01_key$,dom=*next)

          rem --- Read next Customer Comments (arm-05) record

          while moredetail
                k$=key(arm05_dev,end=*break)
                if pos(arm01_key$=k$)<>1 break
                
                rem --- Level break?

                if sequence$="S" then if arm02a.slspsn_code$<>lastcode$ gosub salesperson
                if sequence$="T" then if arm02a.territory$<>lastcode$ gosub territory
                
                read record (arm05_dev,key=k$) arm05a$

                rem --- If this is the first comment print the customer info

                if first gosub print_customer

                rem --- Print the comment line

                OutVect!.addItem("        "+arm05a.std_comments$)
                xwk=fnblank(2)
                rem --- Loop back for next comment

          wend

          rem --- Loop back for next customer record

    wend

rem --- All done

    if total goto std_exit
    done=1
    xwk=fnblank(3)
    OutVect!.addItem("No comments found");xwk=fnblank(2)
    goto std_exit

rem --- Sub Heading
sub_heading:

    BrkVect!.addItem(str(OutVect!.size()/(columns+1)))
    HdrVect!.addItem(code$)

    if cvs(continued$,2)<>""
       rem print (printer_dev) ""
       rem print (printer_dev) @(o[0]),continued$
       rem print (printer_dev) ""
       rem lines=lines+3
       OutVect!.addItem(continued$);xwk=fnblank(2)
       xwk=fnblank(3)
    endif
return

print_customer: rem --- Print customer information

    first=0
    total=total+1
    xwk=fnblank(3)
    OutVect!.addItem(fnmask$(arm01a.customer_id$(1,customer_size),m0$)+"  "+arm01a.customer_name$)
    OutVect!.addItem(arm01a.contact_name$(1,cw))
    OutVect!.addItem(phone$)
    xwk=fnblank(3)

    continued$(o[0]+1)=fnmask$(arm01a.customer_id$(1,customer_size),m0$)
    continued$(o[1])=arm01a.customer_name$
    continued$(o[2])="(Continued)"
    return

salesperson: rem --- Retrieve salesperson code and description

    dim arm10f$:fattr(arm10f$)
    arm10f.firm_id$=firm_id$
    arm10f.slspsn_code$=arm02a.slspsn_code$
    arm10f.code_desc$=nf$
    arm10_key$=arm10f.firm_id$+"F"+arm10f.slspsn_code$
    find record (arc_salecode_dev,key=arm10_key$,dom=*next) arm10f$
    lastcode$=arm10f.slspsn_code$
    code$="Salesperson "+arm10f.slspsn_code$+" "+arm10f.code_desc$
    lines=maxlines+1
    gosub sub_heading
    return

territory: rem --- Retrieve territory code and description

    dim arm10h$:fattr(arm10h$)
    arm10h.firm_id$=firm_id$
    arm10h.territory$=arm02a.territory$
    arm10h.code_desc$=nf$
    arm10_key$=arm10h.firm_id$+"H"+arm10h.territory$
    find record (arc_terrcode_dev,key=arm10_key$,dom=*next) arm10h$
    lastcode$=arm10h.territory$
    code$="Territory "+arm10h.territory$+" "+arm10h.code_desc$
    lines=maxlines+1
    gosub sub_heading
    return

rem --- Functions

rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                            cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+
:                                cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        if pos("E"=cvs(q1$,4)) goto alpha_mask
:          else return str(-num(q1$,err=alpha_mask):q2$,err=alpha_mask)
        alpha_mask:
        q=1
        q0=0
        while len(q2$(q))
              if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
              q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend
rem --- fnBlank : Create blank cells in OutVect!. No return value
   
    def fnblank(q0)
        for q1=1 to q0
            OutVect!.addItem("")
        next q1
        return q1
    fnend
rem #include std_error.src

std_error: rem --- Standard error handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_error_exit)
    call stbl("DIR_SYP")+"bac_error.bbj",err=std_error_exit,pgm(-2),str(tcb(5)),
:                                str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_error_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_error_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_error_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_error_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_error_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_error_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (01Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5)),
:                                   str(err),rd_err_text$,rd_err_act$
    if pos("EXIT"=rd_err_act$) goto std_missing_params_exit
    if pos("ESCAPE"=rd_err_act$) seterr 0;setesc 0
    if pos("RETRY"=rd_err_act$) retry
std_missing_params_exit:
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release:
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next

    release

rem #endinclude std_end.src

    end
