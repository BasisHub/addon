rem --- Print Customer Statements
rem --- Program arr_statements(arr_cb)
rem --- Adapted from the original BBjForm-based version to use jasper

rem --- AddonSoftware
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    setesc std_error
    seterr std_error

    use ::bbjasper.bbj::BBJasperReport
    use ::bbjasper.bbj::BBJasperViewerWindow
    use ::bbjasper.bbj::BBJasperViewerControl

    use ::ado_rptControl.src::ReportControl
    use ::ado_func.src::func
    use ::sys/prog/bao_utilities.bbj::BarUtils

	declare BBJasperReport BBjReport!
    
    params! = Option!.getOptionMap()
    coverParams! = Option!.getOptionMap()
		
    ScreenSize!   = bbjAPI().getSysGui().getSystemMetrics().getScreenSize()
    screen_width  = ScreenSize!.width - 200; rem keep it in the MDI w/ no scroll bars
    screen_height = ScreenSize!.height - 100

rem --- Retrieve the program path

    pgmdir$=stbl("+DIR_PGM",err=*next)

rem --- Set document Directory

    docdir$=stbl("+DOC_DIR_PDF",err=*next)
    mkdir docdir$,err=*next

rem --- Retrieve sysinfo data

    sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=*next)
    firm_id$=sysinfo.firm_id$

    milestone=50
    milestone=num(stbl("+MILESTONE",err=*next),err=*next)

rem --- Get logo to go on form; start w/ company logo, fall back to default logo from config if no company logo specified

    logo_filename$=""
    logo_filename$=func.getCompanyLogo()
    if logo_filename$=""
        logo_file$=stbl("+CUST_IMAGES",err=*next)+stbl("+FORM_LOGO",err=*next)
        if logo_file$<>""
            logo_filename$=BBjAPI().getFileSystem().resolvePath(logo_file$,err=*next)
        endif
    endif

rem --- Set Report Name & Subreport directory

    reportDir$ = stbl("+DIR_REPORTS",err=*next)   
    temp = unt
    open (temp)reportDir$
    reportDir$ = fid(temp)(9)+"/"
    close (temp)
    reportBaseName$ = "CustStatement"
    filename$ = reportDir$ + reportBaseName$ + ".jasper"
    masterBaseName$ = "StatementMaster"
    master_filename$ = reportDir$ + masterBaseName$+".jasper"
    printBaseName$ = "StatementPrint"
    print_filename$ = reportDir$ + printBaseName$+".jasper"

    declare BBJasperReport report!

rem --- Open/Lock files

    files=4,begfile=1,endfile=files
    dim files$[files],options$[files],ids$[files],templates$[files],channels[files]
    files$[1]="arm-01",ids$[1]="ARM_CUSTMAST"
    files$[2]="arm-02",ids$[2]="ARM_CUSTDET"
    files$[3]="art-01",ids$[3]="ART_INVHDR"
    files$[4]="art-11",ids$[4]="ART_INVDET"
    
    call pgmdir$+"adc_fileopen.aon",action,begfile,endfile,files$[all],options$[all],ids$[all],templates$[all],channels[all],batch,status
    if status goto std_exit

    arm01_dev=channels[1]
    arm02_dev=channels[2]
    art01_dev=channels[3]
    art11_dev=channels[4]

rem --- Dimension string templates

    dim arm01a$:templates$[1],arm02a$:templates$[2]
    dim art01a$:templates$[3],art11a$:templates$[4]

rem --- Assign form input values to local variables

    stmt_date$    = option!.getOptionData("CURSTM_DATE")
    message$      = option!.getOptionData("PICK_TEXT")
    cycle$        = option!.getOptionData("AR_CYCLECODE")
    territory$    = option!.getOptionData("TERRITORY")
    age_period$   = option!.getOptionData("PICK_PER")
    age_basis$    = option!.getOptionData("REPORT_TYPE")
    minimum$      = option!.getOptionData("PICK_NO")
    option$       = option!.getOptionData("REPORT_OPTION")
    sequence$     = option!.getOptionData("REPORT_SEQUENCE")
    customer_id$  = option!.getOptionData("CUSTOMER_ID")
    alt_sequence$ = option!.getOptionData("ALT_SEQUENCE")
    rem "CUSTOMER INACTIVE FEATURE"
    inactive$=Option!.getOptionData("CUST_INACTIVE")

    minimum=num(minimum$)
    period=num(age_period$,err=*next)

    if pos(option$="SR")
        if sequence$="C"
            begcust$=customer_id$
            endcust$=customer_id$
        else
            begalt$=alt_sequence$
            endalt$=alt_sequence$
        endif
    endif
    if option$="R"
        endcust$=""
        endalt$=""
    endif

rem --- Initializations

    artype$=fill(2)
    keynum$="PRIMARY"
    begvalue$=begcust$
    endvalue$=endcust$


    if sequence$="A" then 
        keynum$="AO_ALTSEQ_CUST"
        begvalue$=begalt$
        endvalue$=endalt$
    endif

    call stbl("+DIR_PGM")+"adc_getmask.aon","CUSTOMER_ID","","","",m0$,0,customer_size
    call stbl("+DIR_PGM")+"adc_getmask.aon","","AR","A","",m1$,0,0
    
    alias$ = option!.getAlias()
    rptctl_alias$ = alias$
    master_document=1
    print_document=1

    emailFax! = BBjAPI().makeVector()

rem --- See if this document is set up in Addon Report Control

    reportControl!=new ReportControl()
    reportControl!.setDocExtension("pdf")
    rpt_ctl$=reportControl!.getReportControl(rptctl_alias$)
    rpt_ctl$=iff(rpt_ctl$="","NO","YES")
    rpt_id$=iff(rpt_ctl$="YES",reportControl!.getReportID(),"")
    rpt_logo_file$=iff(rpt_ctl$="YES",reportControl!.getLogoFile(),"")
    logo_filename$=iff(rpt_logo_file$="",logo_filename$,rpt_logo_file$);rem doc-specific logo supersedes default or company logo

rem --- Determine 30 day aging period dates

    for x=-5 to 0
        enddate$=""
        call pgmdir$+"adc_daydates.aon",stmt_date$,enddate$,x*30
        period_dates$=enddate$+period_dates$
    next x

rem --- Start progress meter and position Customer Master (arm-01) file

    call pgmdir$+"adc_progress.aon","NC","ARM_CUSTMAST","","","",0,arm01_dev,1,meter_num,status

    read (arm01_dev,knum=keynum$,key=firm_id$,dom=*proceed)

rem --- Read next Customer Master (arm-01) and Detail (arm-02) record

    while 1

        if pos(option$="RS")
            if first_pass=0
                read (arm01_dev,knum=keynum$,key=firm_id$+begvalue$,dir=0,dom=*next)            
            endif   
        endif
 
        read record (arm01_dev,end=*break)arm01a$
 
        first_pass=1
        if firm_id$<>arm01a.firm_id$ break
        if mod(total_recs,milestone)=0 then call pgmdir$+"adc_progress.aon","S","","","","",0,0,milestone,meter_num,status
        if sequence$="C" if endvalue$<>"" if arm01a.customer_id$>endvalue$ break
        if sequence$="A" if endvalue$<>"" if arm01a.alt_sequence$>endvalue$ break
        dim arm02a$:fattr(arm02a$)
        k$=arm01a.firm_id$+arm01a.customer_id$+artype$
        find record (arm02_dev,key=k$,dom=*continue) arm02a$

    rem --- Does this customer meet the selection criteria?

        rem -- Check for inactive customer
        if inactive$="Y" and arm01a.cust_inactive$="Y" then continue

        if option$<>"S" then if arm02a.statements$<>"Y" continue

        if territory$<>"" if arm02a.territory$<>territory$ continue
        if cycle$<>"" if arm02a.ar_cyclecode$<>cycle$ continue

    rem --- Position the Invoice Header (art-01) file for aging

        dim aging[5]
        customer$=arm01a.firm_id$+artype$+arm01a.customer_id$
        read (art01_dev,key=customer$,dom=*next)

    rem --- Read next Invoice Header record

        while 1
            art01_key$=key(art01_dev,end=*break)
            if pos(customer$=art01_key$)<>1 break
            read record (art01_dev) art01a$
            if art01a.invoice_date$>stmt_date$ continue

        rem --- Accumulate any adjustments made to this invoice

            gosub adjustments
            balance=art01a.invoice_amt+applied

        rem --- Age this invoice

            agingdate$=art01a.invoice_date$
            if age_basis$<>"I" agingdate$=art01a.inv_due_date$
            invagepd=pos(agingdate$>period_dates$,8); rem determine invoice aging period for proper accumulation
            if invagepd=0 invagepd=5 else invagepd=int(invagepd/8)
            aging[invagepd]=aging[invagepd]+balance

        wend

    rem --- Above the minimum balance for printing?

        balance=0

        for x=period to 5
            balance=balance+aging[x]
        next x

        if minimum <> 0 then if balance<minimum continue

    rem --- Make Document Archive Record and Get Document ID

        rep_date$=date(0:stbl("+DATE_MASK"))
        rep_date_stamp$=date(0:"%Yd%Mz%Dz")
        rep_time$=date(0:"%hz:%mz %p")
        rep_time_stamp$=date(0:"%Hz%mz%sz")

        new_doc_id$=""
        new_doc_name$=""
        doc_name$="CustomerStatement_"+arm01a.customer_id$
        doc_keywords$="Stmt date: "+fndate$(stmt_date$)

        rd_source_alias$=alias$
        rd_source_type$="O"
        rd_doc_source$="E"
        rd_doc_ext$="PDF"
        rd_source_id$="C"
        rd_source_ref$=arm01a.customer_id$
        rd_doc_keywords$=doc_keywords$

    rem --- Get Next Document Number and update Document Audit for individual pdf

        call stbl("+DIR_SYP")+"bas_sequences.bbj","DOC_NO",new_doc_id$,rd_table_chans$[all] 
        new_doc_name$=new_doc_id$+"_"+doc_name$+".pdf"        
        
        call stbl("+DIR_SYP")+"bac_documents.bbj",new_doc_id$,rep_date_stamp$,rep_time_stamp$,rd_doc_source$,rd_doc_ext$,docdir$,
:           rd_source_type$,rd_source_alias$,rd_source_id$,rd_source_ref$,rd_table_chans$[all],"",new_doc_name$,doc_name$,doc_keywords$

    rem --- Get Next Document Number and update Document Audit for JAS entry for Master doc, if first time thru

        if master_document
            rd_doc_ext$="JAS"
            rd_archive_action$="DOC_ID-NOREPRINT"
            rd_doc_id$=""
            rd_source_id$="C"
            rd_source_ref$=""
            rd_doc_keywords$=""
            gosub document_whse
            rd_archive_action$=""
            master_doc_id$=rd_doc_id$
            master_doc_name$=master_doc_id$+"_"+masterBaseName$+".pdf"
        endif

    rem --- Get Next Document Number and update Document Audit for JAS entry for Print doc, if first time thru

        if print_document
            rd_doc_ext$="JAS"
            rd_archive_action$="DOC_ID-NOREPRINT"
            rd_doc_id$=""
            rd_source_id$="C"
            rd_source_ref$=""
            rd_doc_keywords$=""
            gosub document_whse
            rd_archive_action$=""
            print_doc_id$=rd_doc_id$
            print_doc_name$=print_doc_id$+"_"+printBaseName$+".pdf"
        endif

    rem --- Use ReportControl object to see if customer wants print, email, or fax

        add_to_print_report=1
        if rpt_ctl$="YES"     
            found=reportControl!.getRecipientInfo(rpt_id$,arm01a.customer_id$,"")
            if found         
                if reportControl!.getPrintYN()<>"Y" then add_to_print_report=0
                if reportControl!.getEmailYN()="Y" or reportControl!.getFaxYN()="Y"
                    emailFax!.add(rpt_id$)
                    emailFax!.add(arm01a.customer_id$)
                    emailFax!.add(new_doc_id$)
                endif
            endif
        endif

    rem --- Document Output Report Parameters

        params!.put("DOC_NO",new_doc_id$)
        params!.put("DATE_REP",rep_date$+"  "+rep_time$)

    rem --- Other params

        params!.put("BARISTA_WD",dir(""))
        if logo_filename$<>"" then params!.put("LOGO_FILE",logo_filename$)
        params!.put("FIRM_ID",firm_id$)
        params!.put("STATEMENT_DATE",stmt_date$)
        params!.put("CUSTOMER_ID",arm01a.customer_id$)
        params!.put("MESSAGE",message$)
        params!.put("AGE_BASIS",age_basis$)
        params!.put("AMT_MASK",m1$)
        params!.put("CUST_MASK",m0$)
        params!.put("CUST_SIZE",str(customer_size))
        params!.put("PERIOD_DATES",period_dates$)

        coverParams!.put("INACTIVE_CUST",Translate!.getTranslation("AON_EXCLUDE_INACTIVE")+Translate!.getTranslation("AON_CUSTOMERS")+" = "+inactive$)

    rem --- invoke jasper
        
        if master_document
            master_report! = BarUtils.getBBJasperReport(master_filename$)
            master_report!.putParams(coverParams!)
            master_report!.fill()
            master_document=0
        endif

        if print_document
            print_report! = BarUtils.getBBJasperReport(print_filename$)
            print_report!.putParams(coverParams!)
            print_report!.fill()
            print_document=0
        endif

        report! = BarUtils.getBBJasperReport(filename$)
        report!.putParams(params!)

        locale$ = stbl("!LOCALE")
        locale$ = stbl("+USER_LOCALE",err=*next)
        report!.setLocale(locale$)
     
        report!.fill()
        report!.exportToPDF(BBjAPI().FALSE, docdir$+new_doc_name$)

        master_report!.appendReport(report!)
        if add_to_print_report then print_report!.appendReport(report!)
    
        report!.destroy()
    
    rem --- Loop back for next Customer Master

        total_recs=total_recs+1
		if option$="S" break

    wend

rem --- done

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status

    if total_recs=0
        msg_id$="DOC_OUTPUT_NODATA"
        gosub disp_message
        goto std_exit_no_report
    endif

    if master_report!=null() then goto std_exit

rem --- bring up jasper viewers (Master doc and Print doc)

    declare BBJasperViewerWindow masterViewerWindow!
    declare BBJasperViewerWindow printViewerWindow!
    declare BBjTopLevelWindow bbjWindowMaster!
    declare BBjTopLevelWindow bbjWindowPrint!

rem --- get a pdf path from +DOC_DIR_PDF
rem --- this will eventually be replaced with code to get output formats/types based on doc group and/or print settings
    gosub doc_path

rem --- Master - contains ALL statements

    repTitle$=masterBaseName$
    rd_doc_name$=master_doc_name$
    rd_doc_id$=master_doc_id$
    
    gosub exportMasterToPDF; rem --- always save to doc archive
    
    masterViewerWindow! = new BBJasperViewerWindow(master_report!,5,5,screen_width,screen_height,repTitle$,$00000093$)
    masterViewerControl! = masterViewerWindow!.getViewerControl()
    masterViewerControl!.setGoogleDefaultDocument(repTitle$)
    masterViewerControl!.setDefaultSaveFolder(rd_doc_path$)
    masterViewerControl!.setDefaultSaveFile(repTitle$)
    masterViewerControl!.setFitWidth()

    rem --- set callbacks for when user presses save or email buttons in jasper viewer
    rem --- note: saveAs or saveAsGDoc not intercepted, just regular save
    masterSaveMenuButton! = masterViewerControl!.getControl(BBJasperViewerControl.getSAVE_MENU_BUTTON_NAME())
    masterSaveMenuButton!.setCallback(BBjMenuButton.ON_BUTTON_PUSH ,"OnMasterSaveMenuButton")

    masterSavMenuItem! = masterViewerControl!.getControl(BBJasperViewerControl.getSAVE_MENU_ITEM_NAME())
    masterSavMenuItem!.setCallback(BBjMenuItem.ON_POPUP_ITEM_SELECT,"OnMasterSaveMenuButton")

    masterEmailToolButton! = masterViewerControl!.getControl(BBJasperViewerControl.getEMAIL_TOOL_BUTTON_NAME())
    masterEmailToolButton!.setCallback(BBjMenuButton.ON_TOOL_BUTTON_PUSH ,"OnMasterEmailToolButton")

    masterViewerWindow!.setReleaseOnClose(0)
    masterViewerWindow!.show(0)

    bbjWindowMaster!= masterViewerWindow!.getViewerWindow()
    bbjWindowMaster!.setCallback(bbjWindowMaster!.ON_CLOSE,"close_win")

rem --- Print - contains only those marked in rptctl file for print, or those not in rptctl file

    repTitle$=printBaseName$
    rd_doc_name$=print_doc_name$
    rd_doc_id$=print_doc_id$

    gosub exportPrintToPDF; rem --- always save to doc archive

    printViewerWindow! = new BBJasperViewerWindow(print_report!,50,50,screen_width,screen_height,repTitle$,$00000093$)
    printViewerControl! = printViewerWindow!.getViewerControl()
    printViewerControl!.setGoogleDefaultDocument(repTitle$)
    printViewerControl!.setDefaultSaveFolder(rd_doc_path$)
    printViewerControl!.setDefaultSaveFile(repTitle$)
    printViewerControl!.setFitWidth()

    printSaveMenuButton! = printViewerControl!.getControl(BBJasperViewerControl.getSAVE_MENU_BUTTON_NAME())
    printSaveMenuButton!.setCallback(BBjMenuButton.ON_BUTTON_PUSH ,"OnPrintSaveMenuButton")

    printSavMenuItem! = printViewerControl!.getControl(BBJasperViewerControl.getSAVE_MENU_ITEM_NAME())
    printSavMenuItem!.setCallback(BBjMenuItem.ON_POPUP_ITEM_SELECT,"OnPrintSaveMenuButton")

    printEmailToolButton! = printViewerControl!.getControl(BBJasperViewerControl.getEMAIL_TOOL_BUTTON_NAME())
    printEmailToolButton!.setCallback(BBjMenuButton.ON_TOOL_BUTTON_PUSH ,"OnPrintEmailToolButton")

    printViewerWindow!.setReleaseOnClose(0)
    printViewerWindow!.show(0)

    bbjWindowPrint!= printViewerWindow!.getViewerWindow()
    bbjWindowPrint!.setCallback(bbjWindowPrint!.ON_CLOSE,"close_print_win")

rem --- Event Control

    process_events,err=*same

    release

 document_whse:rem --- Write record to Barista document warehouse (ads_documents)

    call stbl("+DIR_SYP")+"bac_documents.bbj",
:       rd_doc_id$,
:       rep_date_stamp$,
:       rep_time_stamp$,
:       rd_doc_source$,
:       rd_doc_ext$,
:       rd_doc_path$,
:       rd_source_type$,
:       rd_source_alias$,
:       rd_source_id$,
:       rd_source_ref$,
:       rd_table_chans$[all],
:       rd_archive_action$,
:       rd_doc_name$,
:       rep_title$,
:       rd_doc_keywords$

    return

doc_path:rem --- Get Document Path
rem --- forcing a pdf save to this location for release 13.03
rem --- eventually, want to make the save functionality mimic (as closely as possible) Barista
rem --- so it can save based on user's doc group and doc settings

    if rd_doc_path$="" then
        rd_doc_path$=dsk("")+dir("")
        rd_doc_path$=BBjAPI().getFileSystem().resolvePath(stbl("+DOC_DIR_PDF",err=*next),err=*next)+"/"
        mkdir rd_doc_path$,err=*next
    endif

    return

OnMasterSaveMenuButton:
rem --- save save and display confirmation dialog

    show_save_dlg$="YES"
 
exportMasterToPDF:
rem --- exportToPDF and record in doc warehouse

    if rd_doc_path$<>"" and repTitle$<>""
        rd_doc_ext$="PDF"
        rep_title$=repTitle$
        rd_source_id$="C"
        rd_source_ref$=""
        rd_doc_keywords$=""
        master_report!.exportToPDF(BBjAPI().FALSE, rd_doc_path$+rd_doc_name$)
        gosub document_whse
    endif

    if show_save_dlg$="YES"
        msg_id$="DOC_OUTPUT_COMP"
        gosub disp_message
        show_save_dlg$=""
    endif
    return    

OnMasterEmailToolButton:rem --- Add document to fax/email queue
rem --- force exportToPDF first, in case it hasn't already been saved
    
    rem --- force exportToPDF in case not already saved
    gosub exportMasterToPDF

    if rd_doc_id$<>"" and rd_doc_ext$<>""
        call stbl("+DIR_SYP")+"bac_faxemail_jasper.bbj",rd_doc_id$,rd_doc_ext$,rd_table_chans$[all]
    endif

    return

OnPrintSaveMenuButton:
rem --- save save and display confirmation dialog

    show_save_dlg$="YES"
 
exportPrintToPDF:
rem --- exportToPDF and record in doc warehouse

    if rd_doc_path$<>"" and repTitle$<>""
        rd_doc_ext$="PDF"
        rep_title$=repTitle$
        rd_source_id$="C"
        rd_source_ref$=""
        rd_doc_keywords$=""
        print_report!.exportToPDF(BBjAPI().FALSE, rd_doc_path$+rd_doc_name$)
        gosub document_whse
    endif

    if show_save_dlg$="YES"
        msg_id$="DOC_OUTPUT_COMP"
        gosub disp_message
        show_save_dlg$=""
    endif
    return    

OnPrintEmailToolButton:rem --- Add document to fax/email queue
rem --- force exportToPDF first, in case it hasn't already been saved
    
    rem --- force exportToPDF in case not already saved
    gosub exportPrintToPDF

    if rd_doc_id$<>"" and rd_doc_ext$<>""
        call stbl("+DIR_SYP")+"bac_faxemail_jasper.bbj",rd_doc_id$,rd_doc_ext$,rd_table_chans$[all]
    endif
    return

close_win:rem --- Viewer Window Closed

    ok_to_close=1
    if !print_win_closed
        msg_id$ = "CONF_CLOSE_PRINT"
        gosub disp_message
        if msg_opt$<>"Y" then ok_to_close=0 
    endif

    if !ok_to_close then return
        
    if emailFax!.size()
        msg_id$ = "CONF_RPTCTL"
        gosub disp_message
        if msg_opt$="Y"
            for email_fax=0 to emailFax!.size()-1 step 3
                reportControl!.addToDocQueue(emailFax!.get(email_fax),emailFax!.get(email_fax+1),"",emailFax!.get(email_fax+2))    
            next email_fax
        endif
    endif    
    
    reportControl!.destroy(err=*next)
    masterViewerWindow!.destroy(err=*next)
    printViewerWindow!.destroy(err=*next)

    if tcb(13) then exit
    release

close_print_win: rem --- close the Print Copy; no updating, just close the print copy window

    printViewerWindow!.destroy(err=*next)
    print_win_closed=1
    
    return

adjustments: rem --- Accumulate any adjustments for the current invoice

rem --- Position Invoice Detail (art-11) file

    art01_key$=art01a.firm_id$+art01a.ar_type$+art01a.customer_id$+art01a.ar_inv_no$
    read (art11_dev,key=art01_key$,dom=*next)

rem --- Read Invoice Detail (art-11) records

    applied=0

    while 1
        k$=key(art11_dev,end=*break)
        if pos(art01_key$=k$)<>1 break
        read record (art11_dev) art11a$
        if art11a.trans_date$ <= stmt_date$ then applied = applied+art11a.trans_amt+art11a.adjdisc_amt
    wend

    return

rem --- Functions

rem --- fntestvalue: Return maximum numeric value for a given mask

    def fntestvalue(q$)
        q1$=""
        for q=1 to len(q$)
            if pos(q$(q,1)="#0") q1$=q1$+"9"
            if q$(q,1)="." q1$=q1$+"."
        next q
        q=num(q1$)
        return q
    fnend

rem --- Functions used to retrieve form values

    def fnstr_pos(q0$,q1$,q1)=int((pos(q0$=q1$,q1)+q1-1)/q1)
    def fnget_rec_date$(q0$)=rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+cvs(q0$,1+2+4),rd_rec_data$[0,0],40),0]
    def fnget_fld_data$(q0$,q1$)=cvs(rd_rec_data$[fnstr_pos(cvs(q0$,1+2+4)+"."+cvs(q1$,1+2+4),rd_rec_data$[0,0],40),0],2)

rem --- Date/time handling functions

    def fndate$(q$)
        q1$=""
        q1$=date(jul(num(q$(1,4)),num(q$(5,2)),num(q$(7,2)),err=*next),err=*next)
        if q1$="" q1$=q$
        return q1$
    fnend

    def fnyy$(q$)=q$(3,2)
    def fnclock$(q$)=date(0:"%hz:%mz %p")
    def fntime$(q$)=date(0:"%Hz%mz")

rem --- fnmask$: Alphanumeric Masking Function (formerly fnf$)

    def fnmask$(q1$,q2$)
        if q2$="" q2$=fill(len(q1$),"0")
        return str(-num(q1$,err=*next):q2$,err=*next)
        q=1
        q0=0
        while len(q2$(q))
            if pos(q2$(q,1)="-()") q0=q0+1 else q2$(q,1)="X"
            q=q+1
        wend
        if len(q1$)>len(q2$)-q0 q1$=q1$(1,len(q2$)-q0)
        return str(q1$:q2$)
    fnend

rem #include disp_message.src

disp_message: rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,rd_table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_missing_params.src

std_missing_params: rem --- Standard missing parameter handler (15Apr2006)

    rd_err_text$=""
    if tcb(5)<>0 and pgm(-1)=pgm(-2) rd_err_text$=pgm(tcb(5))
    pgmdir$=stbl("+DIR_PGM",err=std_missing_params_exit)
    call pgmdir$+"adc_noparams.aon",err=std_missing_params_exit,pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$
std_missing_params_exit: 
    master_user$=cvs(stbl("+MASTER_USER",err=std_missing_params_release),2)
    sysinfo_template$=stbl("+SYSINFO_TPL",err=std_missing_params_release)
    dim sysinfo$:sysinfo_template$
    sysinfo$=stbl("+SYSINFO",err=std_missing_params_release)
    if cvs(sysinfo.user_id$,2)=master_user$ escape
std_missing_params_release: 
    status=999
    if pgm(-1)<>pgm(-2) exit
    release

rem #endinclude std_missing_params.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    run stbl("+DIR_SYP")+"bas_process_end.bbj",err=*next

std_exit_no_report: 

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    release

rem #endinclude std_end.src

    end

