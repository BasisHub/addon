rem --- Invoice Date Modification Update
rem --- Program aru_dateregister v8.0.0 07Jan2007

rem --- AddonSoftware Version 8.0.0 - 01Jan2007
rem --- Copyright BASIS International Ltd.  All Rights Reserved.
rem --- All Rights Reserved

    setesc std_error
    seterr std_error

rem --- Open/Lock files

    close (printer_dev,err=*next)

rem --- Initializations

    ar_type$=fill(2)

rem --- Get user approval to proceed

    overlay$=""
    next_overlay$=""
    msg_id$="AON_UPDT_QUERY"
    dim x$:stbl("+SYSINFO_TPL")
    dim msg_tokens$[1]
    x$=stbl("+SYSINFO")                                                            
    msg_tokens$[1]=x.task_desc$
    gosub disp_message
    if msg_opt$<>"Y" then goto std_exit_no_update

rem --- Start progress meter and position invoice file

    call pgmdir$+"adc_progress.aon","NC","","","","",0,are06_dev,1,meter_num,status
    read (are06_dev,key=firm_id$,dom=*next)

rem --- Read next Invoice (are-06) record

    while more
          are06_key$=key(are06_dev,end=*break)
          if pos(firm_id$=are06_key$)<>1 break
          read record (are06_dev) are06a$
          call pgmdir$+"adc_progress.aon","S","","","","",0,0,1,meter_num,status

          rem --- Update Open Invoice (art-01) file

          dim art01a$:fattr(art01a$)
          art01a.firm_id$=are06a.firm_id$
          art01a.ar_type$=are06a.ar_type$
          art01a.customer_id$=are06a.customer_id$
          art01a.ar_inv_no$=are06a.ar_inv_no_ver$
          art01a.sequence_00$="00"
          k$=art01a.firm_id$+art01a.ar_type$+art01a.customer_id$+
:            art01a.ar_inv_no$+art01a.sequence_00$
          extract record (art01_dev,key=k$,dom=invalid_inv) art01a$; rem Advisory Locking
          art01a.ar_terms_code$=are06a.ar_terms_code$
          art01a.invoice_date$=are06a.invoice_date$
          art01a.inv_due_date$=are06a.inv_due_date$
          art01a.disc_date$=are06a.disc_date$
          art01a.dates_chg$="*"
          art01a$=field(art01a$)
          write record (art01_dev) art01a$
 
         rem --- Delete Invoice (are-06) record and loop back for the next one
invalid_inv:

          remove (are06_dev,key=are06_key$)

    wend

rem --- All done

    call pgmdir$+"adc_batchdelete.aon",status
    goto std_exit

disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]
    return

rem #include std_error.src

std_error: rem --- Standard error handler (11Oct2017)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$="",err_num=err
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    if err_num=252 then
        E!=BBjAPI().getLastBBjException()
        rd_err_text$=rd_err_text$+$0A$+E!.getClass().getName()
        if E!.getMessage()<>null() then rd_err_text$=rd_err_text$+": "+E!.getMessage()
    endif
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err_num),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src

rem #include std_end.src

std_exit: rem --- Standard program end (01Mar2006)

    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
    call pgmdir$+"adc_progress.aon","D","","","","",0,0,0,meter_num,status
	msg_id$="UPDATE_COMPLETE"
	dim msg_tokens$[1]
	msg_opt$=""
	gosub disp_message

std_exit_no_update:

    release

rem #endinclude std_end.src

    end
