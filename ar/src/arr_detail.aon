rem Customer Detail Listing
rem Proprietary Software. BASIS International Ltd.
rem Program ID: arr_CustDetail_irpt.aon  <Feb 24, 2011>
rem --- Copyright BASIS International Ltd.  All Rights Reserved.

    setesc std_error
    seterr std_error

    use ::bbjasper.bbj::BBJasperReport
    use ::bbjasper.bbj::BBJasperViewerWindow
    use ::bbjasper.bbj::BBJasperViewerControl

    use ::sys/prog/bao_utilities.bbj::BarUtils
    
    ScreenSize!   = bbjAPI().getSysGui().getSystemMetrics().getScreenSize()
    screen_width  = ScreenSize!.width - 50; rem -50 keeps it in the MDI w/ no scroll bars
    screen_height = ScreenSize!.height - 50
    
rem --- Get HashMap of Values in Options Entry Form

    params! = Option!.getOptionMap()
    rem "CUSTOMER INACTIVE FEATURE"
    inactive$=Option!.getOptionData("CUST_INACTIVE")

rem --- Retrieve sysinfo data

    sysinfo_tpl$=stbl("+SYSINFO_TPL",err=*next)
    dim sysinfo$:sysinfo_tpl$
    sysinfo$=stbl("+SYSINFO",err=*next)
    user_id$=sysinfo.user_id$
    proc_date$=date(jul(num(sysinfo.system_date$(1,4)),num(sysinfo.system_date$(5,2)),num(sysinfo.system_date$(7,2))):stbl("+DATE_MASK"))
    firm_id$=sysinfo.firm_id$
    firm_name$=sysinfo.firm_name$
    
rem --- Open files

    num_files=16
    dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
    open_tables$[1]="ARS_PARAMS",     open_opts$[1]="OTA"
    open_tables$[2]="ARC_SALECODE",   open_opts$[2]="OTA@"
    open_tables$[3]="ARC_TERMCODE",   open_opts$[3]="OTA@"
    open_tables$[4]="ARC_TERRCODE",   open_opts$[4]="OTA@"
    open_tables$[5]="ARC_DISTCODE",   open_opts$[5]="OTA@"
    open_tables$[6]="OPC_MESSAGE",   open_opts$[6]="OTA@"
    open_tables$[7]="OPC_DISCCODE",   open_opts$[7]="OTA@"
    open_tables$[8]="OPC_TAXCODE",   open_opts$[8]="OTA@"
    open_tables$[9]="OPC_PRICECDS",   open_opts$[9]="OTA@"
    open_tables$[10]="ARC_CUSTTYPE",   open_opts$[10]="OTA@"
    open_tables$[11]="ARM_CUSTPMTS",   open_opts$[11]="OTA@"
    open_tables$[12]="ART_INVHDR",   open_opts$[12]="OTA@"
    open_tables$[13]="ART_INVDET",   open_opts$[13]="OTA@"
    open_tables$[14]="ARM_CUSTSHIP",   open_opts$[14]="OTA@"
    open_tables$[15]="OPM_CUSTJOBS",   open_opts$[15]="OTA@"
    rem open_tables$[16]="",   open_opts$[16]=""

    gosub open_tables

    ars_params_dev=num(open_chans$[1])
    dim ars_params$:open_tpls$[1]

rem --- Close files after ensuring they exist

    num_files=15
    dim open_tables$[1:num_files],open_opts$[1:num_files],open_chans$[1:num_files],open_tpls$[1:num_files]
    open_tables$[1]="ARC_SALECODE",   open_opts$[1]="C@"
    open_tables$[2]="ARC_TERMCODE",   open_opts$[2]="C@"
    open_tables$[3]="ARC_TERRCODE",   open_opts$[3]="C@"
    open_tables$[4]="ARC_DISTCODE",   open_opts$[4]="C@"
    open_tables$[5]="OPC_MESSAGE",   open_opts$[5]="C@"
    open_tables$[6]="OPC_DISCCODE",   open_opts$[6]="C@"
    open_tables$[7]="OPC_TAXCODE",   open_opts$[7]="C@"
    open_tables$[8]="OPC_PRICECDS",   open_opts$[8]="C@"
    open_tables$[9]="ARC_CUSTTYPE",   open_opts$[9]="C@"
    open_tables$[10]="ARM_CUSTPMTS",   open_opts$[10]="C@"
    open_tables$[11]="ART_INVHDR",   open_opts$[11]="C@"
    open_tables$[12]="ART_INVDET",   open_opts$[12]="C@"
    open_tables$[13]="ARM_CUSTSHIP",   open_opts$[13]="C@"
    open_tables$[14]="OPM_CUSTJOBS",   open_opts$[14]="C@"
    rem open_tables$[15]="",   open_opts$[15]=""

    gosub open_tables

rem --- retrieve retention value from param file

    readrecord(ars_params_dev,key=firm_id$+"AR00",dom=*next)ars_params$
    
rem --- Set additional params

    params!.put("FIRM_ID",firm_id$)
	params!.put("FIRM_NAME",firm_name$)
    params!.put("REPORT_NAME",sysinfo.task_desc$)
    params!.put("BARISTA_WD",dir(""))       
    if params!.get("CUSTOMER_ID_2")="" then params!.put("CUSTOMER_ID_2","~~~~~~")    
    if params!.get("PICK_CHECK")="Y"
        params!.put("COMMENT_YN","Y")
    else
        params!.put("COMMENT_YN","")
    endif

rem --- Customer Inactive Parameters
    if inactive$="Y" then
      params!.put("OPTION_ACTIVE"," AND ARM_CUSTMAST.CUST_INACTIVE<>'Y'")
    else
      params!.put("OPTION_ACTIVE","")
    endif
    params!.put("INACTIVE_CUST",Translate!.getTranslation("AON_EXCLUDE_INACTIVE")+Translate!.getTranslation("AON_CUSTOMERS")+" = "+inactive$)

rem --- Make Document Archive Record and Get ID

    rep_date$=date(0:stbl("+DATE_MASK"))
    rep_date_stamp$=date(0:"%Yd%Mz%Dz")
    rep_time$=date(0:"%hz:%mz %p")
    rep_time_stamp$=date(0:"%Hz%mz%sz")

    rd_source_alias$=rd_alias_id$
    rd_source_type$="O"
    rd_doc_source$="E"
    rd_doc_ext$="JAS"
    rd_archive_action$="DOC_ID-NOREPRINT"
    gosub document_whse
    rd_archive_action$=""

rem --- Document Output Report Parameters

    params!.put("DOC_NO",rd_doc_id$)
    params!.put("DATE_REP",rep_date$+"  "+rep_time$)

rem --- Build Pattern and Replacement string from Vendor Mask

	call stbl("+DIR_PGM")+"adc_getmask.aon","","AR","I","",msk$,0,0
	pattern=pos("-"=msk$,1,0)
	replacement$=""
	if pattern=0
		pattern$="("+pad(".",len(msk$))+")"
		replacement$="$1"
	else
		pattern$="("
		for x9=1 to len(msk$)
			if msk$(x9,1)<>"-"
				pattern$=pattern$+"."
			else
				pattern$=pattern$+")("
			endif
		next x9
		pattern$=pattern$+")"
		for x9=1 to pattern+1
			replacement$=replacement$+"$"+str(x9)+"-"
		next x9
		replacement$=replacement$(1,len(replacement$)-1)
	endif
	params!.put("CUST_PATTERN",pattern$)
	params!.put("CUST_REPLACE",replacement$)

rem --- Set Report Name & Subreport directory

    reportDir$ = stbl("+DIR_REPORTS",err=*next)
    repTitle!=sysinfo.task_desc$+"_"+rd_doc_id$
    repTitle$=repTitle!.replace(" ","_")
    temp = unt
    open (temp)reportDir$
    reportDir$ = fid(temp)(9)+"/"
    close (temp)
    reportBaseName$ = "CustDetail"
    filename$ = reportDir$ + reportBaseName$ + ".jasper"

    declare BBJasperReport report!
    
rem --- Check for user authentication

    report! = BarUtils.getBBJasperReport(filename$)
    report!.putParams(params!)

    locale$ = stbl("!LOCALE")
    locale$ = stbl("+USER_LOCALE",err=*next)
    report!.setLocale(locale$)

rem --- Fill Report and Show
    rc=report!.fill(1)
    if rc<>BBJasperReport.getSUCCESS() then goto close_win
    
    rem --- get a pdf path from +DOC_DIR_PDF
    rem --- this will eventually be replaced with code to get output formats/types based on doc group and/or print settings
    gosub doc_path
    
    declare BBJasperViewerWindow viewerWindow!
    viewerWindow! = new BBJasperViewerWindow(report!,0,0,screen_width,screen_height,repTitle$,$00080093$)

    viewerControl! = viewerWindow!.getViewerControl()
    viewerControl!.setGoogleDefaultDocument(repTitle$)
    viewerControl!.setDefaultSaveFolder(rd_doc_path$)
    viewerControl!.setDefaultSaveFile(repTitle$)
    viewerControl!.setFitWidth()

    rem --- set callbacks for when user presses save or email buttons in jasper viewer
    rem --- note: saveAs or saveAsGDoc not intercepted, just regular save
    saveMenuButton! = viewerControl!.getControl(BBJasperViewerControl.getSAVE_MENU_BUTTON_NAME())
    saveMenuButton!.setCallback(BBjMenuButton.ON_BUTTON_PUSH ,"OnSaveMenuButton")

    emailToolButton! = viewerControl!.getControl(BBJasperViewerControl.getEMAIL_TOOL_BUTTON_NAME())
    emailToolButton!.setCallback(BBjMenuButton.ON_TOOL_BUTTON_PUSH ,"OnEmailToolButton")

    viewerWindow!.setReleaseOnClose(0)
    viewerWindow!.show(0)

    declare BBjTopLevelWindow bbjWindow!
    bbjWindow!= viewerWindow!.getViewerWindow()
    cancelled=1
    bbjWindow!.setCallback(bbjWindow!.ON_CLOSE,"close_win",err=*next); cancelled=0
    if cancelled then goto close_win

rem --- Event Control

    process_events,err=*same

    release

document_whse:rem --- Write record to Barista document warehouse (ads_documents)

    call stbl("+DIR_SYP")+"bac_documents.bbj",
:       rd_doc_id$,
:       rep_date_stamp$,
:       rep_time_stamp$,
:       rd_doc_source$,
:       rd_doc_ext$,
:       rd_doc_path$,
:       rd_source_type$,
:       rd_source_alias$,
:       rd_source_id$,
:       rd_source_ref$,
:       rd_table_chans$[all],
:       rd_archive_action$,
:       rd_doc_name$,
:       rep_title$,
:       rd_doc_keywords$

    return

doc_path:rem --- Get Document Path
rem --- forcing a pdf save to this location for release 13.03
rem --- eventually, want to make the save functionality mimic (as closely as possible) Barista
rem --- so it can save based on user's doc group and doc settings

    if rd_doc_path$="" then
        rd_tmp_dir$=stbl("+DOC_DIR_PDF",err=*endif)
        rd_temp_wd$=dsk("")+dir("")
        if pos(":"=rd_tmp_dir$) setdrive rd_tmp_dir$(1,pos(":"=rd_tmp_dir$)-1),err=*next
        chdir rd_tmp_dir$,err=*next
        rd_doc_path$=dsk("")+dir("")
        if pos(":"=rd_temp_wd$) setdrive rd_temp_wd$(1,pos(":"=rd_temp_wd$)-1)
        chdir rd_temp_wd$
    endif

    return

OnSaveMenuButton:
rem --- exportToPDF and record in doc warehouse

    if rd_doc_path$<>"" and repTitle$<>""
        rd_doc_name$=repTitle$+".pdf"
        rd_doc_ext$="PDF"
        report!.exportToPDF(BBjAPI().FALSE, rd_doc_path$+rd_doc_name$)
        gosub document_whse
    endif
    return    

OnEmailToolButton:rem --- Add document to fax/email queue
rem --- force exportToPDF first, in case it hasn't already been saved
    
    rem --- force exportToPDF in case not already saved
    gosub OnSaveMenuButton

    if rd_doc_id$<>"" and rd_doc_ext$<>""
        call stbl("+DIR_SYP")+"bac_faxemail_jasper.bbj",rd_doc_id$,rd_doc_ext$,rd_table_chans$[all]
    endif

    return

close_win:rem --- Viewer Window Closed
    viewerWindow!.destroy(err=*next)
    rdForm!.destroy(err=*next)  
    if tcb(13) then exit
    release

connect_err:rem --- Connection Error

    declare java.lang.Exception Exception!
    Exception! = bbjapi().getLastException()
    if Exception! = null() then 
       x = msgbox(errmes(-1),16,"Connection Failed")
    else
       x = msgbox(Exception!.getLocalizedMessage(),16,"Connection Failed")
    endif

    release
    
open_tables:rem --- Open Tables

    call stbl("+DIR_SYP")+"bac_open_tables.bbj",
:       open_beg,
:       open_end,
:       open_tables$[all],
:       open_opts$[all],
:       open_chans$[all],
:       open_tpls$[all],
:       rd_table_chans$[all],
:       open_batch,
:       open_status$

    if open_status$<>"" then
        msg_id$="ENTRY_OPEN_ERROR"
        dim msg_tokens$[1]
        msg_tokens$[1]=open_status$
        gosub disp_message
        rdFuncSpace!=bbjAPI().getGroupNamespace().setValue("+build_task","OFF")
        release
    endif

    return
    
disp_message:rem --- Display Message Dialog

    call stbl("+DIR_SYP")+"bac_message.bbj",msg_id$,msg_tokens$[all],msg_opt$,table_chans$[all]

    return

rem #include std_error.src

std_error: rem --- Standard error handler (18Nov2014)

    if tcb(19)>0
        rem --- Escape handler
        if and(chr(tcb(19)),$08$)=$08$
            release
        else
            setesc std_error
            return
        endif
    endif

    if err=0   
        rem --- Get tcb(12) and tcb(10) to send into bac_error
        lock_byte=tcb(10)
        lock_chan=tcb(12)  
    endif

    rd_err_text$=""
    if tcb(2)=0 and tcb(5) then rd_err_text$=pgm(tcb(5),tcb(13),err=*next)
    call stbl("+DIR_SYP")+"bac_error.bbj",pgm(-2),str(tcb(5)),str(err),rd_err_text$,rd_err_act$,lock_byte,lock_chan
    if pos("ESCAPE"=rd_err_act$) seterr 0; setesc 0
    if pos("RETRY"=rd_err_act$) retry
    if pgm(-1)<>pgm(-2) status=999; exit 
    release

rem #endinclude std_error.src
